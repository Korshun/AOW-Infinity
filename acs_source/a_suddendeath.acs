/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * All Out War 2 sudden death script
 * Credit to VoltlocK for originally creating the engine
 * With modifications by Eruanna, Dusk and the Omega Team
 *
 * You may use portions of this script in your project as long as you give
 * credit where credit is due. Please don't be lame and just copy-paste any
 * of this and call it your own. Thanks!
 */

script aow_script995 OPEN {
	Delay (60*(35*5));
	CrateTime = False;
}

script aow_script998 OPEN {
	delay (5);
	while (Timer() < GetCVAR ("aow_suddendeathtime") * 35 * 60) delay (35 * 15);
	ACS_Execute (997, 0);
}

script aow_script997 (void) {
	if (EventScripts) ACS_ExecuteAlways (aow_eventscript, 0, ACTION_SUDDENDEATH, TEAM_NONE);
	GiveActorInventory (100, "SuddenDeathVulnerability", 1);
	GiveActorInventory (TID_BARRACKS_BLUE, "SuddenDeathVulnerability", 1);
	GiveActorInventory (102, "SuddenDeathVulnerability", 1);
	GiveActorInventory (TID_REFINERY_BLUE, "SuddenDeathVulnerability", 1);
	GiveActorInventory (104, "SuddenDeathVulnerability", 1);
	GiveActorInventory (TID_RESCENTRE_BLUE, "SuddenDeathVulnerability", 1);
	GiveActorInventory (106, "SuddenDeathVulnerability", 1);
	GiveActorInventory (TID_FACTORY_BLUE, "SuddenDeathVulnerability", 1);
	GiveActorInventory (108, "SuddenDeathVulnerability", 1);
	GiveActorInventory (TID_OBELISK_BLUE, "SuddenDeathVulnerability", 1);
	GiveActorInventory (115, "SuddenDeathVulnerability", 1);
	GiveActorInventory (200, "SuddenDeathVulnerability", 1);
	GiveActorInventory (TID_BARRACKS_RED, "SuddenDeathVulnerability", 1);
	GiveActorInventory (202, "SuddenDeathVulnerability", 1);
	GiveActorInventory (TID_REFINERY_RED, "SuddenDeathVulnerability", 1);
	GiveActorInventory (204, "SuddenDeathVulnerability", 1);
	GiveActorInventory (TID_RESCENTRE_RED, "SuddenDeathVulnerability", 1);
	GiveActorInventory (206, "SuddenDeathVulnerability", 1);
	GiveActorInventory (TID_FACTORY_RED, "SuddenDeathVulnerability", 1);
	GiveActorInventory (208, "SuddenDeathVulnerability", 1);
	GiveActorInventory (TID_OBELISK_RED, "SuddenDeathVulnerability", 1);
	GiveActorInventory (215, "SuddenDeathVulnerability", 1);
	if (Timer() > 5*35 + 10) {
		if (random (0, 1))
			SetMusic ("razor-ub");
		else
			SetMusic ("razorub2");
	}

	Log(s:"=== Sudden Death begins ===");

	if (!GetCVAR("aow_tournamentmode")) {
		for (int n = 0; n < MAX_PLAYERS; n++) {
			if (!PlayerInGame(n)) continue;
			if (GetPlayerInfo (n, PLAYERINFO_TEAM) == TEAM_RED)
				GiveActorInventory (3800+n, "NuclearStrikeBeacon", 1);
			else 
				GiveActorInventory (3800+n, "IonCannonBeacon", 1);
		}
	}

	SetFont("BIGFONT");
	HudMessageBold (s:"\cgSUDDEN DEATH!\n\nNo more repairs can be done\nEvery death counts!";
		HUDMSG_FADEOUT, 15001, CR_YELLOW,
		INTEL_XPOS, 0.25, 7.0, 1.0);
	
	int degeneration = GetCVar ("aow_degeneration");
	int degeninterval = GetCVar ("aow_degenerationinterval");
	if (degeneration) {
		SetFont("BIGFONT");
		HudMessageBold (s:"WARNING: BUILDING DEGENERATION ACTIVE!!!";
			HUDMSG_FADEOUT, 15002, CR_RED,
			INTEL_XPOS, 0.45, 7.0, 1.0);
	}
	
	if (!GetCVar("aow_ticketpool")) {
		// init tickets
		Tickets[TEAM_BLUE] = StartingTickets;
		Tickets[TEAM_RED] = StartingTickets;
		SetActorSpeed (TID_TICKETCOUNTER_BLUE, StartingTickets);
		SetActorSpeed (TID_TICKETCOUNTER_RED, StartingTickets);
	}
	
	SuddenDeath = true;
	int ticker = 0;
	int i;
	
	SetActivator (-1);
	while (1) {
		if (!(ticker % 5)) {
			degeninterval = GetCVar ("aow_degenerationinterval");
		}
		if (!(ticker % 60)) {
			SetFont("SMALLFONT");
			HudMessageBold (s:"Sudden death!";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
		}
		
		if (degeneration && !(ticker % degeninterval)) {
			// crash all buildings by 1% every interval seconds
			for (i = 0; i <= 1; i++) {
				Thing_Damage (TID_BARRACKS_BLUE+(i*100), HP_BARRACKS / 100);
				Thing_Damage (TID_REFINERY_BLUE+(i*100), HP_REFINERY / 100);
				Thing_Damage (TID_RESCENTRE_BLUE+(i*100), HP_PLANT / 100);
				Thing_Damage (TID_FACTORY_BLUE+(i*100), HP_FACTORY / 100);
				Thing_Damage (TID_OBELISK_BLUE+(i*100), HP_OBELISK / 100);
			}
		}
		
		delay (35);
		ticker++;
	}
}