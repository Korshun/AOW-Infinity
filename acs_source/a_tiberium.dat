// determine should we pick up Tiberium or leave it there
script 357 (void) {
	SetResultValue (0);
	if (CheckInventory ("IsHarvester") && !CheckInventory ("TiberiumCrystal")) {
		SetResultValue (1);
		SetFont ("SMALLFONT");
		HudMessage (s:"\cJTake it back to \cQRefinery!";
			HUDMSG_FADEOUT, 15000, CR_WHITE,
			INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
	}
}

function bool RefineTiberium (int team) {
	if (Team == PlayerTeam() || PlayerTeam() < 0 || PlayerTeam() > 1) {
		Error ("This is not your terminal!");
		return false;
	}
	
	if (!CheckRefinery(Team)) return false;
	
	if (!CheckInventory("IsHarvester") || !CheckInventory("TiberiumCrystal")) {
		Error ("You need to be a \cfHarvester\c- and have \cdTiberium!");
		return false;
	}
	
	TakeInventory ("TiberiumCrystal",1);
	
	// How much to give for others?
	int amount = GetCVar ("aow_tiberium_other");
	if (EnhancedRefining[Team]) {
		int factor = (GetCVar ("aow_tiberium_advperc") << 16) / 100;
		amount = (amount * factor) >> 16;
	}
	
	int self = PlayerNumber ();
	if (GetCVar ("aow_tiberium_self") > 0) {
		GiveCredits (-1, GetCVar ("aow_tiberium_self"));
		GivePoints (1);
		AddExperience (75);
		
		SetFont ("BIGFONT");
		if (amount < 0)
			HudMessage (s:"You have refined the \cDTiberium\c and have earned \cQ$\cD",
				d:GetCVar ("aow_tiberium_self"), s:"\c-!";
				HUDMSG_FADEOUT, 1800+PlayerNumber(), CR_WHITE, 0.5, 0.45, 2.0, 1.0);
		else
			HudMessage (s:"You have refined the \cDTiberium!\n\cQ$\cD",
				d:GetCVar ("aow_tiberium_self"), s:"\c- for you, \cq$\cd", d:GetCVar ("aow_tiberium_other"),
				s:"\c- for teammates!";
				HUDMSG_FADEOUT, 1800+PlayerNumber(), CR_WHITE, 0.5, 0.45, 2.0, 1.0);
			
		// Play this sound separately for the refiner - if aow_tiberium_other is
		// negative, this won't play in the block below.
		LocalAmbientSound ("powerup/credsound", 255);
	}
	
	Log (n:0, s:" refines for ", s:TeamNames[PlayerTeam()], s:" Team");
	
	// If the amount went negative, the admin has disabled awards to others
	if (amount < 0) return true;
	
	// Iterate through players and give credits where appropriate
	// TODO: the refiner shouldn't execute anything here - the HudMessages need
	// to be adjusted, though..
	for (int i = 0; i < MAXPLAYERS; i++) {
		if (PlayerInGame(i) && GetPlayerInfo (i, PLAYERINFO_TEAM) == PlayerTeam() && i != self) {
			GiveCredits (i, amount);
			SetActivator (3800+i);
			SetFont ("SMALLFONT");
			HudMessage (s:"Your teammate has \cDrefined Tiberium\n",
				s:"and has awarded your team with \cQ$\cD", d:amount;
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
			
			// We played this sound already for the refiner
			if (i != self)
				LocalAmbientSound ("powerup/credsound", 255);
		}
	}
	
	return true;
}

// Tiberium refining terminal
script 110 (int Team)
	RefineTiberium (team);