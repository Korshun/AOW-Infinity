/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * All Out War 2 death script
 * Credit to VoltlocK for originally creating the engine
 * With modifications by Eruanna, Dusk and the Omega Team
 *
 * You may use portions of this script in your project as long as you give
 * credit where credit is due. Please don't be lame and just copy-paste any
 * of this and call it your own. Thanks!
 */

// death script: victim POV
script 994 DEATH {
	Thing_ChangeTID (0, 9999);
	TakeInventory ("ThermalCloakingTime", 90);
	TakeInventory ("InUtilMenu", 1);
	SetPlayerProperty (0, 0, PROP_FROZEN);
	SetPlayerProperty (0, 0, PROP_TOTALLYFROZEN);
	
	// tiberium dropped by dead harvesters
	if (CheckInventory ("TiberiumCrystal") && !CheckInventory ("IsInSpawnRoom"))
		SpawnSpotForced ("TiberiumCrystalDropped", 0, 0, 0);
	
	// bomb pack disarm sound
	if (CheckWeapon ("BombPack") && !CheckInventory ("IsTerrorist")) ActivatorSound ("c4/disarm", 127);
	
	// activate plasma cannon cooldown, if player had a plasma cannon
	if (CheckInventory ("PlasmaCannon")) PlasmaCooldown[PlayerNumber()] = Timer() + PlasmaCooldownTime;
	
	// how much is this thing worth?
	int points = 1;
	if (CheckInventory ("IsMech")) {
		if (CheckInventory ("OrcaWeapons") ||
			CheckInventory ("RavenWeapons")) {
			
			points = 3;
		} else if (CheckInventory ("WolverineWeapons") ||
			CheckInventory ("MadCatWeapons") ||
			CheckInventory ("TitanWeapons") ||
			CheckInventory ("GuardianWeapons")) {
			
			points = 4;
		}
	}
		
	// were we on a spree?
	int killer_t = ACS_ExecuteWithResult (964, 5);
	int killer_p = ACS_ExecuteWithResult (964, 9);
	//Log (n:0, s:"\c-: I am ", d:ActivatorTID(), s:" and was killed by ", d:killer_t, s:" (", n:killer_p+1, s:")");
	if (CheckInventory ("SpreeCount") >= 5) {
		SetFont ("SMALLFONT");
		if (killer_p != PlayerNumber() && killer_p != -1) {
			// killed by a player
			HudMessageBold (n:0, s:"\c-'s killing spree was ended by ", n:killer_p+1, s:".";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
		} else if (killer_t == 115) {
			// killed by blue obelisk
			HudMessageBold (n:0, s:"\c-'s killing spree was ended by the \chBlue Obelisk\c-.";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
		} else if (killer_t == 215) {
			// killed by red obelisk
			HudMessageBold (n:0, s:"\c-'s killing spree was ended by the \cgRed Obelisk\c-.";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
		} else if (killer_p == PlayerNumber ()) {
			// killed by something (self, env. kills, etc)
			HudMessageBold (n:0, s:"\c- was lookin' good until he tripped over a log!";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
		}
	}
	
	/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
	 * KILLER POV STARTS HERE
	 */
	
	int n0 = PlayerNumber();
	int t0 = PlayerTeam();
	SetActivatorToTarget (0);
	if ((ActivatorTID () < 3800 || ActivatorTID () > 3831) && ActivatorTID () != 9999) terminate;
	
	int n1 = PlayerNumber();
	int t1 = PlayerTeam();
	
	// remove tickets from the victim team
	TakeTickets (t0, points);
	
	// no points for suicides
	if (n0 != n1 && n1 != -1) {
		GivePoints (2*points);
		ACS_ExecuteAlways (931, 0);
		
		LocalAmbientSound ("game/boink", 127);
		GiveCredits (-1, CreditFragAward);
		AddExperience (50);
		
		delay (5);
		
		if (!FirstBlood) {
			// we got first blood!
			FirstBlood = true;
			
			HudMessageBold (n:0, s:"\c- gets first blood!";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
		}
		
		// check sprees
		GiveInventory ("SpreeCount", 1);
		SetFont ("SMALLFONT");
		switch (CheckInventory ("SpreeCount")) {
		case 5:
			HudMessageBold (n:0, s:"\c- is on a killing spree!";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
			break;
		case 10:
			HudMessageBold (n:0, s:"\c- is on a rampage!";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
			break;
		case 15:
			HudMessageBold (n:0, s:"\c- is dominating!";
				HUDMSG_FADEOUT, 15000, CR_TAN,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
			break;
		case 20:
			HudMessageBold (n:0, s:"\c- is unstoppable!";
				HUDMSG_FADEOUT, 15000, CR_YELLOW,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
			break;
		case 25:
			HudMessageBold (n:0, s:"\c- is Godlike!";
				HUDMSG_FADEOUT, 15000, CR_ORANGE,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
			break;
		case 30:
			HudMessageBold (n:0, s:"\c- is Wicked Sick!";
				HUDMSG_FADEOUT, 15000, CR_RED,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
			break;
		}
	}
	
	ACS_ExecuteAlways (992, 0);
}

// Guardian stomp health
script 923 (void) {
	SetActivatorToTarget (0);
	if (!CheckWeapon ("GuardianWeapons")) terminate;
	SetHealth (GetHealth () + 100);
	if (GetHealth () > GetSpawnHealth ()) SetHealth (GetSpawnHealth ());
}

// [SP] Fragging a monster
Script 947 (int Amount) {
	if (ActivatorTID() >= 3800 && ActivatorTID() <= 3831) {
		LocalAmbientSound("game/boink",255);
		AddExperience(10 * Amount);
		AddCredits(Credits[PlayerNumber()+1], 15 * Amount);
	}
}