// determine should we pick up Tiberium or leave it there
script 357 (void) {
	SetResultValue (0);
	if (CheckInventory ("IsHarvester") && !CheckInventory ("TiberiumCrystal")) {
		SetResultValue (1);
		SetFont ("SMALLFONT");
		HudMessage (s:"\cJTake it back to \cQRefinery!";
			HUDMSG_FADEOUT, 15000, CR_WHITE,
			INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
	}
}

// Tiberium refining terminal
script 110 (int Team) {
	if(Team == PlayerTeam() || PlayerTeam() < 0 || PlayerTeam() > 1) {
		Error ("This is not your terminal!");
		terminate;
	}
	
	if (!CheckRefinery(Team)) terminate;
	
	if (!CheckInventory("IsHarvester") || !CheckInventory("TiberiumCrystal")) {
		Error ("You need to be a \cfHarvester \crand have \cdTiberium!");
		terminate;
	}

	TakeInventory ("TiberiumCrystal",1);
	GiveCredits (-1, 225);
	AddExperience (75);
	
	SetFont ("BIGFONT");
	HudMessage (s:"You have refined the \cDTiberium!\c- Extra \cQ$\cD225\c- for you";
		HUDMSG_FADEOUT, 1800+PlayerNumber(), CR_WHITE, 0.5, 0.45, 2.0, 1.0);
	
	GivePoints (1);
	
	int amount = 75;
	if (EnhancedRefining[Team]) amount = 225;
	
	for (int i = 0; i < 32; i++) {
		if (PlayerInGame(i) && GetPlayerInfo (i, PLAYERINFO_TEAM) == PlayerTeam()) {
			GiveCredits (i, amount);
			SetActivator (3800+i);
			SetFont ("SMALLFONT");
			HudMessage (s:"Your teammate has \cDrefined Tiberium\n",
				s:"and has awarded your team with \cQ$\cD", d:amount;
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
				
			LocalAmbientSound ("powerup/credsound", 255);
		}
	}
}