// All Out War 2 HUD scripts
// Credit to VoltlocK for originally creating the engine
// With modifications by Eruanna, Dusk and the Omega Team

// You may use portions of this script in your project as long as you give credit where credit is
// due. Please don't be lame and just copy-paste any of this and call it your own. Thanks!

// display credits/experience, manage self-heal
script aow_playerstatushud_serverside RESPAWN {

	ACS_ExecuteAlways(aow_playerstatushud_clientside, 0, PlayerNumber());
	
	while (PlayerInGame (PlayerNumber()) && GetHealth() > 0) {
		
		// Send credits as two inventory items because Zandronum sends them as shorts.
		SetInventory("PlayerCredits", Credits[PlayerNumber() + 1] & 0x7fff);
		SetInventory("PlayerCredits2", Credits[PlayerNumber() + 1] >> 15);
		SetInventory("PlayerExperience", Experience[PlayerNumber()]);
		SetInventory("PlayerRank", Rank[PlayerNumber()]);
		
		if (GetHealth () <= GetSpawnHealth ()-50 && !CheckInventory("IsMech") &&
			!CheckInventory("SelfHealCooldown") && !CheckInventory("PowerBerserk")) {
			GiveInventory ("SelfHealCooldown",1);
			GiveInventory ("SelfHealItem",1);
			SetFont ("SMALLFONT");
			HudMessage (s:"\cKSelf-applied medical attention\cJ is available";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
		}
		
		delay (1);
		if (!CheckWeapon("StealthLaserRifle") && !CheckWeapon("Knife"))
			ACS_ExecuteAlways (aow_stealth, 0, 3);
	}
}

script aow_playerstatushud_clientside (int player) CLIENTSIDE
{
	if (player != ConsolePlayerNumber())
		terminate;
		
	while (PlayerInGame (PlayerNumber()) && GetHealth() > 0)
	{
		ShowStatus();
		Delay(1);
	}
}

function void ShowStatus(void) {
	SetFont ("SMALLFONT");
	HudMessage(s:"\cNCredits: \cQ$\cD", d:CheckInventory("PlayerCredits") + (CheckInventory("PlayerCredits2") << 15),
		s:"\n", s:"\cNExperience: \cD",d:CheckInventory("PlayerExperience"), s:"\cQ/\cD",d:GetExpNeed(CheckInventory("PlayerRank")), s:" XP",
		s:"\n", s:RankTitles[CheckInventory("PlayerRank")];
		HUDMSG_PLAIN, 50+PlayerNumber(), CR_WHITE, -0.685, -0.805, 1.5);
	
	SetFont (GetRankPic());
	HudMessage(s:"a";
		HUDMSG_PLAIN, 5000+PlayerNumber(), CR_LIGHTBLUE, -0.92, -0.78, 1.5);	
	
	SetFont ("DOOMFONT");
}

script aow_script111 (int what) {
	int amount;
	switch (what) {
	case 0:
		// donation message
		SetFont ("SMALLFONT");
		HudMessage (s:"You have been donated \cQ$\cD100\cJ\nby ", n:Donor[PlayerNumber()]+1;
			HUDMSG_FADEOUT, 15000, CR_WHITE,
			INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
		break;
	}
}