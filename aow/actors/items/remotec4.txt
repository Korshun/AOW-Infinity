// REMOTE C4
// TODO: make the weapon sprite include the detonator!

Actor RemoteC4Ammo : Ammo
{
	Inventory.amount 0
	Inventory.MaxAmount 4
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 4
}

actor RemoteC4 : AOWWeapon
{
        Tag "Remote C4"
	+NOALERT
	Obituary "%o bonks %k with a 100kg Remote C4."
	Weapon.AmmoType "RemoteC4Ammo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	States {
	Ready:
		NULL A 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "ReadyFull")
		RC4G B 5 A_WeaponReady (WRF_NOPRIMARY)
		loop
	ReadyFull:
		RC4G A -1 A_WeaponReady
		wait
	Deselect:
		NULL A 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "DeselectFull")
		NULL A 0 A_Lower
		RC4G B 1 A_Lower
		goto Deselect+1
	DeselectFull:
		NULL A 0 A_Lower
		RC4G A 1 A_Lower
		loop
	Select:
		NULL A 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "SelectFull")
		RC4G B 1 A_Raise
		NULL A 0 A_Raise
		loop
	SelectFull:
		RC4G A 1 A_Raise
		NULL A 0 A_Raise
		loop
	Fire:
		NULL A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "Ready")
		NULL A 0 A_JumpIfInventory ("InMenu", 1, "Ready")
		RC4G A 1 offset (1, 31)
		RC4G A 1 offset (7, 37)
		RC4G A 1 offset (13, 43)
		RC4G A 1 offset (19, 49)
		RC4G A 1 offset (26, 56)
		RC4G A 1 offset (34, 64)
		RC4G A 1 offset (42, 72)
		RC4G A 1 offset (54, 84) A_PlaySound ("c4/remote", CHAN_WEAPON)
		RC4G A 1 offset (60, 90)
		RC4G A 1 offset (40, 70)
		NULL A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "ReadyFull")
		RC4G B 5 A_FireCustomMissile ("RemoteC4Thrown")
		NULL A 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "FireEndFull")
		goto FireEnd
	FireEnd:
		RC4G B 1 offset (30, 60)
		RC4G B 1 offset (10, 80)
		RC4G B 1 offset (0, 100)
		RC4G B 1 offset (0, 110)
		goto Ready
	FireEndFull:
		RC4G B 1 offset (30, 60)
		RC4G B 1 offset (10, 40)
		RC4G B 1 offset (0, 40)
		RC4G A 1 offset (0, 60)
		RC4G A 1 offset (0, 80)
		RC4G A 1 offset (0, 100)
		RC4G A 1 offset (0, 110)
		goto Ready
	AltFire:
		NULL A 0 A_JumpIfInventory ("InMenu", 1, "Ready")
		NULL A 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "AltFireFull")
		RC4E ABCDEFGH 1
		RC4E I 2
		RC4E J 2 A_PlaySound ("c4/remote2", CHAN_WEAPON)
		RC4E I 4 A_GiveInventory ("C4DetonateCheck", 1)
		RC4E H 6
		RC4E G 1 A_TakeInventory ("C4DetonateCheck", 666)
		RC4E FEDCBA 1
		goto Ready
	AltFireFull:
		RC4D ABCDEFGH 1
		RC4D I 2
		RC4D J 2 A_PlaySound ("c4/remote2", CHAN_WEAPON)
		RC4D I 4 A_GiveInventory ("C4DetonateCheck", 1)
		RC4D H 6
		RC4D G 1 A_TakeInventory ("C4DetonateCheck", 666)
		RC4D FEDCBA 1
		goto ReadyFull
	}
}

actor RemoteC4Thrown {
	PROJECTILE
	-NOGRAVITY
	Radius 6
	Height 8
	Speed 30
	Damage 0
	Scale 0.2
	Translation "16:47=80:111", "168:191=80:111"
	states {
	Spawn:
		RC4T A -1
		stop
	Death:
		RC4T A 35 A_NoGravity // Wait one second and then become armed
		NULL A 0 A_JumpIfInTargetInventory ("IsBlue", 1, "Death.Blue")
		NULL A 0 A_SpawnItemEx ("RemoteC4Armed")
		stop
	Death.Blue:
		NULL A 0 A_SpawnItemEx ("RemoteC4ArmedBlue")
		stop
	}
}



Actor RemoteC4Armed
{
	+NOEXTREMEDEATH
	+NOGRAVITY
	+NOBLOOD
	+SHOOTABLE
	+NOTARGETSWITCH
	Scale 0.2
	Health 10
	Radius 12
	Height 12
	Mass 0x7FFFFFFF
	Speed 0
	Damage 0
	DamageType "Explosion"
	Obituary "%o failed to spot %k's Remote C4!"
	DamageFactor "Fist", 0.0
	DamageFactor "Bullet", 0.0
	DamageFactor "Rifle", 0.0
	DamageFactor "Explosion", 0.0
	DamageFactor "Fire", 0.0
	DamageFactor "Chemical", 0.0
	DamageFactor "Laser", 0.0
	DamageFactor "RazorWire", 0.0
	DamageFactor "ExplosionMech", 0.0
	DamageFactor "Time", 0.0
	DamageFactor "Time", 0.0
	DamageFactor "Artillery", 0.0
	DamageFactor "Mechstomp", 0.0
	DamageFactor "Monster", 0.0
	
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_PlaySound("terrorist/select", CHAN_VOICE)
		NULL A 0 A_GiveInventory("IsRed", 1)
		Goto Idle
	Idle:
		NULL A 0 A_JumpIfInTargetInventory("RemoteC4Ammo", 4, "Death.Disarm")
		NULL A 0 A_JumpIfInTargetInventory("C4DetonateCheck", 1, "Death")
		RC4T A 5
		Loop
	Death:
		NULL A 0 A_NoGravity
		RC4T A 10 A_PlaySound("c4/detonate")
		NULL A 0 A_SpawnItemEx("ExplosionMainLarge")
		NULL A 0 A_PlaySound("weapons/rocklx")
		NULL A 0 A_PlayWeaponSound("weapons/rocklx")
		NULL A 0 ACS_ExecuteAlways(aow_script978, 0, 1)
		NULL A 0 A_Explode(500, 64)
		NULL A 0 A_Explode(400, 196)
		NULL A 0 A_Explode(500, 256)
		NULL A 0 A_Explode(250, 640)
		Goto DeathFinish
	DeathFinish:
		NULL A 0 A_SpawnItemEx("FlameWallDecay", random(-48, 48), random(-48, 48), 4, 0, 0, 0, 0, 0, 160)
		TNT1 A 5
		Stop
	Death.Disarm:
		RC4T A 35 A_PlaySound("c4/disarm")
		RC4T A 1 A_FadeOut(0.05)
		Wait
	}
}

Actor RemoteC4ArmedBlue : RemoteC4Armed
{
	DamageType "Explosion"
	Translation "16:47=192:207", "168:183=192:207", "184:191=240:247"
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_PlaySound("terrorist/select", CHAN_VOICE)
		NULL A 0 A_GiveInventory("IsBlue", 1)
		Goto Idle
	DeathFinish:
		NULL A 0 A_SpawnItemEx("FlameWallDecayBlue", random(-48, 48), random(-48, 48), 4, 0, 0, 0, 0, 0, 160)
		TNT1 A 5
		Stop
	}
}

Actor C4DetonateCheck : Boolean {}
