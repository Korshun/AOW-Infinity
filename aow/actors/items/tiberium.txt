// Tiberium Crystal
Actor TiberiumCrystalGiver : CustomInventory 21407
{
	+AUTOACTIVATE +BRIGHT
	+BLOODLESSIMPACT +NODAMAGETHRUST
	Obituary "%o ventured too close to Tiberium."
	Inventory.respawntics 175 // 5*35
	Inventory.PickupMessage "Picked up a Tiberium Crystal!"
	DamageType "ChemicalSpecial"
	
	States
	{
	Spawn:
		NULL AA 0 Thing_ChangeTID(0, 402)
		TIBE A 35 BRIGHT A_Explode(24, 112)
		Wait
	Pickup:
		NULL A 0 ACS_ExecuteWithResult(aow_script357)
		Stop
	Use:
		NULL A 0 A_GiveInventory("TiberiumCrystal", 1)
		Stop
	HideDoomish:
		TNT1 A 1050 Thing_ChangeTID(0, 0)
		NULL A 0 A_RestoreSpecialPosition
		TNT1 A 1 A_RestoreSpecialDoomThing
		Stop
	}
}

Actor TiberiumCrystalDropped : TiberiumCrystalGiver
{
	States
	{
	Spawn:
		NULL AA 0 Thing_ChangeTID(0, 402)
		NULL A 0 ThrustThing(random(0, 256), random (1, 5), 0, 0)
		NULL A 0 ThrustThingZ(0, 20, 0, 0)
	Idle:
		TIBE AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 35 BRIGHT A_Explode(24, 112)
		NULL A 0 A_SetTranslucent(0.8, 1)
		TIBE A 1 A_FadeOut
		Wait
	}
}

Actor TiberiumCrystal : CustomInventory
{
	+INVBAR
	Inventory.PickupMessage "You aquired yourself some Tiberium."
	Inventory.MaxAmount 1
	Inventory.icon "tibeicon"
	Tag "Tiberium Crystal - bring this to Refinery!"
	States
	{
	Spawn:
		NULL A 0
		Stop
	Use:
		NULL A 0 A_JumpIfInventory("IsMech", 1, "UseMech")
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "UseBlue")
		NULL A 0 A_SpawnItemEx("TiberiumCrystalThrown", 16, 0, 32, 6, 0, 5)
		Stop
	UseBlue:
		NULL A 0 A_SpawnItemEx("TiberiumCrystalThrownBlue", 16, 0, 32, 6, 0, 5)
		Stop
	UseMech:
		NULL A 0 ACS_ExecuteAlways(aow_script697, 0, 255)
		Fail
	}
}

Actor TiberiumCrystalThrown
{
	PROJECTILE
	-NOGRAVITY
	+GHOST +THRUGHOST
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	+BRIGHT
	Obituary "%o \cKwas freaked out by \c-%k\cK's flying Tiberium."
	Radius 16 Height 32
	Speed 10 Damage 0
	Gravity 0.75
	DamageType "Chemical"
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 BRIGHT A_Explode(2, 96)
		NULL A 0 BRIGHT A_SpawnItemEx("ChemicalDeathShot", random(-16, 16), random(-16, 16), random(16, 64), 0, 0, 0.5*random(2, 5), 0, 128, 32)
		TIBE AA 2 BRIGHT A_JumpIf(waterlevel > 1, "Water")
		Loop
	Death:
		NULL A 0 BRIGHT
		NULL A 0 BRIGHT A_SpawnItemEx("TiberiumCrystalSitting", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 4
		Stop
	Water:
		TIBE A 15 BRIGHT A_SetTranslucent(1.0, 0)
	WaterLoop:
		NULL A 0 BRIGHT A_SpawnItemEx("ChemicalDeathShot", random(-16, 16), random(-16, 16), random(16, 64), 0, 0, 0.5*random(2, 5), 0, 128, 32)
		TIBE AAA 1 BRIGHT A_FadeOut(0.05)
		Loop
	}
}

Actor TiberiumCrystalSitting
{
	+BLOODLESSIMPACT +NODAMAGETHRUST
	Obituary "%o was \cDturned green\c- by %k's Tiberium crystal."
	Radius 16 Height 32
	Speed 0 Damage 0
	Mass 0x7FFFFFFF
	DamageType "Chemical"
	ReactionTime 15
	States
	{
	Spawn:
		NULL AA 0 A_SpawnItemEx("TiberiumCrystalFX")
		TIBE AAAAAAAAAAAAAAA 18 BRIGHT A_Explode(24, 128)
		TIBE AAAAAAAAAAAAAAA 17 BRIGHT A_Explode(24, 128)
	Death:
		NULL A 0 A_SetTranslucent(1, 1)
		TIBE AAAAAAAAAAAAAAAAAAAAAAAAA 1 BRIGHT A_FadeOut(0.05)
		Stop
	}
}

Actor TiberiumCrystalFX
{
	+CLIENTSIDEONLY
	ReactionTime 16
	States
	{
	Spawn:
		NULL A 0
		TIBE AAAAAAA 5 BRIGHT A_SpawnItemEx("ChemicalDeathShot", random(-16, 16), random(-16, 16), random(8, 24), 0, 0, 0.5*random(2, 5), 0, 128, 32)
		NULL A 0 A_Countdown
		Loop
	Death:
		TIBE AAAAAAAAAAAAAAAAAAAA 1 BRIGHT A_SpawnItemEx("ChemicalDeathShot", random(-16, 16), random(-16, 16), random(8, 24), 0, 0, 0.5*random(2, 5), 0, 128, 32)
		Stop
	}
}

Actor TiberiumCrystalThrownBlue : TiberiumCrystalThrown
{
	DamageType "Chemical"
	States
	{
	Death:
		NULL A 0 BRIGHT
		NULL A 0 BRIGHT A_SpawnItemEx("TiberiumCrystalSittingBlue", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 4 BRIGHT
		Stop
	}
}

Actor TiberiumCrystalSittingBlue : TiberiumCrystalSitting {DamageType "Chemical"}

Actor TiberiumChemSmoke
{
	+NOSECTOR +NOCLIP +NOINTERACTION +CLIENTSIDEONLY
	States
	{
	Spawn:
		NULL A 0
		TNT1 AAAAAAAA 4 A_SpawnItemEx("ChemicalDeathShot", random(-16, 16), random(-16, 16), random(0, 16), 0, 0, 0.5*random(2, 5), 0, 128, 128)
		Stop
	}
}

Actor TiberiumChemSmokePlayer
{
	+NOSECTOR +NOCLIP +NOINTERACTION +CLIENTSIDEONLY
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("ChemicalDeathShot", random(-16, -1), random(-16, 16), random(32, 56), 0, 0, 0.5*random(2, 5), 0, 128)
		Stop
	}
}

// AOW21-specific tiberium
Actor FactoryTiberium : TiberiumCrystalGiver
{
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("TiberiumMapMarker")
		Goto Super::Spawn+2
	}
}

Actor TiberiumMapMarker : MapMarker 17000
{
	+CLIENTSIDEONLY
	Scale 0.175
	States
	{
	Spawn:
		AMRK B -1
		Stop
	}
}

Actor TiberiumCrystalSound 21408
{
	+CLIENTSIDEONLY +NOINTERACTION
	States
	{
	Spawn:
		NULL AA 0 A_PlaySound("tiberium/loop1", 0, 1.0, 1)
		TNT1 A -1
		Stop
	}
}