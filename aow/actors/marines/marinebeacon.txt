// MARINE BEACON
Actor BlueRandomMarine : RandomSpawner {
	DropItem "BlueRocketSoldier"
	DropItem "BlueSuperShotgunner"
	DropItem "BlueTiberiumRifleman"
	DropItem "BluePlasmaGunner"
	DropItem "BlueFlamerDude"
}

Actor RedRandomMarine : RandomSpawner {
	DropItem "RedRocketSoldier"
	DropItem "RedSuperShotgunner"
	DropItem "RedTiberiumRifleman"
	DropItem "RedPlasmaGunner"
	DropItem "RedFlamerDude"
}

// no count item for this one because it cannot be refilled in any form
Actor MarineBeaconItem : InventoryItem 29016 {
	Inventory.icon "mbcnicon"
	Inventory.PickupMessage "Picked up a marine beacon!"
	States
	{
	Spawn:
		MBCN B -1
		Stop
	Use:
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Failure")
		NULL A 0 A_JumpIfInventory("IsMech", 1, "Use.Mech")
		NULL A 0 A_JumpIfInventory("StealthMode", 1, "Destealth")
		NULL A 0 A_JumpIfInventory("PowerBerserk", 1, "Failure")
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "Use.Blue")
		NULL A 0 A_FireCustomMissile("MarineBeaconThrown", 0, 0)
		Stop
	Use.Blue:
		NULL A 0 A_FireCustomMissile("MarineBeaconThrownBlue", 0, 0)
		Stop
	}
}

Actor MarineBeaconThrown : HealingRadiusThrown {
	States
	{
	Spawn:
		MBCN B -1
		Stop
	Death:
		NULL A 0 A_SpawnItemEx("MarineBeacon", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		NULL A 5
		Stop
	}
}

Actor MarineBeaconThrownBlue : HealingRadiusThrown {
	Translation "32:39=200:207", "168:183=192:207", "184:191=240:247"
	States
	{
	Spawn:
		MBCN B -1
		Stop
	Death:
		NULL A 0 A_SpawnItemEx("MarineBeaconBlue", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
		NULL A 5
		Stop
	}
}

Actor MarineBeacon {
	States
	{
	Spawn:
		MBCN B 35
		MBCN A 8 BRIGHT
		MBCN B 8
		MBCN A 8 BRIGHT
		MBCN B 8
		MBCN A 8 BRIGHT
		MBCN B 8
		Goto Idle
	Idle:
		MBCN A 8 BRIGHT ACS_ExecuteAlways(aow_script951, 0, 3, 1)
		MBCN B 8
		Loop
	Done:
		MBCN B 105
		MBCN B 2 A_FadeOut
		Wait
	}
}

// for some reason inheritance breaks this o_O
Actor MarineBeaconBlue {
	States
	{
	Spawn:
		MBCN B 105
		MBCN A 8 BRIGHT
		MBCN B 8
		MBCN A 8 BRIGHT
		MBCN B 8
		MBCN A 8 BRIGHT
		MBCN B 8
		Goto Idle
	Idle:
		MBCN A 8 BRIGHT ACS_ExecuteAlways(aow_script951, 0, 3, 0)
		MBCN B 8
		Loop
	Done:
		MBCN B 105
		MBCN B 2 A_FadeOut
		Wait
	}
}