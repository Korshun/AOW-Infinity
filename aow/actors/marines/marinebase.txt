Actor IsAIMarine : Boolean {}
Actor MBase : PlayerPawn	//Basic unarmed marine.
{

	Speed 0					//ThrustThing and A_Recoil instead.
	MaxDropOffHeight 63		//Gives the marines a smaller chance of falling into pits. 63, not 64.
	PainSound "*pain"

//-----------------------------------------------------------------------


	+QUICKTORETALIATE
	+NODROPOFF				//In conjunction with MaxDropOffHeight.
	+ISMONSTER				//Needed for the marines to take any projectile Damage in invasion mode.
	-COUNTKILL				//In conjunction with +ISMONSTER.
	+NEVERFAST				//Same as above.
	//+NOTIMEFREEZE
	+LOOKALLAROUND
	-TELESTOMP
	DeathSound "generic/death"


//AOW Base DamageFactoring-----------------------------------------------------------------------

	DamageFactor "Fist", 1.0
	DamageFactor "Fist", 1.0
	DamageFactor "Bullet", 1.0
	DamageFactor "Bullet", 1.0
	DamageFactor "Rifle", 1.0
	DamageFactor "Rifle", 1.0
	DamageFactor "Explosion", 0.3
	DamageFactor "Explosion", 0.3
	DamageFactor "Terrorist", 2.0
	DamageFactor "Suicide", 1.0
	DamageFactor "Fire", 1.0
	DamageFactor "Fire", 1.0
	DamageFactor "Chemical", 2.0
	DamageFactor "Chemical", 2.0
	DamageFactor "ChemicalSpecial", 1.0
	DamageFactor "Laser", 1.0
	DamageFactor "Laser", 1.0
	DamageFactor "RazorWire", 1.0
	DamageFactor "RazorWire", 1.0
	DamageFactor "ExplosionMech", 0.1
	DamageFactor "ExplosionMech", 0.1
	DamageFactor "TimeRed", 1.0
	DamageFactor "TimeBlue", 1.0
	DamageFactor "Repair", 0.0
	DamageFactor "Disarm", 0.0 
	DamageFactor "Deconstruction", 0.0
	DamageFactor "Artillery", 0.3
	DamageFactor "Artillery", 0.3
	DamageFactor "Mechstomp", 1.0
	DamageFactor "Monster", 1.0

//-----------------------------------------------------------------------


	States //Sorry about the ridiculously long state names.
	{

//-----------------------------------------------------------------------
//
//					Spawn and idle states.
//
//-----------------------------------------------------------------------


	Spawn:
		NULL A 0 //Dummy frame needed.
		NULL A 0 A_ChangeFlag("Friendly", 0)//Execute ACS script(s) here.
		NULL A 0 A_Jump(256, "EnablePushablePreIdle")
		Stop

	EnablePushablePreIdle: //Allows you to push marines if they are in your way while idling.
		NULL A 0 A_ChangeFlag("PUSHABLE", 1)
		NULL A 0 A_Jump(256, "Idle")
		Stop

	Idle: //Check if pushed, then look for enemies. If Player out of sight, begin to wander. If underwater, sink down. 
		NULL A 0 A_JumpIf(momx != 0 || momy != 0, "MomentumCheck")

		PLAY A 1 A_LookEx(0, 0, 0, 1200, 360, "DisablePushablePreAlternateChase")
		NULL A 0 A_CheckSight("DisablePushablePreAlternateWander")

		NULL A 0 A_JumpIf(z - floorz == 0, 4) //If non-deep water or at bottom, don't sink.
		NULL A 0 A_JumpIf(waterlevel == 1, "SinkDown1")
		NULL A 0 A_JumpIf(waterlevel == 2, "SinkDown2")
		NULL A 0 A_JumpIf(waterlevel == 3, "SinkDown3")
		NULL A 0
		NULL A 0 A_Jump(256, "Idle")
		Stop


//-----------------------------------------------------------------------


	MomentumCheck:
		NULL A 0 A_JumpIf(momx == 0 && momy == 0, "Idle")
		PLAY A 4
		NULL A 0 A_JumpIf(momx == 0 && momy == 0, "Idle")
		PLAY B 4
		NULL A 0 A_JumpIf(momx == 0 && momy == 0, "Idle")
		PLAY C 4
		NULL A 0 A_JumpIf(momx == 0 && momy == 0, "Idle")
		PLAY D 4
		NULL A 0 A_Jump(256, "CheckIfAnyTargetsWhilePushed")
		Stop


	CheckIfAnyTargetsWhilePushed: //Look for potential targets while being pushed.
		NULL A 0 A_LookEx(0, 0, 0, 1200, 0, "DisablePushablePreAlternateChase")
		NULL A 0 A_Jump(256, "Idle")
		Stop


//-----------------------------------------------------------------------


	SinkDown1: //Ankle deep.
		NULL A 0 ThrustThingZ(0, 2, 1, 1)
		NULL A 0 A_Jump(256, "Idle")
		Stop

	SinkDown2: //Waist deep.
		NULL A 0 ThrustThingZ(0, 1, 1, 1)
		NULL A 0 A_Jump(256, "Idle")
		Stop
	
	SinkDown3: //Cmpletely underwater. Slower sinking.
		NULL A 0 ThrustThingZ(0, 2, 1, 0)
		NULL A 0 A_Jump(256, "Idle")
		Stop








//-----------------------------------------------------------------------
//
//	Chase, check target, check if stuck/underwater and back off states.
//
//-----------------------------------------------------------------------


	See: //Simply for A_SpidRefire. Goes to "ResetTimers".
		NULL A 0 //Dummy frame needed.
		NULL A 0 A_Jump(256, "ResetTimers")
		Stop

	DisablePushablePreAlternateChase:
		NULL A 0 A_ChangeFlag("PUSHABLE", 0)
		NULL A 0 A_Jump(256, "AlternateChase")
		Stop

	AlternateChase:
		NULL A 0 A_Jump(85, "ChaseForwardAndLeft", "ChaseForwardAndRight") //1/3 chance of straferunning.
		NULL A 0 A_Jump(256, "ChaseForward")
		Stop


	ChaseForward:
		PLAY A 4 A_Recoil(-6.125)
		PLAY B 4 A_Recoil(-6.125)
		PLAY C 4 A_Recoil(-6.125)
		PLAY D 4 A_Recoil(-6.125)
		NULL A 0 A_Jump(256, "TurnAndAcquireTargetWhileChase")
		Stop

	ChaseForwardAndLeft: //Straferunning 28% faster.
		NULL A 0 ThrustThing(angle*256/360-38.7, 7, 0, 0)
		PLAY A 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360-38.7, 7, 0, 0)
		PLAY B 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360-38.7, 7, 0, 0)
		PLAY C 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360-38.7, 7, 0, 0)
		PLAY D 4 A_Recoil(-0.84)
		NULL A 0 A_Jump(256, "TurnAndAcquireTargetWhileChase")
		Stop

	ChaseForwardAndRight:
		NULL A 0 ThrustThing(angle*256/360+38.7, 7, 0, 0)
		PLAY A 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360+38.7, 7, 0, 0)
		PLAY B 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360+38.7, 7, 0, 0)
		PLAY C 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360+38.7, 7, 0, 0)
		PLAY D 4 A_Recoil(-0.84)
		NULL A 0 A_Jump(256, "TurnAndAcquireTargetWhileChase")
		Stop


	TurnAndAcquireTargetWhileChase: //Allows for marines to turn and acquire targets.
		NULL AAAAAAAAAAAAAAAA 0 A_Chase("", "")
		NULL A 0 A_Jump(256, "CheckIfTargetInLOS")
		Stop

	CheckIfTargetInLOS: //Every PLAY A frames allows for attack or backing off. This is to make the marines animate properly.
		NULL A 0 A_JumpIfTargetInLOS("TargetInLOS")
		NULL A 0 A_Jump(256, "CheckIfStuck")
		Stop

	TargetInLOS:
		NULL A 0 A_JumpIf(Health <= 39, 3) //If 39 Health or lower, get more aggressive.
		NULL A 0 A_JumpIfCloser(256, "AlternateBackOff") //The threshold of when to back off.
		NULL A 0 A_Jump(random(0, 256), "CheckIfStuck") //Makes the marines less inhuman.
		NULL A 0 A_JumpIfCloser(4096, "Missile") //Max target range.
		NULL A 0 A_Jump(256, "CheckIfStuck")
		Stop


//-----------------------------------------------------------------------


	CheckIfStuck: //This state act as a "dynamic threshold timer". Checks if there is any momentum along the x- and y- axis. If false, add to the threshold timer. If true, clear the threshold timer and check if underwater instead.
		NULL A 0 A_JumpIf(momx != 0 || momy != 0, "NotStuck")
		NULL A 0 A_Jump(256, "Stuck")
		Stop

	NotStuck:
		NULL A 0 A_JumpIfInventory("MoveBackwardsThreshold", 0, "CheckIfUnderwater")
		NULL A 0 A_TakeInventory("MoveBackwardsThreshold", 1)
		NULL A 0 A_Jump(256, "CheckIfUnderwater")
		Stop

	Stuck:
		NULL A 0 A_JumpIf(momx == 0 && momy == 0, "AddToMoveBackWardsThresholdTimer")
		NULL A 0 A_Jump(256, "CheckIfUnderwater")
		Stop


//-----------------------------------------------------------------------


	CheckIfUnderWater:
		NULL A 0 A_JumpIf(waterlevel == 1, "Swim1")
		NULL A 0 A_JumpIf(waterlevel == 2, "Swim2")
		NULL A 0 A_JumpIf(waterlevel == 3, "Swim3")
		NULL A 0 A_Jump(256, "AddToTimers") //If not underwater, add to the regular timers instead.
		Stop


	Swim1:
		NULL A 0 A_JumpIf(z - floorz == 0, 2) //If non-deep water, don't swim.
		NULL A 0 ThrustThingZ(0, 8, 0, 1) //Simulates jumping/swimming in water.
		NULL A 0
		NULL A 0 A_Jump(256, "AddToTimers")
		Stop

	Swim2:
		NULL A 0 ThrustThingZ(0, 16, 0, 1)
		NULL A 0 A_Jump(256, "AddToTimers")
		Stop

	Swim3:
		NULL A 0 ThrustThingZ(0, 32, 0, 1)
		NULL A 0 A_Jump(256, "AddToTimers")
		Stop


//-----------------------------------------------------------------------


	AddToMoveBackWardsThresholdTimer:
		NULL A 0 A_GiveInventory("MoveBackwardsThreshold", 1)
		NULL A 0 A_Jump(256, "CheckMoveBackWardsThresholdTimer")
		Stop

	CheckMoveBackWardsThresholdTimer:
		NULL A 0 A_JumpIfInventory("MoveBackwardsThreshold", 1, "ClearMoveBackWardsThresholdTimer")
		NULL A 0 A_Jump(256, "CheckIfUnderwater")
		Stop

	ClearMoveBackWardsThresholdTimer:
		NULL A 0 A_TakeInventory("MoveBackwardsThreshold", 1)
		NULL A 0 A_Jump(256, "MoveBackwardsAlternate")
		Stop


//-----------------------------------------------------------------------


	MoveBackwardsAlternate: //Same "A_Recoil" and "ThrustThing" values as in "BackOff" states, but with jumps to a different state.
		NULL A 0 A_Jump(256, "MoveBackwards", "MoveBackwardsAndLeft", "MoveBackwardsAndRight")
		Stop


	MoveBackwards:
		PLAY A 4 A_Recoil(6.125)
		PLAY B 4 A_Recoil(6.125)
		PLAY C 4 A_Recoil(6.125)
		PLAY D 4 A_Recoil(6.125)
		NULL A 0 A_Jump(random(128, 256), 2)
		NULL A 0 A_Jump(256, "MoveBackwards")
		Stop
		NULL A 0
		NULL A 0 A_Jump(256, "AddToTimers")
		Stop

	MoveBackwardsAndLeft:
		PLAY A 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		PLAY B 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		PLAY C 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		PLAY D 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		NULL A 0 A_Jump(random(128, 256), 2)
		NULL A 0 A_Jump(256, "MoveBackwardsAndLeft")
		Stop
		NULL A 0
		NULL A 0 A_Jump(256, "AddToTimers")
		Stop

	MoveBackwardsAndRight:
		PLAY A 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		PLAY B 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		PLAY C 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		PLAY D 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		NULL A 0 A_Jump(random(128, 256), 2)
		NULL A 0 A_Jump(256, "MoveBackwardsAndRight")
		Stop
		NULL A 0
		NULL A 0 A_Jump(256, "AddToTimers")
		Stop








//-----------------------------------------------------------------------
//
//	Wander, check target, check if stuck/underwater and back off states.
//
//-----------------------------------------------------------------------


	DisablePushablePreAlternateWander: //Nearly identical to chase states.
		NULL A 0 A_ChangeFlag("PUSHABLE", 0)
		NULL A 0 A_Jump(256, "AlternateWander")
		Stop

	AlternateWander:
		NULL A 0 A_Jump(85, "WanderForwardAndLeft", "WanderForwardAndRight")
		NULL A 0 A_Jump(256, "WanderForward")
		Stop

	WanderForward:
		PLAY A 4 A_Recoil(-6.125)
		PLAY B 4 A_Recoil(-6.125)
		PLAY C 4 A_Recoil(-6.125)
		PLAY D 4 A_Recoil(-6.125)
		NULL A 0 A_Jump(256, "TurnAndAcquireTargetWhileWander")
		Stop

	WanderForwardAndLeft:
		NULL A 0 ThrustThing(angle*256/360-38.7, 7, 0, 0)
		PLAY A 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360-38.7, 7, 0, 0)
		PLAY B 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360-38.7, 7, 0, 0)
		PLAY C 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360-38.7, 7, 0, 0)
		PLAY D 4 A_Recoil(-0.84)
		NULL A 0 A_Jump(256, "TurnAndAcquireTargetWhileWander")
		Stop

	WanderForwardAndRight:
		NULL A 0 ThrustThing(angle*256/360+38.7, 7, 0, 0)
		PLAY A 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360+38.7, 7, 0, 0)
		PLAY B 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360+38.7, 7, 0, 0)
		PLAY C 4 A_Recoil(-0.84)
		NULL A 0 ThrustThing(angle*256/360+38.7, 7, 0, 0)
		PLAY D 4 A_Recoil(-0.84)
		NULL A 0 A_Jump(256, "TurnAndAcquireTargetWhileWander")
		Stop


	TurnAndAcquireTargetWhileWander:
		NULL AAAAAAAAAAAAAAAA 0 A_Wander
		NULL A 0 A_LookEx(0, 0, 0, 1200, 0, "CheckIfTargetInLOS")
		NULL A 0 A_Jump(256, "CheckIfStuckWhileWander")
		Stop


//-----------------------------------------------------------------------


	CheckIfStuckWhileWander:
		NULL A 0 A_JumpIf(momx != 0 || momy != 0, "NotStuckWhileWander")
		NULL A 0 A_Jump(256, "StuckWhileWander")
		Stop

	NotStuckWhileWander:
		NULL A 0 A_JumpIfInventory("MoveBackwardsThreshold", 0, "CheckIfUnderwaterWhileWander")
		NULL A 0 A_TakeInventory("MoveBackwardsThreshold", 1)
		NULL A 0 A_Jump(256, "CheckIfUnderwaterWhileWander")
		Stop

	StuckWhileWander:
		NULL A 0 A_JumpIf(momx == 0 && momy == 0, "AddToMoveBackWardsThresholdTimerWhileWander")
		NULL A 0 A_Jump(256, "CheckIfUnderwaterWhileWander")
		Stop


//-----------------------------------------------------------------------


	CheckIfUnderwaterWhileWander:
		NULL A 0 A_JumpIf(waterlevel == 1, "SwimWhileWander1")
		NULL A 0 A_JumpIf(waterlevel == 2, "SwimWhileWander2")
		NULL A 0 A_JumpIf(waterlevel == 3, "SwimWhileWander3")
		NULL A 0 A_Jump(256, "AlternateWander")
		Stop


	SwimWhileWander1:
		NULL A 0 A_JumpIf(z - floorz == 0, 2)
		NULL A 0 ThrustThingZ(0, 8, 0, 1)
		NULL A 0
		NULL A 0 A_Jump(256, "AlternateWander")
		Stop

	SwimWhileWander2:
		NULL A 0 ThrustThingZ(0, 16, 0, 1)
		NULL A 0 A_Jump(256, "AlternateWander")
		Stop

	SwimWhileWander3:
		NULL A 0 ThrustThingZ(0, 32, 0, 1)
		NULL A 0 A_Jump(256, "AlternateWander")
		Stop


//-----------------------------------------------------------------------


	AddToMoveBackWardsThresholdTimerWhileWander:
		NULL A 0 A_GiveInventory("MoveBackwardsThreshold", 1)
		NULL A 0 A_Jump(256, "CheckMoveBackWardsThresholdTimerWhileWander")
		Stop

	CheckMoveBackWardsThresholdTimerWhileWander:
		NULL A 0 A_JumpIfInventory("MoveBackwardsThreshold", 1, "ClearMoveBackWardsThresholdTimerWhileWander")
		NULL A 0 A_Jump(256, "CheckIfUnderwaterWhileWander")
		Stop

	ClearMoveBackWardsThresholdTimerWhileWander:
		NULL A 0 A_TakeInventory("MoveBackwardsThreshold", 1)
		NULL A 0 A_Jump(256, "MoveBackwardsAlternateWhileWander")
		Stop


//-----------------------------------------------------------------------


	MoveBackwardsAlternateWhileWander:
		NULL A 0 A_Jump(256, "MoveBackwardsWhileWander", "MoveBackwardsAndLeftWhileWander", "MoveBackwardsAndRightWhileWander")
		Stop


	MoveBackwardsWhileWander:
		PLAY A 4 A_Recoil(6.125)
		PLAY B 4 A_Recoil(6.125)
		PLAY C 4 A_Recoil(6.125)
		PLAY D 4 A_Recoil(6.125)
		NULL A 0 A_Jump(random(128, 256), 2)
		NULL A 0 A_Jump(256, "MoveBackwardsWhileWander")
		Stop
		NULL A 0
		NULL A 0 A_Jump(256, "AlternateWander")
		Stop

	MoveBackwardsAndLeftWhileWander:
		PLAY A 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		PLAY B 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		PLAY C 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		PLAY D 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		NULL A 0 A_Jump(random(128, 256), 2)
		NULL A 0 A_Jump(256, "MoveBackwardsAndLeftWhileWander")
		Stop
		NULL A 0
		NULL A 0 A_Jump(256, "AlternateWander")
		Stop

	MoveBackwardsAndRightWhileWander:
		PLAY A 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		PLAY B 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		PLAY C 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		PLAY D 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		NULL A 0 A_Jump(random(128, 256), 2)
		NULL A 0 A_Jump(256, "MoveBackwardsAndRightWhileWander")
		Stop
		NULL A 0
		NULL A 0 A_Jump(256, "AlternateWander")
		Stop








//-----------------------------------------------------------------------
//
//			Back off from target and missile states.
//
//-----------------------------------------------------------------------


	AlternateBackOff: //Same formula as in the chase states, but only backwards movement and different jumps.
		NULL A 0 A_Jump(256, "BackOffBackwards", "BackOffBackwardsAndLeft", "BackOffBackwardsAndRight")
		Stop


	BackOffBackwards:
		NULL A 0 A_FaceTarget
		PLAY A 4 A_Recoil(6.125)
		PLAY B 4 A_Recoil(6.125)
		PLAY C 4 A_Recoil(6.125)
		PLAY D 4 A_Recoil(6.125)
		NULL A 0 A_JumpIf(Health <= 50, "Missile") //A bit desperate.
		NULL A 0 A_Jump(random(128, 256), 2)
		NULL A 0 A_Jump(256, "BackOffBackwards")
		Stop
		NULL A 0 A_SpidRefire
		NULL A 0 A_Jump(256, "Missile")
		Stop

	BackOffBackwardsAndLeft:
		NULL A 0 A_FaceTarget
		PLAY A 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		PLAY B 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		PLAY C 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		PLAY D 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		NULL A 0 A_JumpIf(Health <= 50, "Missile")
		NULL A 0 A_Jump(random(128, 256), 2)
		NULL A 0 A_Jump(256, "BackOffBackwardsAndLeft")
		Stop
		NULL A 0 A_SpidRefire
		NULL A 0 A_Jump(256, "Missile")
		Stop

	BackOffBackwardsAndRight:
		NULL A 0 A_FaceTarget
		PLAY A 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		PLAY B 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		PLAY C 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		PLAY D 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		NULL A 0 A_JumpIf(Health <= 50, "Missile")
		NULL A 0 A_Jump(random(128, 256), 2)
		NULL A 0 A_Jump(256, "BackOffBackwardsAndRight")
		Stop
		NULL A 0 A_SpidRefire
		NULL A 0 A_Jump(256, "Missile")
		Stop


	Missile:
		NULL A 0 //Dummy frame needed.
		NULL A 0 A_Jump(256, "ResetTimers")
		Stop








//-----------------------------------------------------------------------
//
//				Pain, death and respawn states.
//
//-----------------------------------------------------------------------


	Pain.FireSpecial:
		NULL A 0 ACS_ExecuteAlways(aow_script320, 0, 1)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 2)
		NULL A 0 A_Jump(256, "DisablePushablePreAlternatePainBackOff")
	Pain.Fire:
	Pain.Fire:
		NULL A 0 ACS_ExecuteAlways(aow_script320, 0, 1)
		NULL A 0 ACS_ExecuteAlways(aow_script360, 0)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 2)
		NULL A 0 A_Jump(256, "DisablePushablePreAlternatePainBackOff")
	Pain.ChemicalSpecial:
		NULL A 0 ACS_ExecuteAlways(aow_script320, 0, 2)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 2)
		NULL A 0 A_Jump(256, "DisablePushablePreAlternatePainBackOff")
	Pain.Chemical:
	Pain.Chemical:
		NULL A 0 ACS_ExecuteAlways(aow_script320, 0, 2)
		NULL A 0 ACS_ExecuteAlways(aow_script361, 0)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 2)
		NULL A 0 A_Jump(256, "DisablePushablePreAlternatePainBackOff")

	Pain:
		NULL A 0
		NULL A 0 A_Jump(256, "DisablePushablePreAlternatePainBackOff")
		Stop
		
	DisablePushablePreAlternatePainBackOff: //If woken up by getting hurt, clear the +PUSHABLE flag.
		NULL A 0 A_ChangeFlag("PUSHABLE", 0)
		NULL A 0 A_Jump(256, "AlternatePainBackOff")
		Stop	


//-----------------------------------------------------------------------


	AlternatePainBackOff:
		NULL A 0 A_Jump(256, "PainBackOffBackwards", "PainBackOffBackwardsAndLeft", "PainBackOffBackwardsAndRight")
		Stop


	PainBackOffBackwards:
		NULL A 0 A_FaceTarget
		PLAY G 4 A_Recoil(6.125)
		NULL A 0 A_Pain
		PLAY G 4 A_Recoil(6.125)
		NULL A 0 A_Jump(256, "CheckIfTargetInLOS") //Try to attack right back if possible.
		Stop

	PainBackOffBackwardsAndLeft:
		NULL A 0 A_FaceTarget
		PLAY G 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		NULL A 0 A_Pain
		PLAY G 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360+166.7, 7, 0, 0)
		NULL A 0 A_Jump(256, "CheckIfTargetInLOS")
		Stop

	PainBackOffBackwardsAndRight:
		NULL A 0 A_FaceTarget
		PLAY G 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		NULL A 0 A_Pain
		PLAY G 4 A_Recoil(0.84)
		NULL A 0 ThrustThing(angle*256/360-166.7, 7, 0, 0)
		NULL A 0 A_Jump(256, "CheckIfTargetInLOS")
		Stop


//-----------------------------------------------------------------------


	Death:
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 3)
		PLAY H 10
		PLAY I 10 A_PlayerScream
		PLAY J 10 A_NoBlocking
		PLAY KLM 10
		PLAY N -1
		Stop

	XDeath:
		PLAY O 4
		NULL A 0 A_NoBlocking
		NULL AAA 0 A_SpawnItemEx("Credits1", random(-4, 4), random(-4, 4), random(32, 48), random(-4, 4), random(-4, 4), random(4, 8), 0, 0, 128)
		NULL A 0 A_SpawnItemEx("PowerupCrate", random(-4, 4), random(-4, 4), random(32, 48), random(2, 4), random(-2, 2), random(2, 4), 0, 0, 230)
		NULL A 0 A_TakeInventory("Ingame", 1)
		NULL A 0 A_Scream
		NULL A 0 A_PlaySound("misc/gib", 7)
		NULL A 0 A_PlaySound("misc/gib", 6)
		PLAY P 4 A_SpawnItemEx("MarineGoreExplosion")
		PLAY Q 4 A_SpawnItemEx("MarineGoreExplosion2")
		PLAY R 4 A_SpawnItemEx("MarineGoreExplosion3")
		PLAY STU 4
		PLAY V -1
		Stop

	Death.Explosion:
	Death.Explosion:
		PLAY H 1
		NULL A 0 A_Jump(128, "Death")
		Goto XDeath

	Death.FireSpecial:
	Death.Fire:
	Death.Fire:
		// Stop all sound channels
		NULL A 0 A_StopSound(0) NULL A 0 A_StopSound(4)
		NULL A 0 A_StopSound(1) NULL A 0 A_StopSound(5)
		NULL A 0 A_StopSound(2) NULL A 0 A_StopSound(6)
		NULL A 0 A_StopSound(3) NULL A 0 A_StopSound(7)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 3, 0, 0)
		NULL A 0 A_SpawnItemEx("BurningCorpse", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
		NULL A 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0, 0)
		Stop

	Death.ChemicalSpecial:
	Death.Chemical:
	Death.Chemical:
		// Stop all sound channels
		NULL A 0 A_StopSound(0) NULL A 0 A_StopSound(4)
		NULL A 0 A_StopSound(1) NULL A 0 A_StopSound(5)
		NULL A 0 A_StopSound(2) NULL A 0 A_StopSound(6)
		NULL A 0 A_StopSound(3) NULL A 0 A_StopSound(7)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 3, 0, 0)
		NULL A 0 A_SpawnItemEx("ChemicalCorpse", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
		NULL A 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0, 0)
		Stop

	DeathWaitPreTryToRespawn:
		PLAY N 12
		NULL A 0 A_Jump(random(0, 256), "TryToRespawn")
		NULL A 0 A_Jump(256, "DeathWaitPreTryToRespawn")
		Stop

	XDeathWaitPreTryToRespawn:
		PLAY W 12
		NULL A 0 A_Jump(random(0, 256), "TryToRespawn")
		NULL A 0 A_Jump(256, "XDeathWaitPreTryToRespawn")
		Stop


	TryToRespawn: //Try to to respawn every 2 seconds.
		NULL A 0 A_Respawn(1 | 4)
		TNT1 A 70
		NULL A 0 A_Jump(256, "TryToRespawn")
		Stop








//-----------------------------------------------------------------------
//
//						Timer states.
//
//-----------------------------------------------------------------------


	AddToTimers:
		NULL A 0 A_GiveInventory("ClearTarget", 1)
		NULL A 0 A_CheckSight("AddToNoPlayerInSightTimer") //No point in adding a counter if the Player has been out of sight for only a few frames.
		
		NULL A 0 A_JumpIfInventory("NoPlayerInSight", 0, "CheckTimers")
		NULL A 0 A_TakeInventory("NoPlayerInSight", 128) //If Player in sight, clear this timer. Otherwise, the marine will sometimes suddenly Stop and start idling when you go around a corner.
		NULL A 0 A_Jump(256, "CheckTimers")
		Stop
	//These two act as one state.
	AddToNoPlayerInSightTimer:
		NULL A 0 A_GiveInventory("NoPlayerInSight", 1)
		NULL A 0 A_Jump(256, "CheckTimers")
		Stop

	CheckTimers:
		NULL A 0 A_JumpIfInventory("ClearTarget", 1, "ClearTarget")
		NULL A 0 A_JumpIfInventory("NoPlayerInSight", 128, "CheckIfPlayerInSight")
		NULL A 0 A_Jump(256, "AlternateChase")
		Stop


	ClearTarget:
		NULL A 0 A_TakeInventory("ClearTarget", 1)
		NULL A 0 A_ClearTarget
		NULL A 0 A_Jump(256, "CheckTimers") //In case both of the timers have been reached, check again so that the other one will reset as well.
		Stop

	CheckIfPlayerInSight:
		NULL A 0 A_TakeInventory("NoPlayerInSight", 128)
		NULL A 0 A_CheckSight("EnablePushablePreIdle")
		NULL A 0 A_Jump(256, "CheckTimers")
		Stop


//-----------------------------------------------------------------------


	ResetTimers: //This is ONLY connected to "See", "Missile" and "Pain" states. Reset timers so that the marines don't all of sudden decide to idle in the middle of a battle.
		NULL A 0 A_JumpIfInventory("ClearTarget", 0, 4)
		NULL A 0 A_TakeInventory("ClearTarget", 2)
		NULL A 0 A_JumpIfInventory("NoPlayerInSight", 0, 2)
		NULL A 0 A_TakeInventory("NoPlayerInSight", 128)
		NULL A 0
		NULL A 0 A_Jump(256, "AlternateChase")
		Stop
	ClearTargetBlue:
		NULL A 0 A_ClearTarget
		Goto See
	}
}