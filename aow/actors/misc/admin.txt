// Admin pistol
Actor AdminPistol : AOWWeapon {
	+NOAUTOFIRE
	+NOAUTOAIM
	Inventory.PickupMessage "If you see this message, you are \cgHACKING."
	Obituary "%o was smitten by %k's administrative power."
	Weapon.SelectionOrder 85
	Weapon.PreferredSkin "MarineAdmin"
	States
	{
	Spawn:
		NULL A 0 
		Stop
	Ready: 
		GPIS A 1 A_WeaponReady
		Wait
	Deselect:
		TNT1 A 1 A_Lower 
		Loop
	Select: 
		GPIS A 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("AdminPistolRotation", 2, "Fire.Deconstruction")
		NULL A 0 A_JumpIfInventory("AdminPistolRotation", 1, "Fire.Repair")
		Goto Fire.Normal
	Fire.Normal:
		NULL A 0 A_GunFlash
		NULL A 0 A_PlayWeaponSound("pistol/fire")
		NULL A 0 A_FireBullets(0, 0, 1, 10000, "ExplosionPuff")
		NULL A 0 A_FireBullets(0, 0, 1, 100000, "HandgunPuff")
		NULL A 0 A_FireBullets(0, 0, 1, 10000, "ExplosionPuff")
		NULL A 0 A_FireBullets(0, 0, 1, 100000, "HandgunPuff")
		GPIS C 2 
		NULL A 0 A_GunFlash
		NULL A 0 A_PlayWeaponSound("pistol/fire")
		NULL A 0 A_FireBullets(0, 0, 1, 10000, "ExplosionPuff")
		NULL A 0
		NULL A 0 A_FireBullets(0, 0, 1, 100000, "HandgunPuff")
		GPIS B 2
		NULL A 0 A_FireBullets(0, 0, 1, 10000, "ExplosionPuff")
		NULL A 0
		NULL A 0 A_FireBullets(0, 0, 1, 100000, "HandgunPuff")
		GPIS C 2
		NULL A 0 A_Refire
		Goto Ready
	Fire.Repair:
		NULL A 0 A_GunFlash
		NULL A 0 A_PlayWeaponSound("repairgun/fire")
		NULL A 0 A_FireBullets(0.5, 0.5, 2, 0, "RepairDisarmPuff") // visual puff
		NULL A 0 A_FireBullets(0.5, 0.5, 2, 0, "MedicPuff") // visual puff
		NULL A 0 ACS_ExecuteAlways(aow_script650, 0, 500, 0) // repair+heal
		GPIS A 2 BRIGHT A_FireBullets(0, 0, 2, 500, "DisarmPuff") // disarm
		NULL A 0 A_GunFlash
		NULL A 0 A_PlayWeaponSound("repairgun/fire")
		NULL A 0 A_FireBullets(0.5, 0.5, 2, 0, "RepairDisarmPuff") // visual puff
		NULL A 0 A_FireBullets(0.5, 0.5, 2, 0, "MedicPuff") // visual puff
		NULL A 0 ACS_ExecuteAlways(aow_script650, 0, 500, 0) // repair+heal
		GPIS B 2 BRIGHT A_FireBullets(0, 0, 2, 500, "DisarmPuff") // disarm
		NULL A 0 A_ReFire
		Goto Ready
	Fire.Deconstruction:
		NULL A 0 A_GunFlash
		NULL A 0 A_PlayWeaponSound("repairgun/fire")
		GPIS A 1 A_FireBullets(0, 0, 2, 500, "DeconstructionPuff")
		GPIS B 1 A_FireBullets(0, 0, 2, 500, "DeconstructionPuff")
		NULL A 0 A_GunFlash
		NULL A 0 A_PlayWeaponSound("repairgun/fire")
		GPIS A 1 A_FireBullets(0, 0, 2, 500, "DeconstructionPuff")
		GPIS B 1 A_FireBullets(0, 0, 2, 500, "DeconstructionPuff")
		NULL A 0 A_ReFire
		Goto Ready
	AltFire:
		NULL A 0 A_PlayWeaponSound("switch/shotgun")
		GPIS B 4 ACS_ExecuteAlways(aow_gamemastercommand, 0, 13)
		GPIS C 5
		Goto Ready
	Flash:
		PISF A 1 BRIGHT A_Light2
		PISF B 1 BRIGHT A_Light1
				Goto LightDone
	} 
}

Actor AdminPistolRotation : Inventory {Inventory.MaxAmount 2}
Actor TimePuff : AOWBulletPuff {DamageType "TimeRed"}
Actor TimePuffBlue : AOWBulletPuff {DamageType "TimeBlue"}

Actor Administrator : Player {
	Health 200
	Player.MaxHealth 200
	Player.DisplayName "Administrator"
	Player.StartItem "Unarmed", 1
	Player.StartItem "IsAdmin", 1
	Player.ForwardMove 1.2
	Player.SideMove 1.2
	
	+NOSKIN
	+INVULNERABLE
	+NODAMAGE
	+DONTTRANSLATE
	+FULLVOLDEATH
	-PICKUP
	-SHOOTABLE
	
	DeathSound "brain/death" // :V
	Player.MorphWeapon "AdminPistol"
}

Actor AdministratorSwitch : PermanentPowerup {Powerup.Type "Administrator"}
Actor PowerAdministrator : PowerMorph {
	PowerMorph.PlayerClass "Administrator"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_ADDSTAMINA|MRF_NEWTIDBEHAVIOUR|MRF_WHENINVULNERABLE)
}

Actor AdminCrown {
	+CLIENTSIDEONLY +NOINTERACTION +NOGRAVITY
	States
	{
	Spawn:
		CRWN A -1 BRIGHT
		Stop
	}
}
Actor IsAdmin : Inventory {Inventory.MaxAmount 1}
Actor AdminFlight : PermanentPowerup {Powerup.Type "Flight"}