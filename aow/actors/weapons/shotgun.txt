// Shotgun
Actor Boomstick : AOWWeapon
{
	Obituary "%o was smacked down by %k's shotgun."
	Inventory.PickupMessage "You got the shotgun!"
	Weapon.SelectionOrder 17
	Weapon.Kickback 80
	Weapon.AmmoType "ShotgunAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.PreferredSkin "MarineShotgun"
	AttackSound "weapons/shotgf"
	States
	{
	Ready:
		SHTG A 1 A_WeaponReady
		Wait
	Deselect:
		SHTG A 1 A_Lower
		Wait
	Select:
		SHTG A 1 A_Raise
		Wait
	Fire:
		SHTG A 3
		NULL A 0 A_GunFlash
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		NULL A 0 BRIGHT A_FireBullets(5.5, 5.5, 2, 12, "AOWBulletPuff", FBF_NORANDOM, 350)
		SHTG A 2 BRIGHT A_FireBullets(5.5, 5.5, 6, 8, "AOWBulletPuff", FBF_USEAMMO | FBF_NORANDOM, 1000)
		SHTG A 3 BRIGHT
		SHTG BC 4
		SHTG D 4 A_SpawnItemEx("ShotgunShellCasing", 0, 0, ACS_ExecuteWithResult(aow_casingheight, 32), random(1, 3), random(0, 1), 0, random(45, 60), 128)
		SHTG CB 4
		SHTG A 3
		SHTG A 7 A_ReFire
		Goto Ready
	Flash:
		SHTF A 2 BRIGHT A_Light1
		SHTF B 3 BRIGHT A_Light2
		NULL A 0 A_Light0
		Stop
	Spawn:
		SHOT A -1
		Stop
	}
}

Actor ShotgunAmmo : Ammo
{
	Inventory.amount 5
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 50
}

Actor ShotgunPickup : CustomInventory replaces Shotgun 21080
{
	Inventory.PickupMessage "You got the shotgun!"
	Inventory.MaxAmount 0
	Inventory.respawntics 2100
	Inventory.pickupsound "misc/w_pkup"
	States
	{
	Spawn:
		SHOT A -1
		Stop
	Pickup:
		NULL AA 0 A_JumpIfInventory("Boomstick", 1, "Failure")
		NULL A 0 A_GiveInventory("Boomstick", 1)
		NULL A 0 A_GiveInventory("ShotgunAmmo", 5)
		Stop
	Failure:
		NULL A 0
		Fail
	}
}