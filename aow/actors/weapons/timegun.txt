// TIME GUN
Actor TimeGun : AOWWeapon
{
    +WEAPON.NOAUTOFIRE
	+BLOODLESSIMPACT
	DropItem "NewBackpack"
	Obituary "%o was sucked in a time-space continuum vortex."
	Inventory.PickupMessage "\cGYOU HAS ACTIVATED THE TIMESPACE PARADOX D="
	Inventory.PickupSound "Weapons/Pickup"
	Weapon.AmmoGive 100
	Weapon.AmmoType "TimeGunAmmo"
	Weapon.AmmoUse 0
	Weapon.Kickback 25
	Weapon.PreferredSkin "MarineTimegun"
	AttackSound "mech/gauss"

	States
	{
	Spawn:
		NULL A 0
		Stop
	Ready:
		TMGN A 10 A_WeaponReady
		NULL A 0 A_GiveInventory("TimeGunAmmo", 2)
		Loop
	Deselect:
		TMGN A 1 A_Lower
		Loop
	Select:
		NULL A 0
		NULL A 0 ACS_ExecuteAlways(aow_script959, 0)
	SelectLoop:
		TMGN A 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("PowerTimeGunSpeed", 1, "HyperspeedLoop")
		//NULL A 0 A_JumpIfInventory("InMenu", 1, "Ready") -- InMenu is not added yet
		NULL A 0 A_JumpIfInventory("TimeGunRotation", 3, "Fire.Hyperspeed")
		NULL A 0 A_JumpIfInventory("TimeGunRotation", 2, "Fire.Teleport")
		TMGN B 2 bright
		TMGN C 2 bright
		NULL A 0 A_JumpIfInventory("TimeGunRotation", 1, "Fire.Float")
		Goto Fire.Boost
	Fire.Boost:
		NULL A 0 A_JumpIfInventory("TimeGunAmmo", 25, 1)
		Goto OutOfAmmo
		NULL A 0 A_TakeInventory("TimeGunAmmo", 25)
		NULL A 0 A_PlayWeaponSound("mech/gauss")
		NULL A 0 BRIGHT A_JumpIfInventory("IsBlue", 1, "Fire.BoostBlue")
		NULL A 0 A_FireBullets(0, 0, 1, 0, "BoostPuffRed", 0)
		NULL A 0 A_ChangeFlag("PICKUP", 0)
		NULL A 0 ACS_ExecuteAlways(aow_script958, 0, 2) // reward
		TMGN B 8 BRIGHT A_RailAttack(0, 0, 1, "FF 96 00", "FF D4 00", 0, 0, "GoldenPuff")
		TMGN EFA 1
		NULL A 0 A_ChangeFlag("PICKUP", 1)
		TMGN A 20 A_PlayWeaponSound("80mm/reload")
		Goto Ready
	Fire.BoostBlue:
		NULL A 0 bright
		NULL A 0 A_FireBullets(0, 0, 1, 0, "BoostPuffBlue", 0)
		NULL A 0 A_ChangeFlag("PICKUP", 0)
		NULL A 0 ACS_ExecuteAlways(aow_script958, 0, 2)
		TMGN D 3 bright A_RailAttack (0, 2, 1, "FF 96 00","FF D4 00", 0, 0, "GoldenPuff")
		TMGN EFA 1
		NULL A 0 A_ChangeFlag("PICKUP", 1)
		TMGN A 25 A_PlayWeaponSound("80mm/reload")
		Goto Ready
	Fire.Float:
		NULL A 0 A_JumpIfInventory("TimeGunAmmo", 25, 1)
		goto OutOfAmmo
		NULL A 0 A_TakeInventory("TimeGunAmmo", 25)
		NULL A 0 A_PlayWeaponSound("mech/gauss")
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "Fire.FloatBlue")
		NULL A 0 A_FireBullets(0, 0, 1, 0, "FloatPuffRed", 0)
		NULL A 0 A_ChangeFlag("PICKUP", 0)
		NULL A 0 ACS_ExecuteAlways(aow_script958, 0, 2)
		TMGN D 3 Bright A_RailAttack (0, 2, 1, "FF 96 00","FF D4 00", 0, 0, "GoldenPuff")
		TMGN EFA 1
		NULL A 0 A_ChangeFlag("PICKUP", 1)
		TMGN A 25 A_PlaySound("80mm/reload", CHAN_WEAPON)
		Goto Ready
	Fire.FloatBlue:
		NULL A 0 A_FireBullets(0, 0, 1, 0, "FloatPuffBlue", 0)
		NULL A 0 A_ChangeFlag("PICKUP", 0)
		NULL A 0 ACS_ExecuteAlways(aow_script958, 0, 2)
		TMGN D 3 Bright A_RailAttack (0, 2, 1, "FF 96 00","FF D4 00", 0, 0, "GoldenPuff")
		TMGN EFA 1
		NULL A 0 A_ChangeFlag("PICKUP", 1)
		TMGN A 25 A_PlaySound("80mm/reload", CHAN_WEAPON)
		goto Ready
	Fire.Teleport:
		TMGN G 16 BRIGHT A_PlayWeaponSound ("plasmacannon/fire")
		TMGN H 12
		NULL A 0 A_JumpIfInventory("HasClass", 1, 1)
		goto Nope
		NULL A 0 A_JumpIfInventory("TimeGunAmmo", 100, 1)
		goto OutOfAmmo
		NULL A 0 A_TakeInventory("TimeGunAmmo", 100)
		NULL A 0 A_PlaySound("misc/timewarp", CHAN_WEAPON)
		NULL A 0 A_Stop
		NULL A 0 A_SetTranslucent(0.5, 1)
		NULL A 0 SetPlayerProperty(0, 1, 0)
		NULL A 0 A_GiveInventory("GoldHue", 1)
		NULL A 0 A_SpawnItemEx("TimeGunFlareCluster")
		TMGN H 35 BRIGHT A_GiveInventory ("IsTeleporting", 1)
		NULL A 0 A_JumpIfInventory("HasClass", 1, 1)
		goto Nope
		TMGN A 3 BRIGHT ACS_ExecuteAlways(aow_script94, 0, random (0, 1))
		NULL A 0 A_SetTranslucent(1.0, 0)
		NULL A 0 A_SpawnItemEx("TimeGunFlareExplosion")
		NULL A 0 SetPlayerProperty(0, 0, 0)
		NULL A 0 A_TakeInventory("IsTeleporting", 1)
		TMGN A 35 BRIGHT A_PlaySound ("misc/timeunwarp", CHAN_WEAPON)
		goto Ready
	Fire.Hyperspeed:
		TMGN G 16 BRIGHT A_PlayWeaponSound ("plasmacannon/fire")
		TMGN H 12
		NULL A 0 A_PlaySound("misc/timewarp", CHAN_WEAPON)
		NULL A 0 A_SetTranslucent(0.5, 1)
		NULL A 0 A_GiveInventory("TimeGunSpeed", 1)
	HyperspeedLoop:
		// NULL A 0 A_JumpIfInventory("InMenu", 1, "HyperspeedEnd") -- InMenu is not added yet
		TMGN H 1 bright DamageThing (1)
		NULL A 0 bright A_ReFire
	HyperspeedEnd:
		NULL A 0 A_TakeInventory("PowerTimeGunSpeed", 1)
		NULL A 0 A_GiveInventory("GoldHue", 1)
		NULL A 0 A_SetTranslucent(1.0, 0)
		NULL A 0 SetPlayerProperty(0, 0, 0)
		TMGN A 35 BRIGHT A_PlaySound ("misc/timeunwarp", CHAN_WEAPON)
		goto Ready
	Nope:
		TMGN A 35 SetPlayerProperty(0, 0, 0)
		Goto Ready
	OutOfAmmo:
		TMGN A 2 A_PlaySound("Plasma/FailFire", 6)
		NULL A 0 A_PlaySound("Plasma/Vapour", 7)
		NULL A 0 A_TakeInventory("TimeGunCharged", 1)
		TMGN AAAAAA 2 A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 6, -2)
		TMGN A 35
		Goto Ready
	AltFire:
		//NULL A 0 A_JumpIfInventory("InMenu", 1, "Ready") -- InMenu is not added yet
		NULL A 0 ACS_Execute(aow_script958, 0, 1)
		Goto Ready
	}
}

Actor TimeGunAmmo : Ammo
{
	+IGNORESKILL
	Inventory.amount 175
	Inventory.MaxAmount 175
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 175
	States
	{
	Spawn:
		NULL A 0
		Stop
	}
}

Actor IsTeleporting : Boolean {}

Actor TimeGunRotation : Inventory {Inventory.MaxAmount 5}
Actor TimeGunCharged : Inventory {Inventory.MaxAmount 1}

Actor Nope
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	RenderStyle translucent
	Alpha 1.0
	Scale 0.1
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_PlaySound("misc/nope")
		NULL A 0 ThrustThingZ(0, 12, 0, 0)
		NOPE A 3 A_FadeOut(0.1)
		Wait
	}
}

// ===================================================
// ----------------------------------- EFFECT POWERUPS
// ===================================================

// BOOST
actor PowerTimeGunBoost : PowerSpeed
{
	speed 1.4
	powerup.duration -15
}

actor TimeGunBoost : PowerupGiver
{
	+AUTOACTIVATE
	inventory.maxamount 0
	powerup.type "TimeGunBoost"
	inventory.pickupsound ""
	powerup.duration -15
	// powerup.color GoldMap, 0.25
	powerup.color "FF AA 00", 0.15
	states
{
	Spawn:
		NULL A 0
		stop
	}
}

actor TimeGunBoostRed : CustomInventory
{
	inventory.maxamount 0
	inventory.pickupmessage "`"
	inventory.pickupsound "misc/p_pkup"
	states
{
	Spawn:
		TNT1 A 5
		stop
	Pickup:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "Slow")
		NULL A 0 A_JumpIfInventory("Knife", 1, "Failure") // Not on stealths because this reveals them
		NULL A 0 A_JumpIfInventory("StealthLaserRifle", 1, "Failure")
		NULL A 0 ACS_Execute(aow_script958, 0, 0, 0) // intel message
		NULL A 0 A_GiveInventory("TimeGunBoost", 1)
		stop
	Slow:
		NULL A 0 ACS_Execute(aow_script958, 0, 0, 2)
		NULL A 0 A_GiveInventory("TimeGunSlow", 1)
		stop
	Failure:
		NULL A 0
		fail
	}
}

actor TimeGunBoostBlue : TimeGunBoostRed
{
	states
{
	Pickup:
		NULL A 0 A_JumpIfInventory("IsRed", 1, "Slow")
		NULL A 0 A_JumpIfInventory("Knife", 1, "Failure")
		NULL A 0 A_JumpIfInventory("StealthLaserRifle", 1, "Failure")
		NULL A 0 ACS_Execute(aow_script958, 0, 0, 0) // intel message
		NULL A 0 A_GiveInventory("TimeGunBoost", 1)
		stop
	}
}

// FLOAT
actor PowerTimeGunFloat : PowerFlight {}
actor TimeGunFloat : PowerupGiver
{
	+AUTOACTIVATE
	inventory.maxamount 0
	inventory.pickupsound ""
	powerup.type "TimeGunFloat"
	powerup.duration -20
	// powerup.color GoldMap, 0.25
	powerup.color "FF AA 00", 0.15
	states
{
	Spawn:
		NULL A 0
		stop
	}
}

actor TimeGunFloatRed : TimeGunBoostRed
{
	states
{
	Pickup:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "Weaken")
		NULL A 0 A_JumpIfInventory("Knife", 1, "Failure")
		NULL A 0 A_JumpIfInventory("StealthLaserRifle", 1, "Failure")
		NULL A 0 ACS_Execute(aow_script958, 0, 0, 1) // intel message
		NULL A 0 A_GiveInventory("TimeGunFloat", 1)
		stop
	Weaken:
		NULL A 0 ACS_Execute(aow_script958, 0, 0, 3)
		NULL A 0 A_GiveInventory("TimeGunWeaken", 1)
		stop
	}
}

actor TimeGunFloatBlue : TimeGunFloatRed
{
	states
{
	Pickup:
		NULL A 0 A_JumpIfInventory("IsRed", 1, "Weaken")
		NULL A 0 A_JumpIfInventory("Knife", 1, "Failure")
		NULL A 0 A_JumpIfInventory("StealthLaserRifle", 1, "Failure")
		NULL A 0 ACS_Execute(aow_script958, 0, 0, 1) // intel message
		NULL A 0 A_GiveInventory("TimeGunFloat", 1)
		stop
	}
}

// SLOW
actor PowerTimeGunSlow : PowerSpeed
{
	speed 0.6
}

actor TimeGunSlow : PowerupGiver
{
	+AUTOACTIVATE
	inventory.maxamount 0
	powerup.type "TimeGunSlow"
	inventory.pickupsound ""
	powerup.duration -20
	powerup.color "AA 00 00", 0.25
	states
{
	Spawn:
		NULL A 0
		stop
	}
}

// WEAKEN
Actor PowerTimeGunWeaken : PowerDamage
{
	DamageFactor "Fist", 0.7
	DamageFactor "Bullet", 0.7
	DamageFactor "Rifle", 0.7
	DamageFactor "Explosion", 0.7
	DamageFactor "Fire", 0.7
	DamageFactor "Chemical", 0.7
	DamageFactor "Laser", 0.7
	damagefactor "Time", 0.7
	DamageFactor "Disarm", 0.7
	DamageFactor "Flash", 0.7
	DamageFactor "Artillery", 0.7
	DamageFactor "Mechstomp", 0.7
	DamageFactor "Monster", 0.7
	Powerup.Duration -15
}

actor TimeGunWeaken : PowerupGiver
{
	+AUTOACTIVATE
	inventory.maxamount 0
	powerup.type "TimeGunWeaken"
	inventory.pickupsound ""
	powerup.duration -20
	powerup.color "AA 00 00", 0.25
	states
{
	Spawn:
		NULL A 0
		stop
	}
}

// HYPAHSPEED
actor PowerTimeGunSpeed : PowerSpeed
{
	speed 7
}

actor TimeGunSpeed : TimeGunBoost
{
	powerup.type "TimeGunSpeed"
	powerup.duration 0x7fffffff // removed manually
}

// ===================================================
// -------------------------------------- EFFECT PUFFS
// ===================================================
Actor GoldenPuff
{
	+PUFFONACTORS
	+NOBLOCKMAP
	+NOGRAVITY
	+BLOODLESSIMPACT
	+PUFFGETSOWNER
	RenderStyle Add
	Alpha 0.75
	Translation "192:207=160:167", "240:246=232:235", "80:111=160:167"

	States
	{
	Spawn:
	Melee:
	Crash:
		VOLX ABCDEFEDCBA 3 BRIGHT
		Stop
	}
}

actor BoostPuffRed : GoldenPuff
{
	damagetype "Time"
	states
{
	Spawn:
	Melee:
	Crash:
		NULL AA 0 A_SpawnItem("TimeGunBoostRed", -1)
		NULL A 0 A_Explode(5, 16)
		VOLX ABCDEFEDCBA 3 BRIGHT
		stop
	}
}

actor BoostPuffBlue : GoldenPuff
{
	damagetype "Time"
	states
{
	Spawn:
	Melee:
	Crash:
		NULL AA 0 A_SpawnItem("TimeGunBoostBlue", -1)
		NULL A 0 A_Explode(5, 16)
		VOLX ABCDEFEDCBA 3 BRIGHT
		stop
	}
}

actor FloatPuffRed : GoldenPuff
{
	damagetype "Time"
	states
{
	Spawn:
	Melee:
	Crash:
		NULL AA 0 A_SpawnItem("TimeGunFloatRed", -1)
		NULL A 0 A_Explode(5, 16)
		VOLX ABCDEFEDCBA 3 BRIGHT
		stop
	}
}

actor FloatPuffBlue : GoldenPuff
{
	damagetype "Time"
	states
{
	Spawn:
	Melee:
	Crash:
		NULL AA 0 A_SpawnItem("TimeGunFloatBlue", -1)
		NULL A 0 A_Explode(5, 16)
		VOLX ABCDEFEDCBA 3 BRIGHT
		stop
	}
}

// ===================================================
// ----------------------------------- SPECIAL EFFECTS
// ===================================================

// flare effects for teleportation
actor TimeGunFlareCluster
{
	+CLIENTSIDEONLY +NOINTERACTION
	reactiontime 19
	states
{
	Spawn:
		NULL A 0
		NULL AAAAAAAA 0 A_SpawnItemEx("TimeGunFlare", random(-64,64), random(-64,64), random(0,128), 0, 0, 1)
		TNT1 A 2
		NULL A 0 A_Countdown
		loop
	Death:
		NULL A 0 A_SpawnItemEx("TimeGunFlareExplosion")
		stop
	}
}

actor TimeGunFlareExplosion
{
	+CLIENTSIDEONLY +NOINTERACTION
	states
{
	Spawn:
		NULL A 0
		NULL AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TimeGunFlareGravity", 0, 0, 0, random (-12, 12), random (-12, 12), random (4, 9))
		NULL AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TimeGunFlareGravity", 0, 0, 0, random (-12, 12), random (-12, 12), random (4, 9))
		NULL AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TimeGunFlareGravity", 0, 0, 0, random (-12, 12), random (-12, 12), random (4, 9))
		NULL AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TimeGunFlareGravity", 0, 0, 0, random (-12, 12), random (-12, 12), random (4, 9))
		NULL AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TimeGunFlareGravity", 0, 0, 0, random (-12, 12), random (-12, 12), random (4, 9))
		TNT1 A 2 A_SpawnItemEx("GoldBeam")
		stop
	}
}

actor TimeGunFlare : IonRainFlare
{
	+FORCEXYBILLBOARD
	scale 0.04
	Translation "192:207=160:167", "240:246=232:235", "80:111=160:167"
}

actor TimeGunFlareGravity : TimeGunFlare {-NOGRAVITY -NOINTERACTION}

actor GoldBeam : BlueBeam
{
	xscale 1.0
	yscale 3.0
	alpha 0.2
	States
{
	Spawn:
		NULL A 0
		SBGL AAAA 1 A_FadeIn(0.2)
		SBGL A 1 Bright A_FadeOut(0.05)
		Wait
	}
}

// hues
actor GoldHue : PowerupGiver
{
	+INVENTORY.AUTOACTIVATE
	inventory.pickupmessage "wat are you doing"
	inventory.maxamount 0
	powerup.color GoldMap
	powerup.duration -1
	powerup.type "Null"
	states
{
	Spawn:
		NULL A 0
		stop
	}
}

actor PlayerSpeedTrail2 replaces PlayerSpeedTrail
{
	+NOBLOCKMAP
	renderStyle None
	alpha 0.0
	scale 0.0
}
