// Missile Launcher
Actor MissileLauncher : AOWWeapon {
	+NOAUTOFIRE
	Inventory.PickupMessage "You got the missile laucher!"
	Obituary "$OB_MISSILELAUNCHER"
	Weapon.SelectionOrder 13
	Weapon.AmmoType "MissileAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.PreferredSkin "MarineRockets"
	States
	{
	// INIT
	Select:
		NULL A 0 ACS_ExecuteAlways(aow_script963, 0) // init targeter
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "SelectBlue")
		MSLR A 1 A_Raise
		Wait
	SelectBlue:
		MSLB A 1 A_Raise
		Wait
	Deselect:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "DeselectBlue")
		MSLR A 1 A_Lower
		Loop
	DeselectBlue:
		MSLB A 1 A_Lower
		Loop
	Ready:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "ReadyBlue")
		MSLR A 1 A_WeaponReady(WRF_NOSECONDARY)
		Wait
	ReadyBlue:
		MSLB A 1 A_WeaponReady(WRF_NOSECONDARY)
		Wait
	Fire:
		NULL A 0
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		NULL A 0 A_GunFlash
		// MSLB A 0 ACS_ExecuteAlways(SC_SETPITCH, 0, -350, 6)
		NULL A 0 A_PlaySound("missilelauncher/fire", CHAN_WEAPON)
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "FireBlue")
		NULL A 0 A_JumpIfTargetInLOS("Fire.Homing")
		NULL A 0 A_FireCustomMissile("HeavyMissile")
		Goto AnimationRed
	Fire.Homing:
		NULL A 0 A_FireCustomMissile("HeavyHomingMissile")
		Goto AnimationRed
	FireBlue:
		NULL A 0 A_JumpIfTargetInLOS("Fire.HomingBlue")
		NULL A 0 A_FireCustomMissile("HeavyMissileBlue")
		Goto AnimationBlue
	Fire.HomingBlue:
		NULL A 0 A_FireCustomMissile("HeavyHomingMissileBlue")
		Goto AnimationBlue
	AnimationRed:
		MSLR A 1 offset(1, 35)
		MSLR A 1 offset(2, 38)
		MSLR A 1 offset(3, 41) A_Recoil(18)
		MSLR A 1 offset(4, 44)
		MSLR A 1 offset(5, 47)
		MSLR A 1 offset(6, 50)
		MSLR A 1 offset(5, 47)
		MSLR A 1 offset(4, 44)
		MSLR A 1 offset(3, 41)
		MSLR A 1 offset(2, 38)
		MSLR A 1 offset(1, 35)
		MSLR A 1 offset(0, 32)
		MSLR A 44 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		NULL A 0 A_CheckReload
		NULL A 0 A_PlayWeaponSound("missilelauncher/reload")
		MSLR BC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLR D 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLR E 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		NULL A 0 A_ReFire
		Goto Ready
	AnimationBlue:
		MSLB A 1 offset(1, 35)
		MSLB A 1 offset(2, 38)
		MSLB A 1 offset(3, 41) A_Recoil(18)
		MSLB A 1 offset(4, 44)
		MSLB A 1 offset(5, 47)
		MSLB A 1 offset(6, 50)
		MSLB A 1 offset(5, 47)
		MSLB A 1 offset(4, 44)
		MSLB A 1 offset(3, 41)
		MSLB A 1 offset(2, 38)
		MSLB A 1 offset(1, 35)
		MSLB A 1 offset(0, 32)
		MSLB A 44 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		NULL A 0 A_CheckReload
		NULL A 0 A_PlayWeaponSound("missilelauncher/reload")
		MSLB BC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLB D 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLB E 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		NULL A 0 A_ReFire
		Goto Ready
	Flash:
		MSFR A 2 BRIGHT A_Light1
		MSFR B 2 BRIGHT 
		MSFR C 2 BRIGHT A_Light2
		MSFR D 2 BRIGHT
		NULL A 0 A_Light0
		Stop
	Spawn:
		LAUN A -1
		Stop
	}
}

Actor MissileAmmo : Ammo {
	+IGNORESKILL
	Inventory.amount 15
	Inventory.MaxAmount 15
	Ammo.BackpackAmount 15
	Ammo.BackpackMaxAmount 15
}

Actor HeavyMissile {
	PROJECTILE
	-RANDOMIZE
	Radius 11
	Height 8
	Speed 26
	Damage (100)
	Obituary "$OB_MISSILELAUNCHER" // "%o was blown out of %p boots by %k's missile."
	DeathSound "weapons/rocklx"
	DamageType "Explosion"
	Decal "Scorch"
	States
	{
	Spawn:
		MISR A 2 BRIGHT
		NULL A 0 BRIGHT A_PlaySound("ambient/rocket", CHAN_BODY, 1.0, true)
		MISR A 1 BRIGHT A_SpawnItemEx("HeavyMissileSmoke", -16)
		Wait
	Death:
		NULL A 0 A_SpawnItemEx("FlameDecay", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 160)
		NULL A 0 A_StopSound(CHAN_BODY)
		NULL A 0 A_Explode(190, 256)
		//TNT1 A 0 A_RadiusThrust(96, 128, 0)
		NULL A 0 A_SpawnItemEx("HeavyMissileQuake")
		NULL A 5 A_SpawnItemEx("ExplosionMain")
		Stop
	}
}

Actor HeavyMissileBlue : HeavyMissile {
	DamageType "Explosion"
	States
	{
	Spawn:
		MISB A 2 BRIGHT
		NULL A 0 BRIGHT A_PlaySound("ambient/rocket", CHAN_BODY, 1.0, true)
		MISB A 1 BRIGHT A_SpawnItemEx("HeavyMissileSmoke", -16)
		Wait
	Death:
		NULL A 0 A_SpawnItemEx("FlameDecayBlue", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 160)
		Goto Super::Death+1
	}
}

Actor HeavyHomingMissile : HeavyMissile {
	+SEEKERMISSILE
	Obituary "$OB_SEEKERMISSILE" // "%o was found by %k's seeker missile."
	States
	{
	Spawn:
		MISR A 2 BRIGHT
		NULL A 0 BRIGHT A_PlaySound("seeker/beep", CHAN_ITEM, 1.0, true)
		NULL A 0 BRIGHT A_PlaySound("ambient/rocket", CHAN_BODY, 1.0, true)
	Idle:
		NULL A 0 A_SpawnItemEx("HeavyMissileSmoke", -16)
		MISR A 2 BRIGHT A_SeekerMissile(4, 10)
		Loop
	Death:
		NULL A 0 A_StopSound(CHAN_ITEM)
		Goto Super::Death
	}
}

Actor HeavyHomingMissileBlue : HeavyMissile {
	+SEEKERMISSILE
	Obituary "$OB_SEEKERMISSILE" // "%o was found by %k's seeker missile."
	DamageType "Explosion"
	States
	{
	Spawn:
		MISB A 2 BRIGHT
		NULL A 0 BRIGHT A_PlaySound("seeker/beep", CHAN_ITEM, 1.0, true)
		NULL A 0 BRIGHT A_PlaySound("ambient/rocket", CHAN_BODY, 1.0, true)
	Idle:
		NULL A 0 A_SpawnItemEx("HeavyMissileSmoke", -16)
		MISB A 2 BRIGHT A_SeekerMissile(4, 10)
		Loop
	Death:
		NULL A 0 A_StopSound(CHAN_ITEM)
		NULL A 0 A_SpawnItemEx("FlameDecayBlue", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 160)
		Goto Super::Death+1
	}
}

Actor HeavyMissileQuake {
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
		NULL A 0
		NULL A 5 Radius_Quake (5, 5, 0, 7, 0)
		NULL A 4 Radius_Quake (4, 4, 0, 7, 0)
		NULL A 3 Radius_Quake (3, 3, 0, 7, 0)
		NULL A 2 Radius_Quake (2, 2, 0, 7, 0)
		NULL A 1 Radius_Quake (1, 1, 0, 7, 0)
		Stop
	}
}

Actor HeavyMissileSmoke {
	+CLIENTSIDEONLY
	+NOINTERACTION
	Alpha 0.6
	Scale 0.2
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_Jump(256, "Spawn1", "Spawn2", "Spawn3", "Spawn4")
		Goto Spawn1
	Spawn1:
		SMOK A 1 A_FadeOut
		Wait
	Spawn2:
		SMOK B 1 A_FadeOut
		Wait
	Spawn3:
		SMOK C 1 A_FadeOut
		Wait
	Spawn4:
		SMOK D 1 A_FadeOut
		Wait
	}
}