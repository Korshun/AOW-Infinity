// PLASMA CANNON
Actor PlasmaCannon : AOWWeapon {
	Weapon.SelectionOrder 1
	Weapon.Kickback 100
	Weapon.AmmoType "PlasmaCannonAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 4
	Weapon.AmmoType2 "PlasmaCannonAmmo"
	Weapon.AmmoUse2 1
	DropItem "NewBackPack"
	Weapon.PreferredSkin "MarineBFG"
	States
	{
	Ready:
		NULL AA 0 A_JumpIfInventory("IsRed", 1, "ReadyRed")
		PLCG ABCD 3 A_WeaponReady
		Goto Ready+2
	ReadyRed:
		PC2G ABCD 3 A_WeaponReady
		Loop
	Deselect:
		NULL AA 0 A_JumpIfInventory("IsRed", 1, "DeselectRed")
		PLCG AAABBBCCCDDD 1 A_Lower
		Goto Deselect+2
	DeselectRed:
		PC2G AAABBBCCCDDD 1 A_Lower
		Loop
	Select:
		NULL AA 0 A_JumpIfInventory("IsRed", 1, "SelectRed")
		PLCG AAABBBCCCDDD 1 A_Raise
		Goto Select+2
	SelectRed:
		PC2G AAABBBCCCDDD 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "FireBlue")
		PC2G A 8 A_PlayWeaponSound("plasmacannon/fire")
		PC2G EFG 6 BRIGHT
		NULL A 0 BRIGHT A_GunFlash
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		NULL A 0 BRIGHT A_LightInverse
		PC2G H 2 BRIGHT A_FireCustomMissile("PlasmaCannonBall")
		PC2G H 3 BRIGHT A_Light2
		PC2G I 2 BRIGHT A_Light1
		PC2G I 3 BRIGHT A_Light0
		PC2G JKL 5 BRIGHT
		PC2G EF 3
		PC2G G 5 A_Refire
		Goto Ready
	AltFire:
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "AltFireBlue")
		PC2G A 5 A_PlayWeaponSound("plasmacannon/altfire")
		PC2G EFG 5 BRIGHT
		NULL A 0 BRIGHT A_GunFlash
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		NULL A 0 BRIGHT A_LightInverse
		PC2G H 2 BRIGHT A_FireCustomMissile("PlasmaCannonBallSmall")
		PC2G H 2 BRIGHT A_Light2
		PC2G I 2 BRIGHT A_Light1
		PC2G I 2 BRIGHT A_Light0
		PC2G JKL 4 BRIGHT
		PC2G EF 3
		PC2G G 5 A_Refire
		Goto Ready
	FireBlue:
		PLCG A 8 A_PlayWeaponSound("plasmacannon/fire")
		PLCG EFG 6 BRIGHT
		NULL A 0 BRIGHT A_GunFlash
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		NULL A 0 BRIGHT A_LightInverse
		PLCG H 2 BRIGHT A_FireCustomMissile("PlasmaCannonBallBlue")
		PLCG H 3 BRIGHT A_Light2
		PLCG I 2 BRIGHT A_Light1
		PLCG I 3 BRIGHT A_Light0
		PLCG JKL 5 BRIGHT
		PLCG EF 3
		PLCG G 5 A_Refire
		Goto Ready
	AltFireBlue:
		PLCG A 5 A_PlayWeaponSound("plasmacannon/altfire")
		PLCG EFG 5 BRIGHT
		NULL A 0 BRIGHT A_GunFlash
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		NULL A 0 BRIGHT A_LightInverse
		PLCG H 2 BRIGHT A_FireCustomMissile("PlasmaCannonBallSmallBlue")
		PLCG H 2 BRIGHT A_Light2
		PLCG I 2 BRIGHT A_Light1
		PLCG I 2 BRIGHT A_Light0
		PLCG JKL 4 BRIGHT
		PLCG EF 3
		PLCG G 5 A_Refire
		Goto Ready
	Spawn:
		NULL A 0
		Stop
	}
}

Actor PlasmaCannonAmmo : Ammo {
	Inventory.amount 0
	Inventory.MaxAmount 10
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 10
	+IGNORESKILL
	States
	{
	Spawn:
		NULL A 0
		Stop
	}
}

// ===================================================
// ---------------------------- MAIN PROJECTILE ACTORS
// ===================================================
Actor PlasmaCannonBall {
	PROJECTILE
	+FORCEXYBILLBOARD
	+EXTREMEDEATH
	Radius 13
	Height 8
	Speed 40
	Damage (200)

	Obituary "$OB_PLASMACANNON_RED" // "%o was annihilated by %k's \caPlasma Cannon."
	RenderStyle Add
	Alpha 0.75
	DeathSound "plasmacannon/explode"
	DamageType "Explosion"
	Scale 1.5
	Translation "192:207=169:189", "240:246=45:47", "112:127=176:191"
	States
	{
	Spawn:
		PLS1 AABB 1 BRIGHT A_SpawnItemEx("PlasmaFlameTail", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		Loop
	Death:
		PLE1 A 4
		PLE1 A 4 BRIGHT A_Explode(300, 192)
		PLE1 BBBB 2 BRIGHT A_SpawnItemEx("PlasmaFlameDecay", random(-32, 32), random(-32, 32), random(-32, 32), 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		NULL A 0 A_SpawnItemEx("PlasmaFlameCluster", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		PLE1 C 4 BRIGHT
		PLE1 C 4 BRIGHT A_Explode(30, 550, 0)
		PLE1 DEF 8 BRIGHT
		Stop
	}
}

Actor PlasmaCannonBallSmall : PlasmaCannonBall {
	+SEEKERMISSILE
	-EXTREMEDEATH
	Radius 9
	Height 7
	Speed 45
	Damage (100)
	DeathSound "plasmacannon/altexplode"
	Obituary "$OB_PLASMACANNON_ALT_RED" // "%o couldn't escape %k's \caPlasma Cannon."
	Scale 0.9
	States
	{
	Spawn:
		NULL A 0 BRIGHT A_Tracer2
		PLS1 ABA 1 BRIGHT A_SpawnItemEx("PlasmaFlameTailSmall", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		NULL A 0 BRIGHT A_Tracer2
		PLS1 BAB 1 BRIGHT A_SpawnItemEx("PlasmaFlameTailSmall", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		Loop
	Death:
		PLE1 A 3
		PLE1 A 3 BRIGHT A_Explode(128, 128)
		PLE1 B 7 BRIGHT
		PLE1 C 3 BRIGHT
		PLE1 C 3 BRIGHT A_Explode(128, 256)
		PLE1 DEF 7 BRIGHT
		Stop
	}
}

Actor PlasmaCannonBallBlue : PlasmaCannonBall {
	Translation "112:120=200:207", "121:127=241:247"
	Obituary "$OB_PLASMACANNON_BLUE" // "%o was annihilated by %k's \cNPlasma Cannon."
	DamageType "Explosion"
	States
	{
	Death:
		PLE1 A 4
		PLE1 A 4 BRIGHT A_Explode(300, 192)
		PLE1 BBBB 2 BRIGHT A_SpawnItemEx("PlasmaFlameDecayBlue", random(-32, 32), random(-32, 32), random(-32, 32), 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		NULL A 0 A_SpawnItemEx("PlasmaFlameCluster", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		PLE1 C 4 BRIGHT
		PLE1 C 4 BRIGHT A_Explode(30, 550, 0)
		PLE1 DEF 8 BRIGHT
		Stop
	}
}

Actor PlasmaCannonBallSmallBlue : PlasmaCannonBallSmall {
	Translation "112:120=200:207", "121:127=241:247"
	Obituary "$OB_PLASMACANNON_ALT_BLUE" // "%o couldn't escape %k's \cnPlasma Cannon."
	DamageType "Explosion"
}

// =========================================
// ---------------------------- DECAY ACTORS
// =========================================
Actor PlasmaFlameDecay {
	RenderStyle None
	+NOBLOCKMAP
	+NOGRAVITY
	DamageType "Fire"
	Obituary "$OB_FIREDECAY" // "%o burns in %k's firestorm."
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("PlasmaFlameDecayFX", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 15 BRIGHT A_Explode(8, 96, 0)
		Stop
	}
}

Actor PlasmaFlameDecayFX {
	+CLIENTSIDEONLY +NOBLOCKMAP
	+NOGRAVITY +NOINTERACTION
	ReactionTime 66
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 5 A_SpawnItemEx("PlasmaFlame", random(-16, 16), random(-16, 16), random(8, 16), 0, 0, random(1, 3), 0, SXF_TRANSFERTRANSLATION, 32)
		NULL A 0 A_Countdown
		Loop
	Death:
		NULL A 0
		Stop
	}
}

Actor PlasmaFlameDecayBlue : PlasmaFlameDecay {DamageType "Fire"}

Actor PlasmaFlameCluster {
	+NOBLOCKMAP +NOGRAVITY
	+CLIENTSIDEONLY +NOINTERACTION
	ReactionTime 60
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("PlasmaFlame", 0, 0, 0, random(-9, 9), random(-10, 10), random(-10, 10), 0, SXF_TRANSFERTRANSLATION)
		NULL A 0 A_Countdown
		Loop
	Death:
		NULL A 0
		Stop
	}
}


Actor PlasmaFlameTail {
	+CLIENTSIDEONLY +NOBLOCKMAP
	+NOGRAVITY +NOINTERACTION
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("PlasmaFlame", -8, 0, 0, -6, random(-2, -3), random(-1, 1), 0, SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("PlasmaFlame", -8, 0, 0, -6, random(2, 3), random(-1, 1), 0, SXF_TRANSFERTRANSLATION)
		Stop
	}
}

Actor PlasmaFlameTailSmall : PlasmaFlameTail {
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("PlasmaFlameSmall", -8, 0, 0, -5, random(-2, -3), random(-1, 1), 0, SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("PlasmaFlameSmall", -8, 0, 0, -5, random(2, 3), random(-1, 1), 0, SXF_TRANSFERTRANSLATION)
		Stop
	}
}

Actor PlasmaFlame {
	+CLIENTSIDEONLY +NOBLOCKMAP
	+NOGRAVITY +NOINTERACTION
	+FORCEXYBILLBOARD

	Radius 13
	Height 8
	Speed 10
	Damage (0)
	RenderStyle Add
	Alpha 0.75
	Scale 1.2
	States
	{
	Spawn:
		NULL A 0
		PLFX ABCDEFG 3 BRIGHT
		PLFX FG 2 BRIGHT
		Stop
	}
}

Actor PlasmaFlameSmall : PlasmaFlame {
	Scale 0.8
	States
	{
	Spawn:
		NULL A 0
		PLFX ABCDEFG 2 BRIGHT
		Stop
	}
}