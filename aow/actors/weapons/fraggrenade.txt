// Frag grenade
Actor FragGrenade : AOWWeapon
{
        Tag "Fragmentation Grenade"
	+NOALERT
	Inventory.PickupMessage "Fragmentation Grenade"  
	Inventory.pickupsound "misc/ammo_pkup"
	AttackSound "frag/pinpull"
	Weapon.Kickback 5
	Weapon.AmmoType "FragGrenadeAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 1
	Weapon.AmmoType2 "FragGrenadePower"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Scale 0.25
	Weapon.PreferredSkin "MarineFist"
	States
	{
	Ready:
		NULL A 0 
		NULL A 0 A_JumpIfNoAmmo(9)
		FRGS ABCDEFG 1
		FRGI A 1 A_WeaponReady
		Goto Ready+9
		TNT1 A 1 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_JumpIfNoAmmo(4)
		NULL A 0 A_Lower
		FRGI A 1 A_Lower
		Loop
		NULL A 0 A_Lower
		TNT1 A 1 A_Lower
		Loop
	Select:
		NULL A 0 A_Raise
		Loop
	Fire:
		FRGI A 2 A_TakeInventory("FragGrenadePower", 100)
		FRG1 BCDEFGH 2
		NULL A 0 A_PlayWeaponSound("frag/pinpull")
		FRG1 IJK 1
		TNT1 A 10
		TNT1 A 1 A_Refire
		TNT1 A 3 A_PlaySound("frag/voice")
		FRG2 AB 2
		FRG2 C 2 A_PlayWeaponSound("frag/throw")
		NULL A 0 A_TakeInventory("FragGrenadePower", 100)
		FRG2 D 2 A_FireCustomMissile("FragGrenadeThrown1", -1, 1, 10, 5)
		FRG2 EFG 2
		Goto Ready
	Hold:
		TNT1 A 4 A_GiveInventory("FragGrenadePower", 6)
		TNT1 A 1 A_ReFire
		TNT1 A 3 A_PlaySound("frag/voice")
		NULL A 0 A_JumpIfInventory("FragGrenadePower", 80, "Throw5")
		NULL A 0 A_JumpIfInventory("FragGrenadePower", 60, "Throw4")
		NULL A 0 A_JumpIfInventory("FragGrenadePower", 40, "Throw3")
		NULL A 0 A_JumpIfInventory("FragGrenadePower", 20, "Throw2")
		FRG2 AB 2
		FRG2 C 2 A_PlayWeaponSound("frag/throw")
		NULL A 0 A_TakeInventory("FragGrenadePower", 100)
		FRG2 D 2 A_FireCustomMissile("FragGrenadeThrown1", -1, 1, 10, 5)
		FRG2 EFG 2
		Goto Ready
	Throw2:
		FRG2 AB 2
		FRG2 C 2 A_PlayWeaponSound("frag/throw")
		NULL A 0 A_TakeInventory("FragGrenadePower", 100)
		FRG2 D 2 A_FireCustomMissile("FragGrenadeThrown2", -1, 1, 10, 5)
		FRG2 EFG 2
		Goto Ready
	Throw3:
		TNT1 A 2
		TNT1 A 2 A_PlayWeaponSound("frag/throw")
		NULL A 0 A_TakeInventory("FragGrenadePower", 100)
		FRG3 A 2 A_FireCustomMissile("FragGrenadeThrown3", -1, 1, 10, 15)
		FRG3 BCD 2
		TNT1 AA 2
		Goto Ready
	Throw4:
		TNT1 A 2
		TNT1 A 2 A_PlayWeaponSound("frag/throw")
		NULL A 0 A_TakeInventory("FragGrenadePower", 100)
		FRG3 A 2 A_FireCustomMissile("FragGrenadeThrown4", -1, 1, 10, 15)
		FRG3 BCD 2
		TNT1 AA 2
		Goto Ready
	Throw5:
		TNT1 A 2
		TNT1 A 2 A_PlayWeaponSound("frag/throw")
		NULL A 0 A_TakeInventory("FragGrenadePower", 100)
		FRG3 A 2 A_FireCustomMissile("FragGrenadeThrown5", -1, 1, 10, 15)
		FRG3 BCD 2
		TNT1 AA 2
		Goto Ready
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItem("FragGrenadeAmmo")
		Stop
	}
}

Actor FragGrenadeAmmo : Ammo
{
	Inventory.PickupMessage "Picked up a frag grenade."
	Inventory.amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 1
	Scale 0.25
	States
	{
	Spawn:
		FRGP A -1
		Stop
	}
}

Actor FragGrenadePower : Ammo
{
	+IGNORESKILL
	Inventory.amount 0
	Inventory.MaxAmount 100
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 100
}

Actor FragGrenadeThrown1
{
	Obituary "%o didn't see %k's frag grenade."
	Radius 5
	Height 5
	Speed 17
	Damage (0)
	SeeSound ""
	DeathSound "weapons/rocklx"
	PROJECTILE
	-NOGRAVITY
	+RANDOMIZE
	+HEXENBOUNCE
	+NOEXTREMEDEATH
	+FORCERADIUSDMG
	+CANBOUNCEWATER
	+BOUNCEONACTORS
	Scale 0.22
	Gravity 0.6
	BounceFactor 0.5
	DamageType "Rifle"
	ReactionTime 1
	States
	{
	Spawn:
		FRGX ABCDEFGHA 5
		FRGX BB 5
		NULL AA 0 A_Countdown
	Death:
		NULL A 0 A_NoGravity
		NULL A 0 A_StopSoundEx("Body")
		TNT1 A 5 A_Explode(190, 256)
		TNT1 A 1 A_SpawnItemEx("FlameDecay", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 160)
		NULL AAAAAAAA 0 A_CustomMissile("FragGrenadeShrapnel", 0, 0, random(0, 360), 6, random(2, 80))
		TNT1 A 5 A_SpawnItem("ExplosionMain")
		Stop
	}
}

Actor FragGrenadeShrapnel
{
	PROJECTILE
	+RIPPER
	+BLOODSPLATTER
	+HEXENBOUNCE
	Radius 10
	Height 8
	Damage (4)
	Speed 60
	Obituary "%o was shredded by %k's shrapnel."
	Scale 0.3
	Gravity 0.6
	BounceCount 10
	DamageType "Rifle"
	States
	{
	Spawn:
		SHRA A 5 BRIGHT
		SHRA A 4 A_Gravity
		SHRA A 55
		Goto Death1
	Death1:
		SHRA A 4 A_FadeOut(0.2)
		Loop
	}
}

Actor FragGrenadeDropped : FragGrenadeThrown1 {Speed 10}
Actor FragGrenadeThrown2 : FragGrenadeThrown1 {Speed 22}
Actor FragGrenadeThrown3 : FragGrenadeThrown1 {Speed 29}
Actor FragGrenadeThrown4 : FragGrenadeThrown1 {Speed 34}
Actor FragGrenadeThrown5 : FragGrenadeThrown1 {Speed 46}
