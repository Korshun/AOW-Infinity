// CHAINSAW
Actor EagerBeaver : AOWWeapon replaces Chainsaw
{
	+MELEEWEAPON
	// +EXTREMEDEATH
	Weapon.Kickback 0
	Weapon.SelectionOrder 18
	Weapon.upsound "weapons/sawup"
	Weapon.PreferredSkin "MarineChainsaw"
	Inventory.PickupMessage "$GOTCHAINSAW"
	Obituary "$OB_CHAINSAW"
	Tag "Chainsaw"
	Decal "BulletChip"
	States
	{
	Ready:
		NULL A 0 A_TakeInventory("IsSawing", 1)
		NULL A 0 A_PlaySound("weapons/sawidle", CHAN_WEAPON)
		SAWG CD 4 A_WeaponReady
		Goto Ready+1
	Deselect:
		SAWG C 1 A_Lower
		Wait
	Select:
		NULL A 0 A_TakeInventory("Unarmed", 1)
		SAWG C 1 A_Raise
		Wait
	Fire:
		NULL A 0 A_GiveInventory("IsSawing", 1)
		NULL A 0 A_PlayWeaponSound("weapons/sawfull")
		SAWG A 4 A_CustomPunch(random(20, 25), 1, 0, "SawPuff", 96)
		NULL A 0 A_PlayWeaponSound("weapons/sawfull")
		SAWG B 4 A_CustomPunch(random(20, 25), 1, 0, "SawPuff", 96)
		NULL A 0 A_ReFire
		Goto Ready
	Spawn:
		CSAW A -1
		Stop
	}
}

Actor SawPuff : BulletPuff
{
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	// +EXTREMEDEATH
	-ALLOWPARTICLES
	DamageType "Fist"
}

// [Dusk] workaround for +EXTREMEDEATH - this is given when the Player 'fires'
// the chainsaw. When a Player dies, if his target (the killer) has this item,
// he will Goto XDeath instead
Actor IsSawing : Boolean {}
