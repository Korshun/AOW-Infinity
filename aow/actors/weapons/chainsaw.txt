// CHAINSAW
Actor EagerBeaver : AOWWeapon replaces Chainsaw
{
	+MELEEWEAPON
	// +EXTREMEDEATH
	Weapon.Kickback 0
	Weapon.SelectionOrder 18
	Weapon.upsound "weapons/sawup"
	Weapon.PreferredSkin "MarineChainsaw"
	Inventory.PickupMessage "$GOTCHAINSAW"
	Obituary "$OB_CHAINSAW"
	Tag "Chainsaw"
	Decal "BulletChip"
	States
	{
	Ready:
		NULL A 0 A_TakeInventory("IsSawing", 1)
		NULL A 0 A_PlaySound("weapons/sawidle", CHAN_WEAPON)
		SAWG CD 4 A_WeaponReady
		Goto Ready+1
	Deselect:
		SAWG C 1 A_Lower
		Wait
	Select:
		NULL A 0 A_TakeInventory("Unarmed", 1)
		SAWG C 1 A_Raise
		Wait
	Fire:
		NULL A 0 A_GiveInventory("IsSawing", 1)
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Denied")
		NULL A 0 A_PlayWeaponSound("weapons/sawfull")
		SAWG A 4 A_CustomPunch(random(20, 25), 1, 0, "SawPuff", 96)
		NULL A 0 A_PlayWeaponSound("weapons/sawfull")
		SAWG B 4 A_CustomPunch(random(20, 25), 1, 0, "SawPuff", 96)
		NULL A 0 A_ReFire
		Goto Ready
	AltFire:
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Denied")
		NULL A 0 A_FireCustomMissile("ChainSawThrown")
		NULL A 0 A_StopSound(CHAN_WEAPON)
		NULL A 0 A_GiveInventory("Unarmed", 1)
		NULL A 0 A_TakeInventory("EagerBeaver", 1)
		Goto Deselect
	Denied:
		SAWG CD 4 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
	Spawn:
		CSAW A -1
		Stop
	}
}

Actor SawPuff : BulletPuff
{
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	// +EXTREMEDEATH
	-ALLOWPARTICLES
	DamageType "Fist"
}

Actor ChainsawThrown
{
	PROJECTILE
	+RIPPER
	+EXTREMEDEATH
	Speed 40
	Radius 20
	Height 20
	Damage 12
	Scale 0.3
	DamageType "Fist"
	Obituary "$OB_CHAINSAW_ALT" // "%o was mutilated by %k's flying chainsaw!"
	Decal "Scorch"
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_PlaySound("weapons/sawfull", CHAN_VOICE, 1.0, 1)
		SAWW AA 2 A_SpawnItemEx("PulseBallSmokePuff", -8)
		SAWW A 1
		SAWW A 1 A_LowGravity
		SAWW A 2 A_SpawnItemEx("PulseBallSmokePuff", -8)
		Wait
	Death:
		NULL A 0 A_PlaySound("weapons/rocklx", CHAN_VOICE)
		NULL A 0 A_SpawnItemEx("ExplosionMain")
		NULL A 0 A_SpawnItemEx("ChainSawThrownBoom")
		Stop
	}
}

Actor ChainSawThrownBoom
{
	PROJECTILE
	+RIPPER
	+BLOODLESSIMPACT
	+PAINLESS
	DamageType "Explosion"
	Obituary "$OB_CHAINSAW_ALT"
	Damage 0
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_Explode(128, 128)
		Stop
	}
}


// [Dusk] workaround for +EXTREMEDEATH - this is given when the Player 'fires'
// the chainsaw. When a Player dies, if his target (the killer) has this item,
// he will Goto XDeath instead
Actor IsSawing : Boolean {}