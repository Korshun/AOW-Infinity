// Rifle
Actor Rifle : AOWWeapon 21042
{
	+NOEXTREMEDEATH
	+ALT_AMMO_OPTIONAL
	Obituary "$OB_RIFLE" // "%o was crippled by %k's rifle."
	Weapon.SelectionOrder 18
	Weapon.Kickback 150
	Weapon.AmmoType "RifleAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType2 "RifleGrenadeAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 2
	Inventory.PickupMessage "You got the Rifle!"
	Weapon.PreferredSkin "MarineBase"
	States
	{
	Ready:
		RIFL A 1 A_WeaponReady
		Loop
	Deselect:
		RIFL A 1 A_Lower
		Loop
	Select:
		RIFL A 1 A_Raise
		Loop
	Fire:
		RIFL A 2
		NULL A 0 A_Light2
		NULL A 0 A_SpawnItemEx("RifleShellCasing", 8, 8, ACS_ExecuteWithResult(aow_casingheight, 32), random(1, 3), random(0, 1), random(1, 3), random(45, 60))
		NULL A 0 A_PlaySound("rifle/fire", CHAN_AUTO)
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		NULL A 0 A_FireBullets(0, 0, 0, 0, "RiflePuff", 0)
		RIFL B 2 BRIGHT A_RailAttack(25, 0, true, none, none, true, 0, "RiflePuff")
		Goto Fire.Done
	Fire.Done:
		RIFL C 1 BRIGHT offset(0, 36) A_Light1
		RIFL C 1 BRIGHT offset(0, 40)
		RIFL B 1 BRIGHT offset(0, 42) A_Light0
		RIFL A 1 offset(0, 44)
		RIFL A 1 offset(0, 40)
		RIFL A 1 offset(0, 36)
		RIFL A 8 offset(0, 32)
		RIFL A 4 A_ReFire
		Goto Ready
	AltFire:
		NULL A 0 A_JumpIfNoAmmo("AltRejected")
		RIFL A 4
		RIFL A 2 A_PlayWeaponSound("grenade/fire")
		RIFL A 1 offset(0, 33) BRIGHT A_FireCustomMissile("RifleGrenade")
		RIFL A 1 offset(0, 34) 
		RIFL A 1 offset(0, 36) A_Light1
		RIFL A 1 offset(0, 37)
		RIFL A 1 offset(0, 38) A_Light0
		RIFL A 1 offset(0, 36)
		RIFL A 1 offset(0, 34)
		RIFL A 10 offset(0, 32)
		RIFL A 20 A_PlayWeaponSound("rifle/reload")
		RIFL A 4 A_ReFire
		Goto Ready
	AltRejected:
		RIFL A 8 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	Spawn:
		RIFL X -1
		Stop
	}
}

Actor RifleAmmo : Ammo
{
	Inventory.amount 8
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 50
}

Actor RifleGrenadeAmmo : Ammo
{
	Inventory.amount 1
	Inventory.MaxAmount 8
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 8
}

Actor RifleGrenade : GrenadeGrenade {Obituary "%o was popped by %k's rifle grenade."}
Actor RiflePuff : AOWBulletPuff {DamageType "Rifle"}