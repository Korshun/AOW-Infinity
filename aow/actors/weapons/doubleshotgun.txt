// Double-barreled Shotgun
Actor DoubleShotgun : AOWWeapon
{
	Inventory.PickupMessage "You got the double shotgun!"
	Obituary "$OB_DOUBLESHOTGUN" // "%o was splattered by %k's double shotgun."
	AttackSound "weapons/sshotf"
	DropItem "Boomstick"
	Weapon.SelectionOrder 16
	Weapon.Kickback 30
	Weapon.AmmoType "ShotgunAmmo"
	Weapon.AmmoUse 2
	Weapon.AmmoGive 0
	Weapon.PreferredSkin "MarineSSG"
	States
	{
	Ready:
		SHT2 A 1 A_WeaponReady
		Loop
	Deselect:
		SHT2 A 1 A_Lower
		Loop
	Select:
		SHT2 A 1 A_Raise
		Loop
	Fire:
		SHT2 A 3
		NULL A 0 BRIGHT A_GunFlash
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		SHT2 A 7 BRIGHT A_FireBullets(15.7, 11.7, 20, 10, "HandgunPuff", FBF_USEAMMO | FBF_NORANDOM, 512)
		Goto Reload
	Reload:
		SHT2 B 7
		SHT2 C 7 A_CheckReload
		NULL AA 0 A_SpawnItemEx("ShotgunShellCasing", 0, 0, 32, random(1, 3), random(0, 1), 0, random(45, 60))  //Moved SSG Shell drop here. Previous method fucked up model interpolation -DS
		SHT2 D 7 A_OpenShotgun2
		SHT2 E 7
		SHT2 F 7 A_LoadShotgun2
		SHT2 G 6
		SHT2 H 6 A_CloseShotgun2
		SHT2 A 5 A_ReFire
		Goto Ready
	Flash:
		SHT2 I 3 BRIGHT A_Light1
		SHT2 J 4 BRIGHT A_Light2
		TNT1 A 28 A_Light0
		Stop
	Spawn:
		NULL A 0
		Stop
	}
}

Actor DoubleShotgunPickup : CustomInventory replaces SuperShotgun 21086
{
	Inventory.PickupMessage "You got the double shotgun!"
	Inventory.MaxAmount 0
	Inventory.respawntics 2100
	Inventory.pickupsound "misc/w_pkup"
	States
	{
	Spawn:
		SGN2 A -1
		Stop
	Pickup:
		NULL AA 0 A_JumpIfInventory("DoubleShotgun", 1, "Failure")
		NULL A 0 A_GiveInventory("DoubleShotgun", 1)
		NULL A 0 A_GiveInventory("ShotgunAmmo", 8)
		NULL A 0 A_GiveInventory("Refunds", 20)
		Stop
	Failure:
		NULL A 0
		Fail
	}
}