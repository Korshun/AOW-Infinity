// Fists
Actor Unarmed : AOWWeapon
{
	//$Category Weapons
	Tag "$TAG_UNARMED"
	Inventory.PickupMessage "$TAG_UNARMED"
	Obituary "$OB_UNARMED"
	Weapon.PreferredSkin "MarineFist"

	+WIMPY_WEAPON
	+MELEEWEAPON
	
	DropItem "Nothing"
	Weapon.Kickback 165
	Weapon.SelectionOrder 20

	States
	{
	Ready:
		PUNG A 1 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_Lower
		PUNG A 1 A_Lower
		Loop
	Select:
		NULL A 0 A_JumpIfInventory("PowerBerserk", 1, "Remove")
		NULL A 0 A_JumpIfInventory("FistReplaced", 1, "ReplaceCleanup")
		NULL A 0 A_Raise
		PUNG A 1 A_Raise
		Goto Select+2
	Fire:
		PUNG B 2 A_JumpIfInventory("FistSwitch", 1, 6)
		PUNG C 2
		NULL A 0 A_GiveInventory("FistSwitch", 1)
		PUNG D 5 A_CustomPunch(35, 1, 1, "FistPuff", 64)
		PUNG C 2
		PUNG B 5 A_ReFire
		Goto Ready
		PUNG E 2
		PUNG F 2
		NULL A 0 A_TakeInventory("FistSwitch", 1)
		PUNG G 5 A_CustomPunch(35, 1, 1, "FistPuff", 64)
		PUNG F 2
		PUNG E 5 A_ReFire
		Goto Ready
	Remove:
		PUNG A 1 offset(1, 32)
		NULL A 0 A_GiveInventory("BerserkFists", 1)
		NULL A 0 A_SelectWeapon("BerserkFists")
		NULL A 0 A_GiveInventory("FistReplaced", 1)
		Goto Deselect
	ReplaceCleanup:
		NULL A 0 A_TakeInventory("BerserkFists", 1)
		NULL A 0 A_TakeInventory("FistReplaced", 1)
		Goto Select
	Spawn:
		NULL A 0
		Stop
	}
}

Actor BerserkFists : Unarmed
{
	-WIMPY_WEAPON // well it's not, is it?
	Obituary "\cG%o \cGgot %p SHIT punched the fuck out by %k."
	Weapon.SelectionOrder 0
	States
	{
	Select:
		PUNG A 5
		NULL A 0 A_JumpIfInventory("PowerBerserk", 1, 1)
		Goto Remove
		NULL A 0 A_JumpIfInventory("FistReplaced", 1, "ReplaceCleanup")
		NULL A 0 A_Raise
		PUNG A 1 A_Raise
		Goto Select+4
	Ready:
		PUNG A 1 A_WeaponReady(WRF_NOSWITCH)
		Wait
	Fire:
		NULL A 0 A_JumpIfInventory("FistSwitch", 1, "Fire2")
		PUNG B 1 offset(-30, 40)
		PUNG B 1 offset(-24, 38)
		//PUNG C 1 offset(-16, 36)
		PUNG C 1 offset(-9, 35)
		NULL A 0 A_GiveInventory("FistSwitch", 1)
		PUNG D 1 offset(0, 33) A_CustomPunch(250, 1, 1, "BerserkPuff", 135)
		PUNG D 1 offset(15, 32)
		//PUNG C 1 offset(10, 37)
		PUNG C 1 offset(5, 38)
		PUNG B 1 offset(0, 39) A_ReFire
		Goto Ready
	Fire2:
		PUNG E 1 offset(30, 40)
		PUNG E 1 offset(24, 38)
		//PUNG F 1 offset(16, 36)
		PUNG F 1 offset(9, 35)
		NULL A 0 A_GiveInventory("FistSwitch", 1)
		PUNG G 1 offset(0, 33) A_CustomPunch(250, 1, 1, "BerserkPuff", 135)
		PUNG G 1 offset(-5, 32)
		//PUNG F 1 offset(1, 37)
		PUNG F 1 offset(6, 38) A_TakeInventory("FistSwitch", 1)
		PUNG E 1 offset(10, 39) A_ReFire
		Goto Ready
	Remove:
		PUNG A 1 offset(1, 32)
		NULL A 0 A_GiveInventory("Unarmed", 1)
		NULL A 0 A_SelectWeapon("Unarmed")
		NULL A 0 A_GiveInventory("FistReplaced", 1)
		Goto Deselect
	ReplaceCleanup:
		NULL A 0 A_TakeInventory("Unarmed", 1)
		NULL A 0 A_TakeInventory("FistReplaced", 1)
		Goto Select
	}
}

Actor FistSwitch : Boolean {}
Actor FistReplaced : Boolean {}

Actor FistPuff : BulletPuff
{
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	-ALLOWPARTICLES
	+PUFFONACTORS
	DamageType "Fist"
	States
	{
	Crash:
		NULL A 0 BRIGHT
		PUFF A 4 BRIGHT A_SpawnItem("FistWall")
		PUFF B 4 
		PUFF CD 4
		Stop
	Melee:
		NULL A 0 BRIGHT
		PUFF A 4 BRIGHT A_SpawnItem("FistHit")
		Stop
	Spawn:
		NULL A 0 BRIGHT
		PUFF A 4 BRIGHT A_SpawnItem("FistHit")
		Stop
	}
}

Actor BerserkPuff : FistPuff
{
	+EXTREMEDEATH
	States
	{
	Melee:
		NULL A 0
		NULL A 0 BRIGHT A_SpawnItem("FistHit")
		Stop
	}
}

Actor FistHit
{
	+CLIENTSIDEONLY
	+NOCLIP
	Radius 1
	Height 1
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 1 A_PlaySound("fist/hit")
		Stop
	}
}

Actor FistWall : FistHit
{
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 1 A_PlaySound("fist/wall")
		Stop
	}
}