/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * Portal
 *
 * The Utility-built Portal is a teleporter device. Build two to create a
 * portal link. Mechs cannot use this, as can't harvesters. (on the interest
 * of no quick cash-ups) They take a beating to take down, but have the
 * weakness that enemies can use them as well. The team distinguishing exists
 * only to serve as identification and limits.
 */

Actor Utility_PortalBlue
{
	+SOLID
	+SHOOTABLE
	+BUMPSPECIAL
	+NOBLOOD
	+ISMONSTER
	
	Radius 16
	Height 128
	Health 3000
	PainChance 255
	Mass 0x7FFFFFF
	DamageFactor "Disarm", 0.0
	DamageFactor "RazorWire", 0.0
	DamageFactor "RazorWire", 0.0
	DamageFactor "Fist", 0.5
	DamageFactor "Fist", 0.5
	DamageFactor "Mechstomp", 0.0
	DamageFactor "Explosion", 1.5
	DamageFactor "Explosion", 1.5
	DamageFactor "MechExplosionBlue", 2.5
	DamageFactor "MechExplosionRed", 2.5
	DamageFactor "Fire", 0.6
	DamageFactor "Fire", 0.6
	DamageFactor "Chemical", 0.6
	DamageFactor "Chemical", 0.6
	States
	{
	Spawn:
		NULL AA 0 ACS_ExecuteAlways(aow_portal_tidrequest, 0, TEAM_BLUE)
		TNT1 A 8 A_CheckSight("CantSeeShit")
		NULL A 0 A_JumpIf(waterlevel >= 1, "CantSeeShit")
		TNT1 A -1
		Stop
	CantSeeShit:
		NULL A 0 A_GiveInventory("Utility_PortalNotInSight", 1)
		TNT1 A -1
		Stop
	Inactive:
		NULL A 0 A_UnsetSolid
		NULL A 0 A_TakeInventory("IsActive", 1)
		TNT1 A 35 A_CheckTerrain
		Wait
	Active:
		NULL A 0 A_GiveInventory("IsActive", 1)
		NULL A 0 A_SetSolid
	Idle:
		NULL A 0 A_JumpIfHealthLower(1250, "Damaged")
		TNT1 A 35 A_CheckTerrain
		Loop
	Damaged:
		TNT1 AA 17 A_SpawnItemEx("Utility_PortalSparkCluster", 0, 0, random(16, 64), 0, 0, 0, 0, 0, (Health/5)+60)
		Loop
	Pain:
		NULL A 0 A_JumpIfInventory("IsActive", 1, "Idle")
		Goto Inactive+2
	Death:
		TNT1 A 1
		NULL A 0 A_PlaySound("weapons/rocklx")
		NULL A 0 A_SpawnItemEx("ExplosionMain")
		NULL A 0 ACS_ExecuteAlways(aow_script345, 0, 21, tid)
		TNT1 A 10
		Stop
	Disapproved:
		TNT1 A 20 A_PlaySound("misc/nope")
		Stop
	Death.Deconstruction:
		NULL A 0 A_NoBlocking
		NULL A 0 ACS_ExecuteAlways(aow_script345, 0, 21, tid)
		TNT1 A 2 A_PlaySound("utility/deconstruct")
		Stop
	}
}

Actor Utility_PortalRed : Utility_PortalBlue
{
	States
	{
	Spawn:
		NULL AA 0 ACS_ExecuteAlways(aow_portal_tidrequest, 0, TEAM_RED)
		TNT1 A 8 A_CheckSight("CantSeeShit")
		NULL A 0 A_JumpIf(waterlevel >= 1, "CantSeeShit")
		TNT1 A -1
		Stop
	Pain:
		NULL A 0 A_JumpIfInventory("IsActive", 1, "Active")
		Goto Inactive+2
	}
}

// This Actor is used to check whether the portal
// fits in the space it's wanted to be spawned at.
Actor Utility_PortalChecker
{
	+SOLID
	Radius 80
	Height 128
	States
	{
	Spawn:
		TNT1 A 5
		NULL A 0
		Stop
	}
}

// stands.. the thing needs some structure, doesn't it?
Actor Utility_PortalStand
{
	+SOLID
	Radius 12
	Height 44
	Gravity 10000
	States
	{
	Spawn:
		NULL A 0
		DGIZ A 8 A_CheckSight("CantSeeShit")
		NULL A 0 A_JumpIf(waterlevel >= 1, "CantSeeShit")
		DGIZ A -1
		Stop
	CantSeeShit:
		NULL A 0
		Stop
	Disapproved:
		"----" A 0 A_NoBlocking
		"----" A 2 A_SetTranslucent(1.0, 1)
		"----" A 2 A_FadeOut 
		Wait
	}
}

// Decorative spheres that spawn on top of the stands
Actor Utility_PortalSphereBlue
{
	+NOINTERACTION +NOGRAVITY
	+FORCEXYBILLBOARD
	RenderStyle add
	Alpha 0.0
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	Init.Active:
		GIZB CDABCD 3 BRIGHT A_FadeIn(0.1)
		Goto Active
	Init.Inactive:
		GIZB CCDD 3 A_FadeIn(0.1)
		Goto Inactive
	Active:
		NULL A 0 A_SetTranslucent(0.6)
		GIZB ABCD 3 BRIGHT
		Goto Active+1
	Inactive:
		NULL A 0 A_SetTranslucent(0.4)
		GIZB ABCD 6
		Goto Inactive+1
	}
}

Actor Utility_PortalSphereRed : Utility_PortalSphereBlue
{
	States
	{
	Init.Active:
		GIZR CDABCD 3 BRIGHT A_FadeIn(0.1)
		Goto Active
	Init.Inactive:
		GIZR CCDD 3 A_FadeIn(0.1)
		Goto Inactive
	Active:
		NULL A 0 A_SetTranslucent(0.6)
		GIZR ABCD 3 BRIGHT
		Goto Active+1
	Inactive:
		NULL A 0 A_SetTranslucent(0.4)
		GIZR ABCD 6
		Goto Inactive+1
	}
}

// Effect cluster, designates active portal
Actor Utility_PortalFX
{
	Gravity 10000
	Translation "112:127=[255,0,0]:[128,0,0]"
	States
	{
	Spawn:
	Active:
		TNT1 A 5
		TNT1 A 35 A_SpawnItemEx("Utility_PortalTelefogCluster", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		Wait
	Init.Inactive:
	Inactive:
		TNT1 A -1
		Stop
	}
}

Actor Utility_PortalFXBlue : Utility_PortalFX
{
	Translation "112:127=[64,64,128]:[24,24,128]"
}

Actor Utility_PortalTelefogCluster
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		NULL A 0
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Utility_PortalTelefog",
			32,0,random(-8, 8), 0, 0, random(2, 6), random(0, 360), SXF_TRANSFERTRANSLATION)
		Stop
	}
}

Actor Utility_PortalTelefog
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	RenderStyle add
	Alpha 0.75
	States
	{
	Spawn:
		TFOG ABCDEFGHIJ 3 BRIGHT A_FadeOut(0.05)
		Stop
	}
}

// Effect sparks, designates damaged portals.
Actor Utility_PortalSparkCluster
{
	+CLIENTSIDEONLY +NOCLIP
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_PlaySound("world/spark")
		NULL AAAAAAAAAAA 0 A_CustomMissile("Utility_PortalSpark", 0, 0, random(-180, 180), 2, random (8, 32))
		Goto Death
	Death:
		NULL A 0
		Stop
	}
}

Actor Utility_PortalSpark
{
	PROJECTILE
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOCLIP
	-NOGRAVITY
	Radius 8
	Height 8
	Speed 4
	RenderStyle add
	Alpha 0.75
	Scale 0.05
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 ThrustThingZ(0, random(15, 40), 0, 0)
		NULL A 0 A_Jump(256, "Spawn1", "Spawn2", "Spawn3", "Spawn4", "Spawn5", "Spawn6")
		Goto Spawn1
	Spawn1:
		SPKY A 15
		Goto Death
	Spawn2:
		SPKY B 12 BRIGHT
		Goto Death
	Spawn3:
		SPKY C 16 BRIGHT
		Goto Death
	Spawn4:
		SPKY D 14 BRIGHT
		Goto Death
	Spawn5:
		SPKY E 13 BRIGHT
		Goto Death
	Spawn6:
		SPKY F 11 BRIGHT
		Goto Death
	Death:
		"----" AAAAAAAA 1 A_FadeOut(0.05)
		Stop
	}
}

// Dummy item to mark the portal as invalid with
Actor Utility_PortalNotInSight : Boolean {}
Actor IsActive : Boolean {}