// Bomb pack
Actor BombPack : AOWWeapon
{
	Inventory.PickupMessage "Picked up the bomb pack... \cGwait what?!"
	Inventory.pickupsound "terrorist/pickup"
	Obituary "$OB_BOMBPACK_WTF" // "%o couldn't stand %k running around screaming."
	Weapon.SelectionOrder 1
	Weapon.Kickback 0
	Weapon.AmmoType "Bomb-PackAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 1
				Weapon.PreferredSkin "MarineBackpack"
	+NOEXTREMEDEATH
	+WEAPON.CHEATNOTWEAPON
	States
	{
	Ready:
		NULL AA 0 A_ChangeFlag("PICKUP", 0)
		NULL A 0 A_PlaySound("terrorist/scream", CHAN_ITEM, 1.0, true)
		Goto ReadyLoop
	ReadyLoop:
		SBOM A 1 A_WeaponReady(WRF_NOSWITCH)
		Wait
	Deselect:
		NULL A 0 A_StopSound(CHAN_ITEM)
		TNT1 A 1 A_Lower
		Wait
	Select:
		NULL A 0 A_PlayWeaponSound("terrorist/select")
		SBOM A 1 A_Raise
		Wait
	Fire:
		NULL A 0 A_TakeInventory("PowerInvulnerable", 1)
		NULL A 0 A_FireCustomMissile("DummyMissile")
		SBOM A 5 A_PlayWeaponSound("terrorist/activate")
		SBOM B 5
		SBOM C 30
		SBOM C 1 A_GiveInventory("IsTerrorist", 1)
		SBOM C -1 DamageThing (1000000)
		Stop
	Spawn:
		NULL A 0
		Stop
	}
}

Actor Bomb-PackAmmo : Ammo
{
	+INVENTORY.IGNORESKILL
	Inventory.icon "TNT1A0"
	Inventory.amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 0
}

Actor DummyMissile
{
	PROJECTILE
	Speed 0 Radius 0
	Height 0
	States
	{
	Spawn:
		NULL A 0
		Stop
	}
}

Actor IsTerrorist : Inventory
{
	Inventory.MaxAmount 1
}

Actor KamikazeExplosion
{
	Obituary "$OB_BOMBPACK" // "%o \cIwas wiped off the face of Earth by %k."
	PROJECTILE
	+NOGRAVITY +NOINTERACTION
	Damage 100
	DamageType "Explosion"
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("KamikazeExplosionFX")
		NULL A 0 A_Explode(7000, 128)
		NULL A 0 A_Explode(2000, 256)
		TNT1 A 10 A_Explode(400, 1536)
		Stop
	}
}

Actor KamikazeExplosionBlue : KamikazeExplosion {DamageType "Explosion"}
Actor KamikazeExplosionFX
{
	+CLIENTSIDEONLY +NOINTERACTION +NOGRAVITY
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("BombPackQuake")
		NULL A 0 A_SpawnItemEx("NukeMushroom")
		NULL A 0 A_SpawnItemEx("ExplosionMain", random (-48, 48), random(-48, 48), random (16, 32))
		NULL A 0 A_PlaySound("weapons/rocklx", CHAN_BODY)
		NULL A 0 A_PlaySound("weapons/rocklx", CHAN_WEAPON)
		NULL AA 0 A_SpawnItemEx("BombPackSmoke", 0, 0, random (32, 64), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		NULL AAA 0 A_SpawnItemEx("BombPackSmoke", 0, 0, random (64, 96), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		NULL AAAA 0 A_SpawnItemEx("BombPackSmoke", 0, 0, random (96, 128), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		NULL AAAAA 0 A_SpawnItemEx("BombPackSmoke", 0, 0, random (128, 144), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		NULL AAAAAA 0 A_SpawnItemEx("BombPackSmoke", 0, 0, random (144, 160), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		NULL AAAAAAA 0 A_SpawnItemEx("BombPackSmoke", 0, 0, random (160, 192), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		NULL AAAAAAAA 0 A_SpawnItemEx("BombPackSmoke", 0, 0, random (192, 224), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		NULL AAAAAAAAAA 0 A_SpawnItemEx("BombPackSmoke", 0, 0, random (224, 256), random (-1.5, 1.5), random (-1.5, 1.5))
		Stop
	}
}

Actor BombPackSmoke : NukeSmoke
{
	+FORCEXYBILLBOARD
	Scale 1.5
}

Actor BombPackQuake
{
	+CLIENTSIDEONLY +NOSECTOR +NOCLIP +NOINTERACTION
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 10 Radius_Quake(9, 10, 0, 24, 0)
		TNT1 A 10 Radius_Quake(8, 10, 0, 20, 0)
		TNT1 A 10 Radius_Quake(7, 10, 0, 19, 0)
		TNT1 A 10 Radius_Quake(6, 10, 0, 18, 0)
		TNT1 A 10 Radius_Quake(5, 10, 0, 16, 0)
		TNT1 A 10 Radius_Quake(3, 10, 0, 12, 0)
		TNT1 A 10 Radius_Quake(1, 10, 0, 6, 0)
		Stop
	}
}

// given on resign to take Player out of 'terrorism mode'
Actor NoMoreTerrorism : CustomInventory
{
	Inventory.amount 0
	Inventory.MaxAmount 999999
	+AUTOACTIVATE
	+ALWAYSPICKUP
	States
	{
	Spawn:
		NULL A 0
		Stop
	Pickup:
		TNT1 A 3 A_ChangeFlag("PICKUP", 1)
		TNT1 A 5 A_StopSound(CHAN_ITEM)
		Stop
	}
}

// old stuff
Actor BombPackKamikaze
{
	Obituary "%o was killed."
	Radius 13
	Height 8
	Speed 0
	Damage 0
	RenderStyle Add
	Alpha 0.75
	DeathSound "weapons/rocklx"
	PROJECTILE
	-RANDOMIZE
	+STRIFEDAMAGE
	//+FULLVOLDEATH
	+NOEXTREMEDEATH
	+NODAMAGETHRUST
	DamageType "Terrorist"
	Scale 2.0
	States
	{
	Spawn:
		NULL A 0 BRIGHT
		Goto Death
		NULL A 0 BRIGHT A_SetShootable
		NULL A 0 BRIGHT A_Die
		Stop
	Death:
		NULL A 0
		NULL A 0 A_SpawnItemEx("ExplosionMainLarge", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		NULL A 0 A_PlaySound("weapons/rocklx")
		NULL A 0 A_PlayWeaponSound("weapons/rocklx")
		NULL A 0 BRIGHT A_Explode(100, 512, 0)
		NULL A 0 BRIGHT A_Explode(700, 128, 0)
		TNT1 A 1 BRIGHT A_Explode(700, 256, 0)
		NULL AAAAA 0 A_SpawnItemEx("FlameWallDecay", random(-48, 48), random(-48, 48), 4, 0, 0, 0, 0, 0, 0)
		NULL A 0 A_SpawnItemEx("BombPackKamikazeSuicide", -1, 0, 0, 0, 0, 0, 0, 0, 0)
		Stop
	}
}

Actor BombPackKamikazeSuicide
{
	RenderStyle None
	-RANDOMIZE
	+NOCLIP
	PROJECTILE
	Speed 1
	Damage 0
	Scale 0.7
	DamageType "Suicide"
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 5 BRIGHT A_Explode(250, 16, 1)
		Stop
	}  
}