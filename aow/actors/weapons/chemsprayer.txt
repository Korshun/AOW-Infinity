// Chemical sprayer
Actor ChemicalSprayer : AOWWeapon 21066
{
        //$Category Weapons
	Tag "Chemical Sprayer"
	+ALT_AMMO_OPTIONAL
	Inventory.PickupMessage "You got the chemical sprayer!"
	AttackSound "Chem/fire"
	Weapon.SelectionOrder 9
	Weapon.Kickback 1
	Weapon.AmmoType "ChemicalAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 25
	Weapon.AmmoType2 "ChemicalGrenadeAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 0
	Weapon.PreferredSkin "MarinePlasma"
	States
	{
	Ready:
		NULL A 0 A_JumpIfInventory("ChemSprayReady", 1, "Idle")
		CHPA ABC 2
		NULL A 0 A_GiveInventory("ChemSprayReady")
	Idle:
		NULL A 0 A_StopSoundEx("Weapon")
		CHPA C 1 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_TakeInventory("ChemSprayReady")
		NULL A 0 A_StopSoundEx("Weapon")
		CHPA C 1 A_Lower
		Goto Deselect+1
	Select:
		CHPA A 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_PlaySound("chem/fire", CHAN_WEAPON, 1.0, 1)
		NULL A 0 A_GunFlash
		CHMG A 1 BRIGHT A_FireCustomMissile("ChemicalShot", random(-2, 2), 0, 8, random(-2, 2))
		CHMG A 1 BRIGHT A_FireCustomMissile("ChemicalShotDummy", random(-2, 2), 0, 8, random(-2, 2))
		CHMG B 1 BRIGHT A_FireCustomMissile("ChemicalShot", random(-2, 2), 1, 8, random(-2, 2))
		CHMG B 1 BRIGHT A_FireCustomMissile("ChemicalShotDummy", random(-2, 2), 0, 8, random(-2, 2))
		CHMG C 1 A_ReFire
		CHMG CD 3
		NULL A 0 A_StopSoundEx("Weapon")
		Goto Ready
	AltFire:
		NULL A 0 A_JumpIfNoAmmo("AltRejected")
		CHPA C 15
		CHGR B 6 A_PlayWeaponSound("chem/grenade")
		CHGR A 8 BRIGHT A_FireCustomMissile("ChemicalGrenade", 0, 1, 8, 0)
		CHGR BC 3
		NULL A 0 A_ReFire
		Goto Ready
	AltRejected:
		CHPA C 35 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		NULL A 0 A_Light0
		Stop
	Spawn:
		CHMG X -1
		Stop
	}
}

Actor ChemicalAmmo : Ammo
{
	Inventory.amount 50
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 50
	Ammo.BackpackMaxAmount 200
	States
	{
	Spawn:
		NULL A 0
		Stop
	}
}

Actor ChemicalGrenadeAmmo : Ammo
{
	Inventory.amount 1
	Inventory.MaxAmount 4
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 4
	States
	{
	Spawn:
		NULL A 0
		Stop
	}
}

Actor ChemSprayReady : Inventory {Inventory.MaxAmount 1}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
Actor ChemicalShot
{
	PROJECTILE
	+FORCEXYBILLBOARD
	Obituary "%o was poisoned by %k's chemical sprayer."
	RenderStyle Add
	Alpha 0.9
	Radius 8
	Height 8
	Speed 25
	Damage (5)
	DamageType "Chemical"
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 4
		TIBX ABCDEFGHIJK 2 BRIGHT
	Clear:
		NULL A 0 A_SpawnItemEx("AnthraxDecay", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 240)
		TIBX L 2 BRIGHT
		TIBX MMNNOOPPQQ 1 BRIGHT A_FadeOut(0.1)
		Stop
	Death:
		TNT1 A 1 //A_Explode(2, 172)
		NULL A 0 A_SpawnItemEx("AnthraxDecay", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 240)
		TIBX H 3 BRIGHT A_Stop
		TIBX IIJJKKLLMMOOPPQQ 1 BRIGHT A_FadeOut(0.07)
		Stop
	}
}

Actor ChemicalShotDummy
{
	PROJECTILE
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	-ACTIVATEIMPACT -ACTIVATEPCROSS
	RenderStyle Add
	Alpha 0.9
	Radius 8
	Height 8
	Speed 25
	Damage (0)
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 4
		TIBX ABCDEFGHIJKL 2 BRIGHT
		TIBX MMNNOOPPQQ 1 BRIGHT A_FadeOut(0.1)
		Stop
	Death:
		TIBX H 3 BRIGHT A_Stop
		TIBX IIJJKKLLMMOOPPQQ 1 BRIGHT A_FadeOut(0.07)
		Stop
	}
}

Actor AnthraxDecay
{
	RenderStyle None
	Alpha 0.5
	+NOBLOCKMAP
	+LOWGRAVITY
	+NODAMAGETHRUST
	Scale 2.0
	DamageType "Chemical"
	Obituary "%o chokes in %k's chemicals."
	States
	{
	Spawn:
		NULL AA 0 A_PlaySound("chem/chem")
		NULL A 0 A_SpawnItemEx("AnthraxDecayFX")
		TNT1 AAAAAAAAAAA 15 BRIGHT A_Explode(4, 128)
		Stop
	}
}

Actor AnthraxDecayFX
{
	+LOWGRAVITY +NOBLOCKMAP
	+CLIENTSIDEONLY
	ReactionTime 33
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 5 A_SpawnItemEx("ChemicalFlame", random(-16, 16), random(-16, 16), random(8, 16), 0, 0, random(1, 3), 0, 128, 32)
		NULL A 0 A_Countdown
		Loop
	Death:
		NULL A 0
		Stop
	}
}

Actor AnthraxDecayBlue : AnthraxDecay {DamageType "Chemical"}
Actor AnthraxDecaySpecial : AnthraxDecay {DamageType "ChemicalSpecial"} // neutral

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
Actor ChemicalGrenade
{
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	+LOWGRAVITY
	Radius 11
	Height 8
	Speed 35
	Damage (30)
	SeeSound ""
	DeathSound "weapons/rocklx"
	Obituary "%o was mutated by %k's chemical grenade."
	DamageType "Chemical"
	Decal "ChemScorchStrong"
	Translation "80:111=120:127"
	States
	{
	Spawn:
		SGRN A 2 BRIGHT A_SpawnItemEx("PulseBallSmokePuff", -25)
		Loop
	Death:
		NULL A 0 BRIGHT A_SpawnItem("ChemicalExplosion")
		NULL A 0 BRIGHT A_Gravity
		NULL A 0 BRIGHT A_Explode(90, 128, 1)
		TNT1 AAAAAAAAA 70 A_SpawnItemEx("AnthraxDecay", random(-64, 64), random(-64, 64), 4)
		NULL A 0
		Stop
	}
}
