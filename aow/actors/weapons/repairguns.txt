// Repair gun
Actor RepairGun : AOWWeapon
{
	Weapon.Kickback 0
	Inventory.pickupsound "misc/w_pkup"
	Obituary "%o was repaired to death by %k."
	AttackSound "repairgun/fire"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.PreferredSkin "MarineBackpack"
	DropItem "EngineerPack"
	States
	{
	Ready:
		NULL A 0 A_JumpIfInventory("RepairGunUpgrade", 1, "ReadyEnhanced")
		REP1 A 35 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_JumpIfInventory("RepairGunUpgrade", 1, "DeselectEnhanced")
		REP1 A 1 A_Lower
		Wait
	Select:
		NULL A 0 A_JumpIfInventory("RepairGunUpgrade", 1, "SelectEnhanced")
		REP1 A 1 A_Raise
		Wait
	Fire:
		NULL A 0 A_JumpIfInventory("RepairGunUpgrade", 1, "FireEnhanced")
		REP1 C 2
		NULL A 0 BRIGHT A_FireBullets(0, 0, 1, 2, "RepairDisarmPuff", 0, 800)
		NULL A 0 BRIGHT ACS_ExecuteAlways(aow_script650, 0, 10, 0)
		REP1 B 2 BRIGHT ACS_ExecuteAlways(aow_script654, 0) // display target Health
		REP1 C 2 A_Refire
		Goto Ready
	AltFire:
		NULL A 0 A_JumpIfInventory("RepairGunUpgrade", 1, 3)
		REP1 A 1 A_JumpIfTargetInLOS("Fire")
		NULL A 0 A_Jump(256, 2)
		REP2 A 1 A_JumpIfTargetInLOS("Fire")
		NULL A 0
		Goto Ready
	ReadyEnhanced:
		REP2 A 1 A_WeaponReady
		Wait
	DeselectEnhanced:
		REP2 A 1 A_Lower
		Wait
	SelectEnhanced:
		REP2 A 1 A_Raise
		Wait
	FireEnhanced:
		REP2 C 1
		NULL A 0 BRIGHT A_FireBullets(0, 0, 1, 1, "EnhancedRepairDisarmPuff", 0, 800)
		NULL A 0 BRIGHT ACS_ExecuteAlways(aow_script650, 0, 10, 0)
		REP2 B 1 BRIGHT ACS_ExecuteAlways(aow_script654, 0)
		REP2 A 2 A_Refire
		Goto Ready
	}
}
Actor RepairGunUpgrade : Boolean {}

Actor HealGun : RepairGun
{
	Inventory.PickupMessage "Picked up the Healing Gun."
	Obituary "%o suffers from medicophobia."
	Weapon.PreferredSkin "MarineMedic"
	DropItem "MedicPack"
	States
	{
	Spawn:
		NULL A 0
		Stop
	Ready:
		REP4 A 1 A_WeaponReady
		Loop
	Deselect:
		REP4 A 1 A_Lower
		Loop
	Select:
		REP4 A 1 A_Raise
		Loop
	AltFire:
		NULL A 0 A_JumpIfTargetInLOS("Fire")
		Goto Ready
	Fire:
		REP4 C 2
		NULL A 0 BRIGHT A_FireBullets(0, 0, 1, 0, "MedicPuff", 0, 800) // visual ONLY
		NULL A 0 BRIGHT ACS_ExecuteAlways(aow_script650, 0, 6, 1) // actually does the healing
		REP4 B 2 BRIGHT ACS_ExecuteAlways(aow_script654, 0) // display target Health
		REP4 C 2 A_Refire
		Goto Ready
	}
}

Actor BombSquadGun : RepairGun
{
	Inventory.PickupMessage "Picked up the Bomb-Squad Gun."
	AttackSound "repairgun/fire"
	Obituary "%o was disarmed by %k."
	+THRUGHOST
	States
	{
	Ready:
		REP3 A 1 A_WeaponReady
		Loop
	Deselect:
		REP3 A 1 A_Lower
		Loop
	Select:
		REP3 A 1 A_Raise
		Loop
	Fire:
		REP3 C 2
		REP3 B 2 BRIGHT A_FireBullets(2.0, 2.0, 1, 6, "DisarmPuff", 0, 500)
		REP3 C 2 A_Refire
		Goto Ready
	AltFire: Stop
	}
}

actor RemoverGunHeat : Ammo
{
	Inventory.Maxamount 100
	Inventory.Amount 1
	+IGNORESKILL
}

actor RemoverGun : RepairGun {
	AttackSound "repairgun/fire"
	Weapon.Ammotype "RemoverGunHeat"
	+AMMO_OPTIONAL
	States {
	Ready:
		NULL A 0 A_TakeInventory("RemoverGunHeat",3)
		REP5 AAAAA 1 A_WeaponReady
		Loop
	Deselect:
		REP5 A 1 A_Lower
		Loop
	Select:
		REP5 A 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "Ready")
		NULL A 0 A_JumpIfInventory ("InMenu", 1, "Ready")
		NULL A 0 A_JumpIfInventory ("RemoverGunHeat", 100, "Cool")
		REP5 C 2
		NULL A 0 A_GiveInventory("RemoverGunHeat",1)
		REP5 B 2 bright A_FireBullets (0.0, 0.0, 1, 1, "DeconstructionPuff", 1, 250)
		REP5 C 2 A_Refire
		Goto Ready
	Cool:
		REP5 A 35 A_PlaySound("Plasma/FailFire")
		NULL A 0 A_PlaySound("Plasma/Vapour")
	CoolingLoop:
		REP5 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 4 A_TakeInventory("RemoverGunHeat",2)
		Goto Ready
	AltFire:
		NULL A 0 A_JumpIfTargetInLOS ("Fire")
		Goto Ready
	}
}

Actor DisarmPuff
{
	+NOINTERACTION
	+PUFFONACTORS
	+BLOODLESSIMPACT
	+FORCEXYBILLBOARD
	+ALWAYSPUFF
	+THRUGHOST
	+PUFFGETSOWNER
	+NODAMAGETHRUST
	RenderStyle Add
	Alpha 0.5
	Scale 0.25
	DamageType "Disarm"
	Translation "112:127=160:167"
	States
	{
	Spawn:
		CHMX AA 1 A_Explode(9, 64)
		CHMX BC 2 BRIGHT
	Melee:
		CHMX DEF 2 BRIGHT
		Stop
	}
}
Actor RepairDisarmPuff : DisarmPuff
{
	Translation "0:255=0:255"
	States
	{
	Spawn:
		PLCX AA 1 A_Explode(9, 64)
		PLCX BC 2 BRIGHT
	Melee:
		PLCX DEF 2 BRIGHT
		Stop
	}
}

Actor EnhancedRepairDisarmPuff : DisarmPuff
{
	Translation "112:127=250:254"
	States
	{
	Spawn:
		CHMX AA 1 A_Explode(7, 64)
		CHMX BC 2 BRIGHT
	Melee:
		CHMX DEF 2 BRIGHT
		Stop
	}
}

Actor MedicPuff : DisarmPuff
{
	Translation "112:127=174:189"
	States
	{
	Spawn:
		CHMX ABC 2 BRIGHT
		Goto Melee
	}
}

actor DeconstructionPuff : DisarmPuff {
        Translation "0:255=0:255"
	DamageType "Deconstruction"
	States {
	Spawn:
		NULL A 0
		NULL A 0 A_Explode (9, 128)
		DCPF ABC 2 bright
	Melee:
		DCPF DEF 2 bright
		stop
	}
}
