// LASER RIFLE
Actor StealthLaserRifle : AOWWeapon
{
        Tag "Laser Rifle (Stealth)"
	+NOEXTREMEDEATH
	+ALT_AMMO_OPTIONAL
	DropItem "LaserRifle"
	Inventory.PickupMessage "You got the laser rifle!"
	Obituary "%o was burned by %k's laser rifle."
	Weapon.SelectionOrder 8
	Weapon.Kickback 100
	Weapon.AmmoType "LaserRifleAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoType2 "StealthCharge"
	Weapon.AmmoUse2 3
	Weapon.AmmoGive2 100
	Weapon.slotnumber 8
	Weapon.PreferredSkin "MarineGrenade"
	States
	{
	Ready:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "Ready.Blue")
		NULL A 0 A_JumpIfInventory("StealthMode", 1, "Ready.Stealth")
		LSRF A 35 A_WeaponReady
		NULL A 0 A_GiveInventory("StealthCharge", 1)
		NULL A 0 A_TakeInventory("StealthFire", 1)
		Goto Ready+1
	Ready.Blue:
		NULL A 0 A_JumpIfInventory("StealthMode", 1, "Ready.StealthBlue")
		LSRB A 35 A_WeaponReady
		NULL A 0 A_GiveInventory("StealthCharge", 1)
		NULL A 0 A_TakeInventory("StealthFire", 1)
		Loop
	Ready.Stealth:
		NULL A 0 A_JumpIfInventory("StealthCharge", 1, 1)
		Goto AltFireOff
		LSRF A 35 A_WeaponReady
		NULL A 0 A_TakeInventory("StealthCharge", 1)
		Goto Ready
	Ready.StealthBlue:
		NULL A 0 A_JumpIfInventory("StealthCharge", 1, 1)
		Goto AltFireOff
		LSRB A 35 A_WeaponReady
		NULL A 0 A_TakeInventory("StealthCharge", 1)
		Goto Ready.Blue
	Deselect:
		NULL A 0 A_TakeInventory("StealthFire", 1)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 3)
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "Deselect.Blue")
		LSRF A 1 A_Lower
		Wait
	Deselect.Blue:
		LSRB A 1 A_Lower
		Wait
	Select:
		NULL A 0 A_TakeInventory("StealthFire", 1)
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "Select.Blue")
		LSRF A 1 A_Raise
		Wait
	Select.Blue:
		LSRB A 1 A_Raise
		Wait
	Fire:
		NULL A 0 A_JumpIfInventory("StealthMode", 1, "Fire.Stealth")
	FireGo:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "Fire.Blue")
		NULL A 0 A_PlayWeaponSound("laser/fire")
		LSRF B 1 BRIGHT offset(0, 34) A_Light2
		NULL A 0 BRIGHT offset(0, 34) A_FireBullets(0, 0, 1, 0, "LaserPuff", FBF_NORANDOM, 5000)
		LSRF B 1 BRIGHT offset(0, 36) A_RailAttack(16, 0, 1, none, Red, 1, 0, "LaserPuff")
		LSRF C 2 BRIGHT offset(0, 34)
		LSRF A 5 offset(0, 32) A_Light0
		NULL A 0 A_Refire
		LSRF D 3 offset(3, 34)
		LSRF D 3 offset(1, 31)
		LSRF E 2 offset(-2, 28)
		LSRF E 2 offset(-6, 27)
		LSRF FFFF 2 A_FireCustomMissile("OverheatSmokeSpawner", 0, false)
		LSRF F 1 offset(-3, 30)
		LSRF F 1 offset(-1, 33)
		LSRF F 1 offset(2, 37)
		LSRF F 1 offset(5, 42)
		LSRF F 1 offset(7, 43)
		LSRF F 1 offset(9, 44)
		Goto Restealth
	Fire.Blue:
		NULL A 0 A_PlayWeaponSound("laser/fire")
		LSRB B 1 BRIGHT offset(0, 34) A_Light2
		NULL A 0 BRIGHT offset(0, 34) A_FireBullets(0, 0, 1, 0, "LaserPuffBlue", FBF_NORANDOM, 5000)
		LSRB B 1 BRIGHT offset(0, 36) A_RailAttack(16, 0, 1, none, "40 40 FF", 1, 0, "LaserPuffBlue")
		LSRB C 2 BRIGHT offset(0, 34)
		LSRB A 5 offset(0, 32) A_Light0
		NULL A 0 A_Refire
		LSRB D 3 offset(3, 34)
		LSRB D 3 offset(1, 31)
		LSRB E 2 offset(-2, 28)
		LSRB E 2 offset(-6, 27)
		LSRB FFFF 2 A_FireCustomMissile("OverheatSmokeSpawner", 0, false)
		LSRB F 1 offset(-3, 30)
		LSRB F 1 offset(-1, 33)
		LSRB F 1 offset(2, 37)
		LSRB F 1 offset(5, 42)
		LSRB F 1 offset(7, 43)
		LSRB F 1 offset(9, 44)
		Goto Restealth
	Fire.Stealth:
		NULL A 0 A_JumpIfInventory("StealthMode", 3, "FireGo")
		NULL A 0 A_GiveInventory("StealthFire", 1)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 3)
		Goto FireGo
	Restealth:
		NULL A 0 A_JumpIfInventory("StealthFire", 1, 2)
		Goto Ready
		NULL A 0 A_TakeInventory("StealthFire", 1)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 0)
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "Restealth.Blue")
		LSRF AAAAAAAAAAAAAAA 1 A_WeaponReady
		Goto Ready
	Restealth.Blue:
		LSRB AAAAAAAAAAAAAAA 1 A_WeaponReady
		Goto Ready.Blue
	AltFire:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "AltFire.Blue")
		NULL A 0 A_JumpIfNoAmmo("AltRejected")
		LSRF A 5 A_JumpIfInventory("StealthMode", 1, "AltFireOff")
		LSRF A 50 ACS_ExecuteAlways(aow_stealth, 0)
		Goto Ready
	AltFireOff:
		LSRF A 5 A_JumpIfInventory("StealthMode", 3, "AltRejected")
		LSRF A 5
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 1)
		LSRF A 40 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	AltRejected:
		LSRF A 16 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	AltFire.Blue:
		NULL A 0 A_JumpIfNoAmmo("AltRejected.Blue")
		LSRB A 5 A_JumpIfInventory("StealthMode", 1, "AltFireOff.Blue")
		LSRB A 50 ACS_ExecuteAlways(aow_stealth, 0)
		Goto Ready.Blue
	AltFireOff.Blue:
		LSRB A 5 A_JumpIfInventory("StealthMode", 3, "AltRejected.Blue")
		LSRB A 5
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 1)
		LSRB A 40 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready.Blue
	AltRejected.Blue:
		LSRB A 16 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready.Blue
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItem("LaserRifle")
		Stop
	}
}

Actor LaserRifle : StealthLaserRifle 21050
{
        Tag "Laser Rfile"
	Weapon.AmmoType2 ""
	Weapon.SelectionOrder 9
	States
	{
	AltFire: Stop
	Spawn:
		LSRF X -1
		Stop
	}
}

Actor LaserRifleAmmo : Ammo
{
	Inventory.amount 20
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 200
	States
	{
	Spawn:
		NULL A 0
		Stop
	}
}

Actor LaserPuff : AOWBulletPuff
{
	-ALLOWPARTICLES +PUFFONACTORS
	Scale 0.5
	DamageType "Laser"
	Translation "192:207=169:189", "240:246=45:47", "80:111=169:189"
	RenderStyle Add
	Alpha 0.75
	VSpeed 0

	States
	{
	Spawn:
	Melee:
	Crash:
		VOLX ABCDEFEDCBA 2 BRIGHT
		Stop
	}
}

Actor LaserPuffBlue : LaserPuff
{
	DamageType "Laser"
	Translation "0:255=0:255"
}

Actor StealthCharge : Ammo
{
	+IGNORESKILL
	Inventory.amount 0
	Inventory.MaxAmount 100
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 100
}
