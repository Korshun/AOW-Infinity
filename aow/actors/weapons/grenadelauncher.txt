// Grenade launcher
Actor Grenade_Launcher : AOWWeapon replaces GrenadeLauncher 21046
{
        //$Category Weapons
	Tag "Grenade Launcher"
	+WEAPON.NOAUTOAIM
	Obituary "$OB_GRENADE" // "%o ate %k's grenade."
	Inventory.PickupMessage "You got the grenade launcher!"
	Weapon.SelectionOrder 12
	Weapon.Kickback 100
	Weapon.AmmoType "GrenadeAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "GrenadeAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 2
	Weapon.PreferredSkin "MarineRevenant"
	States
	{
	Spawn:
		GREN X -1
		Stop
	Select:
		GREN A 1 A_Raise
		Wait
	Deselect:
		GREN A 1 A_Lower
		Wait
	Ready:
		GREN A 1 A_WeaponReady
		Wait
	Fire:
		GREN A 6
		GREN A 5 A_PlayWeaponSound("grenade/fire")
		GREN GH 1
		NULL A 0 A_GunFlash
		GREN I 1 A_FireCustomMissile("GrenadeGrenade", 0, 1, 12)
		GREN DEF 2
		GREN EDABC 1
		NULL A 0 A_ReFire
		Goto Ready
	AltFire:
		GREN A 7
		GREN A 4 A_PlayWeaponSound("grenade/fire")
		GREN GH 1
		NULL A 0 A_GunFlash
		GREN I 1 A_FireCustomMissile("GrenadeBounce", 0, 1, 12)
		GREN DEF 2
		GREN EDABC 1
		NULL A 0 A_ReFire
		Goto Ready
	}
}

// orca still uses this
Actor GrenadeSwitch : Boolean {}

Actor GrenadeAmmo : Ammo
{
	 Inventory.Amount 6
	 Inventory.MaxAmount 40
	 Ammo.BackpackAmount 5
	 Ammo.BackpackMaxAmount 40
	 States
	{
	 Spawn:
		NULL A 0
		Stop
	 }
}

Actor GrenadeGrenade
{
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	+LOWGRAVITY
	Radius 11
	Height 8
	Speed 35
	Damage (25)
	DamageType "Rifle"
	SeeSound ""
	DeathSound "weapons/rocklx"
	Obituary "$OB_GRENADE" // "%o ate %k's grenade."
	Decal "Scorch"
	States
	{
	Spawn:
		SGRN A 2 BRIGHT A_SpawnItemEx("PulseBallSmokePuff", -25)
		Loop
	Death:
		NULL A 0 A_NoGravity
		NULL A 0 A_Explode(55, 128, 1)
		NULL A 0 A_RadiusThrust(384, 128, 1) //Weeee
		TNT1 A 5 A_SpawnItemEx("ExplosionMain")
		Stop
	}
}

Actor GrenadeBounce : GrenadeGrenade
{
	+DOOMBOUNCE
	-LOWGRAVITY
	Obituary "$OB_GRENADE_ALT" // "%o played catch with %k's grenade."
	Damage (5)
	BounceCount 2
	States
	{
	Death:
		NULL A 0 A_NoGravity
		NULL A 0 A_Explode(40, 96, 1)
		NULL A 0 A_RadiusThrust(384, 128, 1) //Wee
		TNT1 A 5 A_SpawnItemEx("ExplosionMain")
		Stop
	}
}


// Select/Deselect offset-animations
/*
	Select:
		GREN A 1 offset(130, 154)
		GREN A 1 offset(120, 139)
		NULL A 0 A_PlaySoundEx("Bazooka/Select", "Body")
		GREN A 1 offset(110, 125)
		GREN A 1 offset(100, 112)
		GREN A 1 offset(90, 100)
		GREN A 1 offset(80, 89)
		GREN A 1 offset(70, 79)
		GREN A 1 offset(60, 70)
		GREN A 1 offset(50, 62)
		GREN A 1 offset(40, 55)
		GREN A 1 offset(30, 49)
		GREN A 1 offset(20, 44)
		GREN A 1 offset(15, 40)
		GREN A 1 offset(10, 37)
		GREN A 1 offset(5, 34)
		GREN A 1 offset(0, 0)
		Goto Ready
	Deselect:
		GREN A 1 offset(4, 34)
		GREN A 1 offset(8, 40)
		GREN A 1 offset(16, 49)
		GREN A 1 offset(24, 62)
		GREN A 1 offset(32, 79)
		GREN A 1 offset(40, 100)
		GREN A 1 offset(48, 125)
		GREN A 1 offset(56, 154)
		NULL A 0 offset(1, 32)
		TNT1 A 1 A_Lower
		Wait
*/
