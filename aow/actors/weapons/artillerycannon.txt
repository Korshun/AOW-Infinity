/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * Artillery Cannon (previously named "Mass Driver")
 *
 * The Artillery Cannon is a shoulder-carried howitzer that fires
 * high-powered explosive shells. Alt-Fire shoots heavier shells geared
 * against mechs - these shells are the second most effective infantry-held
 * anti-mech weapon, only Plasma Cannon surpasses this.
 */

Actor ArtilleryCannonSpeedBonus : Boolean {}

Actor ArtilleryCannon : AOWWeapon 21085
{
	Obituary "$OB_ARTILLERYCANNON" // "%o was demolished by %k's artillery cannon."
	Inventory.PickupMessage "You got the artillery cannnon!"
	Weapon.AmmoType "ArtilleryShellAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType2 "ArtilleryShellAmmo"
	Weapon.AmmoUse2 1
	Weapon.PreferredSkin "MarineSRockets"
	Weapon.SelectionOrder 11
	Scale 0.6
	States
	{
	Select:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "SelectBlue")
		MASR A 1 A_Raise
		Wait
	SelectBlue:
		MASB A 1 A_Raise
		Wait	
	Deselect:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "DeselectBlue")
		MASR A 1 A_Lower
		Wait
	DeselectBlue:
		MASB A 1 A_Lower
		Wait
	Ready:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "ReadyBlue")
		MASR A 1 A_WeaponReady
		Wait
	ReadyBlue:
		MASB A 1 A_WeaponReady
		Wait
	Fire:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "Fire.Blue")
		MASR A 1 A_PlayWeaponSound("artillery/fire")
		MASR B 1 A_GunFlash
		MASR B 2 BRIGHT A_FireCustomMissile("ArtilleryShell", 0, 1, 8)
		Goto Fire.End
	Fire.Blue:
		MASB A 1 A_PlayWeaponSound("artillery/fire")
		MASB B 1 A_GunFlash
		MASB B 2 BRIGHT A_FireCustomMissile("ArtilleryShellBlue", 0, 1, 8)
		Goto Fire.EndBlue
	Fire.End:
		//MASR C 1 BRIGHT A_Recoil(5)
		//MASR D 1 BRIGHT
		//MASR E 1 BRIGHT
		//MASR A 2
		MASR C 1 BRIGHT offset(3, 35) A_Recoil(5)
		MASR D 1 BRIGHT offset(6, 38) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASR E 1 BRIGHT offset(9, 41)
		MASR A 1 offset(12, 44) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASR A 1 offset(15, 47)
		MASR A 1 offset(18, 50) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASR A 1 offset(15, 47)
		MASR A 1 offset(12, 44) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASR A 1 offset(9, 41)
		MASR A 1 offset(6, 38) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASR A 1 offset(3, 35)
		MASR A 1 offset(0, 32) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		NULL A 0 A_JumpIfInventory("ArtilleryCannonSpeedBonus", 1, "Fire.EndFast")
		MASR A 23
		MASR A 10 A_PlaySound("artillery/reload", 5)
		MASR A 18 A_SpawnItemEx("ArtilleryCasing", 0, 8, 32, random(1, 3), random(0, 1), 0, random(45, 60), 128)
		NULL A 0 A_ReFire
		Goto Ready
	Fire.EndBlue:
		MASB C 1 BRIGHT offset(3, 35) A_Recoil(5)
		MASB D 1 BRIGHT offset(6, 38) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASB E 1 BRIGHT offset(9, 41)
		MASB A 1 offset(12, 44) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASB A 1 offset(15, 47)
		MASB A 1 offset(18, 50) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASB A 1 offset(15, 47)
		MASB A 1 offset(12, 44) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASB A 1 offset(9, 41)
		MASB A 1 offset(6, 38) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASB A 1 offset(3, 35)
		MASB A 1 offset(0, 32) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		NULL A 0 A_JumpIfInventory("ArtilleryCannonSpeedBonus", 1, "Fire.EndFastBlue")
		MASB A 21
		MASB A 10 A_PlaySound("artillery/reload", 5)
		MASB A 18 A_SpawnItemEx("ArtilleryCasing", 0, 8, 32, random(1, 3), random(0, 1), 0, random(45, 60), 128)
		NULL A 0 A_ReFire
		Goto Ready
	Fire.EndFast:
		MASR A 14
		MASR A 7 A_PlaySound("artillery/reload", 5)
		MASR A 12 A_SpawnItemEx("ArtilleryCasing", 0, 8, 32, random(1, 3), random(0, 1), 0, random(45, 60), 128)
		NULL A 0 A_ReFire
		Goto Ready
	Fire.EndFastBlue:
		MASB A 14
		MASB A 7 A_PlaySound("artillery/reload", 5)
		MASB A 12 A_SpawnItemEx("ArtilleryCasing", 0, 8, 32, random(1, 3), random(0, 1), 0, random(45, 60), 128)
		NULL A 0 A_ReFire
		Goto Ready
	AltFire:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "AltFire.Blue")
		MASR B 1 A_PlayWeaponSound("artillery/fire")
		NULL A 0 A_GunFlash
		MASR B 1 BRIGHT A_FireCustomMissile("ArtilleryShell80mm", 0, 1, 8)
		Goto AltFire.End
	AltFire.Blue:
		MASB B 1 A_PlayWeaponSound("artillery/fire")
		NULL A 0 A_GunFlash
		MASB B 1 BRIGHT A_FireCustomMissile("ArtilleryShell80mmBlue", 0, 1, 8)
		Goto AltFire.EndBlue
	AltFire.End:
		//MASR C 1 BRIGHT A_Recoil(5)
		//MASR D 1 BRIGHT
		//MASR E 1 BRIGHT
		//MASR A 2
		MASR C 1 BRIGHT offset(4, 36) A_Recoil(7)
		MASR D 1 BRIGHT offset(8, 40) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASR E 1 BRIGHT offset(12, 44)
		MASR A 1 offset(16, 48) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASR A 1 offset(20, 52)
		MASR A 1 offset(24, 56) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASR A 1 offset(20, 52)
		MASR A 1 offset(16, 48) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASR A 1 offset(12, 44)
		MASR A 1 offset(8, 40) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASR A 1 offset(4, 36)
		MASR A 1 offset(0, 32) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		NULL A 0 A_JumpIfInventory("ArtilleryCannonSpeedBonus", 1, "AltFire.EndFast")
		MASR A 44
		MASR A 10 A_PlaySound("artillery/reload", 5)
		MASR A 18 A_SpawnItemEx("ArtilleryCasing80mm", 0, 8, 32, random(1, 3), random(0, 1), 0, random(45, 60), 128)
		NULL A 0 A_ReFire
		Goto Ready
	AltFire.EndBlue:
		MASB C 1 BRIGHT offset(4, 36) A_Recoil(7)
		MASB D 1 BRIGHT offset(8, 40) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASB E 1 BRIGHT offset(12, 44)
		MASB A 1 offset(16, 48) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASB A 1 offset(20, 52)
		MASB A 1 offset(24, 56) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASB A 1 offset(20, 52)
		MASB A 1 offset(16, 48) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASB A 1 offset(12, 44)
		MASB A 1 offset(8, 40) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		MASB A 1 offset(4, 36)
		MASB A 1 offset(0, 32) A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 11, -2)
		NULL A 0 A_JumpIfInventory("ArtilleryCannonSpeedBonus", 1, "AltFire.EndFastBlue")
		MASB A 44
		MASB A 10 A_PlaySound("artillery/reload", 5)
		MASB A 18 A_SpawnItemEx("ArtilleryCasing80mm", 0, 8, 32, random(1, 3), random(0, 1), 0, random(45, 60), 128)
		NULL A 0 A_ReFire
		Goto Ready
	AltFire.EndFast:
		MASR A 36
		MASR A 9 A_PlaySound("artillery/reload", 5)
		MASR A 12 A_SpawnItemEx("ArtilleryCasing80mm", 0, 8, 32, random(1, 3), random(0, 1), 0, random(45, 60), 128)
		NULL A 0 A_ReFire
		Goto Ready
	AltFire.EndFastBlue:
		MASB A 36
		MASB A 9 A_PlaySound("artillery/reload", 5)
		MASB A 12 A_SpawnItemEx("ArtilleryCasing80mm", 0, 8, 32, random(1, 3), random(0, 1), 0, random(45, 60), 128)
		NULL A 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 4 A_Light1
		TNT1 A 3 A_Light2
		Goto LightDone
	Spawn:
		MASD X -1
		Stop
	}
}

Actor ArtilleryShellAmmo : Ammo
{
	Inventory.amount 2
	Inventory.MaxAmount 30
	Ammo.BackpackAmount 2
	Ammo.BackpackMaxAmount 30
}

Actor ArtilleryShell
{
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	Radius 11
	Height 8
	Speed 75
	Damage (33)
	DamageType "Artillery"
	DeathSound "artillery/explosion"
	Decal "Scorch"
	Scale 0.6
	Gravity 0.05
	Translation "112:127=176:191"
	Obituary "$OB_ARTILLERYCANNON"
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 BRIGHT A_PlaySound("ambient/shell", CHAN_BODY, 1.0, 1)
		SHRP AAAAA 1 BRIGHT A_SpawnItemEx("PulseBallSmokePuff", -25)
		Loop
	Death:
		NULL A 0 A_NoGravity
		NULL A 0 A_StopSound(CHAN_BODY)
		NULL A 0 A_SpawnItemEx("FlameDecay", random(-32, 32), random(-32, 32), 4)
		NULL A 0 A_SpawnItemEx("ExplosionMain")
		NULL A 0 A_SpawnItemEx("HeavyMissileQuake")
		TNT1 A 1 A_Explode(80, 184, 1)
		TNT1 A 4 A_Explode(95, 256, 1)
		Stop
	}
}

Actor ArtilleryShell80mm : ArtilleryShell
{
	Translation "112:127=176:191", "80:111=104:111"
	Speed 50
	Damage (63)
	DamageType "ExplosionMech"
	Scale 0.74
	States
	{
	Death:
		NULL A 0 A_NoGravity
		NULL A 0 A_StopSound(CHAN_BODY)
		NULL A 0 A_SpawnItemEx("FlameDecay", random(-32, 32), random(-32, 32), 4)
		NULL A 0 A_SpawnItemEx("ExplosionMain")
		NULL A 0 A_SpawnItemEx("HeavyMissileQuake")
		TNT1 A 1 A_Explode(100, 224, 1)
		TNT1 A 5 A_Explode(110, 324, 1)
		Stop
	}
}

Actor ArtilleryShellBlue : ArtilleryShell
{
	Translation "112:120=200:207", "121:127=241:247"
	DamageType "Artillery"
	States
	{
	Death:
		NULL A 0 A_NoGravity
		NULL A 0 A_StopSound(CHAN_BODY)
		NULL A 0 A_SpawnItemEx("FlameDecayBlue", random(-32, 32), random(-32, 32), 4)
		NULL A 0 A_SpawnItemEx("ExplosionMain")
		NULL A 0 A_SpawnItemEx("HeavyMissileQuake")
		TNT1 A 1 A_Explode(80, 184, 1)
		TNT1 A 5 A_Explode(95, 256, 1)
		Stop
	}
}

Actor ArtilleryShell80mmBlue : ArtilleryShell80mm
{
	Translation "112:120=200:207", "121:127=241:247", "80:111=104:111"
	DamageType "ExplosionMech"
// I got lazy, so it just inhertits from the 80mm one.
	States
	{
	Death:
		NULL A 0 A_NoGravity
		NULL A 0 A_StopSound(CHAN_BODY)
		NULL A 0 A_SpawnItemEx("FlameDecayBlue", random(-32, 32), random(-32, 32), 4)
		NULL A 0 A_SpawnItemEx("ExplosionMain")
		NULL A 0 A_SpawnItemEx("HeavyMissileQuake")
		TNT1 A 1 A_Explode(100, 224, 1)
		TNT1 A 5 A_Explode(110, 324, 1)
		Stop
	}
}

// Translation RANGES:
// "112:127=176:191", - turns green strap to red for red team
// "112:120=200:207", "121:127=241:247" - turns green strap to blue for blue team
// "80:111=104:111" - makes the shell darker for anti-mech shells