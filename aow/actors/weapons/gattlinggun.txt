// Gatling gun
Actor GatlingGun : AOWWeapon replaces Minigun 21045
{
        //$Category Weapons
	Tag "Gatling Gun"
	Obituary "$OB_GATLING" // "%o was perforated by %k's gatling gun."
	Inventory.PickupMessage "You got the gatling gun!"
	Weapon.SelectionOrder 7
	Weapon.AmmoType "MachineGunAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 25
	Weapon.AmmoType2 "MachineGunAmmo"
	Weapon.AmmoUse2 2
	Weapon.PreferredSkin "MarineChaingun"
	States
	{
	Ready:
		NULL A 0 A_StopSound(CHAN_WEAPON)
		GATG A 5 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_StopSound(CHAN_WEAPON)
		GATG AAA 1 A_Lower
		Loop
	Select:
		GATG A 1 A_Raise
		Loop
	Fire:
		GATG B 5 A_PlaySound("gatling/spinup", 5)
		GATG A 4
		GATG B 3
	Hold:
		GATG A 1 A_PlaySound("minigun/Loop", CHAN_WEAPON, 1.0, 1)
		NULL A 0 A_Light2
		NULL A 0 A_PlaySound("minigun/fire2", 5)
		NULL A 0 A_SpawnItemEx("RifleShellCasing", 2, 8, 32, random(1, 3), random(0, 1), random(1, 3), random(45, 60), 128)
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 3, 0)
		GATF A 1 BRIGHT A_FireBullets(7.2, 7.2, -1, 12, "RiflePuff", FBF_USEAMMO | FBF_NORANDOM)
		GATG B 1 A_Light1
		NULL A 0 A_Light2
		NULL A 0 A_SpawnItemEx("RifleShellCasing", 2, 8, 32, random(1, 3), random(0, 1), random(1, 3), random(45, 60), 128)
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 3, 0)
		GATF B 1 BRIGHT A_FireBullets(7.2, 7.2, -1, 12, "RiflePuff", FBF_USEAMMO | FBF_NORANDOM)
		GATG A 1 A_Light1
		NULL A 0 A_JumpIfNoAmmo("FireEnd")
		NULL A 0 A_ReFire
		Goto FireEnd
	FireEnd:
		NULL A 0 A_Light0
		GATG B 3 A_PlaySound("gatling/spin", 5)
		NULL A 0 A_StopSound(CHAN_WEAPON)
		GATG A 5 A_ReFire
		GATG B 6 A_ReFire
		GATG AB 7
		Goto Ready
	AltFire:
		GATG B 5 A_PlaySound("gatling/spinup", 5)
		GATG A 4
		GATG B 3
		Goto AltHold
	AltHold:
		NULL A 0 A_PlaySound("gatling/alt", CHAN_WEAPON, 1, 1.0)
		NULL A 0 A_Light2
		NULL AA 0 BRIGHT A_SpawnItemEx("RifleShellCasing", 2, 8, 32, random(1, 3), random(0, 1), random(1, 3), random(45, 60), 128)
		GATF A 2 BRIGHT A_FireBullets(2.6, 1.6, -1, 20, "ExplosionPuffSmall", FBF_USEAMMO | FBF_NORANDOM, 3072)
		GATG B 3 A_Light1
		NULL A 0 A_Light2
		NULL AA 0 BRIGHT A_SpawnItemEx("RifleShellCasing", 2, 8, 32, random(1, 3), random(0, 1), random(1, 3), random(45, 60), 128)
		GATF B 2 BRIGHT A_FireBullets(2.6, 1.6, -1, 20, "ExplosionPuffSmall", FBF_USEAMMO | FBF_NORANDOM, 3072)
		GATG A 3 A_Light1
		NULL A 0 A_JumpIfNoAmmo("AltFireEnd")
		NULL A 0 A_ReFire
		Goto AltFireEnd
	AltFireEnd:
		NULL A 0 A_Light0
		GATG B 3 A_PlaySound("gatling/spin", 5)
		NULL A 0 A_StopSound(CHAN_WEAPON)
		GATG A 5 A_ReFire
		GATG B 6 A_ReFire
		GATG AB 7
		Goto Ready
	Spawn:
		GATG X -1
		Stop
	}
}

Actor ExplosionPuff
{
	+PUFFONACTORS
	+CLIENTSIDEONLY
	DamageType "Explosion"
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 14 A_SpawnItemEx("ExplosionSmall", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		Stop
	}
}

Actor ExplosionPuffSmall : ExplosionPuff
{
	DamageType "Explosion"
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 14 A_SpawnItemEx("ExplosionSmaller", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		Stop
	}
}
