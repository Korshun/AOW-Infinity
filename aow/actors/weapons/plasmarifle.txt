Actor PlasmaGun : AOWWeapon replaces PlasmaRifle 21087 {
	Inventory.PickupMessage "You got the plasma gun!"
	Obituary "%o was incinerated by %k's plasma gun."
	Weapon.AmmoType "PlasmaRifleAmmo"
	Weapon.AmmoGive 25
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "PlasmaHeat"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.Kickback 25
	Weapon.SelectionOrder 5
	Weapon.PreferredSkin "MarinePlasma"
	States
	{
	Spawn:
		PLAS A -1
		Stop
	Ready: 
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "ReadyBlue")
		ENRG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaHeat", 2)
		ENRG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaHeat", 2)
		ENRG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaHeat", 2)
		ENRG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaHeat", 2)
		Goto WarningRemove
	ReadyBlue:
		ENBG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaHeat", 2)
		ENBG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaHeat", 2)
		ENBG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaHeat", 2)
		ENBG A 3 A_WeaponReady
		NULL A 0 A_TakeInventory("PlasmaHeat", 2)
		Goto WarningRemove
	Deselect:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "DeselectBlue")
		ENRG A 1 A_Lower 
		Loop
	DeselectBlue:
		ENBG A 1 A_Lower
		Loop
	Select:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "SelectBlue")
		ENRG A 1 A_Raise 
		Loop
	SelectBlue:
		ENBG A 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("PlasmaHeat", 100, "Overheat")
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "FireBlue")
		ENRG A 1 A_GunFlash
		NULL A 0 A_PlayWeaponSound("Weapons/PlasmaShot")
		ENRG A 1 offset(1, 35) A_FireCustomMissile("PlasmaShot", 0, 1, 2, 0)
		ENRG A 1 offset(0, 37) A_GiveInventory("PlasmaHeat", random(0, 3))
		NULL A 0 A_JumpIfInventory("PlasmaHeat", 75, "Warning")
		ENRG A 1 A_Refire
		Goto Ready
	FireBlue:
		ENBG A 1 A_GunFlash
		NULL A 0 A_PlayWeaponSound("Weapons/PlasmaShot")
		ENBG A 1 offset(1, 35) A_FireCustomMissile("PlasmaShotBlue", 0, 1, 2, 0)
		ENBG A 1 offset(0, 37) A_GiveInventory("PlasmaHeat", random(0, 3))
		NULL A 0 A_JumpIfInventory("PlasmaHeat", 75, "Warning")
		ENBG A 1 A_Refire
		Goto Ready
	Overheat:
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "OverheatBlue")
		ENRG A 2 A_PlaySoundEx("Plasma/FailFire", "SoundSlot6", 0)
		NULL A 0 A_PlaySoundEx("Plasma/Vapour", "SoundSlot7", 0)
		ENRG AAAAAA 2 A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 6, -2)
		ENRG AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 3 A_TakeInventory("PlasmaHeat", 2)
		Goto WarningRemove
	OverheatBlue:
		ENBG A 2 A_PlaySoundEx("Plasma/FailFire", "SoundSlot6", 0)
		NULL A 0 A_PlaySoundEx("Plasma/Vapour", "SoundSlot7", 0)
		ENBG AAAAAA 2 A_FireCustomMissile("OverheatSmokeSpawner", 0, 0, 6, -2)
		ENBG AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 3 A_TakeInventory("PlasmaHeat", 2)
		Goto WarningRemove
	WarningRemove:
		NULL A 0 A_TakeInventory("WarningChecker", 1)
		Goto Ready
	Flash: 
		NULL A 0 A_JumpIfInventory("IsRed", 1, "Flash.Red")
		ENRF A 1 BRIGHT A_Light1
		ENRF B 2 BRIGHT A_Light2
		ENRF C 1 BRIGHT A_Light1
		NULL A 0 A_Light0
		Stop
	Flash.Red:
		ENRR A 1 BRIGHT A_Light1
		ENRR B 2 BRIGHT A_Light2
		ENRR C 1 BRIGHT A_Light1
		NULL A 0 A_Light0
		Stop
	Warning:
		NULL A 0
		ENRG AA 0 A_JumpIfInventory("WarningChecker", 1, "Ready")
		NULL A 0 A_PlaySoundEx("Plasma/Warning", "Voice", 0)
		NULL A 0 A_GiveInventory("WarningChecker", 1)
		Goto Ready
	}
}

Actor PlasmaRifleAmmo : Ammo {
	Inventory.amount 40
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 40
	Ammo.BackpackMaxAmount 300
	States
	{
	Spawn:
		NULL A 0
		Stop
	}
}

Actor PlasmaShot {
	PROJECTILE
	-RANDOMIZE
	Radius 12
	Height 8
	Scale 0.8
	Speed 28
	Damage (15)
	RenderStyle Add
	Alpha 0.75
	DeathSound "weapons/plasmax"
	DamageType "Laser"
	Decal PulseScorch
	Obituary "%o was incinerated by %k's Plasma Rifle."
	Translation "192:207=169:191", "240:247=[63,0,0]:[0,0,0]" 
	States
	{
	Spawn:
		PLSS AB 4 BRIGHT
		Loop
	Death:
		NULL A 0 A_SpawnItemEx("PulseBallSmokePuff", 0, 0, 0, 0, 0, 1, 0, 128, 0)
		PLSE ABCDE 2 BRIGHT 
		Stop
	}
}

Actor PlasmaShotBlue : PlasmaShot {
	Translation "0:255=0:255"
	DamageType "Laser"
}

Actor PlasmaHeat : Ammo {
	+IGNORESKILL
	Inventory.amount 0
	Inventory.MaxAmount 100
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 100
	States
	{
	Spawn:
		NULL A 0
		Stop
	}
}

Actor WarningChecker : Boolean {}

Actor OverheatSmokeSpawner {
	PROJECTILE
	+CLIENTSIDEONLY
	Speed 25
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("PulseBallSmokePuff", 0, 0, -2, 0, 0, 0.1*Random(0, 10))
		Stop
	}
}