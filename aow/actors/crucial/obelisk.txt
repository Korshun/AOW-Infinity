/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * Obelisk of Light
 *
 * The Obelisk is a tower-like structure that guards the base. It exists at
 * game start and is one of the five structures that must be destroyed to win
 * the game.
 *
 * The Obelisk structure is built of map geometry. The actors defined here are
 * invisible actors that are the functional bit in the obelisk. They're monsters
 * which shoot the plasma blast at enemies they see.
 */

Actor RedObeliskThing 21214
{
	Obituary "$OB_OBELISK_RED" // "%o was vaporized by the \cgRed Obelisk\c-."
	Health 99999
	Radius 16
	Height 64
	Mass 999999
	Speed 0
	PainChance 0
	MONSTER
	-ISMONSTER
	+FLOORCLIP
	+NOBLOOD
	+NOICEDEATH
	-SOLID
	+NOCLIP
	-FRIENDLY
	+NOTARGET
	+LOOKALLAROUND
	+NEVERRESPAWN
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 Thing_ChangeTID(0, 215)
		NULL A 0 A_ChangeFlag("ISMONSTER", 1)
		NULL A 0 A_GiveInventory("IsRed", 1)
		NULL A 0 A_TakeInventory("ObeliskCharged", 1)
		TNT1 A 8 A_Look
		Loop
	See:
		NULL A 0 A_Chase("", "", CHF_DONTMOVE)
		NULL A 0 A_GiveInventory("IsRed", 1)
		NULL A 0 A_TakeInventory("ObeliskCharged", 1)
		TNT1 A 3 A_FaceTarget
		TNT1 A 2 A_JumpIfTargetInLOS("Missile")
		TNT1 A 2 A_ClearTarget
		Goto Spawn
	See2:
		TNT1 A 2 A_ClearTarget
		Goto See
	MissileCheck:
		NULL AA 0 A_JumpIfInTargetInventory("IsBlue", 1, "MissileGo")
		NULL A 0 
		Goto See
	Missile:
		NULL A 0 
		TNT1 A 1 A_JumpIfInTargetInventory("IsRed", 1, "See2")
		TNT1 A 1 A_JumpIfCloser(30000, "MissileGo")
		NULL A 0 
		Goto See2
	MissileGo:
		NULL A 0 A_JumpIfInTargetInventory("IsMech", 1, "MissileGoMech")
		NULL A 0 A_JumpIfInventory("ObeliskCharged", 1, "MissileCharged")
		NULL A 0 A_FaceTarget
		TNT1 A 70 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		NULL A 0 A_GiveInventory("ObeliskCharged", 15)
		NULL A 0 A_CposRefire
		NULL A 0 A_PlaySoundEx("obelisk/fire", "Weapon", 0)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Body", 0)
		NULL A 0 A_FaceTarget
		NULL A 0 A_SpawnItemEx("ObbyFlare")
		NULL A 0 A_CustomMissile("ObeliskShotRedInfantry", 12, 0)
		TNT1 A 70 A_FaceTarget
		NULL A 0 A_TakeInventory("ObeliskCharged", 15)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		Goto See
	MissileCharged:
		TNT1 A 15
		NULL A 0 A_PlaySoundEx("obelisk/fire", "Weapon", 0)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Body", 0)
		NULL A 0 A_FaceTarget
		NULL A 0 A_CustomMissile("ObeliskShotRedInfantry", 12, 0)
		TNT1 A 70 A_FaceTarget
		NULL A 0 A_TakeInventory("ObeliskCharged", 15)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		Goto See
	MissileGoMech:
		NULL A 0 A_JumpIfInventory("ObeliskCharged", 1, "MissileChargedMech")
		NULL A 0 A_FaceTarget
		TNT1 A 70 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		NULL A 0 A_GiveInventory("ObeliskCharged", 15)
		NULL A 0 A_CposRefire
		NULL A 0 A_PlaySoundEx("obelisk/fire", "Weapon", 0)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Body", 0)
		NULL A 0 A_FaceTarget
		NULL A 0 A_SpawnItemEx("ObbyFlare")
		NULL A 0 A_CustomMissile("ObeliskShotRed", 12, 0)
		TNT1 A 70 A_FaceTarget
		NULL A 0 A_TakeInventory("ObeliskCharged", 15)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		Goto See
	MissileChargedMech:
		TNT1 A 15
		NULL A 0 A_PlaySoundEx("obelisk/fire", "Weapon", 0)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Body", 0)
		NULL A 0 A_FaceTarget
		NULL A 0 A_CustomMissile("ObeliskShotRed", 12, 0)
		TNT1 A 70 A_FaceTarget
		NULL A 0 A_TakeInventory("ObeliskCharged", 15)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		Goto See
	Death:	
		TNT1 A 5
		Stop
	}
}

Actor BlueObeliskThing 21213 
{
	Obituary "$OB_OBELISK_BLUE" // "%o was vaporized by the \chBlue Obelisk\c-."
	Health 99999
	Radius 16
	Height 64
	Mass 999999
	Speed 0
	PainChance 0
	MONSTER
	-ISMONSTER
	+FLOORCLIP
	+NOBLOOD
	+NOICEDEATH
	-SOLID
	+NOCLIP
	-FRIENDLY
	+NOTARGET
	+LOOKALLAROUND
	+NEVERRESPAWN
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 Thing_ChangeTID(0, 115)
		NULL A 0 A_ChangeFlag("ISMONSTER", 1)
		NULL A 0 A_GiveInventory("IsBlue", 1)
		NULL A 0 A_TakeInventory("ObeliskCharged", 1)
		TNT1 A 8 A_Look
		Loop
	See:
		NULL A 0 A_Chase("", "", CHF_DONTMOVE)
		NULL A 0 A_GiveInventory("IsBlue", 1)
		NULL A 0 A_TakeInventory("ObeliskCharged", 1)
		TNT1 A 3 A_FaceTarget
		TNT1 A 2 A_JumpIfTargetInLOS("Missile")
		TNT1 A 2 A_ClearTarget
		Goto Spawn
	See2:
		TNT1 A 2 A_ClearTarget
		Goto See
	MissileCheck:
		NULL AA 0 A_JumpIfInTargetInventory("IsRed", 1, "MissileGo")
		NULL A 0 
		Goto See
	Missile:
		NULL A 0 
		TNT1 A 1 A_JumpIfInTargetInventory("IsBlue", 1, "See2")
		TNT1 A 1 A_JumpIfCloser(30000, "MissileGo")
		NULL A 0 
		Goto See2
	MissileGo:
		NULL A 0 A_JumpIfInTargetInventory("IsMech", 1, "MissileGoMech")
		NULL A 0 A_JumpIfInventory("ObeliskCharged", 1, "MissileCharged")
		NULL A 0 A_FaceTarget
		TNT1 A 70 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		NULL A 0 A_GiveInventory("ObeliskCharged", 15)
		NULL A 0 A_CposRefire
		NULL A 0 A_PlaySoundEx("obelisk/fire", "Weapon", 0)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Body", 0)
		NULL A 0 A_FaceTarget
		NULL A 0 A_SpawnItemEx("ObbyFlareBlue")
		NULL A 0 A_CustomMissile("ObeliskShotBlueInfantry", 12, 0)
		TNT1 A 70 A_FaceTarget
		NULL A 0 A_TakeInventory("ObeliskCharged", 15)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		Goto See
	MissileCharged:
		TNT1 A 15
		NULL A 0 A_PlaySoundEx("obelisk/fire", "Weapon", 0)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Body", 0)
		NULL A 0 A_FaceTarget
		NULL A 0 A_CustomMissile("ObeliskShotBlueInfantry", 12, 0)
		TNT1 A 70 A_FaceTarget
		NULL A 0 A_TakeInventory("ObeliskCharged", 15)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		Goto See
	MissileGoMech:
		NULL A 0 A_JumpIfInventory("ObeliskCharged", 1, "MissileChargedMech")
		NULL A 0 A_FaceTarget
		TNT1 A 70 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		NULL A 0 A_GiveInventory("ObeliskCharged", 15)
		NULL A 0 A_CposRefire
		NULL A 0 A_PlaySoundEx("obelisk/fire", "Weapon", 0)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Body", 0)
		NULL A 0 A_FaceTarget
		NULL A 0 A_SpawnItemEx("ObbyFlareBlue")
		NULL A 0 A_CustomMissile("ObeliskShotBlue", 12, 0)
		TNT1 A 70 A_FaceTarget
		NULL A 0 A_TakeInventory("ObeliskCharged", 15)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		Goto See
	MissileChargedMech:
		TNT1 A 15
		NULL A 0 A_PlaySoundEx("obelisk/fire", "Weapon", 0)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Body", 0)
		NULL A 0 A_FaceTarget
		NULL A 0 A_CustomMissile("ObeliskShotBlue", 12, 0)
		TNT1 A 70 A_FaceTarget
		NULL A 0 A_TakeInventory("ObeliskCharged", 15)
		NULL A 0 A_PlaySoundEx("obelisk/charge", "Voice", 0)
		Goto See
	Death:	
		TNT1 A 5
		Stop
	}
}

Actor ObbyFlare
{
	RenderStyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	Scale 2.0
	States
	{
	Spawn:
		CACH B 8 BRIGHT
		CACH B 1 BRIGHT A_Fadeout(0.05)
		Wait
	}
}

Actor ObbyFlareBlue
{
	RenderStyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	Scale 2.0
	States
	{
	Spawn:
		PPCX A 8 BRIGHT
		PPCX A 1 BRIGHT A_Fadeout(0.05)
		Wait
	}
}

Actor ObeliskCharged : Inventory
{
		Inventory.amount 0
		Inventory.MaxAmount 15
}

Actor ObeliskShotRed : FastProjectile
{
	Radius 13
	Height 8
	Speed 2000
	Damage (250)
	RenderStyle Add
	Alpha 0.75
	PROJECTILE
	-RANDOMIZE
	+NOEXTREMEDEATH
	+THRUGHOST
	Scale 1.0
	DamageType "Explosion"
	Translation "192:207=169:189", "240:246=45:47", "112:127=176:191"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_SpawnItemEx("PlasmaFlameTail", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		Loop
	Death:
		NULL A 0 A_SpawnItemEx("PlasmaFlameCluster", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		TNT1 A 3 BRIGHT A_Explode(250, 256)
		Stop
	}
}

Actor ObeliskShotRedInfantry : FastProjectile
{
	Radius 13
	Height 8
	Speed 2000
	Damage (40)
	RenderStyle Add
	Alpha 0.75
	PROJECTILE
	-RANDOMIZE
	+NOEXTREMEDEATH
	+THRUGHOST
	Scale 1.0
	DamageType "Superweapon"
	Translation "192:207=169:189", "240:246=45:47", "112:127=176:191"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_SpawnItemEx("PlasmaFlameTail", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		Loop
	Death:
		NULL A 0 A_SpawnItemEx("PlasmaFlameCluster", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		TNT1 A 3 BRIGHT A_Explode(50, 256)
		Stop
	}
}

Actor ObeliskShotBlue : FastProjectile
{
	Radius 13
	Height 8
	Speed 2000
	Damage (250)
	RenderStyle Add
	Alpha 0.75
	PROJECTILE
	-RANDOMIZE
	+NOEXTREMEDEATH
	+THRUGHOST
	Scale 1.0
	DamageType "Explosion"
	Translation "112:120=200:207", "121:127=241:247"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_SpawnItemEx("PlasmaFlameTail", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		Loop
	Death:
		NULL A 0 A_SpawnItemEx("PlasmaFlameCluster", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		TNT1 A 3 BRIGHT A_Explode(250, 256)
		Stop
	}
}

Actor ObeliskShotBlueInfantry : FastProjectile
{
	Radius 13
	Height 8
	Speed 2000
	Damage (40)
	RenderStyle Add
	Alpha 0.75
	PROJECTILE
	-RANDOMIZE
	+NOEXTREMEDEATH
	+THRUGHOST
	Scale 1.0
	DamageType "SuperweaponIon"
	Translation "112:120=200:207", "121:127=241:247"
	States
	{
	Spawn:
		TNT1 A 1 BRIGHT A_SpawnItemEx("PlasmaFlameTail", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		Loop
	Death:
		NULL A 0 A_SpawnItemEx("PlasmaFlameCluster", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		TNT1 A 3 BRIGHT A_Explode(50, 256)
		Stop
	}
}

// ------------------------
// Obelisk decorations
Actor ObeliskVoltageDecorSpawner 29010
{
	+NOINTERACTION
	+NOBLOCKMAP
	+SHOOTABLE
	Health 999999
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 60 A_SpawnItemEx("ObeliskVoltageDecor")
		Loop
	Death:
		NULL A 0
		Stop
	}
}

Actor ObeliskVoltageDecor
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	ReactionTime 35
	States
	{
	Spawn:
		TNT1 AAAAAA 1 A_SpawnItemEx("ObeliskVoltageDecorFX", random (-96, 96), random (-96, 96), -224, 0, 0, 25)
		NULL A 0 A_Countdown
		Loop
	Death:
		NULL A 0
		Stop
	}
}

Actor ObeliskVoltageDecorFX
{
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	RenderStyle add
	Alpha 1.0
	Scale 0.75
	States
	{
	Spawn:
		VOLX ABCDEFEDCB 2 BRIGHT A_FadeOut(0.1)
		Stop
	}
}

Actor ObeliskVoltageDecorSpawnerRound 29013
{
	+NOINTERACTION
	+NOBLOCKMAP
	+SHOOTABLE
	Health 999999
	States
	{
	Spawn:
		NULL A 0
		TNT1 A 60 A_SpawnItemEx("ObeliskVoltageDecorRound")
		Loop
	Death:
		NULL A 0
		Stop
	}
}

Actor ObeliskVoltageDecorRound
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	ReactionTime 14
	States
	{
	Spawn:
		TNT1 AAAAA 1 A_SpawnItemEx("ObeliskVoltageDecorFX", random (0, 160), 0, -224, 0, 0, 25, random (0, 360))
		NULL A 0 A_Countdown
		Loop
	Death:
		NULL A 0
		Stop
	}
}
