// RAVEN MECH
Actor Raven : MechPlayer_Base {
	+NOSKIN
	Radius 58
	Height 144
	Mass 4000
	Health 2000
	Player.MaxHealth 2000
	Player.DisplayName "Raven"
	Player.ForwardMove 0.85
	Player.SideMove 0.25
	Player.soundclass "mech"
	Player.viewheight 132
	Player.attackzoffset 36
	Player.MorphWeapon "RavenWeapons"
	Scale 0.65
	States
	{
	Spawn2:
		RAV2 BB 6 SetPlayerProperty(0, 1, 0)
		RAV2 BA 6
		RAVN ZYXWVUI 6
		NULL A 0 SetPlayerProperty(0, 0, 0)
		Goto Spawn+6
	Spawn:
		RAVN A 1
		NULL A 0 SetPlayerProperty(0, 0, 3)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 3)
		NULL A 0 ACS_Execute(315, 0)
		NULL A 0 A_GiveInventory("MechEjectButton", 1)
		NULL A 0 A_GiveInventory("IsMech", 1)
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, -30, 4, 0, 0, 0, 0, 128, 0)
		RAVN A 5 A_SpawnItemEx("MechStepDamageWeak", 0, -30, 4, 0, 0, 0, 0, 0, 0)
		RAVN I 35
		RAVN IIII 35 A_PlaySound("mech/idle", 5)
		RAVN I 35
		Goto Spawn+6
	See:
		NULL A 0 SetPlayerProperty(0, 0, 3)
		RAVN ABC 3 A_JumpIfHealthLower(300, "SeeDamaged")
		NULL A 0 A_PlaySound("mech/idle", 5)
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, -30, 4)
		RAVN D 3 A_SpawnItemEx("MechStepDamageWeak", 0, -30, 4)
		RAVN EFG 3
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, 30, 4)
		RAVN H 3 A_SpawnItemEx("MechStepDamageWeak", 0, 30, 4)
		NULL A 0 A_JumpIf(waterLevel >= 1, "Drown")
		Goto Spawn
	SeeDamaged:
		RAVN ABC 3 
		NULL A 0 A_PlaySound("mech/idle", 5)
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, -30, 4)
		NULL A 0 A_SpawnItemEx("MechStepDamageWeak", 0, -30, 4)
		RAVN D 3 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(96, 128), 0, 0, 3)
		RAVN EFG 3
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, 30, 4, 0, 0, 0, 0, 128, 0)
		NULL A 0 A_SpawnItemEx("MechStepDamageWeak", 0, 30, 4, 0, 0, 0, 0, 0, 0)
		RAVN H 3 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(96, 128), 0, 0, 3)
		NULL A 0 A_JumpIf(waterlevel >= 1, "Drown")
		Goto Spawn
	Drown:
		NULL A 0 Thing_Damage(0, 200, 12)
		Goto Spawn
	Death:
		NULL A 0
		NULL A 0 A_JumpIfInventory("IsKamikaze", 1, "Death.IonExplosion")
		NULL A 0 A_JumpIfInventory("IsBombCrate", 1, "Death.BombExplosion")
	DeathGo:
		RAVN J 4 A_TakeInventory("Ingame", 1)
		RAVN K 4 A_NoBlocking
		RAVN LMNOPQRS 4
		RAVN T 8
		RAVN T 15 A_SpawnItemEx("MechExplosionSmall", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		DISR Z -1 A_SetTranslucent(0, 0.0)
		Stop
	Death.IonExplosion:
		NULL A 0
		NULL A 0 A_JumpIfInventory("IsRed", 1, "Death.NukeExplosion")
		NULL A 0 A_SpawnItemEx("BlueStrikeKamikaze", 0, 0, 32)
		Goto DeathGo
	Death.NukeExplosion:
		NULL A 0
		NULL A 0 A_SpawnItemEx("NukeKamikaze", 0, 0, 32)
		Goto DeathGo
	Death.BombExplosion:
		NULL A 0
		NULL A 0 A_SpawnItemEx("BombExplosion")
		Goto DeathGo
	}
}

Actor RavenUsed : Raven {Health 1000}

Actor RavenSwitch : PermanentPowerup {Powerup.Type "Raven"}
Actor RavenSwitch2 : PermanentPowerup {Powerup.Type "Raven2"}

Actor PowerRaven : AOWMorph {powermorph.playerclass "Raven"}
Actor PowerRaven2 : AOWMorph {PowerMorph.PlayerClass "RavenUsed"}

Actor RavenSwitchPickup : CustomInventory {
	Inventory.pickupsound "mech/start"
	Translation "112:127=[255,255,255]:[0,0,0]"
	+INVENTORY.AUTOACTIVATE
	Scale 0.65
	States
	{
	Spawn:
		RAV2 B -1
		Loop
	Pickup:
		NULL A 0 ACS_ExecuteWithResult(aow_script353, 0, 0, 0)
		Stop
	Use:
		NULL A 0 ACS_ExecuteAlways(aow_script699, 0, 1)
		NULL A 0 A_GiveInventory("RavenSwitch2", 1)
		Stop
	}
}

Actor RavenWeapons : AOWWeapon {
	+CHEATNOTWEAPON +DONTBOB
	Weapon.SelectionOrder 1
	Weapon.AmmoType "RavenAC2Ammo"
	Weapon.AmmoType2 "Raven-RocketAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 1
	Weapon.AmmoGive 120
	Weapon.AmmoGive2 120
	Obituary "$OB_RAVEN"
	States
	{
	Ready:
		MHUD A 1 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_JumpIfHealthLower(1, 1)
		Goto Ready
		NULL A 1 A_Lower
		Wait
	Select:
		NULL CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 0 A_Raise
		MHUD A 1 A_Raise
		Wait
	WaitReload:
		MHUD A 2
		MHUD A 2 A_ReFire
		Goto Ready
	Fire:
		NULL A 0 A_JumpIfInventory("RavenAC2Ammo", 1, 1)
		Goto Ready
		NULL A 0 Radius_Quake(1, 4, 0, 1, 0)
		NULL A 0 A_FireCustomMissile("RavenAC2FX", 0, 0, -48, 20)
		NULL A 0 A_SpawnItemEx("ArtilleryCasing", 0, -48, 128, random(0, 3), 0, random(1, 3), random(45, 60), 128)
		NULL A 0 A_GunFlash
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "FireBlue")
		MHUD D 8 BRIGHT A_FireCustomMissile("RavenAC2", 0, 1, -48, 20)
		MHUD C 6
		NULL A 0 A_Refire
		Goto Ready
	FireBlue:
		MHUD D 8 BRIGHT A_FireCustomMissile("RavenAC2Blue", 0, 1, -48, 20)
		MHUD C 6
		NULL A 0 A_Refire
		Goto Ready
	AltFire:
		NULL A 0 A_JumpIfInventory("Reload-LRM", 1, "Ready")
		NULL A 0 A_JumpIfInventory("Raven-RocketAmmo", 5, 1)
		Goto Ready
		MHUD A 2 A_PlayWeaponSound("lrm/fire")
		NULL A 0 Radius_Quake (2, 20, 0, 1, 0)
		NULL A 0 ACS_ExecuteAlways(aow_script319, 0, 1, 3) // reload
		NULL A 0 BRIGHT A_GunFlash
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "AltFireBlue")
		MHUD BBBBB 2 A_FireCustomMissile("LongRangeMissile", 0, 1, random(42, 48), 14)
		MHUD A 20
		MHUD A 2 A_Refire
		Goto Ready
	AltFireBlue:
		MHUD BBBBB 2 A_FireCustomMissile("LongRangeMissileBlue", 0, 1, random(42, 48), 14)
		MHUD A 20
		MHUD A 2 A_Refire
		Goto Ready
	Flash:
		NULL A 1 BRIGHT A_Light2
		NULL A 2 BRIGHT A_Light1
		Goto LightDone
	Spawn:
		NULL A 0 
		Stop
	}
}

Actor RavenAC2Ammo : Ammo {
	Inventory.amount 10
	Inventory.MaxAmount 120
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 120
}

Actor RavenAC2 : FastProjectile {
	Radius 4
	Height 8
	Speed 250
	Damage (20)
	PROJECTILE
	-RANDOMIZE
	Obituary "$OB_RAVEN"
	DamageType "Rifle"
	SeeSound "mech/ac2"
	Scale 0.5
	Decal "Scorch"
	Translation "112:127=176:191"
	States
	{
	Spawn:
		SHRP A 1 BRIGHT A_SpawnItemEx("PulseBallSmokePuff", -25)
		Wait
	Death:
		NULL A 0 A_Explode(20, 128)
		NULL A 0 A_SpawnItemEx("RavenAC2Explosion", -1)
		NULL A 5 A_SpawnItemEx("ExplosionSmall")
		Stop
	}
}

Actor RavenAC2FX {
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		NULL AAAAAAAAAAAAAAA 0 A_SpawnItemEx("PulseBallSmokePuff", 0, 0, 0, random(-1, 1), random(-1, 1), random(1, 2))
		Stop
	}
}

Actor RavenAC2Explosion {
	Obituary "$OB_RAVEN"
	DamageType "Explosion"
	States
	{
	Spawn:
		NULL A 0
		NULL A 1 BRIGHT A_Explode(20, 128, 1)
		Stop
	}
}

Actor RavenAC2Blue : RavenAC2 {
	Translation "112:120=200:207", "121:127=241:247"
	DamageType "Rifle"
	States
	{
	Death:
		NULL A 0 A_Explode(20, 128, 0)
		NULL A 0 A_SpawnItemEx("RavenAC2ExplosionBlue", -1)
		NULL A 5 A_SpawnItemEx("ExplosionSmall")
		Stop
	}
}

Actor RavenAC2ExplosionBlue : RavenAC2Explosion {DamageType "Explosion"}

Actor Raven-RocketAmmo : Ammo {
	Inventory.amount 5
	Inventory.MaxAmount 120
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 120
}

Actor DamagedRaven {
	Health 200
	Radius 58
	Height 128
	Mass 2000
	+SHOOTABLE
	PainChance 0
	Scale 0.65
	BloodType "HHMetalHit"
	BloodColor "White"
	DamageFactor "Disarm", 0.0
	Translation "112:127=[255,255,255]:[0,0,0]"
	States
	{
	Spawn:
		RAVN IIII 35
		RAVN I 35 
		RAVN IIII 5 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(96, 128), 0, 0, 3, 0, 128, 0)
		RAVN I 5 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(96, 128), 0, 0, 3, 0, 128, 0)
		Goto Spawn+5
	Death:
		RAVN JKLMNOPQRS 4
		RAVN T 8
		RAVN T 10 A_SpawnItemEx("MechExplosionSmall", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		DISR Z -1 A_SetTranslucent(0, 0.0)
		Stop
	}
}

Actor EjectedRaven {
	Scale 0.65
	Translation "112:127=[255,255,255]:[0,0,0]"
	States
	{
	Spawn:
		RAVN IUVWXYZ 6
		RAV2 AB 6
		NULL A 0 A_SpawnItemEx("RavenSwitchPickup")
		Stop
	}
}

Actor RavenAmmoPackage : CustomInventory {
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	States
	{
	Pickup:
		NULL A 0 A_GiveInventory("RavenAC2Ammo", 600)
		NULL A 0 A_GiveInventory("Raven-RocketAmmo", 120)
		Stop
	}
}