// Wolverine mech
Actor Wolverine : MechPlayer_Base
{
	Health 3000
	Radius 58
	Height 128
	Mass 4000
	PainChance 0
	PainChance "Flash", 255
	Player.DisplayName "Wolverine"
	Player.ForwardMove 0.45
	Player.SideMove 0.15
	Player.MaxHealth 3000
	Player.ViewHeight 96
	Player.AttackZOffset 13
	Player.MorphWeapon "WolverineWeapons"
	Scale 1.75
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 SetPlayerProperty(0, 0, 3)
		NULL A 0 ACS_Execute(aow_script315, 0)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 3)
		NULL A 0 A_GiveInventory("MechEjectButton", 1)
		NULL A 0 A_GiveInventory("IsMech", 1)
		ROB1 A 35
		ROB1 AAAA 35 A_PlaySound("mech/idle", 5)
		ROB1 A 35
		Loop
	See:
		NULL A 0 SetPlayerProperty(0, 0, 3)
		ROB1 CD 5 A_JumpIfHealthLower(500, "SeeDamaged")
		NULL A 0 A_PlaySound("mech/idle", 5)
		NULL A 0 Radius_Quake(2, 8, 0, 2, 0)
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, -24, 4)
		ROB1 E 5 A_SpawnItemEx("MechStepDamageWeak", 0, -24, 4)
		ROB1 DC 5
		NULL A 0 Radius_Quake(2, 8, 0, 2, 0)
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, 24, 4)
		ROB1 B 5 A_SpawnItemEx("MechStepDamageWeak", 0, 24, 4)
		NULL A 0 A_JumpIf(WaterLevel >= 1, "Drown")
		Goto Spawn
	SeeDamaged:
		ROB1 CD 5 
		NULL A 0 A_PlaySound("mech/idle", 5)
		NULL A 0 Radius_Quake(2, 8, 0, 2, 0)
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, -24, 4)
		NULL A 0 A_SpawnItemEx("MechStepDamageWeak", 0, -24, 4)
		ROB1 E 5 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(96, 128), 0, 0, 3)
		ROB1 DC 5
		NULL A 0 Radius_Quake(2, 8, 0, 2, 0)
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, 24, 4)
		NULL A 0 A_SpawnItemEx("MechStepDamageWeak", 0, 24, 4)
		ROB1 B 5 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(96, 128), 0, 0, 3)
		NULL A 0 A_JumpIf(WaterLevel >= 1, "Drown")
		Goto Spawn
	Drown:
		NULL A 0 Thing_Damage(0, 300, 12)
		Goto Spawn
	Death:
		NULL A 0
		NULL A 0 A_JumpIfInventory("IsKamikaze", 1, "Death.IonExplosion")
		NULL A 0 A_JumpIfInventory("IsBombCrate", 1, "Death.BombExplosion")
	DeathGo:
		ROB1 A 10 A_NoBlocking
		NULL A 0 A_StopSound(5)
		NULL A 0 A_StopSound(CHAN_WEAPON)
		ROB1 A 10 A_SpawnItemEx("MechExplosionSmall", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		DISR Z -1 A_SetTranslucent(0, 0.0)
		Stop
	Death.IonExplosion:
		NULL A 0
		NULL A 0 A_JumpIfInventory("IsRed", 1, "Death.NukeExplosion")
		NULL A 0 A_SpawnItemEx("BlueStrikeKamikaze", 0, 0, 32)
		Goto DeathGo
	Death.NukeExplosion:
		NULL A 0
		NULL A 0 A_SpawnItemEx("NukeKamikaze", 0, 0, 32)
		Goto DeathGo
	Death.BombExplosion:
		NULL A 0
		NULL A 0 A_SpawnItemEx("BombExplosion")
		Goto DeathGo
	}
}

Actor WolverineUsed : Wolverine {Health 1500}

Actor WolverineSwitch : PermanentPowerup {Powerup.Type "Wolverine"}
Actor WolverineSwitchUsed : PermanentPowerup {Powerup.Type "WolverineUsed"}

Actor PowerWolverine : PowerMorph
{
	PowerMorph.PlayerClass "Wolverine"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_ADDSTAMINA|MRF_NEWTIDBEHAVIOUR|MRF_WHENINVULNERABLE)
}

Actor PowerWolverineUsed : PowerMorph
{
	PowerMorph.PlayerClass "WolverineUsed"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_ADDSTAMINA|MRF_NEWTIDBEHAVIOUR|MRF_WHENINVULNERABLE)
}

Actor WolverineSwitchPickup : CustomInventory
{
	+AUTOACTIVATE
	Inventory.pickupsound "mech/start"
	Translation "112:127=[255,255,255]:[0,0,0]"
	Scale 1.75
	States
	{
	Spawn:
		ROB1 A -1
		Stop
	Pickup:
		NULL A 0 ACS_ExecuteWithResult(aow_script353)
		Stop
	Use:
		NULL A 0 A_GiveInventory("WolverineSwitchUsed", 1)
		Stop
	}
}

Actor WolverineWeapons : AOWWeapon
{
	Weapon.SelectionOrder 1
	Obituary "%o was overwhelmed by %k's Wolverine."
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.DONTBOB
	Weapon.PreferredSkin "WolverineMech"
	States
	{
	Ready:
		MHUD A 1 A_WeaponReady
		Loop
	Deselect:
		NULL A 0 A_JumpIfHealthLower(1, 1)
		Goto Ready
		NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Goto Deselect+29
	Select:
		NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 A 1 A_Raise
		Wait
	WaitReload:
		MHUD A 2
		MHUD A 2 A_ReFire
		Goto Ready
	Fire:
		NULL A 0 A_JumpIfInventory("Wolverine-WhatWeapon", 2, "FireMinigun")
		NULL A 0 A_JumpIfInventory("Wolverine-WhatWeapon", 1, "FirePulse")
		NULL A 0 A_JumpIfInventory("Reload-LRM", 1, "FirePulse")
		MHUD A 10 A_JumpIfInventory("MechRocketAmmo", 10, 1)
		Goto AltFire
		MHUD A 2 A_PlayWeaponSound("lrm/fire")
		NULL A 0 Radius_Quake(2, 35, 0, 1, 0)
		NULL A 0 A_TakeInventory("MechRocketAmmo", 10)
		NULL A 0 ACS_ExecuteAlways(aow_script319, 0, 1, 4) // reload
		NULL A 0 A_GunFlash
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "FireBlue")
		MHUD BBBBBBBBBB 2 A_FireCustomMissile("LongRangeMissile", 0, 0, 25)
		MHUD A 20
		MHUD A 2 A_Refire
		Goto Ready
	FireBlue:
		MHUD BBBBBBBBBB 2 A_FireCustomMissile("LongRangeMissileBlue", 0, 0, 25)
		MHUD A 20
		MHUD A 2 A_Refire
		Goto Ready
	FirePulse:
		NULL A 0
		NULL A 0 Radius_Quake(1, 4, 0, 1, 0)
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "FirePulseBlue")
		NULL A 0 A_FireCustomMissile("PulseBall", 0, 1, 25)
		MHUD D 2 BRIGHT A_GunFlash
		MHUD C 1
		MHUD C 2 A_Refire
		Goto Ready
	FirePulseBlue:
		NULL A 0 A_FireCustomMissile("PulseBallBlue", 0, 1, 25)
		MHUD D 2 BRIGHT A_GunFlash
		MHUD C 1
		MHUD C 2 A_Refire
		Goto Ready
	FireMinigun:
		MHUD A 10 A_JumpIfInventory("Wolverine-MinigunAmmo", 2, 2)
		NULL A 0 A_PlaySoundEx("minigun/spin", "Weapon", 0)
		Goto AltFire
		NULL A 0 Radius_Quake(2, 8, 0, 1, 0)
		NULL A 0 A_PlaySoundEx("minigun/fire", "Weapon", 1)
		NULL A 0 A_GunFlash
		NULL A 0 A_TakeInventory("Wolverine-MinigunAmmo", 1)
		NULL A 0 A_SpawnItemEx("RifleShellCasing", 2, 8, 30, random(-3, -1), random(0, 1), random(1, 3), random(-60, -45), 128)
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		MHUD B 2 BRIGHT A_FireBullets(3.4, 3.4, -1, 20, "RiflePuff", FBF_USEAMMO | FBF_NORANDOM)
		MHUD A 1
		NULL A 0 A_GunFlash
		NULL A 0 A_TakeInventory("Wolverine-MinigunAmmo", 1)
		NULL A 0 A_SpawnItemEx("RifleShellCasing", 2, 8, 30, random(-3, -1), random(0, 1), random(1, 3), random(-60, -45), 128)
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		MHUD B 2 BRIGHT A_FireBullets(3.4, 3.4, -1, 20, "RiflePuff", FBF_USEAMMO | FBF_NORANDOM)
		MHUD A 1
		NULL A 0 A_ReFire
		NULL A 0 A_PlaySoundEx("minigun/spin", "Weapon", 0)
		MHUD A 4
		Goto Ready
	Flash:
		TNT1 A 1 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	AltFire:
		NULL A 0
		MHUD C 35 ACS_ExecuteAlways(aow_script314, 0)
		MHUD A 18
		Goto Ready
	Spawn:
		NULL A 0 
		Stop
	}
}

Actor PulseBall
{
	PROJECTILE
	+FORCEXYBILLBOARD
	
	Radius 8
	Height 8
	Speed 50
	Damage (20)
	Scale 0.2
	RenderStyle Add
	Alpha 0.9
	SeeSound "plasma/fire"
	DeathSound "plasma/explo"
	Decal PulseScorch
	DamageType "Laser"
	
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("PulseBallSmokePuff", 0, 0, 0, 0, 0, 1)
	Idle:
		PLGX ABCDE 2 BRIGHT
		Loop
	Death:
		PLGX ABCD 3 BRIGHT
		PLGX E 6 BRIGHT A_SpawnItemEx("PulseBallSmokePuff", 0, 0, 0, 0, 0, 1)
		Stop
	}
}

Actor PulseBallBlue : PulseBall {DamageType "Laser"}

Actor MechRocketAmmo : Ammo
{
	Inventory.amount 10
	Inventory.MaxAmount 240
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 240
}

Actor Wolverine-MinigunAmmo : Ammo
{
	Inventory.amount 10
	Inventory.MaxAmount 600
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 600
}

Actor Reload-LRM : Boolean {}

Actor Wolverine-WhatWeapon : Inventory {Inventory.MaxAmount 8}

Actor DamagedWolverine
{
	Health 300
	Radius 58
	Height 128
	Mass 2000
	-SOLID
	-NOBLOCKMAP
	-NOCLIP
	+SHOOTABLE
	PainChance 0
	Scale 1.75
	BloodType "HHMetalHit"
	BloodColor "White"
	Obituary "%o went boom."
	Translation "112:127=[255,255,255]:[0,0,0]"
	DamageFactor "Disarm", 0.0
	States
	{
	Spawn:
		ROB1 AAAA 35
		ROB1 A 35 
		ROB1 AAAA 5 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(96, 128), 0, 0, 3, 0, 128, 0)
		ROB1 A 5 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(96, 128), 0, 0, 3, 0, 128, 0)
		Goto Spawn+5
	Death:
		NULL A 0
		ROB1 A 10 A_NoBlocking
		ROB1 A 10 A_SpawnItemEx("MechExplosionSmall", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		DISR K -1
		Stop
	}
}

Actor EjectedWolverine
{
	Scale 1.75
	Translation "112:127=[255,255,255]:[0,0,0]"
	States
	{
	Spawn:
		ROB1 AAAA 35
		NULL A 0 A_SpawnItemEx("WolverineSwitchPickup")
		Stop
	}
}

Actor WolverineAmmoPackage : CustomInventory
{
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	States
	{
	Pickup:
		NULL A 0 A_GiveInventory("Wolverine-MinigunAmmo", 600)
		NULL A 0 A_GiveInventory("MechRocketAmmo", 240)
		Stop
	}
}