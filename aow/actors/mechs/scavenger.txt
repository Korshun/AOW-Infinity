// Scavenger mech
Actor Scavenger : MechPlayer_Base {
	Health 1500
	Radius 44
	Height 62
	DamageFactor "Falling", 12.0
	Player.DisplayName "Scavenger"
	Player.ForwardMove 0.35
	Player.SideMove 0.15
	Player.soundclass "mech"
	Player.MaxHealth 1500
	Player.viewheight 47
	Player.attackzoffset 13
	Player.ColorRange 112, 127
	Player.MorphWeapon "ScavengerWeapons"
	Scale 1.1
	+NOSKIN
	-NOBLOCKMONST
	BloodType "HHMetalHit"
	BloodColor "White"
	MaxStepHeight 40
	
	States
	{
	Spawn:
		NULL A 0 SetPlayerProperty(0, 0, 3)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 3)
		NULL A 0 A_GiveInventory("ScavengerEject", 1)
		NULL A 0 A_GiveInventory("IsMech", 1)
		ROB2 Q 35
		ROB2 QQQQ 35 A_PlaySound("mech/idle", 5, 1.0)
		ROB2 Q 35
		Loop
	See:
		NULL A 0 A_JumpIfHealthLower(200, "SeeDamaged")
		NULL A 0 A_JumpIf(WaterLevel >= 1, "Drown")
		NULL A 0 A_PlaySound("mech/idle", 5, 1.0, 1)
		NULL A 0 SetPlayerProperty(0, 0, 3)
		ROB2 A 8
		// apparently the left foot is less offset
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, -22, 4)
		NULL A 0 A_SpawnItemEx("MechStepDamageWeak", 0, -22, 4)
		ROB2 BC 8
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, 26, 4)
		NULL A 0 A_SpawnItemEx("MechStepDamageWeak", 0, 26, 4)
		ROB2 D 8
		Goto Spawn
	SeeDamaged:
		NULL A 0 A_JumpIf(WaterLevel >= 1, "Drown")
		NULL A 0 A_PlaySound("mech/idle", 5, 1.0, 1)
		NULL A 0 SetPlayerProperty(0, 0, 3)
		ROB2 A 8
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, -22, 4)
		NULL A 0 A_SpawnItemEx("MechStepDamageWeak", 0, -22, 4)
		NULL A 0 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(48, 64), 0, 0, 3)
		ROB2 BC 8
		NULL A 0 A_SpawnItemEx("MechStepSmokePuff", 0, 26, 4)
		NULL A 0 A_SpawnItemEx("MechStepDamageWeak", 0, 26, 4)
		NULL A 0 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(48, 64), 0, 0, 3)
		ROB2 D 8
		NULL A 0 A_JumpIfHealthLower(200, "SeeDamaged")
		Goto Spawn
	Drown:
		NULL A 0 Thing_Damage(0, 500, 12)
		Goto Spawn
	Missile:
		NULL A 0 SetPlayerProperty(0, 1, 0)
		ROB2 F 7 BRIGHT
		ROB2 Q 10
		NULL A 0 SetPlayerProperty(0, 0, 0)
		Goto Spawn
	Pain.Flash:
		NULL A 0 ACS_ExecuteAlways(aow_script320, 0, 3, 0, 0)
		NULL A 0 ACS_ExecuteAlways(aow_script320, 0, 3, 0, 0)
		NULL A 0 ACS_ExecuteAlways(aow_stealth, 0, 4, 0, 0)
		Goto Spawn
	Death:
		NULL A 0
		NULL A 0 A_JumpIfInventory("IsKamikaze", 1, "Death.IonExplosion")
		Goto DeathGo
	DeathGo:
		NULL A 0 A_TakeInventory("Ingame", 1)
		ROB2 A 10 A_NoBlocking
		NULL A 0 A_StopSound(1) NULL A 0 A_StopSound(5)
		NULL A 0 A_StopSound(2) NULL A 0 A_StopSound(6)
		NULL A 0 A_StopSound(3) NULL A 0 A_StopSound(7)
		NULL A 0 A_StopSound(4) NULL A 0 A_StopSound(8)
		ROB2 GHI 5 BRIGHT
		NULL A 0 A_SpawnItemEx("ScavengerExplosion")
		ROB2 JKLM 5 BRIGHT
		ROB2 NOP 5
		ROB2 P -1
		Stop
	Death.IonExplosion:
		NULL A 0
		NULL A 0 A_JumpIfInventory("IsRed", 1, "Death.NukeExplosion")
		NULL A 0 A_SpawnItemEx("BlueStrikeKamikaze", 0, 0, 32)
		Goto DeathGo
	Death.NukeExplosion:
		NULL A 0
		NULL A 0 A_SpawnItemEx("NukeKamikaze", 0, 0, 32)
		Goto DeathGo
	}
}

Actor ScavengerExplosion {
	+NOINTERACTION
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("ExplosionMainLarge")
		NULL A 0 A_CustomMissile("MechExplosionSmall", 48)
		TNT1 A 8 A_PlaySound("weapons/rocklx")
		NULL A 0 A_SpawnItemEx("ExplosionMainLarge", 12, 16, 32)
		TNT1 A 8 A_PlaySound("weapons/rocklx")
		NULL AAAAA 0 A_SpawnItemEx("MechDeathSmoke", 0, 0, random(64, 120), random(-5, 5), random(-5, 5), random(9, 14), 0, 0, 64)
		NULL A 0 A_SpawnItemEx("ExplosionMainLarge", 14, 32, 64)
		TNT1 A 8 A_PlaySound("weapons/rocklx")
		NULL A 0 A_SpawnItemEx("ExplosionMainLarge", 26, 48, 96)
		TNT1 A 8 A_PlaySound("weapons/rocklx")
		NULL A 0 A_SpawnItemEx("ExplosionMainLarge", 38, 72, 128)
		NULL A 0 A_PlaySound("weapons/rocklx")
		Stop
	}
}
Actor ScavengerSwitch : PermanentPowerup {Powerup.Type "Scavenger"}
Actor PowerScavenger : AOWMorph {powermorph.playerclass "Scavenger"}

Actor ScavengerSwitchPickup : CustomInventory {
	+AUTOACTIVATE
	Inventory.pickupsound "mech/start"
	Translation "112:127=[255,255,255]:[0,0,0]"
	Scale 1.1
	States
	{
	Spawn:
		ROB2 Q -1
		Stop
	Pickup:
		NULL A 0 ACS_ExecuteWithResult(aow_script353)
		Stop
	Use:
		NULL A 0 A_GiveInventory("ScavengerSwitch", 1)
		NULL A 0 ACS_Execute(359, 0, 6)
		Stop
	}
}

Actor PowerScavengerUsed : AOWMorph {powermorph.playerclass "ScavengerUsed"}

Actor ScavengerEject : CustomInventory  {
	+INVBAR
	Inventory.PickupMessage ""
	Inventory.pickupsound ""
	Inventory.MaxAmount 1
	Inventory.icon "ICO_EJCT"
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	Use:
		NULL A 0 ACS_ExecuteAlways(aow_script697, 0, 7)
		Stop
	}
}

Actor EjectedScavengerWeak {
	Health 150
	Radius 44
	Height 62
	Mass 0x7FFFFFFF
	-SOLID
	-NOBLOCKMAP
	-NOCLIP
	+SHOOTABLE
	DamageFactor "Disarm", 0.0
	DamageFactor "Deconstruction", 0.0
	PainChance 0
	Scale 1.1
	BloodType "HHMetalHit"
	BloodColor "White"
	Obituary "%o went boom."
	Translation "112:127=[255,255,255]:[0,0,0]"
	States
	{
	Spawn:
		ROB2 QQQQQ 35
		ROB2 Q 35 
		Goto Idle
	Idle:
		ROB2 Q 5 A_SpawnItemEx("EjectedMechSmokePuff", random(-32, 32), random(-32, 32), random(96, 128), 0, 0, 3)
		Loop
	DeathGo:
		ROB2 Q 10 A_NoBlocking
		ROB2 GHI 5 BRIGHT
		NULL A 0 A_SpawnItemEx("ScavengerExplosion")
		ROB2 JKLM 5 BRIGHT
		ROB2 NOP 5
		ROB2 P -1
		Stop
	}
}

Actor EjectedScavenger {
	Health 300
	Radius 58
	Height 128
	Mass 2000
	-SOLID
	-NOBLOCKMAP
	-NOCLIP
	PainChance 0
	Scale 1.1
	Translation "112:127=[255,255,255]:[0,0,0]"
	States
	{
	Spawn:
		ROB2 QQQQ 35
		NULL A 0 A_SpawnItemEx("ScavengerSwitchPickup")
		Stop
	}
}

Actor ScavengerWeapons : AOWWeapon {
	Weapon.SelectionOrder 1
	Weapon.AmmoUse 1
	Weapon.AmmoGive 400
	Weapon.AmmoType "MachineGunAmmo"
	Obituary "%o couldn't Stop %k's Scavenger."
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.DONTBOB
	Weapon.PreferredSkin "Scavenger"
	States
	{
	Ready:
		MHUD A 1 A_WeaponReady
		Wait
	Deselect:
		NULL A 0 A_JumpIfHealthLower(1, 1)
		Goto Ready
		TNT1 A 1 A_Lower
		Wait
	Select:
		NULL A 0
		NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		MHUD A 1 A_Raise
		Wait
	WaitReload:
		MHUD A 2
		MHUD A 2 A_ReFire
		Goto Ready
	Fire:
		MHUD A 10 A_JumpIfInventory("Wolverine-MinigunAmmo", 2, 2)
		NULL A 0 A_PlaySound("minigun/spin", CHAN_WEAPON)
		Goto AltFire
		NULL A 0 Radius_Quake(2, 8, 0, 1, 0)
		NULL A 0 A_PlaySound("minigun/fire", CHAN_WEAPON, 1.0, 1)
		NULL A 0 A_GunFlash
		NULL A 0 A_SpawnItemEx("RifleShellCasing", 16, -32, 50, random (-3, -1), random(0, 1), random(1, 3), random(-60, -45))
		NULL A 0 A_JumpIfInventory("IsBlue", 1, "FireBlue")
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		MHUD B 2 BRIGHT A_FireBullets(3.4, 3.4, -1, 20, "RiflePuff", FBF_USEAMMO | FBF_NORANDOM)
		MHUD A 1
		NULL A 0 A_GunFlash
		NULL A 0 A_SpawnItemEx("RifleShellCasing", 16, -32, 50, random (-3, -1), random(0, 1), random(1, 3), random(-60, -45))
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		MHUD B 2 BRIGHT A_FireBullets(3.4, 3.4, -1, 20, "RiflePuffBlue", FBF_USEAMMO | FBF_NORANDOM)
		MHUD A 1
		NULL A 0 A_ReFire
		NULL A 0 A_PlayWeaponSound("minigun/spin")
		MHUD A 4
		Goto Ready
	FireBlue:
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		MHUD B 2 BRIGHT A_FireBullets(3.4, 3.4, -1, 20, "RiflePuff", FBF_USEAMMO | FBF_NORANDOM)
		MHUD A 1
		NULL A 0 A_GunFlash
		NULL A 0 A_SpawnItemEx("RifleShellCasing", 16, -32, 50, random (-3, -1), random(0, 1), random(1, 3), random(-60, -45))
		NULL A 0 ACS_ExecuteWithResult(aow_traceroffset, 0, 0)
		MHUD B 2 BRIGHT A_FireBullets(3.4, 3.4, -1, 20, "RiflePuff", FBF_USEAMMO | FBF_NORANDOM)
		MHUD A 1
		NULL A 0 A_ReFire
		NULL A 0 A_PlayWeaponSound("minigun/spin")
		MHUD A 4
		Goto Ready
	Flash:
		TNT1 A 2 BRIGHT A_Light2
		TNT1 A 1 BRIGHT A_Light1
		Goto LightDone
	Spawn:
		NULL A 0
		Stop
	}
}

Actor ScavengerAmmoPackage : CustomInventory {
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	States
	{
	Pickup:
		NULL A 0 A_GiveInventory("Wolverine-MinigunAmmo", 600)
		Stop
	}
}