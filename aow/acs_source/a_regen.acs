//REGENERATION
script aow_script343 OPEN {
	int n, r;
	for (n = 0; n < MAX_PLAYERS; n++) {
		if (!PlayerInGame (n))
			continue;
		
		if (CheckActorInventory (PlayerTid(n), "PlayerRank") < 3) continue;
		if (CheckActorInventory (PlayerTid(n), "IsMech"))
			r = 20;
		else
			r = 5;
		
		if (GetActorHealth (PlayerTid(n))+r >= GetActorSpawnHealth (PlayerTid(n)))
			SetActorHealth (PlayerTid(n), GetActorSpawnHealth (PlayerTid(n)));
		else
			SetActorHealth (PlayerTid(n), GetActorHealth (PlayerTid(n))+r);
	}

	delay (35 * 5);
	restart;
}

// MEDIC REGENERATION
script aow_script352 (void) {
	if (GetHealth() <= 0 || !CheckInventory("IsMedic") || !PlayerInGame (PlayerNumber()))
		terminate;
	
	if (GetHealth() < GetSpawnHealth()) {
		if (CheckInventory("IsMech"))
			HealThing (6);
		else
			HealThing (3);
	}
	
	delay (35);
	restart;
}

// HP/AMMO REGENERATION
script aow_script354 OPEN {
	int i, hp, shp;
	int counter;
	
	while (1) {
		if (Researches[RESERACH_GlobalMedic][0] || Researches[RESERACH_GlobalMedic][1]) {
			for (i = 0; i < MAX_PLAYERS; i++) {
				if (Researches[RESERACH_GlobalMedic][1-GetPlayerInfo(i, PLAYERINFO_TEAM)]) {
					hp = GetActorHealth (PlayerTid(i));
					shp = GetActorSpawnHealth (PlayerTid(i));
					if (hp == shp) continue;
					if (CheckActorInventory (PlayerTid(i), "IsMech")) hp += 2; else hp++;
					if (hp > shp) {hp = shp;}
					SetActorHealth (PlayerTid(i), hp);
				}
			}
		}
		
		if (!counter && (Researches[RESERACH_GlobalAmmo][0] || Researches[RESERACH_GlobalAmmo][1])) {
			for (i = 0; i < MAX_PLAYERS; i++) {
				if (Researches[RESERACH_GlobalAmmo][1-GetPlayerInfo(i, PLAYERINFO_TEAM)])
					GiveActorInventory (PlayerTid(i), "AmmoRegenerationPack", 1);
			}
		}
		
		counter++;
		if (counter >= 10) {counter = 0;}
		delay(35);
	}
}