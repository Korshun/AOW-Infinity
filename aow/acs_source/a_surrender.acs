// SURRENDER SCRIPT
script aow_ccmd_surrender (void) NET {
	if (!PlayerInGame(PlayerNumber())) {
		DisplayCross();
		SETFONT("BigFont");
		HudMessage(s:"You can't surrender as a spectator!";
			HUDMSG_FADEOUT, 240+PlayerNumber(), CR_RED, 0.5, 0.45, 2.0, 1.0);
		terminate;
	}
	
	if (GetCVAR("aowr_surrendermode") == 2) {
		DisplayCross();
		SETFONT("BigFont");
		HudMessage(s:"Surrender is disabled!";
			HUDMSG_FADEOUT, 240+PlayerNumber(), CR_RED, 0.5, 0.45, 2.0, 1.0);
		terminate;
	}
	
	else if (GetCVAR("aowr_surrendermode") == 1) {
		if ( PlayerTeam() == TEAM_RED ) {
			if ( BaseLeft[0] >= 5 ) {
				DisplayCross();
				SETFONT("BigFont");
				HudMessage(s:"It's too early to surrender!";
					HUDMSG_FADEOUT, 240+PlayerNumber(), CR_RED, 0.5, 0.45, 2.0, 1.0);
				terminate;
			}
		}
		if ( PlayerTeam() == TEAM_BLUE ) {
			if ( BaseLeft[1] >= 5 ) {
				DisplayCross();
				SETFONT("BigFont");
				HudMessage(s:"It's too early to surrender!";
					HUDMSG_FADEOUT, 240+PlayerNumber(), CR_RED, 0.5, 0.45, 2.0, 1.0);
				terminate;
			}
		}      
	}
	if (Timer() - LastSurrenderTime[PlayerNumber()] < 35 * SurrenderWaitTime) {
		DisplayCross();
		SETFONT("BigFont");
		HudMessage(s:"Please wait to revote for surrender!";
			HUDMSG_FADEOUT, 240+PlayerNumber(), CR_RED, 0.5, 0.45, 2.0, 1.0);
		terminate;
	}
	LastSurrenderTime[PlayerNumber()] = Timer();
	
	Surrender[PlayerNumber()] = !Surrender[PlayerNumber()];
	SetFont("SMALLFONT");
	if (PlayerTeam() == TEAM_RED)
		HudMessageBold (s:"\cD", d:SurrenderCount(TEAM_RED), s:"\cQ/\cD", d:RedCount(), s:" ",
			s:"\cJof \cARed Team\cJ\n have voted to surrender";
			HUDMSG_FADEOUT, 15000, CR_WHITE,
			INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
	else
		HudMessageBold (s:"\cD", d:SurrenderCount(TEAM_BLUE), s:"\cQ/\cD", d:BlueCount(), s:" ",
			s:"\cJof \cNBlue Team\cJ\n have voted to surrender";
			HUDMSG_FADEOUT, 15000, CR_WHITE,
			INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
	delay(35*2);
	
	ACS_Execute(aow_script751, 0);
}

script aow_script751 (void) {
	int count;
	
	count = SurrenderCount (TEAM_BLUE);
	if ((count > BlueCount()/2 && (BlueCount() <= 6 && RedCount() != 0)) ||
		(count >= BlueCount()-3 && (BlueCount() > 6 && RedCount() != 0))) {
		SetFont("BIGFONT");
		HudMessage (s:"\cNBlue Team surrenders";
			HUDMSG_FADEOUT, 0, CR_YELLOW, 0.5, 0.3, 2.0, 1.0);
		FreezeBold();
		delay(35*2);
		Thing_Destroy(TID_BARRACKS_BLUE);
		delay(5);
		Thing_Destroy(TID_REFINERY_BLUE);
		delay(5);
		Thing_Destroy(TID_RESCENTRE_BLUE);
		delay(5);
		Thing_Destroy(TID_FACTORY_BLUE);
		delay(5);
		Thing_Destroy(TID_REPAIR_BLUE);
		delay(5);
		Thing_Destroy(TID_OBELISK_BLUE);
		Log(s:"Blue team surrenders.");
	}
   
	count = SurrenderCount (TEAM_RED);
	if ((count > RedCount()/2 && RedCount() <= 6 && BlueCount() != 0) ||
		(count >= RedCount()-3 && RedCount() > 6 && BlueCount() != 0)) {
		SetFont("BIGFONT");
		HudMessage (s:"\cARed Team surrenders";
			HUDMSG_FADEOUT, 0, CR_YELLOW, 0.5, 0.3, 2.0, 1.0);
		FreezeBold();
		delay(35*2);
		Thing_Destroy(TID_BARRACKS_RED);
		delay(5);
		Thing_Destroy(TID_REFINERY_RED);
		delay(5);
		Thing_Destroy(TID_RESCENTRE_RED);
		delay(5);
		Thing_Destroy(TID_FACTORY_RED);
		delay(5);
		Thing_Destroy(TID_REPAIR_RED);
		delay(5);
		Thing_Destroy(TID_OBELISK_RED);
		Log(s:"Red team surrenders.");
	}
}