/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * All Out War 2 core mechanism scripts
 * Credit to VoltlocK for originally creating the engine
 * With modifications by Eruanna, Dusk and the Omega Team
 *
 * You may use portions of this script in your project as long as you give
 * credit where credit is due. Please don't be lame and just copy-paste any
 * of this and call it your own. Thanks!
 */

script aow_script999 OPEN {
	RankTitles[0] = "Private";
	RankTitles[1] = "Corporal";
	RankTitles[2] = "Sergeant";
	RankTitles[3] = "Master Sgt.";
	RankTitles[4] = "Officer";
	RankTitles[5] = "Lieutenant";
	RankTitles[6] = "Major";
	RankTitles[7] = "Commando";
	
	if (GetCVar("aowr_disablemechs") == 0)
		ConsoleCommand("set aowr_mechs 1");
	else
		ConsoleCommand("set aowr_mechs 0");

	//Red
	GotBuilding[BUILDING_REFINERY][0] = True;
	GotBuilding[BUILDING_BARRACKS][0] = True;
	GotBuilding[BUILDING_FACTORY][0] = True;
	GotBuilding[BUILDING_RESCENTRE][0] = True;
	GotBuilding[BUILDING_REPAIR][0] = True;
	GotBuilding[BUILDING_OBELISK][0] = True;
	BaseLeft[0] = 5;
	//Blue
	GotBuilding[BUILDING_REFINERY][1] = True;
	GotBuilding[BUILDING_BARRACKS][1] = True;
	GotBuilding[BUILDING_FACTORY][1] = True;
	GotBuilding[BUILDING_RESCENTRE][1] = True;
	GotBuilding[BUILDING_REPAIR][1] = True;
	GotBuilding[BUILDING_OBELISK][1] = True;
	BaseLeft[1] = 5;

	if(GetCVAR("aowr_tickets") <= 1 && GetCVar("aowr_ticketpool") == False)
		ConsoleCommand("set aowr_tickets 75");
	else if(GetCVAR("aowr_tickets") <= 100 && GetCVar("aowr_ticketpool") == True)
		ConsoleCommand("set aowr_tickets 250");
	StartingTickets = GetCVAR("aowr_tickets");
	
	Tickets[0] = StartingTickets;
	Tickets[1] = StartingTickets;

	SetLineSpecial(159,80,342,0,0,0,0);
	
	delay (5);
	
	SpawnSpotForced ("CrateMapMarker", TID_CRATE, 0, 0);
	SpawnSpotForced ("TiberiumMapMarker", TID_TIBERIUM, 0, 0);
	
	delay (5);
	
	// Special case: the war factory isn't present. Accommodate.
	if (!ThingCount (T_NONE, TID_FACTORY_BLUE) && !ThingCount (T_NONE, TID_FACTORY_RED)) {
		GotBuilding[BUILDING_FACTORY][0] = 0;
		GotBuilding[BUILDING_FACTORY][1] = 0;
		BaseLeft[0] = 4;
		BaseLeft[1] = 4;
		Team_GivePoints (TEAM_RED, 1, 0);
		Team_GivePoints (TEAM_BLUE, 1, 0);
	}
	
	// [Dusk] If ticket pool is enabled, spawn ticket counters. These counters
	// are used to inform clients about ticket values, since the left-hand
	// hud is client-sided. "Physically", the ticket counters are map spots
	// spawned at the respective team's Repair Facilities. The counters'
	// angle tells the starting ticket amount, their speed tells the current
	// ticket amount.
	SpawnSpotForced ("MapSpot", TID_REPAIR_BLUE, TID_TICKETCOUNTER_BLUE, 0);
	SpawnSpotForced ("MapSpot", TID_REPAIR_RED, TID_TICKETCOUNTER_RED, 0);
	SetActorAngle (TID_TICKETCOUNTER_BLUE, StartingTickets);
	SetActorAngle (TID_TICKETCOUNTER_RED, StartingTickets);
	if (GetCVar("aowr_ticketpool")) {
		SetActorSpeed (TID_TICKETCOUNTER_BLUE, StartingTickets);
		SetActorSpeed (TID_TICKETCOUNTER_RED, StartingTickets);
	} else {
		SetActorSpeed (TID_TICKETCOUNTER_BLUE, 0);
		SetActorSpeed (TID_TICKETCOUNTER_RED, 0);
	}
	
	// Activate developer mode if the proper cvar is set.
	if (GetCVAR ("aowr_developer")) {
		DeveloperSafetyLock = 0x7FFFFFFF;
		ACS_ExecuteAlways (aow_activatedeveloper, 0);
	}
}

function void ReloadSettings (void) {
	// ==================================
	// DMFLAGS
	if (!GetCVAR ("sv_doubleammo")) ConsoleCommand ("sv_doubleammo 1");
	if (!GetCVAR ("sv_nofov")) ConsoleCommand ("sv_nofov 1");
	if (!GetCVAR ("alwaysapplydmflags")) ConsoleCommand ("alwaysapplydmflags 1");
	if (!GetCVAR ("sv_weapondrop")) ConsoleCommand ("sv_weapondrop 1");
	if (!GetCVAR ("sv_itemrespawn")) ConsoleCommand ("sv_itemrespawn 1");
	if (!GetCVAR ("compat_limited_airmovement")) ConsoleCommand ("compat_limited_airmovement 1");
	
	// [Dusk] the engine assumes that players stay dead for at least a second
	if (GetCVAR ("compat_instantrespawn")) ConsoleCommand ("compat_instantrespawn 0");
	
	// [Dusk] enforce no pointlimit until game is considered over. Pointlimit is used
	// to end the game when needed. We don't want this script to conflict it at any
	// given time.
	if (!GameOver && GetCVAR("pointlimit") != 0) ConsoleCommand("pointlimit 0");
	
	// ==================================
	// CUSTOM CVARS
	// w/ sanity checks
	if (!GetCVAR ("aowr_credflow")) ConsoleCommand ("set aowr_credflow 4");
	if (GetCVAR ("aowr_startcredits") <= 0) ConsoleCommand ("set aowr_startcredits 300");
	if (GetCVAR ("aowr_creditfrag") <= 0) ConsoleCommand ("set aowr_creditfrag 50");	
	if (GetCVAR ("aowr_bonusamount") <= 0) ConsoleCommand ("set aowr_bonusamount 300");	
	if (GetCVAR ("aowr_bonustime") <= 0) ConsoleCommand ("set aowr_bonustime 5");
	if (GetCVAR ("aowr_medicbonusneed") <= 0) ConsoleCommand ("set aowr_medicbonusneed 175");
	if (GetCVAR ("aowr_repairbonus") <= 0) ConsoleCommand ("set aowr_repairbonus 1");	
	if (GetCVAR ("aowr_guardianlimit") <= 0) ConsoleCommand ("set aowr_guardianlimit 2");
	if (GetCVAR ("aowr_ticketpool") <= 0) ConsoleCommand ("set aowr_ticketpool 0");
	//[SP]How long you can stay on a team before you are never switched.
	if (GetCVAR ("aowr_teamstaytime") <= 0) ConsoleCommand("set aowr_teamstaytime 2");
	
	// Plasma Cannon initial delay
	if (!GetCVAR ("aowr_plasmapurchasedelay")) ConsoleCommand("set aowr_plasmapurchasedelay 5");
	//[SP]Maximum number of plasma cannons per team. (Set to -1 for unlimited)
	if (!GetCVAR ("aowr_maxcannonsperteam")) ConsoleCommand("set aowr_maxcannonsperteam 4");
	// [Dusk] Time (in minutes) to determine plasma cannon cooldown. Use -1 to disable
	if (!GetCVAR ("aowr_plasmacooldown")) ConsoleCommand("set aowr_plasmacooldown 3");
	
	//[SP]How long you have to wait to buy a new superweapon beacon. (Minutes) (Set to -1 to disable wait)
	if (!GetCVAR ("aowr_beaconpurchasedelay"))ConsoleCommand("set aowr_beaconpurchasedelay 5");
	
	// [SP]Maximum number of time cops per team. (Set to -1 for unlimited)
	if (!GetCVAR ("aowr_maxtimecopsperteam")) ConsoleCommand("set aowr_maxtimecopsperteam 1");
	
	// [Dusk] How many minutes until Timed C4 can be used. (-1 to disable)
	if (!GetCVAR ("aowr_c4delay")) ConsoleCommand("set aowr_c4delay 5");
	
	//[SP] Time (in minutes) before sudden death.
	// [Dusk] changed default to 45, -1 means immediate
	if (GetCVAR ("aowr_suddendeathtime") == 0) ConsoleCommand("set aowr_suddendeathtime 45");
	
	// [Dusk] Sudden death degeneration
	if (GetCVAR ("aowr_degeneration") <= 0) ConsoleCommand("set aowr_degeneration 0");
	if (GetCVAR ("aowr_degenerationinterval") <= 0) ConsoleCommand("set aowr_degenerationinterval 10");
	
	if (GetCVAR ("aowr_tournamentmode") <= 0) ConsoleCommand("set aowr_tournamentmode 0");
	if (GetCVAR ("aowr_disablemechs") <= 0) ConsoleCommand("set aowr_disablemechs 0");
	if (GetCVAR ("aowr_surrendermode") <= 0) ConsoleCommand("set aowr_surrendermode 0");
	
	// [Dusk] How much score does destroying a building award you, in addition to difference?
	if (GetCVAR ("aowr_buildingpoints") <= 0) ConsoleCommand ("set aowr_buildingpoints 125");
	
	// [Dusk] Tiberium amounts
	if (!GetCVAR ("aowr_tiberium_self"))
		ConsoleCommand ("set aowr_tiberium_self 225");
	if (!GetCVAR ("aowr_tiberium_other"))
		ConsoleCommand ("set aowr_tiberium_other 75");
	if (!GetCVAR ("aowr_tiberium_advperc"))
		ConsoleCommand ("set aowr_tiberium_advperc 300");
}

// Server settings
script aow_script599 OPEN {
	delay (1);
	while (1) {
		ReloadSettings ();
		Delay (35 * 5);
	}
	
	restart;
}

// credit flow
script aow_creditflow OPEN {
	int i, p, redflow, blueflow;
	while (1) {
		p = 25;
		if (GotBuilding[BUILDING_RESCENTRE][0] == 1) p += 25;
		if (GotBuilding[BUILDING_REFINERY][0] == 1) p += 50;
		if (MiniRefinery[0] == 1) p += 75;
		redflow = (GetCVar("aowr_credflow") * p) / 100;
		if (!redflow) redflow = 1;
		
		p = 25;
		if (GotBuilding[BUILDING_RESCENTRE][1] == 1) p += 25;
		if (GotBuilding[BUILDING_REFINERY][1] == 1) p += 50;
		if (MiniRefinery[1] == 1) p += 75;
		blueflow = (GetCVar("aowr_credflow") * p) / 100;
		if (!blueflow) blueflow = 1;
		
		for (i = 0; i < MAX_PLAYERS; i++) {
			if (PlayerInGame(i) && GetActorProperty (3800+i, APROP_HEALTH) > 0 && !IsAdmin[i]) {
				if (GetPlayerInfo (i, PLAYERINFO_TEAM) == TEAM_RED)
					GiveCredits (i, redflow);
				else if (GetPlayerInfo (i, PLAYERINFO_TEAM) == TEAM_BLUE)
					GiveCredits (i, blueflow);
			}
		}
		
		delay (35);
	}
}

//Setting Player scripts
script aow_script10 RESPAWN {
	if (StartTime[PlayerNumber()] == 0) InitPlayer();

	GiveInventory("Ingame",1);
	GiveInventory("IsInSpawnRoom", 1);
	if(PlayerTeam() == TEAM_BLUE) {
		GiveInventory ("BlueTeamArmor",1);
		GiveInventory ("IsBlue",1);
		TakeInventory ("RedTeamArmor",1);
		TakeInventory ("IsRed",1);
	}
	else if(PlayerTeam() == TEAM_RED)
	{
		GiveInventory ("RedTeamArmor",1);
		GiveInventory ("IsRed",1);
		TakeInventory ("BlueTeamArmor",1);
		TakeInventory ("IsBlue",1);
	}
	
	Thing_ChangeTID (0, PlayerNumber()+3800);

	if(GetPlayerInfo(PlayerNumber(),PLAYERINFO_GENDER) == 1) {
		SetActorProperty(0,APROP_DeathSound,"female/death");
		GiveInventory("IsFemale",1);
	}
	
	ACS_ExecuteAlways (aow_script97, 0);
	SetWeapon ("Unarmed");
}

script aow_script11 ENTER {
	delay(5);
	InitPlayer();

	GiveInventory("Ingame",1);
	GiveInventory("IsInSpawnRoom", 1);
	if(PlayerTeam() == TEAM_BLUE)
	{
		GiveInventory("BlueTeamArmor",1);
		GiveInventory("IsBlue",1);
	}
	else if(PlayerTeam() == TEAM_RED)
	{
		GiveInventory("RedTeamArmor",1);
		GiveInventory("IsRed",1);
	}

	Thing_ChangeTID(0,PlayerNumber()+3800);

	if(GetPlayerInfo(PlayerNumber(),PLAYERINFO_GENDER) == 1)
	{
		SetActorProperty(0,APROP_DeathSound,"female/death");
		GiveInventory("IsFemale",1);
	}
	
	delay(5);
	ACS_ExecuteAlways (aow_playerstatushud_serverside, 0); // display credits/exp HUD
	ACS_ExecuteAlways (aow_script92, 0);
	
	// If we're in developer mode, make him gamemaster.
	if (Developer) {
		IsAdmin[PlayerNumber()] = true;
		
		SetFont ("BIGFONT");
		HudMessage(s:"This game is in developer mode. You have gamemaster access.";
			HUDMSG_FADEINOUT, 0, CR_BLUE, 0.5, 0.2, 7.0, 0.5, 1.0);
	}
}

 //--------------------------START UP MESSAGES
script aow_script301 ENTER CLIENTSIDE {
	SetFont ("BIGFONT");
	HudMessage(s:"Welcome to \cFAll Out War: \cHInfinity\cF!";
		HUDMSG_FADEINOUT, 0, CR_WHITE, 0.5, 0.3, 7.0, 0.5, 1.0);
	SetFont ("SMALLFONT");
	HudMessage (s:"Type \cFAOWHelp\cJ in console for information on how to play!";
		HUDMSG_FADEOUT, 0, CR_WHITE, 0.5, 0.45, 7.0, 1.0);
	HudMessage (s:"See \cFOptions \cN-> \cFCustomise Controls\c- for keybinds";
		HUDMSG_FADEOUT, 0, CR_WHITE, 0.5, 0.5, 7.0, 1.0);
	
	delay (20);
	LocalAmbientSound ("voltlock/welcome", 127);
}

script aow_script8 (int Gone) DISCONNECT {
	IsAdmin[Gone] = false;
	SetCreditsSilent(Credits[Gone+1], 0);
	Experience[Gone] = 0;
	Rank[Gone] = RANK_PRIVATE;
	Surrender[Gone] = 0;
	PlasmaCooldown[Gone] = 0;
	StartTime[Gone] = 0;
	
	if (MechGetter[0] == Gone && GettingMech[0])
		GettingMech[0] = 0;
	else if (MechGetter[1] == Gone && GettingMech[1])
		GettingMech[1] = 0;
	
	if (Surrender[Gone]) {
		Surrender[Gone] = false;
		if (PlayersTeam[Gone] == TEAM_RED) {
			HudMessageBold (s:"\cD", d:SurrenderCount (TEAM_RED), s:"\cQ/\cD", d:RedCount(), s:" ",
				s:"\cJof \cARed Team\cJ\n have voted to surrender";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
		} else if (PlayersTeam[Gone] == TEAM_BLUE) {
			HudMessageBold (s:"\cD", d:SurrenderCount (TEAM_BLUE), s:"\cQ/\cD", d:BlueCount(), s:" ",
				s:"\cJof \cNBlue Team\cJ\n have voted to surrender";
				HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS2, 2.0, 1.0);
		}
	}
	else // % of players surrendering could now be higher. Al demands a recount!
		ACS_Execute(aow_script751, 0);
}

script aow_script303 OPEN {
	// these maps are too small to have portals
	delay (5);
	int levelnum = GetLevelInfo (LEVELINFO_LEVELNUM);
	if (levelnum ==  2) {UsePortals = 0;}
	if (levelnum ==  4) {UsePortals = 0;}
	if (levelnum ==  7) {UsePortals = 0;}
	if (levelnum == 15) {UsePortals = 0;}
}

function void InitPlayer (void) {
	SetCreditsSilent(Credits[PlayerNumber()+1], GetCVar("aowr_startcredits"));
	Experience[PlayerNumber()] = 0;
	Rank[PlayerNumber()] = RANK_PRIVATE;
	Surrender[PlayerNumber()] = False;
	PlayersTeam[PlayerNumber()+1] = PlayerTeam();
	StartTime[PlayerNumber()]=Timer();
	HasTeleported[PlayerNumber()] = false;
	RepairBonus[PlayerNumber()] = 0;
	MedicBonus[PlayerNumber()] = 0;
}