// Admin pistol
actor AdminPistol : AOWWeapon {
	+NOAUTOFIRE
	+NOAUTOAIM
	Inventory.PickupMessage "If you see this message, you are \cgHACKING."
	Obituary "%o was smitten by %k's administrative power."
	Weapon.SelectionOrder 85
	Weapon.PreferredSkin "MarineAdmin"
	States {
	Spawn:
		TNT1 A 0 
		stop
	Ready: 
		GPIS A 1 A_WeaponReady
		wait
	Deselect:
		TNT1 A 1 A_Lower 
		loop
	Select: 
		GPIS A 1 A_Raise
		Loop
	Fire:
		GPIS B 0 A_JumpIfInventory ("AdminPistolRotation", 2, "Fire.Deconstruction")
		GPIS B 0 A_JumpIfInventory ("AdminPistolRotation", 1, "Fire.Repair")
		goto Fire.Normal
	Fire.Normal:
		GPIS B 0 A_GunFlash
		GPIS B 0 A_PlayWeaponSound ("pistol/fire")
		GPIS B 0 A_FireBullets (0, 0, 1, 10000, "ExplosionPuff")
		GPIS B 0 A_FireBullets (0, 0, 1, 10000, "ExplosionPuffBlue")
		GPIS B 0 A_FireBullets (0, 0, 1, 100000, "HandgunPuff")
		GPIS B 2 A_FireBullets (0, 0, 1, 100000, "HandgunPuffBlue")
		GPIS C 0 A_FireBullets (0, 0, 1, 10000, "ExplosionPuff")
		GPIS C 0 A_FireBullets (0, 0, 1, 10000, "ExplosionPuffBlue")
		GPIS C 0 A_FireBullets (0, 0, 1, 100000, "HandgunPuff")
		GPIS C 2 A_FireBullets (0, 0, 1, 100000, "HandgunPuffBlue")
		GPIS B 0 A_GunFlash
		GPIS B 0 A_PlayWeaponSound ("pistol/fire")
		GPIS B 0 A_FireBullets (0, 0, 1, 10000, "ExplosionPuff")
		GPIS B 0 A_FireBullets (0, 0, 1, 10000, "ExplosionPuffBlue")
		GPIS B 0 A_FireBullets (0, 0, 1, 100000, "HandgunPuff")
		GPIS B 2 A_FireBullets (0, 0, 1, 100000, "HandgunPuffBlue")
		GPIS C 0 A_FireBullets (0, 0, 1, 10000, "ExplosionPuff")
		GPIS C 0 A_FireBullets (0, 0, 1, 10000, "ExplosionPuffBlue")
		GPIS C 0 A_FireBullets (0, 0, 1, 100000, "HandgunPuff")
		GPIS C 2 A_FireBullets (0, 0, 1, 100000, "HandgunPuffBlue")
		GPIS A 0 A_Refire
		goto Ready
	Fire.Repair:
		GPIS A 0 A_GunFlash
		GPIS A 0 A_PlayWeaponSound ("repairgun/fire")
		GPIS A 0 A_FireBullets (0.5, 0.5, 2, 0, "RepairDisarmPuff") // visual puff
		GPIS A 0 A_FireBullets (0.5, 0.5, 2, 0, "MedicPuff") // visual puff
		GPIS A 0 ACS_ExecuteAlways (650, 0, 500, 0) // repair+heal
		GPIS A 2 BRIGHT A_FireBullets (0, 0, 2, 500, "DisarmPuff") // disarm
		GPIS B 0 A_GunFlash
		GPIS B 0 A_PlayWeaponSound ("repairgun/fire")
		GPIS B 0 A_FireBullets (0.5, 0.5, 2, 0, "RepairDisarmPuff") // visual puff
		GPIS B 0 A_FireBullets (0.5, 0.5, 2, 0, "MedicPuff") // visual puff
		GPIS B 0 ACS_ExecuteAlways (650, 0, 500, 0) // repair+heal
		GPIS B 2 BRIGHT A_FireBullets (0, 0, 2, 500, "DisarmPuff") // disarm
		GPIS A 0 A_ReFire
		goto Ready
	Fire.Deconstruction:
		GPIS A 0 A_GunFlash
		GPIS A 0 A_PlayWeaponSound ("repairgun/fire")
		GPIS A 1 A_FireBullets (0, 0, 2, 500, "DeconstructionPuff")
		GPIS B 1 A_FireBullets (0, 0, 2, 500, "DeconstructionPuff")
		GPIS A 0 A_GunFlash
		GPIS A 0 A_PlayWeaponSound ("repairgun/fire")
		GPIS A 1 A_FireBullets (0, 0, 2, 500, "DeconstructionPuff")
		GPIS B 1 A_FireBullets (0, 0, 2, 500, "DeconstructionPuff")
		GPIS A 0 A_ReFire
		goto Ready
	AltFire:
		GPIS B 0 A_PlayWeaponSound ("switch/shotgun")
		GPIS B 4 ACS_ExecuteAlways (402, 0, 13)
		GPIS C 5
		goto Ready
	Flash:
		PISF A 1 Bright A_Light2
		PISF B 1 Bright A_Light1
    		Goto LightDone
	} 
}

actor AdminPistolRotation : Inventory {inventory.maxamount 2}
actor TimePuff : HHBulletPuff {DamageType "TimeRed"}
actor TimePuffBlue : HHBulletPuff {DamageType "TimeBlue"}

actor Administrator : Player {
	Health 200
	player.maxhealth 200
	Player.DisplayName "Administrator"
	Player.StartItem "Unarmed", 1
	Player.StartItem "IsAdmin", 1
	player.forwardmove 1.2
	player.sidemove 1.2
	
	+NOSKIN
	+INVULNERABLE
	+NODAMAGE
	+DONTTRANSLATE
	+FULLVOLDEATH
	-PICKUP
	-SHOOTABLE
	
	deathsound "brain/death" // :V
	Player.MorphWeapon "AdminPistol"
}

actor AdministratorSwitch : PermanentPowerup {Powerup.Type "Administrator"}
actor PowerAdministrator : PowerMorph {
	PowerMorph.PlayerClass "Administrator"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_ADDSTAMINA|MRF_NEWTIDBEHAVIOUR|MRF_WHENINVULNERABLE)
}

actor AdminCrown {
	+CLIENTSIDEONLY +NOINTERACTION +NOGRAVITY
	states {
	Spawn:
		CRWN A -1 BRIGHT
		stop
	}
}
actor IsAdmin : Inventory {inventory.maxamount 1}
actor AdminFlight : PermanentPowerup {Powerup.Type "Flight"}