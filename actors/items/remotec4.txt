// REMOTE C4
// TODO: make the weapon sprite include the detonator!

actor RemoteC4Ammo : Ammo {
	inventory.amount 0
	inventory.maxamount 4
	ammo.backpackamount 1
	ammo.backpackmaxamount 4
}

actor RemoteC4 : AOWWeapon {
	+NOALERT
	obituary "%o bonks %k with a 100kg Remote C4."
	weapon.ammotype "RemoteC4Ammo"
	weapon.ammouse 1
	weapon.ammogive 5
	states {
	Ready:
		RC4G B 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "ReadyFull")
		RC4G B 5 A_WeaponReady (WRF_NOPRIMARY)
		loop
	ReadyFull:
		RC4G A -1 A_WeaponReady
		wait
	Deselect:
		RC4G B 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "DeselectFull")
		RC4G B 0 A_Lower
		RC4G B 1 A_Lower
		goto Deselect+1
	DeselectFull:
		RC4G A 0 A_Lower
		RC4G A 1 A_Lower
		loop
	Select:
		RC4G B 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "SelectFull")
		RC4G B 1 A_Raise
		RC4G B 0 A_Raise
		loop
	SelectFull:
		RC4G A 0 A_TakeInventory ("RemoteC4Detonator", 1)
		RC4G A 1 A_Raise
		RC4G A 0 A_Raise
		goto SelectFull+1
	Fire:
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "ReadyFull")
		RC4G A 1 offset (1,31)
		RC4G A 1 offset (7,37)
		RC4G A 1 offset (13,43)
		RC4G A 1 offset (19,49)
		RC4G A 1 offset (26,56)
		RC4G A 1 offset (34,64)
		RC4G A 1 offset (42,72)
		RC4G A 1 offset (54,84) A_PlaySound ("c4/remote", CHAN_WEAPON)
		RC4G A 1 offset (60,90)
		RC4G A 1 offset (40,70)
		RC4G B 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "ReadyFull")
		RC4G B 0 A_JumpIfInventory ("IsBlue", 1, "FireBlue")
		RC4G B 5 A_FireCustomMissile ("RemoteC4Thrown")
		RC4G B 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "FireEndFull")
		goto FireEnd
	FireBlue:
		TNT1 A 5 A_FireCustomMissile ("RemoteC4ThrownBlue")
		TNT1 A 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "FireEndFull")
		goto FireEnd
	FireEnd:
		RC4G B 1 offset (30,60)
		RC4G B 1 offset (10,80)
		RC4G B 1 offset (0,100)
		RC4G B 1 offset (0,110)
		goto Ready
	FireEndFull:
		RC4G B 1 offset(30,60)
		RC4G B 1 offset(10,40)
		RC4G B 1 offset(0,40)
		RC4G A 1 offset(0,60)
		RC4G A 1 offset(0,80)
		RC4G A 1 offset(0,100)
		RC4G A 1 offset(0,110)
		goto Ready
	AltFire:
		RC4E A 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "AltFireFull")
		RC4E ABCDEFGH 1
		RC4E I 2
		RC4E J 2 A_PlayWeaponSound ("c4/remote2")
		RC4E I 4 A_GiveInventory ("C4DetonateCheck", 1)
		RC4E H 6 A_TakeInventory ("C4DetonateCheck", 666)
		RC4E GFEDCBA 1
		goto Ready
	AltFireFull:
		RC4D ABCDEFGH 1
		RC4D I 2
		RC4D J 2 A_PlayWeaponSound ("c4/remote2")
		RC4D I 5 A_GiveInventory ("C4DetonateCheck", 1)
		RC4D H 30 A_TakeInventory ("C4DetonateCheck", 666)
		RC4D GFEDCBA 1
		goto ReadyFull
	}
}

actor RemoteC4Thrown {
	PROJECTILE
	-NOGRAVITY
	radius 6
	height 8
	speed 30
	damage 0
	scale 0.2
	health 250
	damagetype "ExplosionRed"
	states {
	Spawn:
		RC4T A -1
		stop
	Death:
		RC4T A 35 A_NoGravity
		RC4T A 0 A_SpawnItem ("RemoteC4Armed")
		stop
	}
}

actor RemoteC4ThrownBlue : RemoteC4Thrown {
	damagetype "ExplosionBlue"
	//translation "16:47=192:207", "168:191=192:207"
	states {
	Death:
		RC4T A 35 A_NoGravity
		RC4T A 0 A_SpawnItem ("RemoteC4ArmedBlue")
		stop
	}
}


actor RemoteC4Armed {
	+NOEXTREMEDEATH
	+NOGRAVITY
	+NOBLOOD
	+SHOOTABLE
	+NOTARGETSWITCH
	-SOLID
	scale 0.2
	health 66
	radius 6
	height 6
	mass 0x7FFFFFFF
	speed 0
	damage 0
	damagetype "ExplosionRed"
	obituary "%o failed to spot %k's Remote C4!"
	translation "16:47=112:127", "168:191=114:127"
	damagefactor "FistRed", 0.0
	damagefactor "FistBlue", 0.0
	damagefactor "BulletRed", 0.0
	damagefactor "BulletBlue", 0.0
	damagefactor "RifleRed", 0.0
	damagefactor "RifleBlue", 0.0
	damagefactor "ExplosionRed", 0.0
	damagefactor "ExplosionBlue", 0.0
	damagefactor "FireRed", 0.0
	damagefactor "FireBlue", 0.0
	damagefactor "ChemicalRed", 0.0
	damagefactor "ChemicalBlue", 0.0
	damagefactor "ChemicalSpecial", 0.0
	damagefactor "LaserRed", 0.0
	damagefactor "LaserBlue", 0.0
	damagefactor "RazorWireRed", 0.0
	damagefactor "RazorWireBlue", 0.0
	damagefactor "ExplosionMechRed", 0.0
	damagefactor "ExplosionMechBlue", 0.0
	damagefactor "TimeRed", 0.0
	damagefactor "TimeBlue", 0.0
	damagefactor "MassDriverRed", 0.0
	damagefactor "MassDriverBlue", 0.0
	damagefactor "Mechstomp", 0.0
	damagefactor "Monster", 0.0
	
	states {
	Spawn:
		RC4T A 0
		RC4T A 0 A_PlaySound ("terrorist/select", CHAN_VOICE)
		RC4T A 0 A_GiveInventory ("IsRed", 1)
		goto Idle
	Idle:
		RC4T A 0 A_JumpIfInTargetInventory ("RemoteC4Ammo", 4, "Death.Disarm")
		RC4T A 0 A_JumpIfInTargetInventory ("C4DetonateCheck", 1, "Death")
		RC4T A 5
		loop
	Death:
		RC4T A 0 A_NoGravity
		RC4T A 10 A_PlaySound ("c4/detonate")
		TNT1 A 0 A_SpawnItemEx ("ExplosionMainLarge")
		TNT1 A 0 A_PlaySound ("weapons/rocklx")
		TNT1 A 0 A_PlayWeaponSound ("weapons/rocklx")
		TNT1 A 0 ACS_ExecuteAlways (978, 0, 1)
		TNT1 A 0 A_Explode (500, 64)
		TNT1 A 0 A_Explode (400, 196)
		TNT1 A 0 A_Explode (500, 256)
		TNT1 A 0 A_Explode (250, 640)
		goto DeathFinish
	DeathFinish:
		TNT1 A 0 A_SpawnItemEx ("FlameWallDecay", random(-48,48), random(-48,48), 4, 0, 0, 0, 0, 0, 160)
		TNT1 A 5
		stop
	Death.Disarm:
		RC4T A 35 A_PlaySound ("c4/disarm")
		RC4T A 1 A_FadeOut (0.05)
		wait
	}
}

actor RemoteC4ArmedBlue : RemoteC4Armed {
	damagetype "ExplosionBlue"
	translation "16:47=112:127", "168:191=112:127"
	states {
	Spawn:
		RC4T A 0
		RC4T A 0 A_PlaySound ("terrorist/select", CHAN_VOICE)
		RC4T A 0 A_GiveInventory ("IsBlue", 1)
		goto Idle
	DeathFinish:
		TNT1 A 0 A_SpawnItemEx("FlameWallDecayBlue", random(-48,48), random(-48,48), 4, 0, 0, 0, 0, 0, 160)
		TNT1 A 5
		stop
	}
}

actor C4DetonateCheck : Boolean {}