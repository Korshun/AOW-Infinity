// Proxy mine
actor ProxyMine : InventoryItem {
	inventory.icon "mineicon"
	tag "Proxy C4 -mine"
	inventory.maxamount 6
	states {
	Use:
		TNT1 A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Failure")
		TNT1 A 0 A_JumpIfInventory ("IsMech", 1, "Use.Mech")
		TNT1 A 0 A_JumpIf (ACS_ExecuteWithResult(346, 2) == 0, "Limit")
		TNT1 A 0 A_JumpIfInventory ("PowerBerserk", 1, "Failure")
		TNT1 A 0 A_PlayWeaponSound ("c4/proxy")
		TNT1 A 0 A_JumpIfInventory ("IsBlue", 1, "UseBlue")
		TNT1 A 0 A_SpawnItemEx ("ProxyMineRed", 32, 0, 1, 0, 0, 0, 0, SXF_SETMASTER)
		TNT1 A 0 ACS_ExecuteAlways (349, 0, 2)
		stop
	UseBlue:
		TNT1 A 0 A_SpawnItemEx ("ProxyMineBlue", 32, 0, 1, 0, 0, 0, 0, SXF_SETMASTER)
		TNT1 A 0 ACS_ExecuteAlways (349, 0, 2)
		stop
	Limit:
		TNT1 A 0 ACS_ExecuteAlways (349, 0, 2)
		// TNT1 A 0 ACS_ExecuteAlways (347, 0) // "At limit!"
		fail
	}
}

actor ProxyMineCount : Inventory {inventory.maxamount 6}

actor ProxyMineRed {
	MONSTER
	-SOLID
	-FRIENDLY
	-COUNTKILL
	+FLOORCLIP
	+NOICEDEATH
	+NOTARGET
	+NEVERRESPAWN
	+NOBLOOD
	+NOCLIP
	+LOOKALLAROUND
	+AMBUSH
	+QUICKTORETALIATE
	+CANPASS
	
	obituary "$OB_MINE" // "%o tripped over a proxy mine."
	radius 8
	height 14
	health 200
	damage 0
	speed 0
	scale .2
	mass 0x7FFFFFFF
	
	damagefactor "FistRed",		0.0	damagefactor "FistBlue",	0.0
	damagefactor "BulletRed",	0.0	damagefactor "BulletBlue",	0.0
	damagefactor "RifleRed",	0.0	damagefactor "RifleBlue",	0.0
	damagefactor "ExplosionRed",	0.0	damagefactor "ExplosionBlue",	0.0
	damagefactor "FireRed",		0.0	damagefactor "FireBlue",	0.0
	damagefactor "ChemicalRed",	0.0	damagefactor "ChemicalBlue",	0.0
	damagefactor "LaserRed",	1.25	damagefactor "LaserBlue",	1.25
	damagefactor "RazorWireRed",	0.0	damagefactor "RazorWireBlue",	0.0
	damagefactor "TimeRed",		0.0	damagefactor "TimeBlue",	0.0
	damagefactor "Superweapon",	0.0	damagefactor "SuperweaponIon",	0.0
	damagefactor "MassDriverRed",	0.0	damagefactor "MassDriverBlue",	0.0
	damagefactor "ExplosionMechRed", 0.0	damagefactor "ExplosionMechBlue", 0.0
	damagefactor "FireSpecial",	0.0
	damagefactor "ChemicalSpecial",	0.0
	damagefactor "Disarm",		1.0
	damagefactor "Deconstruction",	1.0
	damagefactor "Mechstomp",	0.0
	damagefactor "Flash",		0.0
	damagefactor "Monster",		0.0
	translation "112:127=175:190"
	
	states {
	Spawn:
		PC4T A 0
		PC4T A 0 A_ChangeFlag("Friendly", 0)
	Idle:
		PC4T A 6 A_LookEx (LOF_NOSOUNDCHECK, 0, 64)
		Loop
	See:
		PC4T A 0 A_JumpIfCloser(64, "Melee") //Checks if they are close enough. If so goes to Melee.
		PC4T A 2 A_JumpIfTargetInLOS ("Melee") //Not sure what this is for....
		PC4T A 2 A_ClearTarget
		goto Idle
	Clear:
		PC4T A 2 A_ClearTarget
		goto Idle
	Melee:
		PC4T A 0
		PC4T AA 1 A_JumpIfInTargetInventory ("IsBlue", 1, "Death") //If you are blue, you get blown up
		goto Clear
	Death:
		PC4T A 5
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ExplosionMain")
		TNT1 A 0 A_PlaySound ("weapons/rocklx")
		TNT1 A 0 A_CustomMissile ("ProxyMineExplosion")
		TNT1 A 1
		stop
	Death.LaserRed:
	Death.LaserBlue:
	Death.Deconstruction:
	Death.Disarm:
		PC4T A 18 A_PlaySound("c4/disarm")
		PC4T A 1 A_Fadeout(0.1) 
		wait
	}
}

actor ProxyMineExplosion {
	PROJECTILE
	-RANDOMIZE
	+NOCLIP
	speed 1
	damage 0
	obituary "$OB_MINE" // "%o tripped over a proxy mine."
	damagetype "RifleRed"
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_Explode (50, 128, 1)
		stop
	}  
}

actor ProxyMineBlue : ProxyMineRed
{
	translation "112:127=200:207"	
	states
	{
	Melee:
		PC4T A 0
		PC4T AA 1 A_JumpIfInTargetInventory ("IsRed", 1, "Death")
		goto Clear
	Death:
		PC4T A 5
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ExplosionMain")
		TNT1 A 0 A_PlaySound ("weapons/rocklx")
		TNT1 A 0 A_CustomMissile ("ProxyMineExplosionBlue")
		TNT1 A 1
		stop
	}
}

actor ProxyMineExplosionBlue : ProxyMineExplosion {damagetype "RifleBlue"}