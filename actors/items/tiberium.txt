// Tiberium Crystal
actor TiberiumCrystalGiver : CustomInventory 21407 {
	+AUTOACTIVATE +BRIGHT
	+BLOODLESSIMPACT +NODAMAGETHRUST
	obituary "%o ventured too close to Tiberium."
	inventory.respawntics 175 // 5*35
	inventory.pickupmessage "Picked up a Tiberium Crystal!"
	damagetype "ChemicalSpecial"
	
	states {
	Spawn:
		TIBE AA 0 Thing_ChangeTID (0, 402)
		TIBE A 35 BRIGHT A_Explode (24, 112)
		wait
	Pickup:
		TIBE A 0 ACS_ExecuteWithResult (357)
		stop
	Use:
		TNT1 A 0 A_GiveInventory ("TiberiumCrystal", 1)
		stop
	HideDoomish:
		TNT1 A 1050 Thing_ChangeTID (0, 0)
		TNT1 A 0 A_RestoreSpecialPosition
		TNT1 A 1 A_RestoreSpecialDoomThing
		Stop
	}
}

actor TiberiumCrystalDropped : TiberiumCrystalGiver {
	states {
	Spawn:
		TIBE AA 0 Thing_ChangeTID (0, 402)
		TIBE A 0 ThrustThing (random(0, 256), random (1, 5), 0, 0)
		TIBE A 0 ThrustThingZ (0, 20, 0, 0)
	Idle:
		TIBE AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 35 BRIGHT A_Explode (24, 112)
		TIBE A 0 A_SetTranslucent (0.8, 1)
		TIBE A 1 A_FadeOut
		wait
	}
}

actor TiberiumCrystal : CustomInventory {
	+INVBAR
	inventory.pickupmessage "You aquired yourself some Tiberium."
	inventory.maxamount 1
	inventory.icon "tibeicon"
	tag "Tiberium Crystal - bring this to Refinery!"
	states {
	Spawn:
		TNT1 A 0
		stop
	Use:
		TNT1 A 0 A_JumpIfInventory ("IsMech", 1, "UseMech")
		TNT1 A 0 A_JumpIfInventory ("IsBlue", 1, "UseBlue")
		TNT1 A 0 A_SpawnItemEx ("TiberiumCrystalThrown", 16, 0, 32, 6, 0, 5)
		stop
	UseBlue:
		TNT1 A 0 A_SpawnItemEx ("TiberiumCrystalThrownBlue", 16, 0, 32, 6, 0, 5)
		stop
	UseMech:
		TNT1 A 0 ACS_ExecuteAlways (697, 0, 255)
		fail
	}
}

actor TiberiumCrystalThrown {
	PROJECTILE
	-NOGRAVITY
	+GHOST +THRUGHOST
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	+BRIGHT
	obituary "%o \cKwas freaked out by \c-%k\cK's flying Tiberium."
	radius 16 height 32
	speed 10 damage 0
	gravity 0.75
	damagetype "ChemicalRed"
	states {
	Spawn:
		TIBE A 0
		TIBE A 0 BRIGHT A_Explode (2, 96)
		TIBE A 0 BRIGHT A_SpawnItemEx("ChemicalDeathShot", random(-16,16), random(-16,16), random(16,64), 0, 0, 0.5*random(2,5), 0, 128, 32)
		TIBE AA 2 BRIGHT A_JumpIf(waterlevel > 1, "Water")
		loop
	Death:
		TIBE A 0 BRIGHT
		TIBE A 0 BRIGHT A_SpawnItemEx ("TiberiumCrystalSitting", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 4
		stop
	Water:
		TIBE A 15 BRIGHT A_SetTranslucent (1.0, 0)
	WaterLoop:
		TIBE A 0 BRIGHT A_SpawnItemEx("ChemicalDeathShot", random(-16,16), random(-16,16), random(16,64), 0, 0, 0.5*random(2,5), 0, 128, 32)
		TIBE AAA 1 BRIGHT A_FadeOut(0.05)
		loop
	}
}

actor TiberiumCrystalSitting {
	+BLOODLESSIMPACT +NODAMAGETHRUST
	obituary "%o was \cDturned green\c- by %k's Tiberium crystal."
	radius 16 height 32
	speed 0 damage 0
	Mass 0x7FFFFFFF
	damagetype "ChemicalRed"
	reactiontime 15
	states {
	Spawn:
		TIBE AA 0 A_SpawnItemEx ("TiberiumCrystalFX")
		TIBE AAAAAAAAAAAAAAA 18 BRIGHT A_Explode (24, 128)
		TIBE AAAAAAAAAAAAAAA 17 BRIGHT A_Explode (24, 128)
	Death:
		TIBE A 0 A_SetTranslucent (1, 1)
		TIBE AAAAAAAAAAAAAAAAAAAAAAAAA 1 BRIGHT A_FadeOut (0.05)
		stop
	}
}

actor TiberiumCrystalFX {
	+CLIENTSIDEONLY
	reactiontime 16
	states {
	Spawn:
		TIBE A 0
		TIBE AAAAAAA 5 BRIGHT A_SpawnItemEx("ChemicalDeathShot", random(-16,16), random(-16,16), random(8,24), 0, 0, 0.5*random(2,5), 0, 128, 32)
		TIBE A 0 A_Countdown
		loop
	Death:
		TIBE AAAAAAAAAAAAAAAAAAAA 1 BRIGHT A_SpawnItemEx ("ChemicalDeathShot", random(-16,16), random(-16,16), random(8,24), 0, 0, 0.5*random(2,5), 0, 128, 32)
		stop
	}
}

actor TiberiumCrystalThrownBlue : TiberiumCrystalThrown {
	damagetype "ChemicalBlue"
	states {
	Death:
		TIBE A 0 BRIGHT
		TIBE A 0 BRIGHT A_SpawnItemEx ("TiberiumCrystalSittingBlue", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 4 BRIGHT
		stop
	}
}

actor TiberiumCrystalSittingBlue : TiberiumCrystalSitting {damagetype "ChemicalBlue"}

actor TiberiumChemSmoke {
	+NOSECTOR +NOCLIP +NOINTERACTION +CLIENTSIDEONLY
	states {
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAA 4 A_SpawnItemEx ("ChemicalDeathShot", random(-16,16), random(-16,16), random(0,16), 0, 0, 0.5*random(2,5), 0, 128, 128)
		stop
	}
}

actor TiberiumChemSmokePlayer {
	+NOSECTOR +NOCLIP +NOINTERACTION +CLIENTSIDEONLY
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx ("ChemicalDeathShot", random(-16,-1), random(-16,16), random(32, 56), 0, 0, 0.5*random(2,5), 0, 128)
		stop
	}
}

// AOW21-specific tiberium
actor FactoryTiberium : TiberiumCrystalGiver {
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx ("TiberiumMapMarker")
		goto Super::Spawn+2
	}
}

actor TiberiumMapMarker : MapMarker 17000 {
	+CLIENTSIDEONLY
	scale 0.175
	states {
	Spawn:
		AMRK B -1
		stop
	}
}

actor TiberiumCrystalSound 21408 {
	+CLIENTSIDEONLY +NOINTERACTION
	states {
	Spawn:
		TNT1 AA 0 A_PlaySound ("tiberium/loop1", 0, 1.0, 1)
		TNT1 A -1
		stop
	}
}