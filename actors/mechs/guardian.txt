// Guardian Mech
actor Guardian : MechPlayer_Base {
	+NOSKIN
	Speed 1
	Health 5000
	radius 58
	height 128
	Mass 4000
	PainChance 0
	PainChance "Flash", 255
	Player.DisplayName "Guardian"
	player.forwardmove 0.3
	player.sidemove 0.3
	player.soundclass "guardian"
	player.maxhealth 5000
	damagefactor "Falling", 12.0
	player.damagescreencolor "00 00 00"
	player.viewheight 96
	player.attackzoffset 24
	player.morphweapon "GuardianWeapons"
	scale 1.8
	bloodtype "HHMetalHit"
	bloodcolor "White"
	maxstepheight 40
	deathsound "guardian/death"
	States {
	Spawn:
		TERM A 0
		TERM A 0 SetPlayerProperty (0, 0, 0)
		TERM A 0 SetPlayerProperty (0, 0, 3)
		TERM A 0 ACS_Execute (315, 0) // HUD
		TERM A 0 A_GiveInventory ("MechEjectButton", 1)
		TERM A 0 A_GiveInventory ("IsMech", 1)
		TERM A 15 A_PlaySound ("mech/idle", 5)
		Loop
	See:
		TERM A 0 A_JumpIf (waterlevel >= 1, "Drown")
		TERM A 0 SetPlayerProperty (0, 0, 3)
		TERM A 4
		TERM B 8
		TERM B 0 A_SpawnItemEx ("MechStepGuardian", 0, 0, 4)
		TERM C 8 A_PlaySound ("mech/step", 6)
		TERM D 8
		TERM D 0 A_SpawnItemEx ("MechStepGuardian", 0, 0, 4)
		TERM A 4 A_PlaySound ("mech/step", 7)
		goto Spawn
	Drown:
		TERM A 0 Thing_Damage (0, 500, 12)
		goto Spawn
	Missile:
		TERM E 2
		TERM A 0 A_JumpIfInventory ("GuardianAttack", 4, "Attack5")
		TERM A 0 A_JumpIfInventory ("GuardianAttack", 3, "Attack3")
		TERM A 0 A_JumpIfInventory ("GuardianAttack", 2, "Attack2")
		TERM A 0 A_JumpIfInventory ("GuardianAttack", 1, "Attack1")
		goto Spawn
	Attack1:
		TERM H 8 BRIGHT
		TERM I 8 BRIGHT
		goto Spawn
	Attack2:
		TERM E 0
		TERM EFEFEF 5 BRIGHT
		goto Spawn
	Attack3:
		TERM J 24
		TERM K 10 BRIGHT
		TERM J 35
		goto Spawn
	Attack5:
		TERM J 15 BRIGHT
		TERM K 40
		goto Spawn
	Death:
		TERM A 0
		TERM A 0 A_JumpIfInventory("IsKamikaze",1,"Death.IonExplosion")
		TNT1 A 0 A_JumpIfInventory("IsBombCrate", 1, "Death.BombExplosion")
		goto DeathGo
	DeathGo:
		TERM A 0 A_TakeInventory("Ingame",1)
		TERM A 0 A_StopSound (0) TERM A 0 A_StopSound (4)
		TERM A 0 A_StopSound (1) TERM A 0 A_StopSound (5)
		TERM A 0 A_StopSound (2) TERM A 0 A_StopSound (6)
		TERM A 0 A_StopSound (3) TERM A 0 A_StopSound (7)
		TERM M 1
		TERM M 2 A_Scream
		TERM N 0 A_SpawnItemEx ("MechExplosionLarge", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		TERM NOPQRS 4 BRIGHT
		TERM TUVWXYZ 3
		TERM "[" 3 A_NoBlocking
		TERM "[" 2048
		TERM "[" 2 Bright A_SetTranslucent(1,1)
		TERM "[" 2 Bright A_SetTranslucent(0.95,1)
		TERM "[" 2 Bright A_SetTranslucent(0.90,1)
		TERM "[" 2 Bright A_SetTranslucent(0.85,1)
		TERM "[" 2 Bright A_SetTranslucent(0.80,1)
		TERM "[" 2 Bright A_SetTranslucent(0.75,1)
		TERM "[" 2 Bright A_SetTranslucent(0.70,1)
		TERM "[" 2 Bright A_SetTranslucent(0.65,1)
		TERM "[" 2 Bright A_SetTranslucent(0.60,1)
		TERM "[" 2 Bright A_SetTranslucent(0.45,1)
		TERM "[" 2 Bright A_SetTranslucent(0.40,1)
		TERM "[" 2 Bright A_SetTranslucent(0.25,1)
		TERM "[" 2 Bright A_SetTranslucent(0.20,1)
		TERM "[" 2 Bright A_SetTranslucent(0.15,1)
		TERM "[" 2 Bright A_SetTranslucent(0.10,1)
		TERM "[" 2 Bright A_SetTranslucent(0.05,1)
		TERM "[" 2 Bright A_SetTranslucent(0.00,1)
		TNT1 A -1
		stop
	Death.IonExplosion:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory ("IsRed", 1, "Death.NukeExplosion")
		TNT1 A 0 A_SpawnItemEx("BlueStrikeKamikaze", 0, 0, 32)
		goto DeathGo
	Death.NukeExplosion:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("NukeKamikaze", 0, 0, 32)
		goto DeathGo
	Death.BombExplosion:
		PLAY H 0
		PLAY N 0 A_SpawnItemEx("BombExplosion")
		goto DeathGo
	}
}

actor GuardianSwitch : PermanentPowerup {Powerup.Type "Guardian"}
actor GuardianPowerup : InventoryItem {
	inventory.icon "ICO_MECH"
	States {
	Spawn:
		TNT1 A 0
		stop
	Use:
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "Nope")
		TNT1 A 0 A_JumpIfInventory ("IsMech", 1, "Use.Mech")
		TNT1 A 0 A_JumpIfInventory ("PowerBerserk", 1, "Failure")
		TNT1 A 0 A_GiveInventory ("IsMech", 1)
		TNT1 A 0 A_GiveInventory ("GuardianAmmoPackageSmall", 1)
		TNT1 A 0 A_GiveInventory ("MechEjectButton", 1)
		TNT1 A 0 A_GiveInventory ("GuardianSwitch", 1)
		stop
	Nope:
		TNT1 A 0
		fail
	}
}

actor PowerGuardian : AOWMorph {PowerMorph.PlayerClass "Guardian"}

actor GuardianWeapons : AOWWeapon {
	+CHEATNOTWEAPON
	+DONTBOB
	+NOAUTOAIM
	obituary "$OB_GUARDIAN" // %o couldn't get past %k's Guardian.
	weapon.selectionorder 1
	weapon.kickback 150
	Weapon.PreferredSkin "Guardian"
	states {
	Ready:
		JUGS A 0 A_StopSound (CHAN_WEAPON)
		JUGS A 20 A_WeaponReady (WRF_NOSWITCH)
		loop
	Deselect:
		TNT1 A 1 A_Lower
		wait
	Select:
		TNT1 A 1 A_Raise
		wait
	Fire:
		JUGS A 0 A_JumpIfInventory ("Wolverine-WhatWeapon", 3, "Fire.Nuke")
		JUGS A 0 A_JumpIfInventory ("Wolverine-WhatWeapon", 2, "Fire.Flak")
		JUGS A 0 A_JumpIfInventory ("Wolverine-WhatWeapon", 1, "Fire.Missiles")
		goto Fire.Artillery
	Fire.Artillery:
		JUGS A 0 A_StopSound (CHAN_BODY)
		JUGS A 0 A_JumpIfInventory ("Reload-80mm", 1, "Fire.Missiles")
		JUGS A 0 SetPlayerProperty (0, 1, 0)
		JUGS A 0 A_TakeInventory ("ArtilleryShellAmmo", 2)
		JUGS A 0 ACS_ExecuteAlways (319, 0, 3, 5) // reload
		JUGS A 10 A_GiveInventory ("GuardianAttack", 2)
		JUGS A 0 A_PlayWeaponSound ("artillery/fire")
		JUGS A 0 A_JumpIfInventory ("IsBlue", 1, "Fire.ArtilleryBlue")
		JUGS A 10 Bright A_FireCustomMissile ("GuardianShell", 0, 0, -18)
		JUGS A 0 A_PlayWeaponSound ("artillery/fire")
		JUGS A 10 Bright A_FireCustomMissile ("GuardianShell", 0, 0, 18)
		JUGS A 0 SetPlayerProperty (0, 0, 0)
		JUGS A 20 A_TakeInventory ("GuardianAttack", 2)
		goto Ready
	Fire.ArtilleryBlue:
		JUGS A 10 Bright A_FireCustomMissile ("GuardianShellBlue", 0, 0, -18)
		JUGS A 0 A_PlayWeaponSound ("artillery/fire")
		JUGS A 10 Bright A_FireCustomMissile ("GuardianShellBlue", 0, 0, 18)
		JUGS A 0 SetPlayerProperty (0, 0, 0)
		JUGS A 20 A_TakeInventory ("GuardianAttack", 2)
		goto Ready
	Fire.Missiles:
		JUGS A 0 A_StopSound (CHAN_BODY)
		JUGS A 0 A_JumpIfInventory ("Reload-LRM", 1, "Fire.Flak")
		JUGS A 0 SetPlayerProperty (0, 1, 0)
		JUGS A 0 A_TakeInventory ("ShortRangeMissileAmmo", 15)
		JUGS A 0 ACS_ExecuteAlways (319, 0, 1, 6) // reload
		JUGS A 10 A_GiveInventory("GuardianAttack", 2)
		JUGS A 0 A_PlayWeaponSound ("lrm/fire")
		JUGS A 0 A_JumpIfInventory ("IsBlue", 1, "Fire.MissilesBlue")
		JUGS AAAAAAAAAAAAAAA 2 Bright A_FireCustomMissile ("ShortRangeMissile", random (-3, 3), 0, -18, 0, 0, random (-3, 3))
		JUGS A 10
		JUGS A 0 SetPlayerProperty (0, 0, 0)
		JUGS A 20 A_TakeInventory ("GuardianAttack", 2)
		goto Ready
	Fire.MissilesBlue:
		JUGS AAAAAAAAAAAAAAA 2 Bright A_FireCustomMissile ("ShortRangeMissileBlue", random (-3, 3), 0, -18, 0, 0, random (-3, 3))
		JUGS A 10
		JUGS A 0 SetPlayerProperty (0, 0, 0)
		JUGS A 20 A_TakeInventory ("GuardianAttack", 2)
		goto Ready
	Fire.Flak:
		JUGS A 0 A_JumpIfInventory ("MachineGunAmmo", 4, 1)
		goto AltFire
		JUGS A 0 A_PlaySound ("gatling/alt", CHAN_WEAPON, 1, 1.0)
		JUGS A 0 A_Light2
		JUGS A 0 SetPlayerProperty (0, 1, 0)
		JUGS AA 0 A_SpawnItemEx ("RifleShellCasing", 2, 8, 32, random(1,3), random(0,1), random(1,3), random(45,60))
		JUGS A 0 A_TakeInventory ("MachineGunAmmo", 4)
		JUGS A 0 A_GiveInventory ("GuardianAttack", 1)
		JUGS A 0 A_JumpIfInventory ("IsBlue", 1, "Fire.FlakBlue")
		JUGS F 1 BRIGHT A_FireBullets (2.6, 1.6, 2, 10, "ExplosionPuffSmall", 0, 3500)
		JUGS BABA 1
		JUGS F 0 A_Light0
		JUGS F 1 BRIGHT A_FireBullets (2.6, 1.6, 2, 10, "ExplosionPuffSmall", 0, 3500)
		JUGS BABA 1
		JUGS A 3 A_ReFire
		JUGS A 0 SetPlayerProperty (0, 0, 0)
		goto Fire.FlakEnd
	Fire.FlakBlue:
		JUGS F 1 BRIGHT A_FireBullets (2.6, 1.6, 2, 10, "ExplosionPuffSmallBlue", 0, 3500)
		JUGS BABA 1
		JUGS F 0 A_Light0
		JUGS F 1 BRIGHT A_FireBullets (2.6, 1.6, 2, 10, "ExplosionPuffSmallBlue", 0, 3500)
		JUGS BABA 1
		JUGS A 3 A_ReFire
		JUGS A 0 SetPlayerProperty (0, 0, 0)
		goto Fire.FlakEnd
	Fire.FlakEnd:
		JUGS B 0 A_TakeInventory ("GuardianAttack", 1)
		JUGS B 3
		JUGS A 0 A_StopSound (CHAN_WEAPON)
		JUGS A 3 A_PlayWeaponSound ("gatling/spin")
		JUGS B 5
		JUGS A 6 A_ReFire
		JUGS BA 7 A_ReFire
		goto Ready
	Fire.Nuke:
		JUGS A 0 A_StopSound (CHAN_BODY)
		JUGS A 0 A_JumpIfInventory ("Reload-Nuke", 1, "Fire.Artillery")
		JUGS A 0 A_JumpIfInventory ("NukeAmmo", 1, 1)
		Goto AltFire
		JUGS A 0 SetPlayerProperty (0, 1, 0)
		JUGS A 0 A_TakeInventory ("NukeAmmo", 1)
		JUGS A 0 ACS_ExecuteAlways (319, 0, 6, 20) // reload
		JUGS A 10 A_GiveInventory("GuardianAttack", 2)
		JUGS A 0 A_PlayWeaponSound ("mech/artillery")
		JUGS A 0 A_JumpIfInventory ("IsBlue", 1, "Fire.NukeBlue")
		JUGS A 35 Bright A_FireCustomMissile ("MiniNuke", 0, 0, 18, 12)
		JUGS A 0 SetPlayerProperty (0, 0, 0)
		JUGS A 20 A_TakeInventory ("GuardianAttack", 2)
		goto Ready
	Fire.NukeBlue:
		JUGS A 35 Bright A_FireCustomMissile ("IonBomb", 0, 0, 18, 12)
		JUGS A 0 SetPlayerProperty (0, 0, 0)
		JUGS A 20 A_TakeInventory ("GuardianAttack", 2)
		goto Ready
	AltFire:
		JUGS A 0
		JUGS E 53 ACS_ExecuteAlways (314, 0, 4)
		goto Ready
	Denied:
		JUGS A 10 
		goto Ready
	Flash:
		TNT1 A 4 Bright A_Light1
		TNT1 A 3 Bright A_Light2
		Goto LightDone
	Spawn:
		TNT1 A 0
		stop
	}
}

actor GuardianShell : ArtilleryShell80mm {
	obituary "$OB_GUARDIAN"
	speed 70
	damage (210)
	damagetype "ExplosionMechRed"
	scale 0.8
	states {
	Death:
		TNT1 A 0 bright A_NoGravity
		TNT1 A 0 A_StopSound (CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("FlameDecay", random(-32,32), random(-32,32), 4)
		TNT1 A 0 A_SpawnItemEx ("ExplosionMain")
		TNT1 A 1 bright A_Explode(60, 32, 1)
		TNT1 A 5 bright A_Explode(60, 128, 1)
		stop
	}
}

actor GuardianShellBlue : GuardianShell {
	translation "112:120=200:207", "121:127=241:247"
	damagetype "ExplosionMechBlue"
	states {
	Death:
		TNT1 A 0 bright A_NoGravity
		TNT1 A 0 A_StopSound (CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx ("FlameDecayBlue", random(-32,32), random(-32,32), 4)
		TNT1 A 0 A_SpawnItemEx ("ExplosionMain")
		TNT1 A 1 bright A_Explode(60, 32, 1)
		TNT1 A 5 bright A_Explode(60, 128, 1)
		stop
	}
}

actor EjectedGuardian {
	deathsound "guardian/death"
	scale 1.8
	translation "112:127=[255,255,255]:[0,0,0]"
	states {
	Spawn:
		TERM A 0
		TERM M 1
		TERM M 2 A_Scream
		TERM NOPQRS 4 BRIGHT
		TERM TUVWXYZ 3
		TERM "[" 3 A_NoBlocking
		TERM "[" 2048
		TERM "[" 2 Bright A_SetTranslucent(1,1)
		TERM "[" 2 Bright A_SetTranslucent(0.95,1)
		TERM "[" 2 Bright A_SetTranslucent(0.90,1)
		TERM "[" 2 Bright A_SetTranslucent(0.85,1)
		TERM "[" 2 Bright A_SetTranslucent(0.80,1)
		TERM "[" 2 Bright A_SetTranslucent(0.75,1)
		TERM "[" 2 Bright A_SetTranslucent(0.70,1)
		TERM "[" 2 Bright A_SetTranslucent(0.65,1)
		TERM "[" 2 Bright A_SetTranslucent(0.60,1)
		TERM "[" 2 Bright A_SetTranslucent(0.45,1)
		TERM "[" 2 Bright A_SetTranslucent(0.40,1)
		TERM "[" 2 Bright A_SetTranslucent(0.25,1)
		TERM "[" 2 Bright A_SetTranslucent(0.20,1)
		TERM "[" 2 Bright A_SetTranslucent(0.15,1)
		TERM "[" 2 Bright A_SetTranslucent(0.10,1)
		TERM "[" 2 Bright A_SetTranslucent(0.05,1)
		TERM "[" 2 Bright A_SetTranslucent(0.00,1)
		TNT1 A -1
		stop
	}
}

actor NukeAmmo : Ammo {
	+IGNORESKILL
	inventory.amount 1
	inventory.maxamount 2
	ammo.backpackamount 1
	ammo.backpackmaxamount 2
}
actor GuardianAttack : Inventory {inventory.maxamount 10}
actor Reload-Nuke : Boolean {}

actor MiniNuke {
	radius 11
	height 8
	speed 40
	damage (400)
	seesound "mech/artillery"
	deathsound "nuke/boom"
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	+LOWGRAVITY
	+FULLVOLDEATH
	damagetype "ExplosionRed"
	scale 1.25
	translation "112:127=176:191", "80:111=[64,64,64]:[0,0,0]"
	obituary "$OB_GUARDIAN" // %o couldn't get past %k's Guardian.
	states {
	Spawn:
		SHRP A 0
		SHRP A 2 bright A_SpawnItemEx ("PulseBallSmokePuff", -25)
		wait
	Death:
		TNT1 A 0 A_Explode (450, 128, 1)
		TNT1 A 0 A_Explode (1000, 300, 1)
		TNT1 A 0 A_Explode (150, 700, 1)
		TNT1 A 0 A_Explode (100, 1500, 1)
		TNT1 A 5 A_SpawnItemEx ("MiniNukeEffect")
		stop
	}
}

actor MiniNukeEffect {
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	scale 3.0
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetTranslucent (0.75, 1)
		TNT1 A 0 Radius_Quake (7, 35, 0, 50, 0)
		TNT1 A 0 A_SpawnItemEx ("NukeFloor")
		TNT1 A 0 A_SpawnItemEx ("NukeSmokePillar", 0,0,0, 0,0,4.8)
		TNT1 A 0 A_SpawnItemEx ("NukeMushroom2", 0,0,0, 1.5,0,5, 0)
		TNT1 A 0 A_SpawnItemEx ("NukeMushroom2", 0,0,0, 1.5,0,5, 90)
		TNT1 A 0 A_SpawnItemEx ("NukeMushroom2", 0,0,0, 1.5,0,5, 180)
		TNT1 A 0 A_SpawnItemEx ("NukeMushroom2", 0,0,0, 1.5,0,5, 270)
		NKE1 ABCDEFGHIJKLMN 9 BRIGHT
		TNT1 A 0 A_SpawnItemEx ("NukeMushroom", 0,0,500, 0,0,0.5)
		TNT1 A 0 A_SpawnItemEx ("NukeSmokeMushroom", 0,0,500, 0,0,0.5)
		NKE1 OPQRSTU 8 BRIGHT A_FadeOut(0.05)
		stop
	}
}

actor IonBomb : MiniNuke {
	deathsound "ion/strike"
	damagetype "ExplosionBlue"
	translation "112:120=200:207", "121:127=241:247", "80:111=[64,64,64]:[0,0,0]"
	states {
	Death:
		TNT1 A 0 A_Explode (450, 128, 1)
		TNT1 A 0 A_Explode (1000, 300, 1)
		TNT1 A 0 A_Explode (150, 700, 1)
		TNT1 A 0 A_Explode (100, 1500, 1)
		TNT1 A 5 A_SpawnItemEx ("IonFXSmall")
		stop
	}
}

actor ShortRangeMissile : LongRangeMissile {
	obituary "$OB_GUARDIAN" // %o couldn't get past %k's Guardian.
	speed 35
	damage (38)
	deathsound "weapons/rocklx"
	damagetype "ExplosionMechRed"
	scale 0.8
	states {
	Spawn:
		MISL AA 0 A_PlaySound ("ambient/rocket", CHAN_BODY, 1.0, 1)
		MISL A 28 bright
		MISL A -1 bright A_Gravity
	Death:
		TNT1 A 0 A_StopSound (CHAN_BODY)
		TNT1 A 0 A_Explode (38, 128, 0)
		TNT1 A 0 A_SpawnItemEx ("FlameDecay", random (-16,16), random (-16,16), 4, 0, 0, 0, 0, 0, 80)
		TNT1 A 5 A_SpawnItemEx ("ExplosionMain")
		stop
	}
}
actor ShortRangeMissileBlue : ShortRangeMissile {
	damagetype "ExplosionMechBlue"
	states {
	Death:
		TNT1 A 0 A_StopSound (CHAN_BODY)
		TNT1 A 0 A_Explode (38, 128, 0)
		TNT1 A 0 A_SpawnItemEx ("FlameDecayBlue", random (-16,16), random (-16,16), 4, 0, 0, 0, 0, 0, 80)
		TNT1 A 5 A_SpawnItemEx ("ExplosionMain")
		stop
	}
}

actor GuardianAmmoPackage : CustomInventory {
	+AUTOACTIVATE
	inventory.maxamount 0
	states {
	Pickup:
		TNT1 A 0 A_GiveInventory ("MachineGunAmmo", 400)
		TNT1 A 0 A_GiveInventory ("ArtilleryShellAmmo", 30)
		TNT1 A 0 A_GiveInventory ("ShortRangeMissileAmmo", 150)
		TNT1 A 0 A_GiveInventory ("NukeAmmo", 2)
		stop
	}
}

actor GuardianAmmoPackageSmall : CustomInventory {
	+AUTOACTIVATE
	inventory.maxamount 0
	states {
	Pickup:
		TNT1 A 0 A_GiveInventory ("MachineGunAmmo", 100)
		TNT1 A 0 A_GiveInventory ("ArtilleryShellAmmo", 10)
		TNT1 A 0 A_GiveInventory ("ShortRangeMissileAmmo", 40)
		TNT1 A 0 A_GiveInventory ("NukeAmmo", 1)
		stop
	}
}