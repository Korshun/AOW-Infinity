// Player Marine
actor Player : DoomPlayer {
	+NOSKIN
	-TELESTOMP
	health 100
	player.colorrange 112, 127
	player.displayname "Commando"
	player.crouchsprite "PLYC"
	player.startitem "Unarmed", 1
	player.startitem "AOWReloadItem", 1
	player.forwardmove 0.875
	player.sidemove 0.875
	player.maxhealth 100
	
	damagefactor "Explosion", 0.3
	damagefactor "Explosion", 0.3
	damagefactor "Chemical", 2.0
	damagefactor "Chemical", 2.0
	damagefactor "ExplosionMech", 0.1
	damagefactor "ExplosionMech", 0.1
	damagefactor "Disarm", 0.0 
	damagefactor "Deconstruction", 0.0
	damagefactor "Artillery", 0.3
	damagefactor "Artillery", 0.3
	deathsound "generic/death"
	States {
	Spawn:
		PLAY A -1
		Loop
	See:
		PLAY A 5 
		NULL A 0 A_JumpIf(waterlevel > 1, 2)
		NULL A 0 A_CheckFloor(1)
		goto Float
		PLAY B 5 
		NULL A 0 A_JumpIf(waterlevel > 1, 2)
		NULL A 0 A_CheckFloor(1)
		goto Float
		NULL A 0 A_SpawnItemEx ("MarineStomp", 0, 0, 0, 0, 0, -20) 
		PLAY C 5 
		NULL A 0 A_JumpIf(waterlevel > 1, 2)
		NULL A 0 A_CheckFloor(1)
		goto Float
		PLAY D 5 
		NULL A 0 A_JumpIf(waterlevel > 1, 2)
		NULL A 0 A_CheckFloor(1)
		goto Float
		NULL A 0 A_SpawnItemEx ("MarineStomp", 0, 0, 0, 0, 0, -20) 
		goto Spawn
	Float:
		PLAY A 3 A_JumpIf(momz == 0, "See")
		goto See
	Missile:
		PLAY E 14
		Goto Spawn
	Melee:
		PLAY F 6 BRIGHT
		Goto Missile
	MeleeSniper:
		PLAY F 6 BRIGHT
		Goto Missile
	Pain:
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	Pain.Monster:
		NULL A 0 A_GiveInventory ("NoHealXP", 1)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	Pain.Explosion:
	Pain.Explosion:
		NULL A 0 ACS_ExecuteAlways(320, 0)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	Pain.Laser:
	Pain.Laser:
		NULL A 0 ACS_ExecuteAlways(320, 0, 4)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	Pain.RazorWire:
	Pain.RazorWire:
		NULL A 0 A_GiveInventory("RazorWireSpeed",1)
		NULL A 0 ACS_ExecuteAlways(320, 0, 4)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	Pain.FireSpecial:
		NULL A 0 A_GiveInventory ("NoHealXP", 1)
	Pain.Fire:
	Pain.Fire:
		NULL A 0 ACS_ExecuteAlways(320, 0, 1)
		NULL A 0 ACS_ExecuteAlways(360, 0)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	Pain.Chemical:
	Pain.Chemical:
		NULL A 0 ACS_ExecuteAlways(320, 0, 2)
		NULL A 0 ACS_ExecuteAlways(361, 0)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	Pain.ChemicalSpecial:
		NULL A 0 A_GiveInventory ("NoHealXP", 1)
		NULL A 0 ACS_ExecuteAlways(320, 0, 2)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	Death:
		NULL A 0 
		NULL A 0 A_JumpIfInventory ("IsKamikaze",1,"Death.IonExplosion")
		NULL A 0 A_JumpIfInventory ("IsTerrorist",1,"Death.KamikazeExplosion")
		NULL A 0 A_JumpIfInventory ("IsBombCrate",1,"Death.BombExplosion")
		NULL A 0 A_JumpIfInventory ("TimeGun", 1, "Death.TimeRed")
		NULL A 0 A_JumpIfInTargetInventory ("PowerBerserk", 1, "XDeath")
		NULL A 0 A_JumpIfInTargetInventory ("IsSawing", 1, "XDeath")
	DeathGo:
		NULL A 0 ACS_ExecuteAlways(310, 0, 3)
		NULL A 0 A_StopSound (CHAN_ITEM)
		NULL A 0 A_StopSound (CHAN_WEAPON)
		NULL A 0 A_StopSound (5)
		PLAY H 1 A_Scream
		PLAY H 6 A_TakeInventory("Ingame",1)
		PLAY I 6 Thing_ChangeTID(0,9999)
		PLAY JJJ 0 A_SpawnItemEx("Credits1",random(-4,4),random(-4,4),random(32,48),random(2,4),random(-2,2),random(2,4),0,0,128)
		NULL A 0 A_SpawnItemEx("PowerupCrate",random(-4,4),random(-4,4),random(32,48),random(2,4),random(-2,2),random(2,4),0,0,230)
		PLAY J 6 A_NoBlocking
		PLAY K 6
		PLAY L 6 A_PlaySound("misc/thud")
		PLAY M 6
		PLAY N -1
		stop
	Death.MechStomp:
		NULL A 0 ACS_ExecuteAlways (923, 0)
	Death.Falling:
	Death.Suicide:
	XDeath:
		NULL A 0 
		NULL A 0 A_JumpIfInventory ("IsKamikaze", 1, "Death.IonExplosion")
		NULL A 0 A_JumpIfInventory ("IsTerrorist", 1, "Death.KamikazeExplosion")
		NULL A 0 A_JumpIfInventory ("IsBombCrate",1,"Death.BombExplosion")
		NULL A 0 A_JumpIfInventory ("TimeGun", 1, "Death.TimeRed")
	XDeathGo:
		NULL A 0 ACS_ExecuteAlways(310, 0, 3)
		NULL A 0 A_StopSound (CHAN_ITEM)
		NULL A 0 A_StopSound (CHAN_WEAPON)
		NULL A 0 A_StopSound (5)
		PLAY O 4
		NULL A 0 A_NoBlocking
		PLAY PPP 0 A_SpawnItemEx ("Credits1",random(-4,4),random(-4,4),random(32,48),random(-4,4),random(-4,4),random(4,8),0,0,128)
		NULL A 0 A_SpawnItemEx ("PowerupCrate",random(-4,4),random(-4,4),random(32,48),random(2,4),random(-2,2),random(2,4),0,0,230)
		NULL A 0 A_TakeInventory ("Ingame",1)
		NULL A 0 A_Scream
		NULL A 0 A_PlaySound ("misc/gib", 7)
		NULL A 0 A_PlaySound ("misc/gib", 6)
		PLAY P 4 A_SpawnItemEx ("MarineGoreExplosion")
		PLAY Q 4 A_SpawnItemEx ("MarineGoreExplosion2")
		PLAY R 4 A_SpawnItemEx ("MarineGoreExplosion3")
		PLAY STUV 4
		PLAY W -1
		stop
	Death.Explosion:
	Death.Explosion:
		PLAY H 1
		NULL A 0 A_Jump(128,"Death")
		goto XDeath
	Death.FireSpecial:
	Death.Fire:
	Death.Fire:
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom",1,"FireAlt")
		NULL A 0 A_JumpIfInventory ("IsKamikaze",1,"Death.IonExplosion")
		NULL A 0 A_JumpIfInventory ("TimeGun", 1, "Death.TimeRed")
		NULL A 0 A_JumpIfInventory ("IsBombCrate",1,"Death.BombExplosion")
		NULL A 0 A_JumpIfInventory ("IsFemale",1,"DeathF.Fire")
		NULL A 0 A_StopSound (CHAN_ITEM)
		NULL A 0 A_StopSound (CHAN_WEAPON)
		NULL A 0 A_StopSound (5)
		NULL A 0 ACS_ExecuteAlways(310,0,3,0,0)
		PLAY H 1 Thing_ChangeTID(0,9999)
		NULL A 0 A_TakeInventory("Ingame",1)
		NULL A 0 A_SpawnItemEx ("BurningCorpse", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		NULL A 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		stop
	FireAlt:
		PLAY H 1 Thing_ChangeTID(0,9999)
		NULL A 0 A_TakeInventory("Ingame",1)
		NULL A 0 A_SpawnItemEx("BurningCorpseReduced", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		NULL A 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		stop
	Death.ChemicalSpecial:
	Death.Chemical:
	Death.Chemical:
		TNT1 AA 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "ChemFireAlt")
		NULL A 0 A_JumpIfInventory ("IsKamikaze", 1, "Death.IonExplosion")
		NULL A 0 A_JumpIfInventory ("TimeGun", 1, "Death.TimeRed")
		NULL A 0 A_JumpIfInventory ("IsBombCrate", 1, "Death.BombExplosion")
		NULL A 0 A_JumpIfInventory ("IsFemale", 1, "DeathF.Chemical")
		NULL A 0 A_StopSound (CHAN_ITEM)
		NULL A 0 A_StopSound (CHAN_WEAPON)
		NULL A 0 A_StopSound (CHAN_VOICE)
		NULL A 0 A_StopSound (5)
		NULL A 0 ACS_ExecuteAlways(310, 0, 3)
		PLAY H 1 Thing_ChangeTID(0,9999)
		NULL A 0 A_StopSoundEx("Item")
		NULL A 0 A_TakeInventory("Ingame",1)
		NULL A 0 A_SpawnItemEx ("ChemicalCorpse", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		NULL A 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		stop
	ChemFireAlt:
		NULL A 0 A_StopSound (CHAN_ITEM)
		NULL A 0 A_StopSound (CHAN_WEAPON)
		NULL A 0 A_StopSound (CHAN_VOICE)
		NULL A 0 A_StopSound (5)
		NULL A 0 ACS_ExecuteAlways(310,0,3,0,0)
		PLAY H 1 Thing_ChangeTID(0,9999)
		NULL A 0 A_TakeInventory("Ingame",1)
		NULL A 0 A_SpawnItemEx("ChemicalCorpseReduced", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		NULL A 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		Stop
	DeathF.Fire:
		TNT1 AA 0 A_JumpIfInventory("IsInSpawnRoom",1,"FireAlt")
		PLAY H 1 Thing_ChangeTID(0,9999)
		NULL A 0 A_TakeInventory("Ingame",1)
		NULL A 0 A_SpawnItemEx ("BurningCorpseFemale", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		NULL A 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		stop
	DeathF.Chemical:
		TNT1 AA 0 A_JumpIfInventory("IsInSpawnRoom",1,"ChemFireAlt")
		PLAY H 1 Thing_ChangeTID(0,9999)
		NULL A 0 A_TakeInventory("Ingame",1)
		NULL A 0 A_SpawnItemEx ("ChemicalCorpseFemale", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		NULL A 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		stop
	Death.IonExplosion:
		NULL A 0
		NULL A 0 A_JumpIfInventory ("IsRed", 1, "Death.NukeExplosion")
		NULL A 0 A_SpawnItemEx("BlueStrikeKamikaze", 0, 0, 32)
		goto XDeathGo
	Death.NukeExplosion:
		NULL A 0
		NULL A 0 A_SpawnItemEx("NukeKamikaze", 0, 0, 32)
		goto XDeathGo
	Death.KamikazeExplosion:
		NULL A 0
		NULL A 0 A_JumpIfInventory ("IsRed", 1, "Death.KamikazeExplosionRed")
		NULL A 0 A_SpawnItemEx("KamikazeExplosionBlue")
		goto XDeathGo
	Death.KamikazeExplosionRed:
		NULL A 0
		NULL A 0 A_SpawnItemEx("KamikazeExplosion")
		goto XDeathGo
	Death.BombExplosion:
		NULL A 0
		NULL A 0 A_SpawnItemEx("BombExplosion")
		goto XDeathGo
	Death.TimeRed:
	Death.TimeBlue:
		NULL A 0 A_JumpIfInventory("IsKamikaze",1,"Death.IonExplosion")
		NULL A 0 A_JumpIfInventory ("IsBombCrate",1,"Death.BombExplosion")
		NULL A 0
		NULL A 0 A_NoBlocking
		NULL A 0 A_StopSound (CHAN_ITEM)
		NULL A 0 A_StopSound (CHAN_WEAPON)
		NULL A 0 A_StopSound (5)
		NULL A 0 A_TakeInventory("Ingame",1)
		NULL A 0 Thing_ChangeTID(0,9999)
		NULL A 0 A_Stop
		NULL A 0 A_NoGravity
		PLAY GGG 0 A_SpawnItemEx("Credits1",random(-4,4),random(-4,4),random(32,48),random(2,4),random(-2,2),random(2,4),0,0,128)
		NULL A 0 A_SpawnItemEx("PowerupCrate",random(-4,4),random(-4,4),random(32,48),random(2,4),random(-2,2),random(2,4),0,0,230)
		PLAY G 2 A_PlaySound("weapons/timedeath")
		PLAY G 2 Bright A_SetTranslucent(1,1)
		PLAY G 2 Bright A_SetTranslucent(0.95,1)
		PLAY G 2 Bright A_SetTranslucent(0.90,1)
		PLAY G 2 Bright A_SetTranslucent(0.85,1)
		PLAY G 2 Bright A_SetTranslucent(0.80,1)
		PLAY G 2 Bright A_SetTranslucent(0.75,1)
		PLAY G 2 Bright A_SetTranslucent(0.70,1)
		PLAY G 2 Bright A_SetTranslucent(0.65,1)
		PLAY G 2 Bright A_SetTranslucent(0.60,1)
		PLAY G 2 Bright A_SetTranslucent(0.45,1)
		PLAY G 2 Bright A_SetTranslucent(0.40,1)
		PLAY G 2 Bright A_SetTranslucent(0.25,1)
		PLAY G 2 Bright A_SetTranslucent(0.20,1)
		PLAY G 2 Bright A_SetTranslucent(0.15,1)
		PLAY G 2 Bright A_SetTranslucent(0.10,1)
		PLAY G 2 Bright A_SetTranslucent(0.05,1)
		PLAY G 2 Bright A_SetTranslucent(0.00,1)
		PLAY N -1 A_SetTranslucent(0.0,0)
		Stop
	}
}

actor MarineGoreExplosion {
	+CLIENTSIDEONLY
	+NOINTERACTION
	states {
	Spawn:
		TNT1 AAAA 0 A_SpawnItemEx("GibExplosion", 0, 0, 32)
		NULL A 0 A_SpawnItemEx("Giblet1",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		NULL A 0 A_SpawnItemEx("Giblet5",0,0,random(4,40),random(-2,4),random(-6,6),random(-1,10),random(-32,32))
		NULL A 0 A_SpawnItemEx("Giblet2",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		NULL A 0 A_SpawnItemEx("Giblet3",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		NULL A 3
		stop
	}
}

actor MarineGoreExplosion2 {
	+CLIENTSIDEONLY
	+NOINTERACTION
	states {
	Spawn:
		TNT1 AAAA 0 A_SpawnItemEx ("GibExplosion", 0, 0, 32)
		NULL A 0 A_SpawnItemEx ("Giblet4",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		NULL A 0 A_SpawnItemEx ("Giblet5",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		NULL A 0 A_SpawnItemEx ("Giblet6",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		NULL A 0 A_SpawnItemEx ("AOWBrainStem", 0, 0, 50, random(-4, 4), random(-4, 4), random(7, 9))
		NULL A 3
		stop
	}
}


actor MarineGoreExplosion3 {
	+CLIENTSIDEONLY
	+NOINTERACTION
	states {
	Spawn:
		TNT1 AAAA 0 A_SpawnItemEx("GibExplosion", 0, 0, 32)
		NULL A 0 A_SpawnItemEx("Giblet7",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		NULL A 0 A_SpawnItemEx("Giblet8",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		NULL A 0 A_SpawnItemEx("Giblet7",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		NULL A 3
		stop
	}
}


actor MarineStomp  {
	PROJECTILE
	+NOBLOOD
	+CLIENTSIDEONLY
	damage 0
	radius 10
	Speed 20
	deathsound "misc/footstep"
	States
	{
	Spawn:
		NULL A 1
		stop
	Death:
		NULL A 0
		NULL A 0 A_JumpIfInTargetInventory ("StealthMode", 1, 2)
		NULL A 1 A_SpawnItem("PlayerStepSmokePuff")
		NULL A 1
		stop
	}
}