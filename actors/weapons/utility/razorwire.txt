// Razor wire
actor Utility_RazorWire {
	+SHOOTABLE
	+NOBLOOD
	+NODAMAGETHRUST
	health 400
	speed 0
	radius 32
	height 64
	mass 0x7FFFFFFF
	scale 0.25
	renderstyle translucent
	alpha 0.0
	obituary "%o walked into razor wire."
	
	damagetype "RazorWireRed"
	damagefactor "Disarm", 0.0
	damagefactor "RazorWireRed", 0.0
	damagefactor "RazorWireBlue", 0.0
	damagefactor "Mechstomp", 0.0
	states {
	Spawn:
		WIRE AA 0 Thing_SetSpecial (0, 84, 345, 0, TEAM_RED)
		WIRE AAAAAAAAAA 3 A_FadeIn (0.1)
		WIRE A 0 A_ChangeFlag ("NOCLIP", 0)
		WIRE A 35 A_Gravity
	Idle:
		WIRE A 18 A_Explode (13, 64)
		wait
	Death:  
		TNT1 A 0 
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ExplosionMain", 0, 0, 32)
		TNT1 A 0 A_PlaySound ("weapons/rocklx")
		TNT1 A 4
		stop
	Death.Deconstruction:
		"----" A 0 A_NoBlocking
		"----" A 2 A_PlaySound ("utility/deconstruct")
		"----" AAAAAAAAAA 2 A_Fadeout(0.1) 
		stop
	}
}

actor Utility_RazorWireBlue : Utility_RazorWire {
	damagetype "RazorWireBlue"
	states {
	Spawn:
		WIRE A 0 Thing_SetSpecial (0, 84, 345, 0, TEAM_BLUE)
		goto Super::Spawn+2
	}
}

actor PowerRazorWire : PowerSpeed {speed 0.5}

actor RazorWireSpeed : PowerupGiver {
	inventory.maxamount 0
	powerup.type "RazorWire"
	powerup.duration 35
	+AUTOACTIVATE
}