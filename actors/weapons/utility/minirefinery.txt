/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * Mini-Refinery
 *
 * The utility-built mini-refinery is a credit flow booster. Must be built into
 * a tiberium field, in which it will start to refine the crystals there and
 * turning them into cash for you and your friends. A good thing to have around
 * when the main Refinery is destroyed. Credit flow is boosted by 75% when
 * the mini-refinery is active.
 */

actor Utility_MiniRefCacheRed {
	+SHOOTABLE +NOBLOOD
	+ACTIVATEMCROSS +DONTGIB
	+NOICEDEATH +NOGRAVITY
	+ISMONSTER +QUICKTORETALIATE
	obituary "%o ventured too close to the \cARed Mini-Refinery\c-."
	Radius 32 Height 48
	Health 6500
	renderstyle Translucent
	alpha 0.0
	scale 0.1875
	mass 0x7FFFFFFF
	painchance 255
	damagetype ChemicalSpecial
	damagefactor "FistRed", 0.0		damagefactor "FistBlue", 0.2
	damagefactor "BulletRed", 0.0		damagefactor "BulletBlue", 0.5
	damagefactor "RifleRed", 0.0		damagefactor "RifleBlue", 0.5
	damagefactor "ExplosionRed", 0.0	damagefactor "ExplosionBlue", 0.75
	damagefactor "FireRed", 0.0		damagefactor "FireBlue", 0.6
	damagefactor "ChemicalRed", 0.0		damagefactor "ChemicalBlue", 0.0
	damagefactor "LaserRed", 0.0		damagefactor "LaserBlue", 1.0
	damagefactor "RazorWireRed", 0.0	damagefactor "RazorWireBlue", 0.0
	damagefactor "ExplosionMechRed", 0.0	damagefactor "ExplosionMechBlue", 0.25
	damagefactor "TimeRed", 0.0		damagefactor "TimeBlue", 1.0
	damagefactor "Disarm", 0.0		damagefactor "Flash", 0.0
	damagefactor "Superweapon", 0.0		damagefactor "SuperweaponIon", 1.0
	damagefactor "MassDriverRed", 0.0	damagefactor "MassDriverBlue", 0.75
	damagefactor "ChemicalSpecial", 0.0
	damagefactor "Deconstruction",	1.0
	damagefactor "Mechstomp", 0.0
	damagefactor "Monster", 0.0
	States {
	Spawn:
		MREF AA 0 Thing_ChangeTID(0,900)
		MREF AAAAAAAAAA 3 A_FadeIn(0.1)
		MREF A 0 A_SetTranslucent(1.0,0)
		MREF A -1 A_Gravity // wait until the script approves the cache.
		stop
	Approved:
		MREF A 5 A_RadiusThrust (4000, 40, 1)
		MREF A 0 A_ChangeFlag("NOCLIP", 0)
		MREF A 0 A_SetSolid
		MREF A 0 A_SpawnItemEx("MiniRefCacheFlare",0,0,48,0,0,0,128,0)
		MREF A 35 A_PlaySound("misc/i_pkup")
		MREF A 0 A_ChangeFlag("FRIENDLY",0)
		goto Working
	Working:
		MREF A 35 A_SpawnItemEx("MiniRefCacheFlare",0,0,48,0,0,0,128,0)
		loop
	Pain:
		MREF A 0 ACS_ExecuteAlways (930, 0, BUILDING_MINIREF, TEAM_RED)
		MREF A 1 A_Stop
		goto Working
	Death:  
		TNT1 A 0 
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Explode (256, 512)
		TNT1 A 0 A_PlaySound ("weapons/rocklx", CHAN_ITEM)
		TNT1 A 0 A_SpawnItemEx ("ExplosionMainLarge", 0, 0, 32, 0, 0, 0, 0, 128)
		TNT1 AAAA 0 A_SpawnItemEx ("AnthraxDecay", random(-48,48), random(-48,48), 4)
		TNT1 A 4 bright 
		stop
	Disapproved:
	Death.Deconstruction:
		"----" A 2 A_PlaySound ("utility/deconstruct")
		"----" AAAAAAAAAA 2 A_Fadeout(0.1)
		stop
	}
}

ACTOR Utility_MiniRefCacheBlue : Utility_MiniRefCacheRed {
	obituary "%o ventured too close to the \cNBlue Mini-Refinery\c-."
	damagefactor "FistRed", 0.2		damagefactor "FistBlue", 0.0
	damagefactor "BulletRed", 0.5		damagefactor "BulletBlue", 0.0
	damagefactor "RifleRed", 0.5		damagefactor "RifleBlue", 0.0
	damagefactor "ExplosionRed", 0.75	damagefactor "ExplosionBlue", 0.0
	damagefactor "FireRed", 0.6		damagefactor "FireBlue", 0.0
	damagefactor "LaserRed", 1.0		damagefactor "LaserBlue", 0.0
	damagefactor "RazorWireRed", 0.0	damagefactor "RazorWireBlue", 0.0
	damagefactor "ExplosionMechRed", 0.25	damagefactor "ExplosionMechBlue", 0.0
	damagefactor "TimeRed", 1.0		damagefactor "TimeBlue", 0.0
	damagefactor "Superweapon", 1.0		damagefactor "SuperweaponIon", 0.0
	damagefactor "MassDriverRed", 0.75	damagefactor "MassDriverBlue", 0.0
	states {
	Spawn:
		MREF AA 0 Thing_ChangeTID(0,901)
		MREF AAAAAAAAAA 3 A_FadeIn(0.1)
		MREF A 0 A_SetTranslucent(1.0,0)
		MREF A -1 A_Gravity // wait until the script approves the cache.
		stop
	Pain:
		MREF A 0 ACS_ExecuteAlways (930, 0, BUILDING_MINIREF, TEAM_BLUE)
		MREF A 1 A_Stop
		goto Working
	}
}