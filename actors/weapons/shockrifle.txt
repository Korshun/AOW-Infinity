// Shock rifle
actor ShockRifle : AOWWeapon 21068 {
	obituary "%o felt %k's surge."
	inventory.pickupmessage "You got the shock rifle!"
	weapon.selectionorder 4
	weapon.kickback 100
	weapon.ammotype "GeneporiumCharge"
	weapon.ammouse 1
	weapon.ammogive 40
	AttackSound "voltrifle/fire"
	Weapon.PreferredSkin "MarineShockRifle"
	decal "VoltageScorch"
	states {
	Ready:
		VOLT AAABBBCCCDDDCCCBBB 1 A_WeaponReady
		loop
	Deselect:
		VOLT A 1 A_Lower
		loop
	Select:
		VOLT A 1 A_Raise
		loop
	Fire:
		VOLF ABC 3 bright
		VOLF A 0 bright A_ReFire
		VOLF CBA 3 bright
		goto Ready
	Hold:
		// broken
		// VOLF E 0 bright A_FireBullets (0, 0, 1, 0, "VoltageTrailPuff", 0, 1100)
		VOLF E 0 bright A_JumpIfInventory ("IsBlue", 1, "Hold.Blue")
		VOLF E 1 bright A_FireBullets (3.2, 3.2, 3, random (2, 3), "VoltagePuff", 1, 1100)
		VOLF D 2 bright
		VOLF C 0 bright A_ReFire
		VOLF CBA 3 bright
		goto Ready
	Hold.Blue:
		VOLF E 1 bright A_FireBullets (3.2, 3.2, 3, random (2, 3), "VoltagePuffBlue", 1, 1100)
		VOLF D 2 bright
		VOLF C 0 bright A_ReFire
		VOLF CBA 3 bright
		goto Ready
	Spawn:
		VOLF X -1
		stop
	}
}

actor GeneporiumCharge : Ammo {
	inventory.amount 20
	inventory.maxamount 200
	ammo.backpackamount 20
	ammo.backpackmaxamount 200
}

actor VoltagePuff {
	renderstyle Add
	alpha 0.5
	+NOBLOCKMAP +NOGRAVITY
	+PUFFONACTORS +ALWAYSPUFF
	+CLIENTSIDEONLY +NOINTERACTION
	+FORCEXYBILLBOARD
	DamageType "LaserRed"
	Scale 1.2
	states {
	Spawn:
		VOLX A 2 bright
		VOLX A 0 A_SpawnItemEx("VoltageDecay",0,0,0,0,0,0,0,128,192)
		VOLX BCDEFEDCB 2 bright
		stop
	}
}

actor VoltagePuffBlue : VoltagePuff {damagetype "LaserBlue"}

actor VoltagePuff2 {
	renderstyle Add
	alpha 0.5
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	DamageType "LaserRed"
	Scale 1.2
	states {
	Spawn:
		VOLX A 2 bright
		VOLX BCDEFEDCB 2 bright
		stop
	}
}

actor VoltageDecay { //Now with extra hi-res funk!
	+CLIENTSIDEONLY -ACTIVATEIMPACT
	-ACTIVATEPCROSS +NOBLOCKMAP
	+NOGRAVITY +NOINTERACTION
	+FORCEXYBILLBOARD
	renderstyle Add
	alpha 1
	Scale 1.5
	states {
	Spawn:
		TNT1 AA 0 A_PlaySound("voltrifle/decay")
		TNT1 A 0 A_Jump(128,"AltSpawn")
		VOLX AABBCCDDEEFFEECCA 2 bright A_SpawnItemEx("VoltageSpark",0,0,0,random(-2,2),random(-2,2),random(0,2),random(-180,180),128,64)
		stop
	AltSpawn:
		VOLX AACCBBDDFFEECCB 2 bright A_SpawnItemEx("VoltageSpark",0,0,0,random(-2,2),random(-2,2),random(0,2),random(-180,180),128,64)
		stop
	}
}

actor VoltageSpark {
	PROJECTILE
	+CLIENTSIDEONLY -ACTIVATEIMPACT
	-ACTIVATEPCROSS +NOBLOCKMAP
	+FORCEXYBILLBOARD
	+NOCLIP +NONETID
	renderstyle Add
	alpha 0.8
	Scale 0.5
	Speed 10
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(96,"Die")
		TNT1 A 0 A_Jump(128,"AltSpawn")
		VOLX ABCDEFCBA 2 bright
		stop
	AltSpawn:
		VOLX ACBDFECBA 2 bright
		stop
	Die:
		VOLX A 0
		stop
	}
}

// broken for now
/*
actor VoltageTrailPuff : BulletPuff {
	+PUFFGETSOWNER
	+ALWAYSPUFF
	+PUFFONACTORS
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 1 ACS_ExecuteAlways (986, 0, 0, ACS_ExecuteWithResult (964, 5))
		stop
	}
}
*/

// DECAY CLUSTERS (power generator)

actor VoltageDecayClusterBlue {
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	damagetype "LaserBlue"
	states {
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAA 5 A_Explode (5, 96)
		stop
	}
}

actor VoltageDecayClusterRed : VoltageDecayClusterBlue {
	damagetype "LaserRed"
}

actor VoltageDecayClusterFX {
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	damagetype "LaserBlue"
	states {
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAA 5 A_SpawnItemEx ("VoltageDecay", random (-48, 48), random (-48, 48), random (0, 96))
		stop
	}
}

actor VoltageDecayCluster {
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx ("VoltageDecayClusterBlue")
		TNT1 A 0 A_SpawnItemEx ("VoltageDecayClusterRed")
		TNT1 A 0 A_SpawnItemEx ("VoltageDecayClusterFX")
		stop
	}
}