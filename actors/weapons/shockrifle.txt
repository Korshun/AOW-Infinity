// Shock rifle
Actor ShockRifle : AOWWeapon 21068 {
	Obituary "%o felt %k's surge."
	Inventory.PickupMessage "You got the shock rifle!"
	Weapon.SelectionOrder 4
	Weapon.Kickback 100
	Weapon.AmmoType "GeneporiumCharge"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	AttackSound "voltrifle/fire"
	Weapon.PreferredSkin "MarineShockRifle"
	Decal "VoltageScorch"
	States
	{
	Ready:
		VOLT AAABBBCCCDDDCCCBBB 1 A_WeaponReady
		Loop
	Deselect:
		VOLT A 1 A_Lower
		Loop
	Select:
		VOLT A 1 A_Raise
		Loop
	Fire:
		VOLF ABC 3 BRIGHT
		NULL A 0 BRIGHT A_ReFire
		VOLF CBA 3 BRIGHT
		Goto Ready
	Hold:
		// broken
		// VOLF E 0 BRIGHT A_FireBullets(0, 0, 1, 0, "VoltageTrailPuff", 0, 1100)
		VOLF E 1 BRIGHT A_FireBullets(3.2, 3.2, 3, 5, "VoltagePuff", FBF_NORANDOM | FBF_USEAMMO, 1100)
		VOLF D 2 BRIGHT
		NULL A 0 BRIGHT A_ReFire
		VOLF CBA 3 BRIGHT
		Goto Ready
	Spawn:
		VOLF X -1
		Stop
	}
}

Actor GeneporiumCharge : Ammo {
	Inventory.amount 20
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 200
}

Actor VoltagePuff {
	RenderStyle Add
	Alpha 0.5
	+NOBLOCKMAP +NOGRAVITY
	+PUFFONACTORS +ALWAYSPUFF
	+CLIENTSIDEONLY +NOINTERACTION
	+FORCEXYBILLBOARD
	DamageType "Laser"
	Scale 1.2
	States
	{
	Spawn:
		VOLX A 2 BRIGHT
		NULL A 0 A_SpawnItemEx("VoltageDecay", 0, 0, 0, 0, 0, 0, 0, 128, 192)
		VOLX BCDEFEDCB 2 BRIGHT
		Stop
	}
}

Actor VoltagePuff2 {
	RenderStyle Add
	Alpha 0.5
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	DamageType "Laser"
	Scale 1.2
	States
	{
	Spawn:
		VOLX A 2 BRIGHT
		VOLX BCDEFEDCB 2 BRIGHT
		Stop
	}
}

Actor VoltageDecay { //Now with extra hi-res funk!
	+CLIENTSIDEONLY -ACTIVATEIMPACT
	-ACTIVATEPCROSS +NOBLOCKMAP
	+NOGRAVITY +NOINTERACTION
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 1
	Scale 1.5
	States
	{
	Spawn:
		TNT1 AA 0 A_PlaySound("voltrifle/decay")
		NULL A 0 A_Jump(128, "AltSpawn")
		VOLX AABBCCDDEEFFEECCA 2 BRIGHT A_SpawnItemEx("VoltageSpark", 0, 0, 0, random(-2, 2), random(-2, 2), random(0, 2), random(-180, 180), 128, 64)
		Stop
	AltSpawn:
		VOLX AACCBBDDFFEECCB 2 BRIGHT A_SpawnItemEx("VoltageSpark", 0, 0, 0, random(-2, 2), random(-2, 2), random(0, 2), random(-180, 180), 128, 64)
		Stop
	}
}

Actor VoltageSpark {
	PROJECTILE
	+CLIENTSIDEONLY -ACTIVATEIMPACT
	-ACTIVATEPCROSS +NOBLOCKMAP
	+FORCEXYBILLBOARD
	+NOCLIP +NONETID
	RenderStyle Add
	Alpha 0.8
	Scale 0.5
	Speed 10
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_Jump(96, "Die")
		NULL A 0 A_Jump(128, "AltSpawn")
		VOLX ABCDEFCBA 2 BRIGHT
		Stop
	AltSpawn:
		VOLX ACBDFECBA 2 BRIGHT
		Stop
	Die:
		NULL A 0
		Stop
	}
}

// broken for now
/*
Actor VoltageTrailPuff : BulletPuff {
	+PUFFGETSOWNER
	+ALWAYSPUFF
	+PUFFONACTORS
	States
	{
	Spawn:
		NULL A 0
		NULL A 1 ACS_ExecuteAlways(aow_script986, 0, 0, ACS_ExecuteWithResult(aow_script964, 5))
		Stop
	}
}
*/

// DECAY CLUSTERS (power generator)

Actor VoltageDecayClusterBlue {
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	DamageType "Laser"
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAA 5 A_Explode(5, 96)
		Stop
	}
}

Actor VoltageDecayClusterRed : VoltageDecayClusterBlue {
	DamageType "Laser"
}

Actor VoltageDecayClusterFX {
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	DamageType "Laser"
	States
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAA 5 A_SpawnItemEx("VoltageDecay", random (-48, 48), random (-48, 48), random (0, 96))
		Stop
	}
}

Actor VoltageDecayCluster {
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx("VoltageDecayClusterBlue")
		NULL A 0 A_SpawnItemEx("VoltageDecayClusterRed")
		NULL A 0 A_SpawnItemEx("VoltageDecayClusterFX")
		Stop
	}
}