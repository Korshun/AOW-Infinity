// Laser Chaingun
actor LaserChaingun : AOWWeapon {
	obituary "$OB_LASERCHAINGUN" // "%o was incinerated by %k's laser chaingun."
	attacksound "lchaingun/fire"
	inventory.pickupmessage "You got the laser chaingun!"
	weapon.selectionorder 6
	weapon.kickback 100
	weapon.ammotype "LaserChaingunAmmo"
	weapon.ammouse 1
	weapon.ammogive 25
	weapon.preferredskin "MarineMinigun"
	states {
	Ready:
		LCGR AA 0 A_JumpIfInventory ("IsBlue", 1, "ReadyBlue")
		LCGR A 1 A_WeaponReady
		wait
	ReadyBlue:
		LCGB A 1 A_WeaponReady
		wait
	Deselect:
		LCGR AA 0 A_JumpIfInventory ("IsZoomed", 1, "AltFireOff")
		LCGR AA 0 A_JumpIfInventory ("IsBlue", 1, "DeselectBlue")
		LCGR A 1 A_Lower
		wait
	DeselectBlue:
		LCGB A 1 A_Lower
		wait
	Select:
		LCGR AA 0 A_JumpIfInventory ("IsBlue", 1, "SelectBlue")
		LCGR A 1 A_Raise
		wait
	SelectBlue:
		LCGB A 1 A_Raise
		wait
	Fire:
		LCGR AA 0 A_JumpIfInventory ("IsBlue", 1, "FireBlue")
		LCGR A 2 Offset(1,29) A_CheckReload
		LCGR B 0 A_GunFlash
		LCGR B 0 A_PlayWeaponSound("lchaingun/fire")
		LCGR B 0 A_FireBullets (1.0, 1.0, -1, 4, "LaserPuff",1,4000)
		LCGR B 0 BRIGHT A_SpawnItemEx("LaserChainGunLight",35*(1-(sin(pitch)*0.8)),4.5,34*(1-(sin(pitch)*0.8)),0,0,0)
		LCGR B 1 Offset(2,30) BRIGHT
		LCGR C 2 A_CheckReload
		LCGR A 0 A_GunFlash
		LCGR A 0 A_SpawnItemEx("LaserChainGunLight",35*(1-(sin(pitch)*0.8)),4.5,34*(1-(sin(pitch)*0.8)),0,0,0)
		LCGR A 0 A_PlayWeaponSound("lchaingun/fire")
		LCGR A 0 A_FireBullets (1.0, 1.0, -1, 4, "LaserPuff",0,4000)
		LCGR A 0 BRIGHT A_SpawnItemEx("LaserChainGunLight",35*(1-(sin(pitch)*0.8)),4.5,34*(1-(sin(pitch)*0.8)),0,0,0)
		LCGR A 1 Offset(1,31) BRIGHT A_RailAttack(0, 4.8, 1, none, "FF 11 11", 1, 0.0, "LaserPuff")
		LCGR B 2 Offset(0,30) A_CheckReload
		LCGR B 0 A_GunFlash
		LCGR C 0 A_PlayWeaponSound("lchaingun/fire")
		LCGR C 0 A_SpawnItemEx("LaserChainGunLight",35*(1-(sin(pitch)*0.8)),4.5,34*(1-(sin(pitch)*0.8)),0,0,0)
		LCGR C 0 A_FireBullets (1.0, 1.0, -1, 4, "LaserPuff",1,4000)
		LCGR C 1 BRIGHT
		LCGR A 0 A_ReFire
		LCGR ABCABC 2
		goto Ready
	FireBlue:
		LCGB A 2 Offset(1,29) A_CheckReload
		LCGB A 0 A_GunFlash
		LCGB B 0 A_PlayWeaponSound("lchaingun/fire")
		LCGB B 0 A_FireBullets (1.0, 1.0, -1, 4, "LaserPuffBlue",1,4000)
		LCGB B 0 BRIGHT A_SpawnItemEx("LaserChainGunLightBlue",35*(1-(sin(pitch)*0.8)),4.5,34*(1-(sin(pitch)*0.8)),0,0,0)
		LCGB B 1 Offset(2,30) BRIGHT
		LCGB C 2 A_CheckReload
		LCGB A 0 A_GunFlash
		LCGB A 0 A_PlayWeaponSound("lchaingun/fire")
		LCGB A 0 A_FireBullets (1.0, 1.0, -1, 4, "LaserPuffBlue",0,4000)
		LCGB A 0 BRIGHT A_SpawnItemEx("LaserChainGunLightBlue",35*(1-(sin(pitch)*0.8)),4.5,34*(1-(sin(pitch)*0.8)),0,0,0)
		LCGB A 1 Offset(1,31) BRIGHT A_RailAttack(0,4.8,1,none,"11 11 FF",1,0.0,"LaserPuffBlue")
		LCGB B 2 Offset(0,30) A_CheckReload
		LCGB B 0 A_GunFlash
		LCGB C 0 A_PlayWeaponSound("lchaingun/fire")
		LCGB C 0 A_SpawnItemEx("LaserChainGunLightBlue",35*(1-(sin(pitch)*0.8)),4.5,34*(1-(sin(pitch)*0.8)),0,0,0)
		LCGB C 0 A_FireBullets (1.0, 1.0, -1, 4, "LaserPuffBlue",1,4000)
		LCGB C 1 BRIGHT
		LCGB A 0 A_ReFire
		LCGB ABCABC 2
		goto Ready
	AltFire:
		LCGR AA 0 A_JumpIfInventory ("IsZoomed", 1, "AltFireOff")
		LCGR AA 0 A_JumpIfInventory ("IsBlue", 1, "AltFireBlue")
		LCGR A 0 A_GiveInventory ("IsZoomed", 1)
		LCGR A 18 A_ZoomFactor (2.5)
		goto Ready
	AltFireOff:
		LCGR AA 0 A_JumpIfInventory ("IsBlue", 1, "AltFireOffBlue")
		LCGR A 17 A_ZoomFactor (1.0)
		LCGR A 0 A_TakeInventory ("IsZoomed", 1)
		goto Ready
	AltFireBlue:
		LCGB A 0 A_GiveInventory ("IsZoomed", 1)
		LCGB A 18 A_ZoomFactor (2.5)
		goto Ready
	AltFireOffBlue:
		LCGB A 17 A_ZoomFactor (1.0)
		LCGB A 0 A_TakeInventory ("IsZoomed", 1)
		goto Ready
	Flash:
		TNT1 A 0 A_JumpIf (ACS_ExecuteWithResult(910, 0) == 0,"SoftwareFlash") // Ups software framerate by quite a bit.
		TNT1 A 0 A_SpawnItemEx("LaserChainGunLight",35*(1-(sin(pitch)*0.8)),4.5,32*(1-(sin(pitch)*0.8)),0,0,0)
		TNT1 A 0 A_SpawnItemEx("LaserChainGunLightBlue",35*(1-(sin(pitch)*0.8)),4.5,32*(1-(sin(pitch)*0.8)),0,0,0)
		TNT1 A 2 A_Light2
		TNT1 A 1 A_Light1
		goto LightDone
	SoftwareFlash:
		TNT1 A 0 //Nothing!!
		goto Flash+4
	Spawn:
		LSCL X -1
		stop
	}
}

actor LaserChaingunAmmo : Ammo {
	inventory.amount 50
	inventory.maxamount 400
	ammo.backpackamount 50
	ammo.backpackmaxamount 400
	states {
	Spawn:
		CELL A -1
		stop
	}
}

actor LaserChaingunPickup : CustomInventory 21083 {
	inventory.pickupmessage "You got the laser chaingun!"
	inventory.maxamount 0
	inventory.respawntics 2100
	inventory.pickupsound "misc/w_pkup"
	states {
	Spawn:
		LSCL X -1
		stop
	Pickup:
		TNT1 AA 0 A_JumpIfInventory ("LaserChaingun", 1, "Failure")
		TNT1 A 0 A_GiveInventory ("LaserChaingun", 1)
		TNT1 A 0 A_GiveInventory ("LaserChaingunAmmo", 25)
		stop
	Failure:
		TNT1 A 0
		fail
	}
}