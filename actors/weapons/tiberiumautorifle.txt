// TIBERIUM AUTORIFLE
actor TiberiumAutorifleWeak : AOWWeapon {
	+AMMO_OPTIONAL
	inventory.pickupmessage "You got the Tiberium Autorifle!"
	weapon.selectionorder 13
	weapon.kickback 100
	weapon.ammotype "TE21Burst"
	weapon.ammouse 1
	weapon.ammogive 3
	weapon.ammotype2 "TE21CalAmmo"
	weapon.ammouse2 1
	weapon.ammogive2 20
	obituary "%o was skewered by %k's Tiberium Autorifle."
	Weapon.PreferredSkin "MarineMachineGun"
	states
	{
	Ready:
		E21C A 1 A_WeaponReady
		loop
	Deselect:
		E21C A 1 A_Lower
		loop
	Select:
		E21C A 1 A_Raise
		loop
	Fire:
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "Ready")
		TNT1 A 0 A_JumpIfNoAmmo ("Reload")
		E21C B 0 A_GunFlash
		E21C B 0 A_PlaySound("toxin/fire")
		E21C B 0 A_JumpIfInventory ("IsBlue",1,"FireBlue")
		E21C B 2 bright A_FireCustomMissile ("TiberiumDart", 0, 1, 10)
		E21C C 2 bright
		E21C D 2 A_JumpIfNoAmmo ("Reload")
		E21C A 2 A_ReFire
		goto Ready
	FireBlue:
		E21C B 2 bright A_FireCustomMissile ("TiberiumDartBlue", 0, 1, 10)
		E21C C 2 bright
		E21C D 2 A_JumpIfNoAmmo ("Reload")
		E21C A 2 A_ReFire
		goto Ready
	Reload:
		E21C A 0 A_JumpIfInventory ("TE21CalAmmo", 3, 2)
		E21C A 5 A_CheckReload
		goto Ready
		E21C A 15
		E21C A 0 A_TakeInventory ("TE21CalAmmo", 3)
		E21C A 0 A_GiveInventory ("TE21Burst", 3)
		goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		TNT1 A 0 A_Light0
		stop
	Spawn:
		E21C X -1
		stop
	}
}

actor TiberiumAutorifle : TiberiumAutorifleWeak {
	obituary "%o was skewered and mutated by %k's Tiberium Autorifle."
	weapon.ammotype "TE21Burst6"
	weapon.ammouse 1
	Dropitem "TiberiumAutorifleWeak"
	weapon.ammogive 3
	states {
	Reload:
		E21C A 0 A_JumpIfInventory ("TE21CalAmmo", 6, 2)
		E21C A 5 A_CheckReload
		goto Ready
		E21C A 15
		E21C A 0 A_TakeInventory ("TE21CalAmmo", 6)
		E21C A 0 A_GiveInventory ("TE21Burst6", 6)
		goto Ready
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem ("TiberiumAutorifleWeak")
		stop
	}
}

actor TE21CalAmmo : Ammo {
	+IGNORESKILL
	inventory.amount 20
	inventory.maxamount 120
	ammo.backpackamount 20
	ammo.backpackmaxamount 120
}

actor TE21Burst : Ammo {
	+IGNORESKILL
	inventory.amount 3
	inventory.maxamount 3
	ammo.backpackamount 3
	ammo.backpackmaxamount 3
}

actor TE21Burst6 : Ammo {
	+IGNORESKILL
	inventory.amount 6
	inventory.maxamount 6
	ammo.backpackamount 6
	ammo.backpackmaxamount 6
}

actor TiberiumDart : FastProjectile {
	PROJECTILE
	-RANDOMIZE
	radius 13
	height 8
	speed 60
	damage (9)
	damagetype "RifleRed"
	renderstyle add
	alpha 0.75
	scale 0.15
	deathsound "toxin/explode"
	decal "ChemScorch"
	states {
	Spawn:
		E21X A -1 bright
		stop
	Death:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx ("TiberiumDartExplosion", -1)
		TNT1 A 5 A_SpawnItemEx ("ChemicalExplosionSmall")
		TNT1 A 0 A_SpawnItemEx("AnthraxDecay", random(-16,16), random(-16,16), 4, 0, 0, 0, 0, 0, 160)
		stop
	}
}

actor TiberiumDartBlue : TiberiumDart {
	DamageType "RifleBlue"
	states {
	Death:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx ("TiberiumDartExplosionBlue", -1)
		TNT1 A 5 A_SpawnItemEx ("ChemicalExplosionSmall")
		TNT1 A 0 A_SpawnItemEx("AnthraxDecayBlue", random(-16,16), random(-16,16), 4, 0, 0, 0, 0, 0, 160)
		stop
	}
}

actor TiberiumDartExplosion {
	PROJECTILE
	+NOCLIP
	DamageType "ChemicalRed"
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 5 A_Explode(6, 64)
		stop
	}
}

actor TiberiumDartExplosionBlue : TiberiumDartExplosion {DamageType "ChemicalBlue"}

actor TiberiumAutoriflePickup : CustomInventory 21084 {
	inventory.pickupmessage "You got the Tiberium autorifle!"
	inventory.maxamount 0
	inventory.respawntics 2100
	inventory.pickupsound "misc/w_pkup"
	states {
	Spawn:
		E21C X -1
		stop
	Pickup:
		TNT1 AA 0 A_JumpIfInventory ("TiberiumAutorifleWeak", 1, "Failure")
		TNT1 A 0 A_JumpIfInventory ("TiberiumAutorifle", 1, "Failure")
		TNT1 A 0 A_GiveInventory ("TiberiumAutorifleWeak", 1)
		TNT1 A 0 A_GiveInventory ("TE21CalAmmo", 20)
		TNT1 A 0 A_GiveInventory ("Refunds", 20)
		stop
	Failure:
		TNT1 A 0
		fail
	}
}