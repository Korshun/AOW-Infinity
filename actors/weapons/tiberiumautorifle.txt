// TIBERIUM AUTORIFLE
Actor TiberiumAutorifleWeak : AOWWeapon {
	+AMMO_OPTIONAL
	Inventory.PickupMessage "You got the Tiberium Autorifle!"
	Weapon.SelectionOrder 13
	Weapon.Kickback 100
	Weapon.AmmoType "TE21Burst"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 3
	Weapon.AmmoType2 "TE21CalAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 20
	Obituary "%o was skewered by %k's Tiberium Autorifle."
	Weapon.PreferredSkin "MarineMachineGun"
	States
	{
	Ready:
		E21C A 1 A_WeaponReady
		Loop
	Deselect:
		E21C A 1 A_Lower
		Loop
	Select:
		E21C A 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		NULL A 0 A_JumpIfNoAmmo("Reload")
		NULL A 0 A_GunFlash
		NULL A 0 A_PlaySound("toxin/fire")
		E21C B 2 BRIGHT A_FireCustomMissile("TiberiumDart", 0, 1, 10)
		E21C C 2 BRIGHT
		E21C D 2 A_JumpIfNoAmmo("Reload")
		E21C A 2 A_ReFire
		Goto Ready
	Reload:
		NULL A 0 A_JumpIfInventory("TE21CalAmmo", 3, 2)
		E21C A 5 A_CheckReload
		Goto Ready
		E21C A 15
		NULL A 0 A_TakeInventory("TE21CalAmmo", 3)
		NULL A 0 A_GiveInventory("TE21Burst", 3)
		Goto Ready
	Flash:
		NULL A 2 A_Light1
		NULL A 2 A_Light2
		NULL A 0 A_Light0
		Stop
	Spawn:
		E21C X -1
		Stop
	}
}

Actor TiberiumAutorifle : TiberiumAutorifleWeak {
	Obituary "%o was skewered and mutated by %k's Tiberium Autorifle."
	Weapon.AmmoType "TE21Burst6"
	Weapon.AmmoUse 1
	DropItem "TiberiumAutorifleWeak"
	Weapon.AmmoGive 3
	States
	{
	Reload:
		NULL A 0 A_JumpIfInventory("TE21CalAmmo", 6, 2)
		E21C A 5 A_CheckReload
		Goto Ready
		E21C A 15
		NULL A 0 A_TakeInventory("TE21CalAmmo", 6)
		NULL A 0 A_GiveInventory("TE21Burst6", 6)
		Goto Ready
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItem("TiberiumAutorifleWeak")
		Stop
	}
}

Actor TE21CalAmmo : Ammo {
	+IGNORESKILL
	Inventory.amount 20
	Inventory.MaxAmount 120
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 120
}

Actor TE21Burst : Ammo {
	+IGNORESKILL
	Inventory.amount 3
	Inventory.MaxAmount 3
	Ammo.BackpackAmount 3
	Ammo.BackpackMaxAmount 3
}

Actor TE21Burst6 : Ammo {
	+IGNORESKILL
	Inventory.amount 6
	Inventory.MaxAmount 6
	Ammo.BackpackAmount 6
	Ammo.BackpackMaxAmount 6
}

Actor TiberiumDart : FastProjectile {
	PROJECTILE
	-RANDOMIZE
	Radius 13
	Height 8
	Speed 60
	Damage (9)
	DamageType "Rifle"
	RenderStyle add
	Alpha 0.75
	Scale 0.15
	DeathSound "toxin/explode"
	Decal "ChemScorch"
	States
	{
	Spawn:
		E21X A -1 BRIGHT
		Stop
	Death:
		NULL A 0
		NULL A 0 A_SpawnItemEx("TiberiumDartExplosion", -1)
		NULL A 5 A_SpawnItemEx("ChemicalExplosionSmall")
		NULL A 0 A_SpawnItemEx("AnthraxDecay", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 160)
		Stop
	}
}

Actor TiberiumDartExplosion {
	PROJECTILE
	+NOCLIP
	DamageType "Chemical"
	States
	{
	Spawn:
		NULL A 0
		NULL A 5 A_Explode(6, 64)
		Stop
	}
}

Actor TiberiumAutoriflePickup : CustomInventory 21084 {
	Inventory.PickupMessage "You got the Tiberium autorifle!"
	Inventory.MaxAmount 0
	Inventory.respawntics 2100
	Inventory.pickupsound "misc/w_pkup"
	States
	{
	Spawn:
		E21C X -1
		Stop
	Pickup:
		TNT1 AA 0 A_JumpIfInventory("TiberiumAutorifleWeak", 1, "Failure")
		NULL A 0 A_JumpIfInventory("TiberiumAutorifle", 1, "Failure")
		NULL A 0 A_GiveInventory("TiberiumAutorifleWeak", 1)
		NULL A 0 A_GiveInventory("TE21CalAmmo", 20)
		NULL A 0 A_GiveInventory("Refunds", 20)
		Stop
	Failure:
		NULL A 0
		Fail
	}
}