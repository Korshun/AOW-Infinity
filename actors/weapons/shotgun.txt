// Shotgun
actor Boomstick : AOWWeapon {
	obituary "%o was smacked down by %k's shotgun."
	inventory.pickupmessage "You got the shotgun!"
	weapon.selectionorder 17
	weapon.kickback 80
	weapon.ammotype "ShotgunAmmo"
	weapon.ammouse 1
	weapon.ammogive 5
	weapon.preferredskin "MarineShotgun"
	attacksound "weapons/shotgf"
	states {
	Ready:
		SHTG A 1 A_WeaponReady
		wait
	Deselect:
		SHTG A 1 A_Lower
		wait
	Select:
		SHTG A 1 A_Raise
		wait
	Fire:
		SHTG A 3
		SHTG A 0 A_GunFlash
		SHTG A 0 A_JumpIfInventory("IsBlue",1,"FireBlue")
		SHTG A 0 Bright A_FireBullets (5.5, 5.5, 2, 6, "HandgunPuff",0,350)
		SHTG A 2 Bright A_FireBullets (5.5, 5.5, 6, 4, "HandgunPuff",1,1000)
		SHTG A 3 Bright
		SHTG BC 4
		SHTG D 4 A_SpawnItemEx("ShotgunShellCasing",0,0,32,random(1,3),random(0,1),0,random(45,60),128)
		SHTG CB 4
		SHTG A 3
		SHTG A 7 A_ReFire
		goto Ready
	FireBlue:
		SHTG A 0 Bright A_FireBullets (5.5, 5.5, 2, 6, "HandgunPuffBlue",0,350)
		SHTG A 2 Bright A_FireBullets (5.5, 5.5, 6, 4, "HandgunPuffBlue",1,1000)
		goto Fire+5
	Flash:
		SHTF A 2 Bright A_Light1
		SHTF B 3 Bright A_Light2
		SHTF B 0 A_Light0
		stop
	Spawn:
		SHOT A -1
		stop
	}
}

actor ShotgunAmmo : Ammo {
	inventory.amount 5
	inventory.maxamount 50
	ammo.backpackamount 5
	ammo.backpackmaxamount 50
}

actor ShotgunPickup : CustomInventory replaces Shotgun 21080 {
	inventory.pickupmessage "You got the shotgun!"
	inventory.maxamount 0
	inventory.respawntics 2100
	inventory.pickupsound "misc/w_pkup"
	states {
	Spawn:
		SHOT A -1
		stop
	Pickup:
		TNT1 AA 0 A_JumpIfInventory ("Boomstick", 1, "Failure")
		TNT1 A 0 A_GiveInventory ("Boomstick", 1)
		TNT1 A 0 A_GiveInventory ("ShotgunAmmo", 5)
		TNT1 A 0 A_GiveInventory ("Refunds", 20)
		stop
	Failure:
		TNT1 A 0
		fail
	}
}