// Chemical sprayer
actor ChemicalSprayer : AOWWeapon 21066 {
	+ALT_AMMO_OPTIONAL
	inventory.pickupmessage "You got the chemical sprayer!"
	attacksound "Chem/fire"
	weapon.selectionorder 9
	weapon.kickback 1
	weapon.ammotype "ChemicalAmmo"
	weapon.ammouse 1
	weapon.ammogive 25
	weapon.ammotype2 "ChemicalGrenadeAmmo"
	weapon.ammouse2 1
	weapon.ammogive2 0
	Weapon.PreferredSkin "MarinePlasma"
	states {
	Ready:
		CHPA A 0 A_JumpIfInventory ("ChemSprayReady", 1, "Idle")
		CHPA ABC 2
		CHPA C 0 A_GiveInventory ("ChemSprayReady")
	Idle:
		CHPA C 0 A_StopSoundEx("Weapon")
		CHPA C 1 A_WeaponReady
		loop
	Deselect:
		CHPA C 0 A_TakeInventory ("ChemSprayReady")
		CHPA C 0 A_StopSoundEx("Weapon")
		CHPA C 1 A_Lower
		goto Deselect+1
	Select:
		CHPA A 1 A_Raise
		loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("IsInSpawnRoom",1,"Ready")
		CHMG A 0 A_PlaySound ("chem/fire", CHAN_WEAPON, 1.0, 1)
		CHMG A 0 A_GunFlash
		CHMG A 0 A_JumpIfInventory ("IsBlue", 1, "Fire.Blue")
		CHMG A 1 BRIGHT A_FireCustomMissile("ChemicalShot",random(-2,2),0,8,random(-2,2))
		CHMG A 1 BRIGHT A_FireCustomMissile("ChemicalShotDummy",random(-2,2),0,8,random(-2,2))
		CHMG B 1 BRIGHT A_FireCustomMissile("ChemicalShot",random(-2,2),1,8,random(-2,2))
		CHMG B 1 BRIGHT A_FireCustomMissile("ChemicalShotDummy",random(-2,2),0,8,random(-2,2))
		CHMG C 1 A_ReFire
		CHMG CD 3
		CHMG D 0 A_StopSoundEx("Weapon")
		goto Ready
	Fire.Blue:
		CHMG A 1 BRIGHT A_FireCustomMissile("ChemicalShotBlue",random(-2,2),0,8,random(-2,2))
		CHMG A 1 BRIGHT A_FireCustomMissile("ChemicalShotDummy",random(-2,2),0,8,random(-2,2))
		CHMG B 1 BRIGHT A_FireCustomMissile("ChemicalShotBlue",random(-2,2),1,8,random(-2,2))
		CHMG B 1 BRIGHT A_FireCustomMissile("ChemicalShotDummy",random(-2,2),0,8,random(-2,2))
		CHMG C 1 A_ReFire
		CHMG CD 3
		CHMG D 0 A_StopSoundEx("Weapon")
		goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfNoAmmo ("AltRejected")
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom",1,"Ready")
		CHPA C 15
		CHGR B 6 A_PlayWeaponSound ("chem/grenade")
		CHGR A 0 A_JumpIfInventory("IsBlue",1,"AltFireBlue")
		CHGR A 8 BRIGHT A_FireCustomMissile ("ChemicalGrenade", 0, 1, 8, 0)
		CHGR BC 3
		CHGR C 0 A_ReFire
		goto Ready
	AltFireBlue:
		CHGR A 8 BRIGHT A_FireCustomMissile ("ChemicalGrenadeBlue",0,1,8,0)
		CHGR BC 3
		CHGR C 0 A_ReFire
		goto Ready
	AltRejected:
		CHPA C 35 A_WeaponReady (WRF_NOSECONDARY)
		goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		TNT1 A 0 A_Light0
		stop
	Spawn:
		CHMG X -1
		stop
	}
}

actor ChemicalAmmo : Ammo {
	inventory.amount 50
	inventory.maxamount 200
	ammo.backpackamount 50
	ammo.backpackmaxamount 200
	states {
	Spawn:
		TNT1 A 0
		stop
	}
}

actor ChemicalGrenadeAmmo : Ammo {
	inventory.amount 1
	inventory.maxamount 4
	ammo.backpackamount 1
	ammo.backpackmaxamount 4
	states {
	Spawn:
		TNT1 A 0
		stop
	}
}

actor ChemSprayReady : Inventory {inventory.maxamount 1}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
actor ChemicalShot {
	PROJECTILE
	+FORCEXYBILLBOARD
	obituary "%o was poisoned by %k's chemical sprayer."
	renderstyle Add
	alpha 0.9
	Radius 8
	Height 8
	Speed 25
	Damage (random (4, 5))
	DamageType "ChemicalRed"
	Scale 0.5
	States {
	Spawn:
		TNT1 A 4
		TIBX ABCDEFGHIJK 2 bright
	Clear:
		TNT1 A 0 A_SpawnItemEx("AnthraxDecay", random(-16,16), random(-16,16), 4, 0, 0, 0, 0, 0, 240)
		TIBX L 2 bright
		TIBX MMNNOOPPQQ 1 bright A_FadeOut(0.1)
		stop
	Death:
		TNT1 A 1 //A_Explode(2,172)
		TNT1 A 0 A_SpawnItemEx("AnthraxDecay", random(-16,16), random(-16,16), 4, 0, 0, 0, 0, 0, 240)
		TIBX H 3 bright A_Stop
		TIBX IIJJKKLLMMOOPPQQ 1 bright A_FadeOut(0.07)
		stop
	}
}

actor ChemicalShotBlue : ChemicalShot {
	DamageType "ChemicalBlue"
	States {
	Clear:
		TNT1 A 0 A_SpawnItemEx("AnthraxDecayBlue", random(-16,16), random(-16,16), 4, 0, 0, 0, 0, 0, 240)
		TIBX L 2 bright
		TIBX MMNNOOPPQQ 1 bright A_FadeOut(0.1)
		stop
	Death:
		TNT1 A 1 //A_Explode(2,172)
		TNT1 A 0 A_SpawnItemEx("AnthraxDecayBlue", random(-16,16), random(-16,16), 4, 0, 0, 0, 0, 0, 240)
		TIBX H 3 bright A_Stop
		TIBX IIJJKKLLMMOOPPQQ 1 bright A_FadeOut(0.07)
		stop
	}
}

actor ChemicalShotDummy {
	PROJECTILE
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	-ACTIVATEIMPACT -ACTIVATEPCROSS
	renderstyle Add
	alpha 0.9
	Radius 8
	Height 8
	Speed 25
	Damage (0)
	Scale 0.5
	States {
	Spawn:
		TNT1 A 4
		TIBX ABCDEFGHIJKL 2 bright
		TIBX MMNNOOPPQQ 1 bright A_FadeOut(0.1)
		stop
	Death:
		TIBX H 3 bright A_Stop
		TIBX IIJJKKLLMMOOPPQQ 1 bright A_FadeOut(0.07)
		stop
	}
}

actor AnthraxDecay {
	renderstyle None
	alpha 0.5
	+NOBLOCKMAP
	+LOWGRAVITY
	+NODAMAGETHRUST
	Scale 2.0
	DamageType "ChemicalRed"
	obituary "%o chokes in %k's chemicals."
	states {
	Spawn:
		TNT1 AA 0 A_PlaySound("chem/chem")
		TNT1 A 0 A_SpawnItemEx ("AnthraxDecayFX")
		TNT1 AAAAAAAAAAA 15 BRIGHT A_Explode(4, 128)
		stop
	}
}

actor AnthraxDecayFX {
	+LOWGRAVITY +NOBLOCKMAP
	+CLIENTSIDEONLY
	reactiontime 33
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 5 A_SpawnItemEx("ChemicalFlame", random(-16,16), random(-16,16), random(8,16), 0, 0, random(1,3), 0, 128, 32)
		TNT1 A 0 A_Countdown
		loop
	Death:
		TNT1 A 0
		stop
	}
}

actor AnthraxDecayBlue : AnthraxDecay {damagetype "ChemicalBlue"}
actor AnthraxDecaySpecial : AnthraxDecay {damagetype "ChemicalSpecial"} // neutral

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
actor ChemicalGrenade {
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	+LOWGRAVITY
	radius 11
	height 8
	speed 35
	damage (30)
	seesound ""
	deathsound "weapons/rocklx"
	obituary "%o was mutated by %k's chemical grenade."
	damagetype "ChemicalRed"
	decal "ChemScorchStrong"
	translation "80:111=120:127"
	states {
	Spawn:
		SGRN A 2 bright A_SpawnItemEx ("PulseBallSmokePuff", -25)
		loop
	Death:
		TNT1 A 0 bright A_SpawnItem ("ChemicalExplosion")
		TNT1 A 0 bright A_Gravity
		TNT1 A 0 bright A_Explode (90, 128, 1)
		TNT1 AAAAAAAAA 70 A_SpawnItemEx("AnthraxDecay", random(-64,64), random(-64,64), 4)
		TNT1 A 0
		stop
	}
}

actor ChemicalGrenadeBlue : ChemicalGrenade {
	DamageType "ChemicalBlue"
	states {
	Death:
		TNT1 A 0 bright A_SpawnItem ("ChemicalExplosion")
		TNT1 A 0 bright A_Gravity
		TNT1 A 0 bright A_Explode (90, 128, 1)
		TNT1 AAAAAAAAA 70 A_SpawnItemEx("AnthraxDecayBlue", random(-64,64), random(-64,64), 4)
		TNT1 A 0
		stop
	}
}