// Double-barreled Shotgun
actor DoubleShotgun : AOWWeapon {
	inventory.pickupmessage "You got the double shotgun!"
	obituary "$OB_DOUBLESHOTGUN" // "%o was splattered by %k's double shotgun."
	attacksound "weapons/sshotf"
	DropItem "Boomstick"
	weapon.selectionorder 16
	weapon.kickback 30
	weapon.ammotype "ShotgunAmmo"
	weapon.ammouse 2
	weapon.ammogive 0
	weapon.preferredskin "MarineSSG"
	states {
	Ready:
		SHT2 A 1 A_WeaponReady
		loop
	Deselect:
		SHT2 A 1 A_Lower
		loop
	Select:
		SHT2 A 1 A_Raise
		loop
	Fire:
		SHT2 A 3
		SHT2 A 0 Bright A_GunFlash
		SHT2 AA 0 A_JumpIfInventory("IsBlue",1,"FireBlue")
		SHT2 A 7 Bright A_FireBullets (15.7, 11.7, 20, 5, "HandgunPuff", 1, 512)
		goto Reload
	FireBlue:
		SHT2 A 7 Bright A_FireBullets (15.7, 11.7, 20, 5, "HandgunPuffBlue", 1, 512)
		goto Reload
	Reload:
		SHT2 B 7
		SHT2 C 7 A_CheckReload
		SHT2 DD 0 A_SpawnItemEx ("ShotgunShellCasing", 0,0,32, random(1,3),random(0,1),0, random(45,60))  //Moved SSG Shell drop here. Previous method fucked up model interpolation -DS
		SHT2 D 7 A_OpenShotgun2
		SHT2 E 7
		SHT2 F 7 A_LoadShotgun2
		SHT2 G 6
		SHT2 H 6 A_CloseShotgun2
		SHT2 A 5 A_ReFire
		goto Ready
	Flash:
		SHT2 I 3 Bright A_Light1
		SHT2 J 4 Bright A_Light2
		TNT1 A 28 A_Light0
		stop
	Spawn:
		TNT1 A 0
		stop
	}
}

actor DoubleShotgunPickup : CustomInventory replaces SuperShotgun 21086 {
	inventory.pickupmessage "You got the double shotgun!"
	inventory.maxamount 0
	inventory.respawntics 2100
	inventory.pickupsound "misc/w_pkup"
	states {
	Spawn:
		SGN2 A -1
		stop
	Pickup:
		TNT1 AA 0 A_JumpIfInventory ("DoubleShotgun", 1, "Failure")
		TNT1 A 0 A_GiveInventory("DoubleShotgun", 1)
		TNT1 A 0 A_GiveInventory("ShotgunAmmo", 8)
		TNT1 A 0 A_GiveInventory("Refunds", 20)
		stop
	Failure:
		TNT1 A 0
		fail
	}
}