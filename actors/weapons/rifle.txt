// Rifle
actor Rifle : AOWWeapon 21042 {
	+NOEXTREMEDEATH
	+ALT_AMMO_OPTIONAL
	obituary "$OB_RIFLE" // "%o was crippled by %k's rifle."
	attacksound "rifle/fire"
	weapon.selectionorder 18
	weapon.kickback 150
	weapon.ammotype "RifleAmmo"
	weapon.ammouse 1
	weapon.ammogive 10
	weapon.ammotype2 "RifleGrenadeAmmo"
	weapon.ammouse2 1
	weapon.ammogive2 2
	inventory.pickupmessage "You got the Rifle!"
	Weapon.PreferredSkin "MarineBase"
	states {
	Ready:
		RIFL A 1 A_WeaponReady
		loop
	Deselect:
		RIFL A 1 A_Lower
		loop
	Select:
		RIFL A 1 A_Raise
		loop
	Fire:
		RIFL A 2
		RIFL A 0 A_Light2
		RIFL A 0 A_SpawnItemEx ("RifleShellCasing", 8, 8, 32, random(1,3), random(0,1), random(1,3), random(45,60))
		RIFL A 0 A_PlaySound("rifle/fire",CHAN_AUTO)
		RIFL A 0 A_JumpIfInventory ("IsBlue", 1, "FireBlue")
		// RIFL B 2 bright A_FireBullets (0, 0, 1, 12, "RiflePuff")
		RIFL B 0 bright A_FireCustomMissile("RifleFakePuff",0,0)
		RIFL B 2 bright A_RailAttack (25, 0, true, none, none, true, 0, "RiflePuff")
		RIFL C 1 bright offset (0, 36) A_Light1
		RIFL C 1 bright offset (0, 40)
		RIFL B 1 bright offset (0, 42) A_Light0
		RIFL A 1 offset (0, 44)
		RIFL A 1 offset (0, 40)
		RIFL A 1 offset (0, 36)
		RIFL A 8 offset (0, 32)
		RIFL A 4 A_ReFire
		goto Ready
	FireBlue:
		// RIFL B 2 bright A_FireBullets (0, 0, 1, 12, "RiflePuffBlue")
		RIFL B 0 bright A_FireCustomMissile("RifleFakePuff",0,0)
		RIFL B 2 bright A_RailAttack (25, 0, true, none, none, true, 0, "RiflePuffBlue")
		goto Fire+8
	AltFire:
		RIFL A 0 A_JumpIfNoAmmo ("AltRejected")
		RIFL A 4
		RIFL A 2 A_PlayWeaponSound ("grenade/fire")
		RIFL A 0 A_JumpIfInventory ("IsBlue", 1, "AltFireBlue")
		RIFL A 1 offset (0, 33) bright A_FireCustomMissile("RifleGrenade")
		RIFL A 1 offset (0, 34) 
		RIFL A 1 offset (0, 36) A_Light1
		RIFL A 1 offset (0, 37)
		RIFL A 1 offset (0, 38) A_Light0
		RIFL A 1 offset (0, 36)
		RIFL A 1 offset (0, 34)
		RIFL A 10 offset (0, 32)
		RIFL A 20 A_PlayWeaponSound ("rifle/reload")
		RIFL A 4 A_ReFire
		goto Ready
	AltFireBlue:
		RIFL A 1 offset (0, 33) bright A_FireCustomMissile ("RifleGrenadeBlue")
		goto AltFire+5
	AltRejected:
		RIFL A 8 A_WeaponReady (WRF_NOSECONDARY)
		goto Ready
	Spawn:
		RIFL X -1
		stop
	}
}

actor RifleFakePuff : FastProjectile // This behaves very funkily. Cool by me, though. [Bloax]
{
  Radius 1
  Height 1
  Speed 200 // Instant, probably a bug, but whatever.
  +RIPPER
  +CLIENTSIDEONLY // Why not.
  States
{
  Spawn:
	TNT1 A 0 //Surprise, nothing!
	TNT1 A 1 A_FadeOut(0.01) //In case shit goes asdf
	loop
  Death:
	TNT1 A 0 A_SpawnItemEx("RiflePuff",0,0,6,0,0,1.66)
	stop
}
}

actor RifleAmmo : Ammo {
	inventory.amount 8
	inventory.maxamount 50
	ammo.backpackamount 10
	ammo.backpackmaxamount 50
}

actor RifleGrenadeAmmo : Ammo {
	inventory.amount 1
	inventory.maxamount 8
	ammo.backpackamount 1
	ammo.backpackmaxamount 8
}

actor RifleGrenade : GrenadeGrenade {obituary "%o was popped by %k's rifle grenade."}
actor RifleGrenadeBlue : RifleGrenade {damagetype "RifleBlue"}
actor RiflePuff : HHBulletPuff {DamageType "RifleRed"}
actor RiflePuffBlue : HHBulletPuff {DamageType "RifleBlue"}