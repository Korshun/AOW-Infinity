// Bomb pack
actor BombPack : AOWWeapon {
	inventory.pickupmessage "Picked up the bomb pack... \cGwait what?!"
	inventory.pickupsound "terrorist/pickup"
	obituary "$OB_BOMBPACK_WTF" // "%o couldn't stand %k running around screaming."
	weapon.selectionorder 1
	weapon.kickback 0
	weapon.ammotype "Bomb-PackAmmo"
	weapon.ammouse 1
	weapon.ammogive 1
        weapon.preferredskin "MarineBackpack"
	+NOEXTREMEDEATH
	+WEAPON.CHEATNOTWEAPON
	states {
	Ready:
		SBOM AA 0 A_ChangeFlag ("PICKUP", 0)
		SBOM A 0 A_PlaySound ("terrorist/scream", CHAN_ITEM, 1.0, true)
		goto ReadyLoop
	ReadyLoop:
		SBOM A 1 A_WeaponReady (WRF_NOSWITCH)
		wait
	Deselect:
		TNT1 A 0 A_StopSound (CHAN_ITEM)
		TNT1 A 1 A_Lower
		wait
	Select:
		SBOM A 0 A_PlayWeaponSound ("terrorist/select")
		SBOM A 1 A_Raise
		wait
	Fire:
		SBOM A 0 A_TakeInventory ("PowerInvulnerable", 1)
		SBOM A 0 A_FireCustomMissile ("DummyMissile")
		SBOM A 5 A_PlayWeaponSound("terrorist/activate")
		SBOM B 5
		SBOM C 30
		SBOM C 1 A_GiveInventory ("IsTerrorist", 1)
		SBOM C -1 DamageThing (1000000)
		stop
	Spawn:
		TNT1 A 0
		stop
	}
}

actor Bomb-PackAmmo : Ammo {
	+INVENTORY.IGNORESKILL
	inventory.icon "TNT1A0"
	inventory.amount 1
	inventory.maxamount 1
	ammo.backpackamount 0
	ammo.backpackmaxamount 0
}

actor DummyMissile {
	PROJECTILE
	speed 0 radius 0
	height 0
	states {
	Spawn:
		TNT1 A 0
		stop
	}
}

actor IsTerrorist : Inventory {
	inventory.maxamount 1
}

actor KamikazeExplosion {
	obituary "$OB_BOMBPACK" // "%o \cIwas wiped off the face of Earth by %k."
	PROJECTILE
	+NOGRAVITY +NOINTERACTION
	damage 100
	damagetype "ExplosionRed"
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx ("KamikazeExplosionFX")
		TNT1 A 0 A_Explode (7000, 128)
		TNT1 A 0 A_Explode (2000, 256)
		TNT1 A 10 A_Explode (400, 1536)
		stop
	}
}

actor KamikazeExplosionBlue : KamikazeExplosion {damagetype "ExplosionBlue"}
actor KamikazeExplosionFX {
	+CLIENTSIDEONLY +NOINTERACTION +NOGRAVITY
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx ("BombPackQuake")
		TNT1 A 0 A_SpawnItemEx ("NukeMushroom")
		TNT1 A 0 A_SpawnItemEx ("ExplosionMain", random (-48, 48), random(-48, 48), random (16, 32))
		TNT1 A 0 A_PlaySound ("weapons/rocklx", CHAN_BODY)
		TNT1 A 0 A_PlaySound ("weapons/rocklx", CHAN_WEAPON)
		TNT1 AA 0 A_SpawnItemEx ("BombPackSmoke", 0, 0, random (32, 64), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		TNT1 AAA 0 A_SpawnItemEx ("BombPackSmoke", 0, 0, random (64, 96), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		TNT1 AAAA 0 A_SpawnItemEx ("BombPackSmoke", 0, 0, random (96, 128), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		TNT1 AAAAA 0 A_SpawnItemEx ("BombPackSmoke", 0, 0, random (128, 144), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		TNT1 AAAAAA 0 A_SpawnItemEx ("BombPackSmoke", 0, 0, random (144, 160), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		TNT1 AAAAAAA 0 A_SpawnItemEx ("BombPackSmoke", 0, 0, random (160, 192), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		TNT1 AAAAAAAA 0 A_SpawnItemEx ("BombPackSmoke", 0, 0, random (192, 224), random (-1.5, 1.5), random (-1.5, 1.5))
		TNT1 A 2
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx ("BombPackSmoke", 0, 0, random (224, 256), random (-1.5, 1.5), random (-1.5, 1.5))
		stop
	}
}

actor BombPackSmoke : NukeSmoke {
	+FORCEXYBILLBOARD
	scale 1.5
}

actor BombPackQuake {
	+CLIENTSIDEONLY +NOSECTOR +NOCLIP +NOINTERACTION
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 10 Radius_Quake (9, 10, 0, 24, 0)
		TNT1 A 10 Radius_Quake (8, 10, 0, 20, 0)
		TNT1 A 10 Radius_Quake (7, 10, 0, 19, 0)
		TNT1 A 10 Radius_Quake (6, 10, 0, 18, 0)
		TNT1 A 10 Radius_Quake (5, 10, 0, 16, 0)
		TNT1 A 10 Radius_Quake (3, 10, 0, 12, 0)
		TNT1 A 10 Radius_Quake (1, 10, 0, 6, 0)
		stop
	}
}

// given on resign to take player out of 'terrorism mode'
actor NoMoreTerrorism : CustomInventory {
	inventory.amount 0
	inventory.maxamount 999999
	+AUTOACTIVATE
	+ALWAYSPICKUP
	states {
	Spawn:
		TNT1 A 0
		stop
	Pickup:
		TNT1 A 3 A_ChangeFlag ("PICKUP", 1)
		TNT1 A 5 A_StopSound (CHAN_ITEM)
		stop
	}
}

// old stuff
actor BombPackKamikaze
{
  obituary "%o was killed."
  radius 13
  height 8
  speed 0
  damage 0
  renderstyle Add
  alpha 0.75
  deathsound "weapons/rocklx"
  PROJECTILE
  -RANDOMIZE
  +STRIFEDAMAGE
  //+FULLVOLDEATH
  +NOEXTREMEDEATH
  +NODAMAGETHRUST
  DamageType "Terrorist"
  Scale 2.0
  states
  {
  Spawn:
    PLS1 A 0 bright
    goto Death
    PLS1 A 0 bright A_SetShootable
    PLS1 A 0 bright A_Die
    Stop
  Death:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("ExplosionMainLarge", 0, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 0 A_PlaySound("weapons/rocklx")
    TNT1 A 0 A_PlayWeaponSound("weapons/rocklx")
    TNT1 A 0 bright A_Explode(100,512,0)
    TNT1 A 0 bright A_Explode(700,128,0)
    TNT1 A 1 bright A_Explode(700,256,0)
    TNT1 AAAAA 0 A_SpawnItemEx("FlameWallDecay", random(-48,48), random(-48,48), 4, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_SpawnItemEx("BombPackKamikazeSuicide", -1, 0, 0, 0, 0, 0, 0, 0, 0)
    stop
  }
}

actor BombPackKamikazeSuicide
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 1
  Damage 0
  Scale 0.7
  DamageType "Suicide"
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 5 bright A_Explode(250,16,1)
    stop
  }  
}