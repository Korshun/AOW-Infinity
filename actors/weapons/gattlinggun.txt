// Gatling gun
actor GatlingGun : AOWWeapon replaces Minigun 21045 {
	obituary "$OB_GATLING" // "%o was perforated by %k's gatling gun."
	inventory.pickupmessage "You got the gatling gun!"
	weapon.selectionorder 7
	weapon.ammotype "MachineGunAmmo"
	weapon.ammouse 1
	weapon.ammogive 25
	weapon.ammotype2 "MachineGunAmmo"
	weapon.ammouse2 2
	weapon.preferredskin "MarineChaingun"
	states {
	Ready:
		GATG A 0 A_StopSound (CHAN_WEAPON)
		GATG A 5 A_WeaponReady
		loop
	Deselect:
		GATG A 0 A_StopSound (CHAN_WEAPON)
		GATG AAA 1 A_Lower
		loop
	Select:
		GATG A 1 A_Raise
		loop
	Fire:
		GATG B 5 A_PlaySound ("gatling/spinup", 5)
		GATG A 4
		GATG B 3
	Hold:
		GATG A 1 A_PlaySound ("minigun/loop", CHAN_WEAPON, 1.0, 1)
		GATG A 0 A_Light2
		GATG A 0 A_PlaySound ("minigun/fire2", 5)
		GATG A 0 A_SpawnItemEx("RifleShellCasing",2,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GATG A 0 A_JumpIfInventory ("IsBlue", 1, "FireBlue")
		GATF A 1 BRIGHT A_FireBullets (7.2, 7.2, -1, 6, "RiflePuff")
		GATG B 1 A_Light1
		GATG B 0 A_Light2
		GATF B 0 A_SpawnItemEx("RifleShellCasing",2,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GATF B 1 BRIGHT A_FireBullets (7.2, 7.2, -1, 6, "RiflePuff")
		GATG A 1 A_Light1
		GATG A 0 A_JumpIfNoAmmo ("FireEnd")
		GATG A 0 A_ReFire
		goto FireEnd
	FireEnd:
		GATG B 0 A_Light0
		GATG B 3 A_PlaySound ("gatling/spin", 5)
		GATG A 0 A_StopSound (CHAN_WEAPON)
		GATG A 5 A_ReFire
		GATG B 6 A_ReFire
		GATG AB 7
		goto Ready
	AltFire:
		GATG B 5 A_PlaySound ("gatling/spinup", 5)
		GATG A 4
		GATG B 3
		goto AltHold
	AltHold:
		GATF A 0 BRIGHT A_PlaySound ("gatling/alt", CHAN_WEAPON, 1, 1.0)
		GATF A 0 BRIGHT A_Light2
		GATF AA 0 BRIGHT A_SpawnItemEx("RifleShellCasing",2,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GATF A 0 BRIGHT A_JumpIfInventory ("IsBlue", 1, "AltFireBlue")
		GATF A 2 BRIGHT A_FireBullets (2.6, 1.6, -1, 10, "ExplosionPuffSmall", 1, 3072)
		GATG B 3 A_Light1
		GATF B 0 A_Light2
		GATF BB 0 BRIGHT A_SpawnItemEx("RifleShellCasing",2,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GATF B 2 BRIGHT A_FireBullets (2.6, 1.6, -1, 10, "ExplosionPuffSmall", 1, 3072)
		GATG A 3 A_Light1
		GATG A 0 A_JumpIfNoAmmo ("AltFireEnd")
		GATG A 0 A_ReFire
		goto AltFireEnd
	AltFireEnd:
		GATG B 0 A_Light0
		GATG B 3 A_PlaySound ("gatling/spin", 5)
		GATG A 0 A_StopSound (CHAN_WEAPON)
		GATG A 5 A_ReFire
		GATG B 6 A_ReFire
		GATG AB 7
		goto Ready
	FireBlue:
		GATF A 0 BRIGHT A_Light2
		GATF A 1 BRIGHT A_FireBullets (7.2, 7.2, -1, 6, "RiflePuffBlue")
		GATG B 1 A_Light1
		GATF B 0 BRIGHT A_Light2
		GATF B 0 BRIGHT A_SpawnItemEx("RifleShellCasing",2,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GATF B 1 BRIGHT A_FireBullets (7.2, 7.2, -1, 6, "RiflePuffBlue")
		GATG A 1 A_Light2
		GATG A 0 A_JumpIfNoAmmo ("FireEnd")
		GATG A 0 A_ReFire
		goto FireEnd
	AltFireBlue:
		GATF A 2 BRIGHT A_FireBullets (2.6, 1.6, -1, 10, "ExplosionPuffSmallBlue", 1, 3072)
		GATG B 3 A_Light1
		GATF B 0 A_Light2
		GATF BB 0 BRIGHT A_SpawnItemEx("RifleShellCasing",2,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		GATF B 2 BRIGHT A_FireBullets (2.6, 1.6, -1, 10, "ExplosionPuffSmallBlue", 1, 3072)
		GATG A 3 A_Light1
		GATG A 0 A_JumpIfNoAmmo ("AltFireEnd")
		GATG A 0 A_ReFire
		goto AltFireEnd
	Spawn:
		GATG X -1
		stop
	}
}

actor ExplosionPuff {
	+PUFFONACTORS
	+CLIENTSIDEONLY
	damagetype "ExplosionRed"
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 14 A_SpawnItemEx ("ExplosionSmall", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
		stop
	}
}

actor ExplosionPuffBlue : ExplosionPuff {damagetype "ExplosionBlue"}

actor ExplosionPuffSmall : ExplosionPuff {
	damagetype "ExplosionRed"
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 14 A_SpawnItemEx ("ExplosionSmaller", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
		stop
	}
}
actor ExplosionPuffSmallBlue : ExplosionPuffSmall {damagetype "ExplosionBlue"}