// Repair gun
actor RepairGun : AOWWeapon {
	weapon.kickback 0
	inventory.pickupsound "misc/w_pkup"
	obituary "%o was repaired to death by %k."
	attacksound "repairgun/fire"
	weapon.ammouse 0
	weapon.ammogive 0
	weapon.PreferredSkin "MarineBackpack"
	dropitem "EngineerPack"
	states {
	Ready:
		REP1 A 0 A_JumpIfInventory ("RepairGunUpgrade", 1, "ReadyEnhanced")
		REP1 A 35 A_WeaponReady
		loop
	Deselect:
		REP1 A 0 A_JumpIfInventory ("RepairGunUpgrade", 1, "DeselectEnhanced")
		REP1 A 1 A_Lower
		wait
	Select:
		REP1 A 0 A_JumpIfInventory ("RepairGunUpgrade", 1, "SelectEnhanced")
		REP1 A 1 A_Raise
		wait
	Fire:
		REP1 C 0 A_JumpIfInventory ("RepairGunUpgrade", 1, "FireEnhanced")
		REP1 C 2
		REP1 B 0 bright A_FireBullets (0, 0, 1, 2, "RepairDisarmPuff", 0, 800)
		REP1 B 0 bright ACS_ExecuteAlways (650, 0, 10, 0)
		REP1 B 2 bright ACS_ExecuteAlways (654, 0) // display target health
		REP1 C 2 A_Refire
		Goto Ready
	AltFire:
		REP1 A 0 A_JumpIfInventory("RepairGunUpgrade",1,3)
		REP1 A 1 A_JumpIfTargetInLOS ("Fire")
		REP1 A 0 A_Jump(256,2)
		REP2 A 1 A_JumpIfTargetInLOS ("Fire")
		REP2 A 0
		goto Ready
	ReadyEnhanced:
		REP2 A 1 A_WeaponReady
		wait
	DeselectEnhanced:
		REP2 A 1 A_Lower
		wait
	SelectEnhanced:
		REP2 A 1 A_Raise
		wait
	FireEnhanced:
		REP2 C 1
		REP2 B 0 bright A_FireBullets (0, 0, 1, 1, "EnhancedRepairDisarmPuff", 0, 800)
		REP2 B 0 bright ACS_ExecuteAlways (650, 0, 10, 0)
		REP2 B 1 bright ACS_ExecuteAlways (654, 0)
		REP2 A 2 A_Refire
		Goto Ready
	}
}
actor RepairGunUpgrade : Boolean {}

actor HealGun : RepairGun {
	Inventory.PickupMessage "Picked up the Healing Gun."
	Obituary "%o suffers from medicophobia."
	Weapon.PreferredSkin "MarineMedic"
	dropitem "MedicPack"
	states {
	Spawn:
		TNT1 A 0
		stop
	Ready:
		REP4 A 1 A_WeaponReady
		loop
	Deselect:
		REP4 A 1 A_Lower
		loop
	Select:
		REP4 A 1 A_Raise
		loop
	Fire:
		REP4 A 0 A_JumpIfTargetInLOS ("AltFire")
		goto Ready
	AltFire:
		REP4 C 2
		REP4 B 0 bright A_FireBullets (0, 0, 1, 0, "MedicPuff", 0, 800) // visual ONLY
		REP4 B 0 bright ACS_ExecuteAlways (650, 0, 6, 1) // actually does the healing
		REP4 B 2 bright ACS_ExecuteAlways (654, 0) // display target health
		REP4 C 2 A_Refire
		goto Ready
	}
}

actor BombSquadGun : RepairGun {
	Inventory.PickupMessage "Picked up the Bomb-Squad Gun."
	AttackSound "repairgun/fire"
	Obituary "%o was disarmed by %k."
	+THRUGHOST
	states {
	Ready:
		REP3 A 1 A_WeaponReady
		Loop
	Deselect:
		REP3 A 1 A_Lower
		Loop
	Select:
		REP3 A 1 A_Raise
		Loop
	Fire:
		REP3 C 2
		REP3 B 2 bright A_FireBullets (2.0, 2.0, 1, 6, "DisarmPuff", 0, 500)
		REP3 C 2 A_Refire
		Goto Ready
	AltFire: stop
	}
}

actor DisarmPuff {
	+NOINTERACTION
	+PUFFONACTORS
	+BLOODLESSIMPACT
	+FORCEXYBILLBOARD
	+ALWAYSPUFF
	+THRUGHOST
	+PUFFGETSOWNER
	+NODAMAGETHRUST
	renderstyle Add
	alpha 0.5
	scale 0.25
	damagetype "Disarm"
	translation "112:127=160:167"
	states {
	Spawn:
		CHMX AA 1 A_Explode (9, 64)
		CHMX BC 2 bright
	Melee:
		CHMX DEF 2 bright
		stop
	}
}
actor RepairDisarmPuff : DisarmPuff {
	translation ""
	states {
	Spawn:
		PLCX AA 1 A_Explode (9, 64)
		PLCX BC 2 bright
	Melee:
		PLCX DEF 2 bright
		stop
	}
}

actor EnhancedRepairDisarmPuff : DisarmPuff {
	translation "112:127=250:254"
	states {
	Spawn:
		CHMX AA 1 A_Explode (7, 64)
		CHMX BC 2 bright
	Melee:
		CHMX DEF 2 bright
		stop
	}
}

actor MedicPuff : DisarmPuff {
	translation "112:127=174:189"
	states {
	Spawn:
		CHMX ABC 2 bright
		goto Melee
	}
}
actor DeconstructionPuff : DisarmPuff {damagetype "Deconstruction" translation "0:0=0:0"}