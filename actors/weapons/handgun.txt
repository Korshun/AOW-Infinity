actor HandgunAmmo : Ammo 
{
	Inventory.MaxAmount 400
}

actor HandgunClip : Ammo 
{
	Inventory.MaxAmount 10
}

actor Handgun : AOWWeapon replaces Pistol 21041
{
	Tag "$TAG_HANDGUN"
	Obituary "$OB_HANDGUN"
	Inventory.PickupMessage "$TAG_HANDGUN"
	Weapon.PreferredSkin "MarinePistol"
	Weapon.SelectionOrder 19
		
	Weapon.AmmoType "HandgunClip"
	Weapon.AmmoType2 "HandgunAmmo"
	Weapon.AmmoGive2 10
	
	+WEAPON.WIMPY_WEAPON
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	
	AttackSound "pistol/fire"
	
	States
	{
	Spawn:
		PIST A -1
		stop
	Ready:
		// One-handed gun: allow shooting while swimming.
		NULL A 0 A_JumpIf(waterlevel >= 3, "Underwater")
		PISG A 1 A_WeaponReady
		NULL A 0 A_JumpIfInventory("AOWReload", 1, "Reload")
		Loop
	Deselect:
		NULL A 0 A_Lower
		PISG A 1 A_Lower
		Loop
	Select:
		NULL A 0 A_JumpIf(waterlevel >= 3, "Underwater")
		NULL A 0 A_Raise
		PISG A 1 A_Raise
		Loop
	Underwater:
		NULL A 0 A_SelectWeapon("Unarmed")
		Goto Deselect

	Fire:
		NULL A 0 A_JumpIf(waterlevel >= 3, "Ready")
		NULL A 0 A_JumpIfInventory("HandgunClip", 1, 1)
		Goto Reload
		NULL A 0 A_SpawnItemEx("ShellCasing",8,8,32,random(1,3),random(0,1),random(1,3),random(45,60))
		NULL A 0 A_TakeInventory("HandgunClip", 1)
		NULL A 0 A_AlertMonsters
		NULL A 0 A_FireBullets(2.0, 2.0, -1, 8, "BulletPuff", FBF_NORANDOM, 1536)
		Goto FireEnd
	FireEnd:
		PISG B 4 A_GunFlash
		PISG CC 1 A_WeaponReady
		PISG BBB 1 A_WeaponReady
		NULL A 0 A_ReFire
		Goto Ready
	Flash:
		PISF A 1 Bright A_Light2
		PISF A 1 Bright A_Light1
    	Goto LightDone

	AltFire:
	Reload:
		NULL A 0 A_JumpIfInventory("HandgunAmmo", 1, 1)
		Goto Ready
		NULL A 0 A_JumpIfInventory("HandgunClip", 10, "Ready")
		PISG A 35 Offset(0, 50) A_PlayWeaponSound("CLIPOUT")
	ReloadLoop:
		NULL A 0 A_JumpIfInventory("HandgunClip", 10, "ReloadEnd")
		NULL A 0 A_JumpIfInventory("HandgunAmmo", 1, 1)
		Goto ReloadEnd
		NULL A 0 A_TakeInventory("HandgunAmmo", 1)
		NULL A 0 A_GiveInventory("HandgunClip", 1)
		Loop
	ReloadEnd:
		PISG A 1 A_PlayWeaponSound("CLIPIN")
		Goto Ready
	}
}
