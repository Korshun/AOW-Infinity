// Grenade launcher
actor Grenade_Launcher : AOWWeapon replaces GrenadeLauncher 21046 {
	+WEAPON.NOAUTOAIM
	obituary "$OB_GRENADE" // "%o ate %k's grenade."
	inventory.pickupmessage "You got the grenade launcher!"
	weapon.selectionorder 12
	weapon.kickback 100
	weapon.ammotype "GrenadeAmmo"
	weapon.ammouse 1
	weapon.ammotype2 "GrenadeAmmo"
	weapon.ammouse2 1
	weapon.ammogive 2
	weapon.preferredskin "MarineRevenant"
	states {
	Spawn:
		GREN X -1
		stop
	Select:
		GREN A 1 A_Raise
		wait
	Deselect:
		GREN A 1 A_Lower
		wait
	Ready:
		GREN A 1 A_WeaponReady
		wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("IsInSpawnRoom",1,"Ready")
		GREN A 6
		GREN A 5 A_PlayWeaponSound ("grenade/fire")
		GREN GH 1
		GREN I 0 A_GunFlash
		GREN I 0 A_JumpIfInventory("IsBlue",1,"FireBlue")
		GREN I 1 A_FireCustomMissile ("GrenadeGrenade", 0, 1, 12)
		GREN DEF 2
		GREN EDABC 1
		GREN A 0 A_ReFire
		goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "Ready")
		goto Ready
		GREN A 7
		GREN A 4 A_PlayWeaponSound ("grenade/fire")
		GREN GH 1
		GREN I 0 A_GunFlash
		GREN I 0 A_JumpIfInventory ("IsBlue",1,"AltFireBlue")
		GREN I 1 A_FireCustomMissile ("GrenadeBounce", 0, 1, 12)
		GREN DEF 2
		GREN EDABC 1
		GREN A 0 A_ReFire
		goto Ready
	FireBlue:
		GREN I 1 A_FireCustomMissile ("GrenadeGrenadeBlue", 0, 1, 12)
		GREN DEF 2
		GREN EDABC 1
		GREN A 0 A_ReFire
		goto Ready
	AltFireBlue:
		GREN I 1 A_FireCustomMissile ("GrenadeBounceBlue", 0, 1, 12)
		GREN DEF 2
		GREN EDABC 1
		GREN A 0 A_ReFire
		goto Ready
	}
}

// orca still uses this
actor GrenadeSwitch : Boolean {}

actor GrenadeAmmo : Ammo {
	 Inventory.Amount 6
	 Inventory.MaxAmount 40
	 Ammo.BackpackAmount 5
	 Ammo.BackpackMaxAmount 40
	 states {
	 Spawn:
		TNT1 A 0
		stop
	 }
}

actor GrenadeGrenade {
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	+LOWGRAVITY
	radius 11
	height 8
	speed 35
	damage (25)
	damagetype "RifleRed"
	seesound ""
	deathsound "weapons/rocklx"
	obituary "$OB_GRENADE" // "%o ate %k's grenade."
	decal "Scorch"
	states {
	Spawn:
		SGRN A 2 bright A_SpawnItemEx ("PulseBallSmokePuff", -25)
		loop
	Death:
		TNT1 B 0 bright A_NoGravity
		TNT1 B 0 bright A_Explode(55,128,1)
		TNT1 B 0 bright A_RadiusThrust(384,128,1) //Weeee
		TNT1 A 5 A_SpawnItemEx ("ExplosionMain")
		stop
	}
}

actor GrenadeBounce : GrenadeGrenade {
	+DOOMBOUNCE
	-LOWGRAVITY
	obituary "$OB_GRENADE_ALT" // "%o played catch with %k's grenade."
	damage (5)
	bouncecount 2
	states {
	Death:
		TNT1 A 0 bright A_NoGravity
		TNT1 A 0 bright A_Explode (40, 96, 1)
		TNT1 A 0 bright A_RadiusThrust(384,128,1) //Wee
		TNT1 A 5 A_SpawnItemEx ("ExplosionMain")
		stop
	}
}

actor GrenadeGrenadeBlue : GrenadeGrenade {DamageType "RifleBlue"}
actor GrenadeBounceBlue : GrenadeBounce {DamageType "RifleBlue"}

// Select/Deselect offset-animations
/*
	Select:
		GREN A 1 offset (130,154)
		GREN A 1 offset (120,139)
		GREN A 0 A_PlaySoundEx("Bazooka/Select","Body")
		GREN A 1 offset (110,125)
		GREN A 1 offset (100,112)
		GREN A 1 offset (90,100)
		GREN A 1 offset (80,89)
		GREN A 1 offset (70,79)
		GREN A 1 offset (60,70)
		GREN A 1 offset (50,62)
		GREN A 1 offset (40,55)
		GREN A 1 offset (30,49)
		GREN A 1 offset (20,44)
		GREN A 1 offset (15,40)
		GREN A 1 offset (10,37)
		GREN A 1 offset (5,34)
		GREN A 1 offset (0,0)
		Goto Ready
	Deselect:
		GREN A 1 offset (4,34)
		GREN A 1 offset (8,40)
		GREN A 1 offset (16,49)
		GREN A 1 offset (24,62)
		GREN A 1 offset (32,79)
		GREN A 1 offset (40,100)
		GREN A 1 offset (48,125)
		GREN A 1 offset (56,154)
		TNT1 A 0 offset (1,32)
		TNT1 A 1 A_Lower
		Wait
*/