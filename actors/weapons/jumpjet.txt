// Jumpjet Gatling gun
actor JumpJetGatlingGun : GatlingGun {
	obituary "$OB_GATLING_JET" // "%o was perforated by a flying %k's gatling gun."
	weapon.ammotype2 "JumpJetFuel"
	weapon.ammouse2 1
	+ALT_AMMO_OPTIONAL
	DropItem "GatlingGun"
	Weapon.PreferredSkin "MarineChaingun"
	states {
	Ready:
		GATG A 1 A_WeaponReady
		GATG AA 0 A_JumpIfInventory ("JumpJetSwitch", 1, "ReadyJumpJet")
		NULL A 0 A_StopSound (6)
		GATG AA 35 A_WeaponReady
		NULL A 0 A_StopSound (6)
		goto Idle
	Idle:
		NULL A 0 SetPlayerProperty (0, 0, 3)
		NULL A 0 A_GiveInventory ("JumpJetFuel", 1)
		GATG A 35 A_WeaponReady
		loop
	ReadyJumpJet:
		GATG A 5 A_WeaponReady
		NULL A 0 SetPlayerProperty (0, 1, 3)
		NULL A 0 A_JumpIfInventory ("JumpJetFuel", 1, "ReadyJumpJet")
		goto AltFireOff
	Deselect:
		GATG A 1 A_Lower
		NULL A 0 A_JumpIfInventory ("JumpJetSwitch", 1, "DeselectJumpJet")
		NULL A 0 A_StopSound (CHAN_WEAPON)
		NULL A 0 A_StopSound (6)
		loop
	DeselectJumpJet:
		NULL A 0 A_PlaySound ("jumpjet/stop", 5)
		NULL A 0 A_StopSound (6)
		NULL A 0 SetPlayerProperty (0, 0, 3)
		NULL A 0 A_ChangeFlag ("PICKUP", 1)
		NULL A 0 A_TakeInventory ("JumpJetSwitch", 1)
		goto Deselect
	AltFire:
		NULL A 0 A_JumpIfInventory("JumpJetSwitch", 1, "AltFireOff")
		NULL A 0 A_JumpIfInventory("JumpJetFuel", 1, "AltFireGo")
		GATG A 35 A_WeaponReady (WRF_NOSECONDARY)
		goto Ready
	AltFireGo:
		GATG A 70 A_PlaySound ("jumpjet/start", 5)
		NULL A 0 SetPlayerProperty (0, 1, 3)
		NULL A 0 A_ChangeFlag ("PICKUP", 0)
		NULL A 0 A_GiveInventory("JumpJetSwitch", 1)
		NULL A 0 ACS_ExecuteAlways (331, 0) // begin fuel drain
		NULL A 0 A_PlaySound ("jumpjet/loop", 6, 1.0, 1)
		goto Ready
	AltFireOff:
		NULL A 0 A_PlaySound ("jumpjet/stop", 5)
		NULL A 0 A_StopSound (CHAN_WEAPON)
		NULL A 0 A_StopSound (6)
		NULL A 0 A_LowGravity
		GATG A 70 SetPlayerProperty (0, 0, 3)
		NULL A 0 A_ChangeFlag ("PICKUP", 1)
		NULL A 0 A_TakeInventory ("JumpJetSwitch", 1)
		NULL A 0 A_Gravity
		goto Ready
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItem ("GatlingGun")
		stop
	}
}

actor JumpJetSwitch : Inventory {
    inventory.amount 0
    inventory.maxamount 1
}

actor JumpJetCount : Inventory {
    inventory.amount 0
    inventory.maxamount 25
}

actor JumpJetFuel : Ammo {
	inventory.amount 1
	inventory.maxamount 100
	ammo.backpackamount 100
	ammo.backpackmaxamount 100
	states {
	Spawn:
		NULL A 0
		stop
	}
}

actor JumpjetFire {
	+NOSECTOR +NOCLIP +NOINTERACTION +CLIENTSIDEONLY
	states {
	Spawn:
		NULL A 0
		NULL A 0 A_SpawnItemEx ("OrcaFlame", 0, -5, 0, 0, 0, random(-9,-12))
		NULL A 0 A_SpawnItemEx ("OrcaFlame", 0,  5, 0, 0, 0, random(-9,-12))
		stop
	}
}

// [Dusk] Deactivates jumpjet forcibly
actor JumpjetDeactivate : CustomInventory {
	+ALWAYSPICKUP +AUTOACTIVATE
	states {
	Spawn:
		NULL A 0
		stop
	Pickup:
		NULL A 0 A_Gravity
		NULL A 0 A_StopSound (6)
		NULL A 0 SetPlayerProperty (0, 0, 3)
		NULL A 0 A_ChangeFlag ("PICKUP", 1)
		TNT1 A 5 A_TakeInventory ("JumpJetSwitch", 1)
		stop
	}
}