// Flamethrower
Actor Flamer : AOWWeapon 21047 {
	+ALT_AMMO_OPTIONAL
	Obituary "$OB_FLAMER"
	Inventory.PickupMessage "You got the flamethrower!"
	AttackSound "flamer2/fire"
	Weapon.SelectionOrder 10
	Weapon.Kickback 1
	Weapon.AmmoType "FlamerAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Weapon.AmmoType2 "PhosphorusGrenadeAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 0
	Decal BulletChip
	Weapon.PreferredSkin "MarineFlamer"
	States
	{
	Ready:
		NULL A 0
		FLMR BCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCD 2 A_WeaponReady
		NULL A 0 A_StopSound(CHAN_WEAPON)
		Loop
	Deselect:
		NULL A 0 A_StopSound(CHAN_WEAPON)
	DeselectLoop:
		FLMR A 1 A_Lower
		Loop
	Select:
		FLMR A 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		FLMR A 2 A_PlaySound("flamer2/up", CHAN_WEAPON)
		FLMR A 2
		NULL A 0 A_GunFlash
		NULL A 0 A_PlaySound("flamer2/fire", CHAN_WEAPON, 1.0, true)
		FLMR A 2 BRIGHT offset(1, 32) A_FireCustomMissile("FlamethrowerFire", random(-2, 2), 1, 5, random(2, 6))
		FLMR A 2 BRIGHT offset(2, 33) A_FireCustomMissile("FlamethrowerFire", random(-2, 2), 0, 5, random(2, 6))
		FLMR A 1 A_ReFire
		FLMR A 1
		FLMR A 2 A_PlaySound("flamer2/down", CHAN_WEAPON)
		Goto Ready
	Hold:
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		NULL A 0 A_GunFlash
		FLMR A 2 BRIGHT offset(4, 35) A_FireCustomMissile("FlamethrowerFire", random(-2, 2), 1, 5, random(2, 6))
		FLMR A 2 BRIGHT offset(5, 36) A_FireCustomMissile("FlamethrowerFire", random(-2, 2), 0, 5, random(2, 6))
		FLMR A 1 A_ReFire
		FLMR A 1 offset(4, 35)
		FLMR A 1 offset(2, 33)
		FLMR A 2 A_PlaySound("flamer2/down", CHAN_WEAPON)
		Goto Ready
	AltFire:
		NULL A 0 A_JumpIfInventory("IsInSpawnRoom", 1, "Ready")
		NULL A 0 A_JumpIfNoAmmo("AltRejected")
		FLMR A 15
		FLMR A 6 A_PlaySound("grenade/fire", 6)
		FLMR A 1 offset(0, 32) A_FireCustomMissile("PhosphorusGrenade", 2, 1, 10, 0)
		Goto AltReload
	AltReload:
		FLMR A 1 offset(5, 35)
		FLMR A 1 offset(10, 38)
		FLMR A 1 offset(15, 41)
		FLMR A 1 offset(20, 44)
		FLMR A 1 offset(25, 47)
		FLMR A 2 offset(30, 50)
		FLMR A 1 offset(25, 47)
		FLMR A 1 offset(20, 44)
		FLMR A 1 offset(15, 41)
		FLMR A 1 offset(10, 38)
		FLMR A 1 offset(5, 35)
		FLMR A 1 offset(0, 32)
		NULL A 0 A_ReFire
		Goto Ready
	AltRejected:
		FLMR A 35 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready
	Flash:
		NULL A 2 A_Light1
		NULL A 2 A_Light2
		NULL A 0 A_Light0
		Stop
	Spawn:
		FLMR X -1
		Stop
	}
}

Actor FlamerAmmo : Ammo {
	Inventory.amount 100
	Inventory.MaxAmount 400
	Ammo.BackpackAmount 100
	Ammo.BackpackMaxAmount 400
	States
	{
	Spawn:
		NULL A 0
		Stop
	}
}

Actor PhosphorusGrenadeAmmo : Ammo {
	Inventory.amount 1
	Inventory.MaxAmount 4
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 4
	States
	{
	Spawn:
		NULL A 0
		Stop
	}
}

Actor FlameThrowerFire {
	PROJECTILE
	+FORCEXYBILLBOARD
	
	Obituary "$OB_FLAMER"
	RenderStyle Add
	Alpha 0.5
	Scale 0.41
	Radius 4
	Height 4
	Speed 25
	Damage (0)
	DamageType "Fire"
	States
	{
	Spawn:
		FLMX B 0
		FLMX BCDEFGHIJK 2 BRIGHT A_Explode(2, 128, 0)
		NULL A 0 A_SpawnItemEx("FlameDecay", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 240)
		FLMX LMNO 2 BRIGHT A_FadeOut(0.16)
		Stop
	Death:
		NULL A 0 A_Explode(8, 128)
		NULL A 0 A_SpawnItemEx("FlameDecay", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 240)
		TNT1 AAA 1 A_SpawnItemEx("FlameThrowerFireRemains", random(0, 1), 0, random(0, 8), random(0, 1), 0, random(0, 3), random(0, 12)*30, 0, 32)
		Stop
	}
}

Actor FlameThrowerFireBlue : FlameThrowerFire {
	DamageType "Fire"
	States
	{
	Spawn:
		FLMX A 4
		FLMX ABCDEFGHIJK 2 BRIGHT A_Explode(2, 128, 0)
		NULL A 0 A_SpawnItemEx("FlameDecayBlue", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 240)
		FLMX LMNOPQ 2 BRIGHT A_FadeOut(0.15)
		Stop
	Death:
		NULL A 0 A_Explode(8, 128)
		NULL A 0 A_SpawnItemEx("FlameDecayBlue", random(-16, 16), random(-16, 16), 4, 0, 0, 0, 0, 0, 240)
		TNT1 AAA 1 A_SpawnItemEx("FlameThrowerFireRemains", random(0, 1), 0, random(0, 8), random(0, 1), 0, random(0, 3), random(0, 12)*30, 0, 32)
		Stop
	}
}

Actor FlameThrowerFireRemains {
	PROJECTILE
	+RANDOMIZE
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	
	RenderStyle Add
	Radius 2
	Height 4
	Alpha 0.4
	Speed 2
	Damage 0
	Scale 0.8
	States
	{
	Spawn:
		NULL A 4
		FLMB A 2 BRIGHT
		FLMB BCDEFGH 2 BRIGHT
		FLMB ABCDEFGH 2 BRIGHT A_FadeOut(0.05)
		Stop
	}
}

// Phosphorus grenade
Actor PhosphorusGrenade {
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	+LOWGRAVITY
	Obituary "$OB_FLAMER_GRENADE"
	Radius 11
	Height 8
	Speed 35
	Damage (30)
	DeathSound "weapons/rocklx"
	DamageType "Fire"
	Decal "Scorch"
	Translation "80:111=163:167"
	States
	{
	Spawn:
		SGRN A 2 BRIGHT A_SpawnItemEx("PulseBallSmokePuff", -25)
		Loop
	Death:
		NULL A 0 A_Gravity
		NULL A 0 A_Explode(120, 128, 1)
		NULL A 5 A_SpawnItem("ExplosionMain")
		NULL A 0 A_SpawnItem("FlameDecay")
		TNT1 AAAAAAAA 70 A_SpawnItem("FlameDecay")
		NULL A 0
		Stop
	}
}

Actor PhosphorusGrenadeBlue : PhosphorusGrenade {
	DamageType "Fire"
	States
	{
	Death:
		NULL A 0 A_Gravity
		NULL A 0 A_Explode(120, 128, 1)
		NULL A 5 A_SpawnItem("ExplosionMain")
		NULL A 0 A_SpawnItem("FlameDecayBlue")
		TNT1 AAAAAAAA 70 A_SpawnItem("FlameDecayBlue")
		NULL A 0
		Stop
	}
}