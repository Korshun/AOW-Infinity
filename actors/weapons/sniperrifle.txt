// Sniper rifle
actor SniperRifle : AOWWeapon {
	obituary "$OB_SNIPER" // "%o found %k's bullet in %p head."
	attacksound "rc50/fire"
	weapon.selectionorder 3
	weapon.kickback 80
	weapon.ammotype "SniperAmmo"
	weapon.ammouse 1
	weapon.ammogive 5
	DropItem "SniperRifleWeak"
	inventory.pickupmessage "You got the sniper rifle!"
	+NOEXTREMEDEATH
	Weapon.PreferredSkin "MarineSniper"
	states {
	Ready:
		SNIE AA 0 A_JumpIfInventory ("IsZoomed", 1, "ReadyZoomed")
		SNIE A 1 A_WeaponReady
		wait
	ReadyZoomed:
		TNT1 A 1 A_WeaponReady
		loop
	Deselect:
		SNIE AA 0 A_JumpIfInventory ("IsZoomed", 1, "AltFireOff")
		SNIE A 1 A_Lower
		wait
	Select:
		SNIE A 1 A_Raise
		loop
	Fire:
		SNIP A 0 A_GunFlash
		SNIE AA 0 A_JumpIfInventory ("IsZoomed", 1, "FireZoomed")
		SNIF F 0 A_JumpIfInventory("IsBlue",1,"FireBlue")
		SNIF F 0 A_FireBullets (7.2, 7.2, -1, 25, "RiflePuff")
		goto Reload
	FireBlue:
		SNIF F 0 A_FireBullets (7.2, 7.2, -1, 25, "RiflePuffBlue")
		goto Reload
	Reload:
		SNIF F 2 BRIGHT
		SNIE A 8
		SNIR ABCDEFGHIJ 2
		SNIR C 5 offset (0, 40) A_PlaySound ("rc50/boltopen")
		SNIR C 5 offset (0, 35) A_SpawnItemEx("RifleShellCasing",0,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		SNIR C 10 offset (0, 33) A_PlaySound ("rc50/boltclose")
		SNIR BA 3
		SNIE A 1 A_ReFire
		goto Ready
	FireZoomed:
		TNT1 A 0 A_JumpIfInventory("IsBlue", 1, "FireZoomedBlue")
		TNT1 A 0 A_RailAttack (75, 0, 0, none, none, 1, 0, "RiflePuff")
		TNT1 A 1 bright A_FireBullets (0.0, 0.0, 1, 0, "RiflePuff")
		goto ReloadZoomed
	FireZoomedBlue:
		TNT1 A 0 A_RailAttack (75, 0, 0, none, none, 1, 0, "RiflePuffBlue")
		TNT1 A 1 bright A_FireBullets (0.0, 0.0, 1, 0, "RiflePuffBlue")
		goto ReloadZoomed
	ReloadZoomed:
		TNT1 A 30
		TNT1 A 5 offset (0, 40) A_PlaySound ("rc50/boltopen")
		TNT1 A 5 offset (0, 35) A_SpawnItemEx("RifleShellCasing",0,8,32,random(1,3),random(0,1),random(1,3),random(45,60),128)
		TNT1 A 10 offset (0, 33) A_PlaySound ("rc50/boltclose")
		TNT1 A 6
		TNT1 A 1 A_ReFire
		goto Ready
	AltFire:
		SNIE A 1 A_JumpIfInventory ("IsZoomed", 1, "AltFireOff")
		SNIE A 0 A_GiveInventory ("IsZoomed", 1)
		SNIE A 4 A_ZoomFactor (6.0)
		SNIE A 1 offset (0, 32)
		SNIE A 1 offset (0, 40)
		SNIE A 1 offset (0, 48)
		SNIE A 1 offset (0, 56)
		SNIE A 1 offset (0, 64)
		SNIE A 1 offset (0, 72)
		SNIE A 1 offset (0, 80)
		SNIE A 1 offset (0, 88)
		SNIE A 1 offset (0, 96)
		SNIE A 1 offset (0, 104)
		SNIE A 1 offset (0, 112)
		SNIE A 1 offset (0, 120)
		SNIE A 1 offset (0, 128)
		SNIE A 1 offset (0, 160)
		goto Ready
	AltFireOff:
		SNIE A 4 offset (0, 160) A_ZoomFactor (1.0)
		SNIE A 1 offset (0, 128)
		SNIE A 1 offset (0, 120)
		SNIE A 1 offset (0, 112)
		SNIE A 1 offset (0, 104)
		SNIE A 1 offset (0, 96)
		SNIE A 1 offset (0, 88)
		SNIE A 1 offset (0, 80)
		SNIE A 1 offset (0, 72)
		SNIE A 1 offset (0, 64)
		SNIE A 1 offset (0, 56)
		SNIE A 1 offset (0, 48)
		SNIE A 1 offset (0, 40)
		SNIE A 1 offset (0, 32)
		SNIE A 0 A_TakeInventory ("IsZoomed", 1)
		goto Ready
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem("SniperRifleWeak")
		stop
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		TNT1 A 0 A_Light0
		stop
	}
}

actor IsZoomed : Boolean {}

actor SniperRifleWeak : SniperRifle 21081 {
	states {
	Fire:
		SNIE AA 0 A_JumpIfInventory ("IsZoomed", 1, "FireZoomed")
		SNIP F 0 A_GunFlash
		SNIF F 0 A_JumpIfInventory("IsBlue",1,"FireBlue")
		SNIF F 0 A_FireBullets (7.2, 7.2, -1, 17, "RiflePuff")
		goto Reload
	FireBlue:
		SNIF F 0 A_FireBullets (7.2, 7.2, -1, 17, "RiflePuffBlue")
		goto Reload
	FireZoomed:
		TNT1 A 0 A_JumpIfInventory("IsBlue", 1, "FireZoomedBlue")
		TNT1 A 0 A_RailAttack (50, 0, 0, none, none, 1, 0, "RiflePuff")
		TNT1 A 1 bright A_FireBullets (0.0, 0.0, 1, 0, "RiflePuff")
		goto ReloadZoomed
	FireZoomedBlue:
		TNT1 A 0 A_RailAttack (50, 0, 0, none, none, 1, 0, "RiflePuffBlue")
		TNT1 A 1 bright A_FireBullets (0.0, 0.0, 1, 0, "RiflePuffBlue")
		goto ReloadZoomed
	Spawn:
		SNIP X -1
		stop
	}
}

actor SniperAmmo : Ammo {
	inventory.amount 4
	inventory.maxamount 40
	ammo.backpackamount 4
	ammo.backpackmaxamount 40
	states {
	Spawn:
		TNT1 A 0
		stop
	}
}